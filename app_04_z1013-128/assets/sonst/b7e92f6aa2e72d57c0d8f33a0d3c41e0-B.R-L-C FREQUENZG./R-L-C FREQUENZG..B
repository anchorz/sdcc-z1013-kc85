Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10 CLS
   20 PRINT "Dieses Programm simuliert":PRINT
   30 PRINT "den Frequenzgang passiver":PRINT        
   40 PRINT "R-C-L Netzwerke":PRINT
   50 PRINT:PRINT
   60 DEF FN LG(L)=LN(L)/LN(10)
   70 EP=2*PI
   80 INPUT "Anzahl der Elemente:";NR
   90 PRINT
  100 INPUT "Quellwiderstand (OHM):";SS   
  110 PRINT
  120 INPUT "Lastwiderstand (OHM):";RR 
  130 N=NR:EX=N+3
  140 DIM FG(EX),A$(EX),WR(EX)
  150 DIM WL(EX),WC(EX),D(30)
  160 PRINT:PRINT
  170 PRINT "Element-Anordnung und Art:":PRINT
  180 PRINT "L->Laengszweig":PRINT
  190 PRINT "Q->Querzweig":PRINT
  200 PRINT "R-L-C-K(Schwingkreis)":PRINT
  210 FOR N=1 TO NR
  220 PRINT N;" ";
  230 INPUT A$(N)
  240 NEXT N
  250 PRINT:PRINT
  260 PRINT "Werte der Elemente:":PRINT
  270 FOR N=1 TO NR
  280 PRINT N;" ";
  290 IF A$(N)="LR" THEN INPUT "Laengswiderstand  (OHM):";WR(N):FG(N)=1
  300 IF A$(N)="LL" THEN INPUT "Laengspule (H):";WL(N):FG(N)=2
  310 IF A$(N)="LC" THEN INPUT "Laengskondensator (F):";WC(N):FG(N)=3
  320 IF A$(N)="LK" THEN INPUT "Laengskreis(RP,L,C):";WR(N),WL(N),WC(N):FG(N)=4
  330 IF A$(N)="QR" THEN INPUT "Querwiderstand (OHM):";WR(N):FG(N)=5
  340 IF A$(N)="QL" THEN INPUT "Querspule (H):";WL(N):FG(N)=6
  350 IF A$(N)="QC" THEN INPUT "Querkondensator (F):";WC(N):FG(N)=7
  360 IF A$(N)="QK" THEN INPUT "Querkreis(RV,L,C):";WR(N),WL(N),WC(N):FG(N)=8
  370 NEXT N
  380 PRINT:PRINT
  390 INPUT "Startfrequenz:";F 
  400 PRINT
  410 INPUT "Schrittweite (Hz):";H
  420 CLS
  430 PRINT "Vierpolrechnung laeuft!":PRINT
  440 FOR Z=1 TO 30
  450 FF=F*EP    
  460 C1=1:C3=0:C5=0:C7=1:C2=0:C4=0      
  470 C6=0:C8=0
  480 FOR N=NR TO 1 STEP -1
  490 GOSUB 740
  500 A1=1:A3=0:A5=0:A7=1:A2=0
  510 A4=0:A6=0:A8=0
  520 ON FG(N) GOSUB 770,780,790,800,850,860,870,880
  530 GOSUB 650
  540 NEXT N
  550 GOSUB 740
  560 GOSUB 940
  570 PRINT "F=";F;"Hz","D=";D(Z);"dB"              
  580 IF D(Z)>MAX THEN MAX=D(Z)
  590 PRINT
  600 F=F+H
  610 IF Z=15 THEN INPUT "weiter->ENT.";Y       
  620 NEXT Z
  630 INPUT "graph.Ausgabe->ENT";Y
  640 GOTO 1120
  650 C1=A1*B1-A2*B2+A3*B5-A4*B6
  660 C2=A1*B2+A2*B1+A3*B6+A4*B5
  670 C3=A1*B3-A2*B4+A3*B7-A4*B8
  680 C4=A1*B4+A2*B3+A3*B8+A4*B7
  690 C5=A5*B1-A6*B2+A7*B5-A8*B6
  700 C6=A5*B2+A6*B1+A7*B6+A8*B5
  710 C7=A5*B3-A6*B4+A7*B7-A8*B8
  720 C8=A5*B4+A6*B3+A7*B8+A8*B7
  730 RETURN
  740 B1=C1:B2=C2:B3=C3:B4=C4
  750 B5=C5:B6=C6:B7=C7:B8=C8
  760 RETURN
  770 A3=WR(N):RETURN
  780 A4=FF*WL(N):RETURN
  790 A4=-1/(FF*WC(N)):RETURN
  800 G=1/WR(N):BL=-1/(FF*WL(N))
  810 BC=FF*WC(N)
  820 B=BC+BL:DR=G*G+B*B
  830 A3=G/DR:A4=-B/DR
  840 RETURN
  850 A5=1/WR(N):RETURN
  860 A6=-1/(FF*WL(N)):RETURN
  870 A6=FF*WC(N):RETURN
  880 R=WR(N):XL=FF*WL(N)
  890 XC=-1/(FF*WC(N)):X=XL+XC
  900 DR=R*R+X*X:A5=R/DR
  910 A6=-X/DR:RETURN
  920 PP=MM*NN-MI*NI
  930 PL= MM*NI+MI*NN:RETURN 
  940 W=SQR(RR/SS):MM=B1
  950 MI=B2:NN=W:NI=0:GOSUB 920
  960 V1=PP:I1=PL:W=1/SQR(SS*RR)
  970 MM=B3:MI=B4:NN=W
  980 GOSUB 920
  990 V2=PP:I2=PL
 1000 W=1/W:MM=B5:MI=B6:NN=W
 1010 GOSUB 920
 1020 V3=PP:I3=PL:W=SQR(SS/RR)
 1030 MM=B7:MI=B8:NN=W
 1040 GOSUB 920
 1050 V4=PP:I4=PL
 1060 MM=0.5:MI=0 
 1070 NN=V1+V2+V3+V4
 1080 NI=I1+I2+I3+I4
 1090 GOSUB 920
 1100 D(Z)=20*FN LG(SQR(PP*PP+PL*PL))
 1110 RETURN
 1120 CLS
 1130 FOR I=1 TO 30
 1140 D(I)=INT(D(I)/MAX*224)             
 1150 G=INT(D(I)/8)
 1160 R=D(I)/8-G
 1170 FOR J=31 TO 31-G STEP -1 
 1180 PRINT AT(J,I);CHR$(255)  
 1190 NEXT J
 1200 R=R*10
 1210 IF R>8.75 THEN Z$=CHR$(254):GOTO 1290
 1220 IF R>7.5 AND R<=8.75 THEN Z$=CHR$(253):GOTO 1290
 1230 IF R>6.25 AND R<=7.5 THEN Z$=CHR$(252):GOTO 1290
 1240 IF R>5 AND R<=6.25 THEN Z$=CHR$(251):GOTO 1290
 1250 IF R>3.75 AND R<=5 THEN Z$=CHR$(250):GOTO 1290
 1260 IF R>2.5 AND R<=3.75 THEN Z$=CHR$(249):GOTO 1290
 1270 IF R>1.25 AND R<=2.5 THEN Z$=CHR$(248):GOTO 1290
 1280 GOTO 1300
 1290 PRINT AT(30-G,I);Z$   
 1300 NEXT I
 1310 INPUT "weiter->ENT.";Y
 1320 CLS
 1330 PRINT
 1340 PRINT
 1350 PRINT
 1360 PRINT "neuer Freqenzbereich->(F)":PRINT
 1370 PRINT "Aenderung der BE-Werte->(E)"
 1380 PRINT
 1390 PRINT "neues Netzwerk->(N)"
 1400 PRINT
 1410 INPUT Y$
 1420 IF Y$="F" THEN CLS:GOTO 380
 1430 IF Y$="E" THEN CLS:GOTO 270
 1440 IF Y$="N" THEN CLEAR:GOTO 10
 1450 GOTO 1410
 1460 END
   10 CLS
   20 PRINT "graphische Darstellung von":PRINT
   30 PRINT "Funktionen in :":PRINT:PRINT    
   50 PRINT "Polarkoordinaten ->RUN  500":PRINT    
   60 PRINT "Parameterdarst.  ->RUN 1000":PRINT       
   70 PRINT "kart.Koordinaten ->RUN 2000":PRINT:PRINT
   80 END
  500 CLS
  505 PRINT "graphische Darstellung von":PRINT    
  510 PRINT "Funktionen in Polarkoordinaten":PRINT
  515 PRINT "Funktion eingeben!":PRINT:PRINT:EDIT 545   
  520 CLS
  525 INPUT "W0{Grad}=";W0
  530 PRINT:PRINT
  535 INPUT "W1{Grad}=";W1
  540 CLS
  545 DEF FN R(FI)=1+COS(FI)
  550! S4 und K druecken-Start         mit RUN 520 
  555 W0=W0/180*PI 
  560 W1=W1/180*PI
  565 I=0
  570 MIX=0:MAX=0:MIY=0:MAY=0
  575 H=PI/60                        
  580 N=INT(ABS(W1-W0)/H)+1
  585 DIM U(N),V(N)
  590 FOR FI=W0 TO W1 STEP H
  595 U(I)=FN R(FI)*COS(FI)
  600 V(I)=FN R(FI)*SIN(FI)
  605 IF U(I)<MIX THEN MIX=U(I) 
  610 IF U(I)>MAX THEN MAX=U(I)
  615 IF V(I)<MIY THEN MIY=V(I)
  620 IF V(I)>MAY THEN MAY=V(I)
  625 I=I+1
  630 NEXT FI
  635 R=ABS(MAY-MIY)
  640 P=ABS(MAX-MIX)
  645 IF P<R THEN P=R
  650 S=128/P
  655 Z=I
  660 FOR K=0 TO Z  
  665 I=INT((U(K)-MIX)*S)
  670 X=INT(I/4)
  675 F=(V(K)-MIY)*S
  680 Y=INT(F/4)
  685 POKE-4128-32*Y+X,(220+I-4*X-4*INT(F)+16*Y)
  690 IF ABS(U(K))<1E-3 AND ABS(V(K))<1E-3 THEN GOSUB 710
  695 NEXT K 
  700 CLEAR
  705 GOTO 525
  710 FOR J=0 TO 31
  715 PRINT AT(J,X);"!"                          
  720 PRINT AT(31-Y,J);"-"                                            
  725 NEXT J
  730 RETURN
 1000 CLS
 1005 PRINT "graphische Darstellung von":PRINT    
 1010 PRINT "Funktionen in Parameterdarst.":PRINT:PRINT             
 1015 PRINT "Funktion eingeben!":PRINT:PRINT:EDIT 1045 
 1020 CLS
 1025 INPUT "T0 {rad.}=";T0
 1030 PRINT:PRINT
 1035 INPUT "T1 {rad.}=";T1
 1040 CLS
 1045 DEF FN X(T)=2*COS(T)-4*COS(2*T)
 1050 DEF FN Y(T)=2*SIN(T)-4*SIN(2*T)
 1055! S4 und K druecken-Start         mit RUN 1020! 
 1060 I=0
 1065 MIX=0:MAX=0:MIY=0:MAY=0
 1070 H=PI/45                        
 1075 N=INT(ABS(T1-T0)/H)+1  
 1080 DIM U(N),V(N)
 1085 FOR T=T0 TO T1  STEP H
 1090 U(I)=FN X(T)         
 1095 V(I)=FN Y(T)          
 1100 IF U(I)<MIX THEN MIX=U(I) 
 1105 IF U(I)>MAX THEN MAX=U(I)
 1110 IF V(I)<MIY THEN MIY=V(I)
 1115 IF V(I)>MAY THEN MAY=V(I)
 1120 I=I+1
 1125 NEXT T 
 1130 R=ABS(MAY-MIY)
 1135 P=ABS(MAX-MIX)
 1140 IF P<R THEN P=R
 1145 S=128/P
 1150 Z=I
 1155 FOR K=0 TO Z  
 1160 I=INT((U(K)-MIX)*S)
 1165 X=INT(I/4)
 1170 F=(V(K)-MIY)*S
 1175 Y=INT(F/4)
 1180 POKE-4128-32*Y+X,(220+I-4*X-4*INT(F)+16*Y)
 1185 IF ABS(U(K))<1E-3 AND ABS(V(K))<1E-3 THEN GOSUB 1205
 1190 NEXT K 
 1195 CLEAR
 1200 GOTO 1025
 1205 FOR J=0 TO 31
 1210 PRINT AT(J,X);"!"                          
 1215 PRINT AT(31-Y,J);"-"
 1220 NEXT J
 1225 RETURN
 2000 CLS
 2005 PRINT "graphische Darstellung von":PRINT    
 2010 PRINT "Funktionen in kart.Koordinaten":PRINT
 2015 PRINT "Funktion eingeben!":PRINT:PRINT:EDIT 2065 
 2020 CLS
 2025 INPUT "X0=";X0
 2030 PRINT:PRINT
 2035 INPUT "X1=";X1
 2040 CLS
 2045 S=(X1-X0)/128 
 2050 DIM Y(128)       
 2055 X=X0
 2060 MAX=0:MIN=0
 2065 DEF FN A(X)=X*SIN(X)/2 
 2070! S4 und k druecken-Start         mit RUN 2020!  
 2075 FOR I=0 TO 127        
 2080 Y(I)=FN A(X)
 2085 IF Y(I)>MAX THEN MAX=Y(I)
 2090 IF Y(I)<MIN THEN MIN=Y(I)
 2095 X=X+S
 2100 NEXT I
 2105 FOR I=0 TO 127 
 2110 X=INT(I/4)
 2115 F=(Y(I)-MIN)/S 
 2120 Y=INT(F/4)
 2125 POKE-4128-32*Y+X,(220+I-4*X-4*INT(F)+16*Y)
 2130 IF ABS(Y(I))<1E-3 THEN GOSUB 2150
 2135 NEXT I
 2140 CLEAR
 2145 GOTO 2025
 2150 FOR J=0 TO 31
 2155 PRINT AT(31-Y,J);"-"
 2160 NEXT J
 2165 RETURN
