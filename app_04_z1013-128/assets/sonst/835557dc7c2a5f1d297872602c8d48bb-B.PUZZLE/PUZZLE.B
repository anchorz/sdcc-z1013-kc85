Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10 REM SCHIEBEPUZZLLE
   20 WINDOW:CLS
   30 REM ZUFAELLIGE ANF.ZAHL
   40 PRINT "ZUM PROGRAMMSTAR BELIEBIGE"
   50 PRINT "TASTE DRUECKEN !":LET I=0
   60 LET I=I-1:LET A=RND(I)
   70 IF INKEY$="" THEN GOTO 60:ELSE CLS
   80 DIM GF(6,6):DEF FN ZU(I)=INT(RND(I)*5+1):LET AN=0
   90 REM ZUFAELLIGE GRAFIKVERTEILUNG
  100 FOR I=1 TO 5
  110 FOR J=1 TO 5
  120 LET GR=J+192:GOSUB 1000
  130 NEXT J,I
  140 REM ZEILEN- UND SPALTENNUMMER
  150 LET J=1
  160 FOR I=3 TO 19 STEP 4
  170 PRINT AT(I,9);J:LET J=J+1
  180 NEXT I
  190 FOR I=12 TO 28 STEP 4 
  200 PRINT AT(23,I);J:LET J=J+1
  210 NEXT I
  220 WINDOW 0,9,0,9
  230 PRINT "VERSCHIEBE";:PRINT "SOLANGE,":PRINT "BIS DIE":PRINT "GRAFIKZ."
  240 PRINT "ZEILEN-":PRINT "WEISE GE-":PRINT "ORDNET":PRINT "SIND !"
  250 WINDOW 10,31,0,9 
  260 REM GRAFIKFELD
  270 FOR Z=1 TO 5
  280 LET P=Z*4-2
  290 FOR I=0 TO 3
  300 LET J=1
  310 FOR S=12 TO 28 STEP 4
  320 LET MU=GF(Z,J)
  330 PRINT AT(P+I,S);CHR$(MU)+CHR$(MU)+CHR$(MU)+CHR$(MU)
  340 LET J=J+1  
  350 NEXT S,I,Z 
  360 REM ENDBFRAGE
  370 FOR I=1 TO 5
  380 FOR J=2 TO 5
  390 IF GF(I,1)<>GF(I,J) THEN LET AN=AN+1:GOTO 460 
  400 NEXT J,I
  460 PRINT "ZEILEN-":PRINT "ODER":PRINT "SPALTEN-":PRINT "NUMMER"
  470 INPUT NR
  480 IF NR<1 OR NR>10 THEN CLS:GOTO 460
  490 PRINT:PRINT "VERSCHIE-"  
  500 PRINT "BUNG MIT":PRINT "KURSOR-":PRINT "TASTEN"
  510 IF NR>5 THEN LET NR=NR-5:GOTO 560 
  520 LET KU$=INKEY$
  530 IF KU$=CHR$(9) THEN CLS:GOSUB 1100:GOTO 270
  540 IF KU$=CHR$(8) THEN CLS:GOSUB 1200:GOTO 270
  550 GOTO 520
  560 LET KU$=INKEY$
  570 IF KU$=CHR$(10) THEN CLS:GOSUB 1300:GOTO 270 
  580 IF KU$=CHR$(11) THEN CLS:GOSUB 1400:GOTO 270
  590 GOTO 560
 1000 REM UP ZUFALLSZAHLEN
 1010 LET Z=FN ZU(1):LET S=FN ZU(1)  
 1020 IF GF(Z,S)<>0 THEN GOTO 1010
 1030 LET GF(Z,S)=GR
 1040 RETURN
 1100 REM UP NACH RECHTS
 1110 FOR I=6 TO 2 STEP -1
 1120 LET GF(NR,I)=GF(NR,I-1)
 1130 NEXT I
 1140 LET GF(NR,1)=GF(NR,6) 
 1150 RETURN
 1200 REM UP NACH LINKS
 1210 LET GF(NR,6)=GF(NR,1)
 1220 FOR I=1 TO 5
 1230 LET GF(NR,I)=GF(NR,I+1) 
 1240 NEXT I
 1250 RETURN
 1300 REM UP NACH UNTEN
 1310 FOR I=6 TO 2 STEP -1
 1320 LET GF(I,NR)=GF(I-1,NR)
 1330 NEXT I
 1340 LET GF(1,NR)=GF(6,NR)
 1350 RETURN
 1400 REM UP NACH OBEN
 1410 LET GF(6,NR)=GF(1,NR)
 1420 FOR I=1 TO 5
 1430 LET GF(I,NR)=GF(I+1,NR)
 1440 NEXT I
 1450 RETURN
