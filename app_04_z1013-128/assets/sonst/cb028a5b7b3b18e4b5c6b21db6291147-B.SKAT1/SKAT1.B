Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

    1 CLS:PRINT AT(0,0);" ":CLEAR 1000:INK 3
    3 A1$=CHR$(8):A2$=CHR$(9):A3$=CHR$(10):A4$=CHR$(11)
    5 A5$=STRING$(5,A1$)+A3$:A6$=A5$+"   ":A7$=STRING$(5,A6$)
    6 A8$=STRING$(5,A4$):A9$=STRING$(4,A1$):AA$=STRING$(27,A2$)
    7 AB$=STRING$(22,A2$)
   10 PRINT INK 6;AT(1,2);"SKAT"
   20 PRINT AT(4,0);"Ich mische die Karten, verteile"      
   30 PRINT AT(6,0);"und sortiere sie. Ich versichere"                         
   40 PRINT AT(8,0);"Ihnen,dass ich Ihnen nicht in"        
   50 PRINT AT(10,0);"die Karten schaue oder deren"       
   60 PRINT AT(12,0);"Verteilung manipuliere."
   70 PRINT AT(15,0);"Wenn Sie >ENTER< in der rechten"  
   80 PRINT AT(17,0);"unteren Ecke sehen  druecken"
   90 PRINT AT(19,0);"Sie bitte die Taste >ENTER<."
  100 DIM B(3,12),PT(3),K(32),R(2),HR(2),SK(3),R5(3),R2(3),S1(3),S2(3),S3(3)
  110 DIM GP(3):ZV=INT(RND(1)*3)+1:Q=1:GOSUB 590
  120 CLS:PRINT AT(0,0);" "
  121 PRINT INK 7;AT(2,2);"Einen Moment,"                       
  122 PRINT INK 7;AT(4,2);"Ich mische die Karten"
  130 FOR I=1 TO 32:K(I)=1:NEXT I
  140 FOR Z=1 TO 3:FOR T=1 TO 10
  150 FOR I=INT(RND(1)*32)+1 TO 32:IF K(I)=0 THEN NEXT I:GOTO 150
  160 GOSUB 560
  170 K(I)=0:B(Z,T)=W:NEXT T,Z
  180 Z1=1:FOR I=1 TO 32:IF K(I)=0 THEN NEXT I:GOTO 200
  190 GOSUB 560:R(Z1)=W:Z1=2:NEXT I:Z1=0
  200 Z=1:GOSUB 2420
  210 GOSUB 2470:GOSUB 1640:GOSUB 1800
  220 GOSUB 2220:Z=ZV:FOR RD=1 TO 10:GOSUB 630
  230 PRINT:PRINT STRING$(6,CHR$(10))
  240 PRINT "Dieser Stich geht an Spieler"Z;:PRINT CHR$(8);
  250 IF Z=SP THEN PRINT ", dem     Einzelspieler.":ELSE PRINT
  260 GOSUB 590
  270 Z2=Z:IF RD<10 THEN GOSUB 2470
  280 Z=Z2:FB=0:NEXT RD
  285 CLS:FOR I=1 TO 5:BEEP:PAUSE 2:NEXT I:INK 6
  290 PRINT "Wertung:"Q;A1$;". Spiel ("INT((Q-1)/3+1);A1$;". Runde )"      
  300 FOR I=1 TO 2:Z1=VAL(RIGHT$(STR$(HR(I)),1)):GOSUB 3000:PT(SP)=PT(SP)+W:NEXT I
  310 INK 7:PRINT:PRINT "Einzelspieler war Spieler Nr"SP;A1$;"."
  320 PRINT:IF SP=1 THEN PRINT "Sie erreichten"PT(SP)"Punkte!" 
  330 IF SP<>1 THEN PRINT "Er erreichte"PT(SP)"Punkte!"
  340 PRINT:IF SF=1 AND PT(SP)>90 THEN FG=1:GOTO 370
  350 IF SF=0 AND PT(SP)>60 THEN FG=1:GOTO 370
  360 FG=0
  370 IF(FG=1 AND SP=1)OR(FG=0 AND SP<>1)THEN PRINT "Sie haben gewonnen":GOTO 390
  380 PRINT "Sie haben verloren"
  390 ZV=ZV+1:IF ZV=4 THEN ZV=1
  400 IF PT(SP)>90 OR PT(SP)<30 THEN R5(SP)=R5(SP)+1:PRINT "Schneider";:TT=1
  410 IF SF=1 AND TT=1 THEN PRINT " & angesagt" 
  420 TT=0:PRINT:IF SF=1 THEN R5(SP)=R5(SP)+1
  430 IF HF=1 THEN R5(SP)=R5(SP)+1:HF=0
  440 IF GR=1 THEN GR=0:S2(SP)=24*(R5(SP)+1):GOTO 455
  450 S2(SP)=(R5(SP)+1)*R2(SP)
  455 PRINT INK 6;AT(16,0);"Spielstand" 
  460 IF(PT(SP)<61)OR(PT(SP)<91 AND SF=1)THEN S2(SP)=-2*S2(SP)
  470 SF=0:IF S2(SP)>=GW OR FG=0 THEN 510
  480 PRINT "Moment...";:IF SP=1 THEN PRINT "Sie haben sich ";:GOTO 500  
  490 PRINT "SPIELER"SP "hat sich ";
  500 PRINT "ueberreizt.":PRINT "---verloren!":S2(SP)=-2*S2(SP) 
  510 IF KF=1 THEN S2(SP)=2*S2(SP):KF=0
  520 IF RF=1 THEN S2(SP)=2*S2(SP):RF=0
  525 PRINT INK 6;AT(18,0);"Spieler1 Spieler2 Spieler3 Spiel"      
  530 GP(SP)=GP(SP)+S2(SP):FOR I=0 TO 2:PRINT AT(20,I*09);GP(I+1):NEXT I        
  540 PRINT AT(20,28);S2(SP)
  550 FOR I=1 TO 3:PT(I)=0:NEXT I:Q=Q+1:GOSUB 590:GOTO 120
  560 W=(INT((I-1)/4)+1)+10*(((I-1)/4-INT((I-1)/4))*4+1)
  570 IF INT((I-1)/4)+1=8 THEN W=W+200
  580 RETURN
  590     PRINT INK 6;AT(31,25);">ENTER<"
  600 P$=INKEY$:IF P$<>CHR$(13)THEN 600
  610 PRINT AT(31,25);"       "   
  620 RETURN
  630 FB=0:ON Z GOTO 640,650,650
  640 GOSUB 3080:GOSUB 3250:GOTO 750
  650 IF SP=Z THEN 700
  660 GOSUB 1560
  670 IF H=0 THEN GOSUB 1480
  680 IF H-10*INT(H/10)=6 THEN GOSUB 1590
  690 SK(Z)=H:GOSUB 3250:GOTO 750
  700 GOSUB 1480:IF VAL(RIGHT$(STR$(H),1))>5 THEN GOSUB 1460:GOTO 720
  710 GOTO 730
  720 IF VAL(RIGHT$(STR$(H),1))>5 AND H<200 THEN GOSUB 1560:IF H=0 THEN GOSUB 1460
  730 IF H=0 THEN GOSUB 1560
  740 GOTO 690
  750 FB=INT(SK(Z)/10):Z=Z+1:IF Z=4 THEN Z=1
  760 IF Z=1 THEN GOSUB 3080:GOSUB 3250:GOTO 990
  770 IF Z=SP THEN 910
  780 IF SK(SP)>0 THEN 830
  790 GOSUB 1510:IF H=0 THEN GOSUB 1480
  800 IF H=0 THEN ON 1+INT(RND(1)*2)GOSUB 1560,1590
  810 SK(Z)=H:IF INT(H/10)<>FB AND H<100 THEN SK(Z)=-H
  820 GOSUB 3250:GOTO 990
  830 IF SK(SP)>100 THEN 890
  840 GOSUB 1510:IF H>SK(1)THEN 810
  850 IF H>0 THEN GOSUB 1530:GOTO 810
  860 GOSUB 1480
  870 IF H=0 THEN GOSUB 1590
  880 GOTO 810
  890 GOSUB 1480:IF H=0 THEN GOSUB 1590
  900 GOTO 810
  910 ZW=Z-1:IF ZW=0 THEN ZW=3
  920 IF FB>9 THEN 970
  930 GOSUB 1510:IF H>SK(ZW)THEN 810
  940 IF H>0 THEN GOSUB 1530:GOTO 810
  950 GOSUB 1480:IF H>0 THEN 810
  960 GOSUB 1590:GOTO 810
  970 GOSUB 1460:IF H=0 THEN GOSUB 1590
  980 GOTO 810
  990 Z=Z+1:IF Z=4 THEN Z=1
 1000 IF Z=1 THEN GOSUB 3080:GOSUB 3250:GOTO 1370
 1010 IF Z=SP THEN 1190
 1020 GOSUB 1330:IF H1=SK(SP)THEN 1090
 1030 IF FB<10 THEN 1060
 1040 GOSUB 1480:IF H=0 THEN GOSUB 1560
 1050 GOTO 1170
 1060 GOSUB 1510:IF H=0 THEN GOSUB 1560
 1070 IF H=0 THEN GOSUB 1480
 1080 GOTO 1170
 1090 IF FB<9 THEN 1120
 1100 ON 1+INT(RND(1)*2)GOSUB 1460,1480:IF H>0 THEN 1170
 1110 GOSUB 1590:GOTO 1170
 1120 GOSUB 1510:IF H>SK(SP)THEN 1170
 1130 IF H>0 THEN GOSUB 1530
 1140 IF H=0 THEN GOSUB 1480:IF H<SK(PS)THEN GOSUB 1460
 1150 IF H=0 THEN GOSUB 1530
 1160 IF H=0 THEN GOSUB 1590
 1170 SK(Z)=H:IF INT(H/10)<>FB AND H<100 THEN SK(Z)=-H
 1180 GOSUB 3250:GOTO 1370
 1190 IF FB>9 THEN 1270
 1200 GOSUB 1510:GOSUB 1330:IF H>H1 THEN 1170
 1210 IF H>0 THEN GOSUB 1530:GOTO 1170
 1220 GOSUB 1350:IF SS>8 THEN 1260
 1230 GOSUB 1590:IF H=0 THEN GOSUB 1480:GOTO 1170
 1240 IF H-10*INT(H/10)>4 THEN GOSUB 1480
 1250 GOTO 1170
 1260 GOSUB 1480:IF H=0 THEN GOSUB 1590
 1270 GOSUB 1350:IF SS>10 THEN 1310
 1280 GOSUB 1480:IF H=0 THEN GOSUB 1590:GOTO 1170
 1290 IF VAL(RIGHT$(STR$(H),2))<6 THEN 1170
 1300 GOSUB 1460:GOTO 1170
 1310 GOSUB 1480:IF H=0 THEN GOSUB 1590
 1320 GOTO 1170
 1330 H1=0:FOR I=1 TO 3:IF H1<SK(I)THEN H1=SK(I)
 1340 NEXT I:RETURN
 1350 SS=0:FOR I=1 TO 3:Z1=VAL(RIGHT$(STR$(SK(I)),1)):GOSUB 3000:SS=SS+W
 1360 NEXT I:RETURN
 1370 FOR Z1=1 TO 3:FOR I=1 TO 10:IF ABS(SK(Z1))=B(Z1,I)THEN B(Z1,I)=0
 1380 NEXT I,Z1:IF SK(1)>SK(2)AND SK(1)>SK(3)THEN Z=1:GOTO 1400
 1390 GOTO 1420
 1400 IF INT(SK(1)/10)<>FB AND SK(1)<100 THEN 1420
 1410 GOTO 1440
 1420 IF SK(2)>SK(3)THEN Z=2:GOTO 1440
 1430 Z=3
 1440 SS=0:FOR I=1 TO 3:Z1=VAL(RIGHT$(STR$(SK(I)),1)):GOSUB 3000
 1450 SS=SS+W:SK(I)=0:NEXT I:PT(Z)=PT(Z)+SS:RETURN
 1460 H=0:FOR I=1 TO 10:IF H<B(Z,I)AND B(Z,I)>100 THEN H=B(Z,I)
 1470 NEXT I:RETURN
 1480 H=1000:FOR I=1 TO 10:IF H>B(Z,I)AND B(Z,I)>100 THEN H=B(Z,I)
 1490 NEXT I:IF H=1000 THEN H=0
 1500 RETURN
 1510 H=0:FOR I=1 TO 10:IF H<B(Z,I)AND INT(B(Z,I)/10)=FB THEN H=B(Z,I)
 1520 NEXT I:RETURN
 1530 H=1000:FOR I=1 TO 10:IF H>B(Z,I)AND INT(B(Z,I)/10)=FB THEN H=B(Z,I)
 1540 NEXT I:IF H=1000 THEN H=0
 1550 RETURN
 1560 H1=0:H=0:FOR I=1 TO 10:IF H1<B(Z,I)-10*INT(B(Z,I)/10)AND B(Z,I)<100 THEN 1580
 1570 NEXT I:H1=0:RETURN
 1580 H=B(Z,I):H1=B(Z,I)-10*INT(B(Z,I)/10):GOTO 1570
 1590 H1=10:H=1000:FOR I=1 TO 10
 1600 IF H1>VAL(RIGHT$(STR$(B(Z,I)),1))AND B(Z,I)<100 AND B(Z,I)>0 THEN 1630
 1610 NEXT I:IF H=1000 THEN H=0
 1620 RETURN
 1630 H=B(Z,I):H1=VAL(RIGHT$(STR$(B(Z,I)),1)):GOTO 1610
 1640 FOR Z=1 TO 3:R1=0:R2(Z)=0:R3=0:R5(Z)=0
 1650 FOR I=1 TO 4:K(I)=0:NEXT I:Z1=0
 1660 FOR I=1 TO 10:IF B(Z,I)>200 THEN Z1=Z1+1:K(Z1)=B(Z,I)
 1670 NEXT I:R1=Z1:Z1=0:FOR H=248 TO 218 STEP-10:GOSUB 1680:NEXT H:GOTO 1700
 1680 Z1=Z1+1:FOR I=1 TO 4:IF K(I)=H THEN K(Z1+4)=1:RETURN
 1690 NEXT I:K(Z1+4)=0:RETURN
 1700 FOR I=5 TO 7:IF K(I)=K(I+1)THEN NEXT I:I=8
 1710 R5(Z)=I-4:S3(Z)=R5(Z):FOR I=1 TO 4:K(I)=0:NEXT I
 1720 FOR I=1 TO 10:IF B(Z,I)<100 THEN FB=INT(B(Z,I)/10):K(FB)=K(FB)+1
 1730 NEXT I:FOR I=1 TO 4:IF R3<K(I)THEN R3=K(I):R2(Z)=I
 1740 NEXT I:R2(Z)=8+R2(Z):R4=R2(Z)*(R5(Z)+1):R8=0:FOR I=1 TO 10
 1750 IF B(Z,I)-10*INT(B(Z,I)/10)=7 OR B(Z,I)-10*INT(B(Z,I)/10)=6 THEN R8=R8+1
 1760 NEXT I:R8=R8/2:R6=(R3-1+R1)*(5+R1)*R8:IF R6>R4 THEN R6=R4:S2(Z)=R4
 1770 IF R6<18 THEN R7=18:GOTO 1790
 1780 READ R4:IF R4<=R6 THEN R7=R4:GOTO 1780
 1790 R6=R7:RESTORE:S1(Z)=R6:NEXT Z:RETURN
 1800 GOSUB 590:PG=ZV:Z1=ZV:GOSUB 1810:PH=Z1:GOSUB 1810:SP=Z1:GOTO 1830
 1810 Z1=Z1+1:IF Z1<4 THEN RETURN
 1820 Z1=1:RETURN
 1830 GOSUB 1890:IF H=0 THEN SP=PG:GOTO 1870:! 1860
 1840 GOSUB 1960:IF H=1 THEN 1830
 1850 PH=PG:GOTO 1870
 1860 GOSUB 1890:IF H=0 THEN SP=PH:GOTO 2030
 1870 GOSUB 1960:IF H=1 THEN 1860
 1880 GOTO 2030
 1890 IF SP=1 THEN 1920
 1900 READ R4:PRINT:PRINT SP ":"R4;:IF R4<S1(SP)+.5 THEN H=1:RETURN
 1910 PRINT "Weg";:H=0:RETURN 
 1920 READ R4:PRINT:PRINT INK 6;R4;
 1930 GOSUB 3600:IF P$="J"THEN H=1:RETURN
 1940 H=0:ZW=R4:RETURN
 1960 PRINT PH ":";:IF PH=1 THEN 1990
 1970 IF R4<=S1(PH)THEN PRINT "Ja";:H=1:ZW=R4:RETURN
 1980 PRINT "Weg";:H=0:ZW=R4:RETURN
 1990 PRINT INK 6;
 2000 GOSUB 3600:IF P$="J"THEN H=1:RETURN
 2010 H=0:ZW=R4:RETURN
 2030 PRINT:PRINT INK 7;"Spiel geht an Spieler";SP
 2040 IF SP=1 THEN GOSUB 590:GOSUB 2530:RETURN
 2050 FOR I=1 TO 2:B(SP,I+10)=R(I):NEXT I
 2060 FOR I=1 TO 4:K(I)=0:NEXT I:FOR I=1 TO 12:FB=INT(B(SP,I)/10)
 2070 IF FB<10 THEN K(FB)=K(FB)+1
 2080 NEXT I:F1=0:FB=0:FOR I=1 TO 4:IF F1<K(I)THEN FB=I:F1=K(I)
 2090 NEXT I:P=FB:GOSUB 2800:GOSUB 2160:PRINT " IST TRUMPF":S1(SP)=8+P
 2100 S2(SP)=(1+S3(SP))*S1(SP):FOR I=1 TO 2
 2110 A=INT(RND(1)*12)+1:IF B(SP,A)>100 OR B(SP,A)-10*INT(B(SP,A)/10)=7 THEN 2110
 2120 HR(I)=B(SP,A):B(SP,A)=0
 2130 NEXT I
 2140 Z1=0:FOR I=1 TO 12:IF B(SP,I)>0 THEN Z1=Z1+1:K(Z1)=B(SP,I)
 2150 NEXT I:FOR I=1 TO 10:B(SP,I)=K(I):NEXT I:GOSUB 590:GOSUB 2470:RETURN
 2160 PRINT "__";
 2170 IF P=1 THEN PRINT "Karo";:RETURN   
 2180 IF P=2 THEN PRINT "Herz";:RETURN
 2190 IF P=3 THEN PRINT "Pik";:RETURN
 2200 IF P=4 THEN PRINT "Kreuz";:RETURN
 2210 PRINT "Grand";:RETURN
 2220 IF SP<>1 THEN 2320
 2230 PRINT:FOR T=2 TO 3
 2240 H1=0:FOR J=1 TO 10:IF B(T,J)>100 THEN H1=H1+1
 2250 NEXT J:IF H1>4-SF AND T<>SP THEN PRINT "_Spieler"T ": Kontra !":KF=1
 2260 FOR J=1 TO 700:NEXT J
 2270 NEXT T:IF KF=0 THEN 2400
 2280 IF SP=1 THEN 2360
 2290 FOR J=1 TO 10:IF VAL(RIGHT$(STR$(B(SP,J)),1))>6 OR B(SP,J)>200 THEN H1=H1+1
 2300 NEXT J:IF H1>5 THEN PRINT "_Spieler"SP ": Re !!":RF=1:FOR J=1 TO 1000:NEXT J
 2310 GOTO 2400
 2320 PRINT:PRINT "Kontra ";
 2330 GOSUB 3600:IF P$="J"THEN KF=1
 2340 GOTO 2230
 2360 PRINT:PRINT "Re ";
 2370 GOSUB 3600:IF P$="J"THEN RF=1
 2380 GOTO 2400
 2400 GOSUB 2470
 2410 RETURN
 2420 FOR T=1 TO 10
 2430 FOR I=1 TO 10:IF Z1<B(Z,I)THEN Z1=B(Z,I)
 2440 NEXT I:K(T)=Z1:FOR I=1 TO 10:IF B(Z,I)=Z1 THEN B(Z,I)=0
 2450 NEXT I:Z1=0:NEXT T
 2460 FOR J=1 TO 10:B(Z,J)=K(J):NEXT J:RETURN
 2470 CLS:PRINT INK 6;"Ihre Karten:":PRINT
 2480 Z=1:FOR T=1 TO 9
 2490 IF T=5 THEN PRINT:PRINT STRING$(4,CHR$(10))                                 
 2495 IF T=9 THEN PRINT:PRINT STRING$(4,CHR$(10))
 2500 PRINT T;:GOSUB 3560:NEXT T
 2510 T=10:PRINT 0;:GOSUB 3560
 2520 PRINT:PRINT STRING$(4,CHR$(10)):PRINT:RETURN
 2530 PRINT INK 6;"Hand ";
 2540 GOSUB 3600
 2550 IF P$="J"THEN HF=1:FOR I=1 TO 2:HR(I)=R(I):NEXT I:GOTO 2720
 2560 GOTO 2580
 2580 F1=1:GOSUB 2470:PRINT:PRINT INK 6;"Skat";:PRINT                            
 2585 FOR I=1 TO 2:B(1,10+I)=R(I):NEXT I
 2590 FOR T=11 TO 12:PRINT T;:GOSUB 3560:NEXT T
 2610 K(1)=13:K(2)=13:INPUT "Druecken Nr";K(1):PRINT AA$;     
 2615 PRINT "Nr";:INPUT "";K(2)
 2620 IF K(1)=0 THEN K(1)=10
 2630 IF K(2)=0 THEN K(2)=10
 2640 IF K(1)=K(2)THEN PRINT AB$;A4$;A4$;:GOTO 2610
 2650 FOR I=1 TO 2
 2660 IF K(I)<1 OR K(I)>12 THEN PRINT AB$;A4$;A4$;:GOTO 2610
 2670 HR(I)=B(1,K(I)):NEXT I
 2680 Z1=0:FOR I=1 TO 2:B(1,K(I))=0:NEXT I:FOR I=1 TO 12
 2690 IF B(1,I)<>0 THEN Z1=Z1+1:K(Z1)=B(1,I)
 2700 NEXT I:FOR I=1 TO 10:B(1,I)=K(I):NEXT I
 2710 F4=1:Z=1:GOSUB 2420
 2720 GOSUB 2470:PRINT INK 6;"Was ist Trumpf?":PRINT "1. Karo     2. Herz" 
 2730 PRINT "3. Pik      4. Kreuz    5. Grand" 
 2750 P$=INKEY$:P=VAL(P$)
 2760 GR=0
 2770 IF P>5 OR P<1 THEN 2750
 2780 PRINT "Nr."P
 2790 IF P=5 THEN 2910
 2800 S2(1)=(P+8)*(S3(1)+1):FOR Z=1 TO 3:FOR T=1 TO 10
 2810 IF B(Z,T)<100 AND INT(B(Z,T)/10)=P THEN B(Z,T)=100+B(Z,T)
 2820 NEXT T,Z
 2830 FOR T=11 TO 12:IF B(SP,T)<100 AND INT(B(SP,T)/10)=P THEN B(SP,T)=100+B(SP,T)
 2840 NEXT T:Z=1:GOSUB 2420:IF F1=0 THEN GOSUB 2470
 2850 IF F4=0 THEN 2900
 2860 F4=0:SF=0:PRINT "Schneider ansagen ";
 2870 GOSUB 3600:IF P$="J"THEN SF=1
 2880 GOTO 2900
 2900 GOSUB 2930:RETURN
 2910 GR=1:S2(1)=24*(S3(1)+1):Z=1:GOSUB 2420:IF F1=0 THEN GOSUB 2470
 2920 GOSUB 2850:RETURN
 2930 Z1=1:R2(SP)=8+P:IF P=5 THEN R2(SP)=24
 2940 FOR T=1 TO 4:K(T)=0:NEXT T
 2950 FOR T=1 TO 4:FOR I=1 TO 10:IF B(SP,I)=258-10*T THEN K(T)=1
 2960 NEXT I,T:FOR T=1 TO 3:IF K(T)=K(T+1)THEN Z1=Z1+1:NEXT T
 2970 IF HF=1 THEN Z1=Z1+1
 2980 R5(SP)=Z1:S2(SP)=(R5(SP)+1)*R2(SP)
 2990 GW=ZW:RETURN
 3000 ON Z1 GOTO 3010,3010,3010,3020,3030,3040,3050,3060
 3010 W=0:RETURN
 3020 W=3:RETURN
 3030 W=4:RETURN
 3040 W=10:RETURN
 3050 W=11:RETURN
 3060 W=2:RETURN
 3080 PRINT INK 6;AT(18,19);"Karte Nr.?"  
 3090 P$=INKEY$:!IFP$=""THEN3090
 3095 IF P$<"0"OR P$>"9"THEN 3090
 3100 P=VAL(P$):IF P=0 THEN P=10
 3110 IF B(1,P)=0 THEN 3090
 3120 IF SK(2)=0 AND SK(3)=0 THEN 3220
 3130 GE=2:IF SK(2)=0 THEN GE=3
 3140 F=0:FOR G=1 TO 10
 3150 IF B(1,G)>100 THEN F=F+1
 3160 NEXT G
 3170 IF F>0 AND B(1,P)<100 AND SK(GE)>100 THEN 3210
 3180 F=0:FOR G=1 TO 10:IF B(1,G)>100 THEN NEXT G
 3190 IF INT(B(1,G)/10)=INT(SK(GE)/10)AND INT(B(1,P)/10)<>INT(SK(GE)/10)THEN 3210
 3200 NEXT G:GOTO 3220
 3210 PRINT AT(18,27);"Bedienen! ":GOSUB 590:GOTO 3080
 3220 SK(1)=B(1,P)
 3230 IF FB>0 AND INT(SK(1)/10)<>FB AND SK(1)<100 THEN SK(1)=-SK(1)
 3240 RETURN
 3250!
 3260 IF Z=SP THEN PRINT INK 6;" * ";:GOTO 3280
 3270 PRINT INK 6;Z;
 3280 Q$=RIGHT$(STR$(SK(Z)),2)
 3290 GOSUB 3300:RETURN
 3300 Z1=VAL(LEFT$(Q$,1)):IF Z1=0 THEN PRINT "     ";:RETURN
 3310 ON 5-Z1 GOTO 3320,3330,3340,3350
 3320 B$="":INK 5:GOTO 3360
 3330 B$="":INK 5:GOTO 3360
 3340 B$="":INK 2:GOTO 3360
 3350 B$="":INK 2
 3360 IF F2=1 THEN F2=0:INK 3:PRINT "     ";:RETURN
 3370 Z1=VAL(RIGHT$(Q$,1))
 3380 ON 9-Z1 GOTO 3390,3400,3410,3420,3430,3440,3450,3460
 3390 A$="B":GOTO 3470
 3400 A$="A":GOTO 3470
 3410 A$="10":GOTO 3470
 3420 A$="K":GOTO 3470
 3430 A$="D":GOTO 3470
 3440 A$="9":GOTO 3470
 3450 A$="8":GOTO 3470
 3460 A$="7"
 3470 PRINT INK 7;"";A7$;A5$;
 3480 PRINT INK 7;"";A8$;A9$;
 3490 PRINT A$;A3$;A1$;
 3500 IF A$="10"THEN PRINT A1$;
 3510 PRINT B$;A3$;A3$;A2$;
 3520 PRINT B$;A3$;A1$;
 3530 IF A$="10"THEN PRINT A1$;
 3540 PRINT A$;A2$;A8$;
 3550 INK 3:RETURN
 3560 Q$=RIGHT$(STR$(B(Z,T)),2):GOSUB 3300:RETURN
 3570 DATA 18,20,22,23,24,27,30,33,35,36,44,45,46,48,50,54,55,60,63,66,70
 3580 DATA 72,77,80,81,84,88,90,96,99,100,108,110,120,144,168
 3600 PRINT INK 6;"(J/N)? ";:BEEP:PAUSE 3:BEEP
 3610 P$=INKEY$:IF P$="J"THEN PRINT "Ja";:RETURN
 3615 IF P$<>"N"THEN 3610
 3620 PRINT "Nein";:RETURN
