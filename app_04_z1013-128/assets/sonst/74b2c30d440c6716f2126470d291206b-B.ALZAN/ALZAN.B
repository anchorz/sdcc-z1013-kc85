Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10 REM ALZAN ADVENTURE
   30 WINDOW 0,31,0,31:CLS:FOR B=0 TO 3:WINDOW 10,31,1,31
   40 PRINT AT(16,6);"version for Z-1013":PRINT AT(17,9);"c/1988 rothe":PAUSE 20
   50 PRINT "         "      
   70 PRINT "                     "          
   90 PRINT "                     "   
  110 PRINT "                     "  
  130 PRINT "           "  
  150 PRINT "                     "  
  170 PRINT "                     "  
  190 PRINT "                     "     
  192 PRINT "                     "     
  193 PRINT "            "  
  194 PRINT:PRINT " ***  A D V E N T U R E  ***":PAUSE 20:PRINT
  195 Z=9:Y=19:I=0:L=1 :FOR M=0 TO 30 :WINDOW Z,Y,I,L:CLS:L=L+1:NEXT:PAUSE 1 :NEXT B
  210 CLEAR 3072:WINDOW:CLS       
  230 DIM C(5),O(11),S(10),A$(47),M$(19),O$(11),P$(2),V$(43):GOSUB 6330
  250 R=1
  270 A=47:O=11:V=43
  290 DATA 0,0,3,3,0,0,0,15,16,0,4
  310 RESTORE 290
  330 FOR X=1 TO O
  350 READ O(X)
  370 NEXT X
  390 X=RND(-ABS(DEEK(30))/3600)
  410 IF S(2)=0 THEN 550
  430 IF C(2)<>0 THEN C(2)=C(2)-1
  450 IF S(3)<>0 THEN 550
  470 PRINT "ES IST DUNKEL. SIE SOLLTEN BES-","SER FUER ETWAS LICHT SORGEN,"
  490 PRINT "SONST KOENNTE ES SCHWIERIGKEITEN","GEBEN."
  510 IF C(3)<>0 THEN C(3)=C(3)-1
  530 GOTO 790
  550 REM RAUMBESCHREIBUNG
  570 PRINT
  590 ON R GOSUB 5050,5210,5290,5350,5410,5470,5510,5550,5610,5650,5710,5790
  610 IF R>12 THEN ON R-12 GOSUB 5830,5890,5950,6010,6070,6150,6210
  630 F=0
  650 FOR X=1 TO O
  670 IF O(X)<>R THEN 770
  690 IF F<>0 THEN 750
  710 PRINT "FERNER GIBT ES HIER:":PRINT STRING$(20,"")   
  730 F=1 
  750 PRINT "   ";O$(X)
  770 NEXT X
  790 REM BEFEHLSVERARBEITUNG
  810 T=1
  830 GOTO 1390
  850 IF C(1)<>0 THEN C(1)=C(1)-1
  870 IF C(4)<>0 THEN C(4)=C(4)-1
  890 PRINT AT(15,5);" <?> ---->  H E L P .":GOSUB 5130
  910 PRINT AT(15,1);STRING$(29,"")   
  930 WINDOW 20,21,1,30:CLS:GOSUB 8410:INPUT "  >";Y$:IF Y$="?"THEN GOSUB 8530
  950 WINDOW 3,16,0,31:CLS:Y=0
  970 P$(2)="00"
  990 FOR W=1 TO 2
 1010 GOSUB 3510
 1030 IF Y>=LEN(Y$)THEN 1110
 1050 IF P$(W)="00"THEN 1010
 1070 NEXT W
 1090 W=W-1
 1110 IF P$(W)<>"00"THEN 1170
 1130 WINDOW 20,21,1,30:CLS:PRINT AT(20,9);"PARDON ?":WINDOW 3,16,0,31
 1150 GOTO 410
 1170 REM FESTSTELLEN OB STANDORTS-
 1190 Z=1:IF P$(2)<>"00"AND P$(1)>P$(2)THEN N$=P$(1):P$(1)=P$(2):P$(2)=N$
 1210 T$=MID$(M$(R),Z,2)
 1230 IF T$="00"THEN 1350
 1250 IF T$<>P$(1)THEN 1310
 1270 R=VAL(MID$(M$(R),Z+2,2))
 1290 GOTO 410
 1310 Z=Z+4
 1330 GOTO 1210
 1350 T=0
 1370 M=0
 1390 REM FESTSTELLEN OB BEDINGUNGEN              ERFUELLT WERDEN MUESSEN
 1410 C=0
 1430 C=C+1
 1450 IF T=0 THEN 1510
 1470 E$=C$(C)
 1490 GOTO 1730
 1510 IF A>=C THEN 1650
 1530 IF M<>0 THEN 790
 1550 WINDOW 19,21,1,30:CLS       
 1570 PRINT " SIE KOENNEN NICHT";
 1590 IF VAL(P$(1))<13 THEN PRINT " DIESEN WEG","           GEHEN";
 1610 PRINT ".":PAUSE 30:CLS: WINDOW 3,17,0,31
 1630 GOTO 410
 1650 IF LEFT$(A$(C),2)<>P$(1)THEN 1430
 1670 Y$=MID$(A$(C),3,2)
 1690 IF Y$<>"00"AND Y$<>P$(2)THEN 1430
 1710 E$=MID$(A$(C),5)
 1730 REM ABFRAGEN DER BEDINGUNGSTABELLE
 1750 E=1
 1770 IF MID$(E$,E,1)="."THEN 2070
 1790 P=ASC(MID$(E$,E,1))-64
 1810 N=VAL(MID$(E$,E+1,2))
 1830 ON P GOSUB 1910,1930,1950,1970,1990,2010,2030,2050
 1850 IF K=0 THEN 1430
 1870 E=E+3
 1890 GOTO 1770
 1910 K=-(N=R):RETURN
 1930 K=-(O(N)=R OR O(N)<0):RETURN
 1950 K=-(O(N)<>R AND O(N)>=0):RETURN
 1970 K=-(O(N)<0):RETURN
 1990 K=S(N):RETURN
 2010 K=-(S(N)=0):RETURN
 2030 K=-(C(N)=1):RETURN
 2050 K=-(INT(RND(1)*100+1<=N)):RETURN
 2070 REM DURCHFUEHREN DER AKTIONEN
 2090 M=1
 2110 E=E+1
 2130 IF MID$(E$,E,1)="."THEN 1430
 2150 P=ASC(MID$(E$,E,1))-64
 2170 IF MID$(E$,E+1,1)<>"."THEN N=VAL(MID$(E$,E+1,2))
 2190 B=0
 2210 ON P GOSUB 2310,2510,2710,2850,2930,2970,3010,3070,3150,3190,3250,3290
 2230 IF P>12 THEN ON P-12 GOSUB 3310,3350,3390,3430,3490
 2250 ON B GOTO 410,790,850
 2270 E=E+3
 2290 GOTO 2130
 2310 PRINT
 2330 PRINT "SIE TRAGEN:"
 2350 F=1
 2370 FOR X=1 TO O
 2390 IF O(X)>=0 THEN 2450
 2410 PRINT "   ";O$(X)
 2430 F=0
 2450 NEXT X
 2470 IF F=1 THEN PRINT "   NICHTS."
 2490 B=1:RETURN
 2510 IF S(1)<4 THEN 2590
 2530 PRINT "SO VIEL KOENNEN SIE NICHT TRAGEN"
 2550 B=1
 2570 RETURN
 2590 IF O(N)=-1 THEN 2670
 2610 O(N)=-1
 2630 S(1)=S(1)+1
 2650 RETURN
 2670 PRINT "SIE HABEN ES BEREITS."
 2690 GOTO 2550
 2710 IF O(N)=-1 THEN 2790
 2730 PRINT "SIE HABEN ES NICHT."
 2750 B=1
 2770 RETURN
 2790 O(N)=R
 2810 S(1)=S(1)-1
 2830 RETURN
 2850 PRINT
 2870 ON N GOSUB 4250,4290,4370,4410,4450,4490,4530,4570,4610,4650,4690,4730
 2890 IF N>12 THEN ON N-12 GOSUB 4770,4850,4890,4930
 2910 RETURN
 2930 S(N)=1
 2950 RETURN
 2970 S(N)=0
 2990 RETURN
 3010 C(N)=VAL(MID$(E$,E+3,2))
 3030 E=E+2
 3050 RETURN
 3070 X=O(N)
 3090 O(N)=O(N+1)
 3110 O(N+1)=X
 3130 RETURN
 3150 O(N)=R
 3170 RETURN
 3190 IF O(N)<0 THEN S(1)=S(1)-1
 3210 O(N)=0
 3230 RETURN
 3250 R=N
 3270 RETURN
 3290 PRINT " OKEY."
 3310 B=2
 3330 RETURN
 3350 B=3
 3370 RETURN
 3390 B=1
 3410 RETURN
 3430 PRINT "SIE WOLLEN AUFGEBEN ?"
 3450 INPUT W$
 3470 IF LEFT$(W$,1)<>"J"THEN 3390
 3490 GOTO 6310
 3510 REM SCHLUESSELWORT ABTRENNEN
 3530 W$=""
 3550 P$(W)="00"
 3570 GOSUB 3990
 3590 IF D=1 THEN RETURN
 3610 FOR Q=1 TO 4
 3630 W$=W$+MID$(Y$,Y,1)
 3650 GOSUB 3890
 3670 IF D=1 THEN 3750
 3690 NEXT Q
 3710 GOSUB 3890
 3730 IF D=0 THEN 3710
 3750 W$=W$+STRING$(4-LEN(W$)," "):IF W$="    "THEN RETURN
 3770 FOR Q=1 TO V
 3790 IF W$=MID$(V$(Q),3)THEN 3850
 3810 NEXT Q
 3830 RETURN
 3850 P$(W)=LEFT$(V$(Q),2)
 3870 RETURN
 3890 Y=Y+1
 3910 D=-(Y>LEN(Y$))
 3930 IF D=1 THEN RETURN
 3950 D=-(MID$(Y$,Y,1)=" ")
 3970 RETURN
 3990 Y=Y+1
 4010 D=-(Y>LEN(Y$))
 4030 IF D=1 THEN RETURN
 4050 IF MID$(Y$,Y,1)=" "THEN 3990
 4070 RETURN
 4090 CLS
 4110 FOR N=1 TO 50
 4130 PRINT N;V$(N)
 4150 NEXT N
 4170 END
 4190 REM AKTIONSMELDUNGEN
 4210 REM MELDUNG 1 BEWIRKT
 4230 REM SPRUNG ZUR ZEILE 7010
 4250 PRINT "ACH DU LIEBE GUETE, SCHON TOT!"
 4270 RETURN
 4290 PRINT TAB(8);"---HOPPLA---"
 4310 PRINT "EL GRABBO, DER DIEB DER STADT,","SCHNAPPT SICH IHR GELD UND"
 4330 PRINT "VERSCHWINDET IM DICHTEN NEBEL."
 4350 RETURN
 4370 PRINT "HALTET DEN DIEB, RUFT DER PLATZ-";"ANWEISER, ABER SIE ENTKOMMEN" 
 4390 RETURN
 4410 PRINT "DER DECKEL IST BEREITS OFFEN !! "
 4430 RETURN
 4450 PRINT "ES KOSTET MEHR, ALS SIE AUF-    ";"BRINGEN KOENNEN."
 4470 RETURN
 4490 PRINT "SEHR WOHL, MEIN HERR!
 4510 RETURN
 4530 PRINT "DER KANALDECKEL IST OFFEN."
 4550 RETURN
 4570 PRINT "DER KANALDECKEL IST VERSCHLOSSEN."
 4590 RETURN
 4610 PRINT "DER LADENBESITZER IST GROESSER","ALS SIE..."
 4630 RETURN
 4650 PRINT "SIE BRAUCHEN EINE LEITER, UM","UEBER DIESE MAUER ZU KOMMEN."
 4670 RETURN
 4690 PRINT "ES IST BEREITS AN!"
 4710 RETURN
 4730 PRINT "WAS FUER EIN GEISTESBLITZ!"
 4750 RETURN
 4770 PRINT "SIE UEBERWAELTIGEN HEIMLICH","DIE WACHEN UND KOENNEN SICH"
 4790 PRINT "NOCH EIN BUENDEL BANKNOTEN  ","SCHNAPPEN,KEINER HAT ETWAS"
 4810 PRINT "GEMERKT (LUSTIGES VOELKCHEN","DIESE ALZANER)."
 4830 RETURN
 4850 PRINT "SIE HABEN ALLES GENOMMEN, WAS","HIER WAR."
 4870 RETURN
 4890 PRINT "ICH SEHE KEINE LAMPE...?"
 4910 RETURN
 4930 PRINT "DAS KINO IST FUER EINEN PRIVA-  ";"TEN ZWECK GEMIETET !";:PAUSE 10
 4950 RETURN
 4970 RETURN
 4990 REM STANDORT BESCHREIBUNGEN
 5010 REM STANDORT 1 BEWIRKT
 5030 REM SPRUNG ZUR ZEILE 8010
 5050 PRINT "  +++ WILLKOMMEN IN ALZAN +++":PRINT:FOR I=1 TO 90:BEEP:NEXT        
 5070 PRINT "SIE MUESSEN DIE MAUERN UEBERWIN-";"DEN, UM AUS DIESER STADT VON",
 5090 PRINT "DIEBEN UND HALSABSCHNEIDERN ZU","    E N T K O M M E N !!":PAUSE 90
 5110 WINDOW 3,16,0,31:CLS:  GOSUB 8370
 5130 FOR I=1 TO 3 :PRINT AT(31-(5*RND(1)),30*RND(1)+1);CHR$(14)
 5150 PRINT AT(31-(5*RND(1)),30*RND(1)+1);CHR$(23)
 5170 FOR M=0 TO 49:PRINT AT(31-(5*RND(1)),30*RND(1)+1);" ":NEXT M,I:WINDOW 3,16,0,31
 5190 RETURN
 5210 PRINT "SIE SIND IN DER HAUPTSTRASSE","VOR EINEM KURZWARENLADEN. DIE"
 5230 PRINT "HAUPTSTRASSE VERLAEUFT IN OST-","WEST-RICHTUNG. EINE KLEINE"      
 5250 PRINT "GASSE FUEHRT NEBEN DEM LADEN","NACH NORDEN.";
 5270 RETURN
 5290 PRINT "SIE SIND IM LADEN. DER LADEN-   ";"BESITZER SIEHT VERSCHLAGEN AUS."
 5310 PRINT "ABER ER HAT VIELE SCHOENE WAREN ";"IM SCHAUFENSTER."              
 5330 RETURN
 5350 PRINT "SIE SIND IN EINER SCHMALEN GAS-","SE HINTER GROSSEN HAEUSERN."
 5370 PRINT "UNTER EINER FEUERLEITER STEHEN","VIELE VOLLE MUELLTONNEN." 
 5390 RETURN
 5410 PRINT "SIE SIND AUF DER FEUERLEITER.","SIE FUEHRT ZU EINER TUER IN DER"
 5430 PRINT "HAEUSERN." 
 5450 RETURN
 5470 PRINT "SIE SIND UEBER EINE VERSTECKTE","TREPPE IN DEN LADEN GELANGT." 
 5490 RETURN
 5510 PRINT "SIE BEFINDEN SICH AUF SCHLEICH-","WEGEN ZWISCHEN DEN HAEUSERN." 
 5530 RETURN
 5550 PRINT "JETZT SIND SIE AN DER STADT-","MAUER. HIER BEFINDET SICH EINE"
 5570 PRINT "UNBENUTZTE TUER IN DER MAUER." 
 5590 RETURN
 5610 PRINT "SIE SIND AN EINER KREUZUNG.   " 
 5630 RETURN
 5650 PRINT "SIE SIND AN DER STADTMAUER. DER","NEBEL IST SO DICK, DASS SIE"
 5670 PRINT "NICHT WEIT SEHEN KOENNEN." 
 5690 RETURN
 5710 PRINT "SIE STUERZEN VON DER MAUER -","GENAU AUF DIE SEE-KLIPPEN 100"
 5730 PRINT "METER TIEF UNTEN. MACHT NICHTS.","VIELLEICHT HABEN SIE NAECHSTES"
 5750 PRINT "MAL MEHR GLUECK!"
 5770 RETURN
 5790 PRINT "SIE SIND VOR DER STADTBANK.   "  
 5810 RETURN
 5830 PRINT "IN DER BANK BEFINDEN SICH VIELE","WAECHTER DIE ZIEMLICH GELANG-" 
 5850 PRINT "WEILT DREINSCHAUEN." 
 5870 RETURN
 5890 PRINT "SIE SIND IN EINER SACKGASSE","ANGELANGT, DOCH DA BEFINDET"
 5910 PRINT "SICH EIN KANALDECKEL IN DER","STRASSE..." 
 5930 RETURN
 5950 PRINT "SIE SIND IN EINER KLEINEN   ","NISCHE UNTERHALB(DER EINSTIEGS-",
 5970 PRINT "OEFFNUNG. EIN GANG FUEHRT NACH","SUEDEN." 
 5990 RETURN
 6010 PRINT "DER GANG FUEHRT ZU EINER ALTEN","GRABSTAETTE, IN DER VIELE SKE-",
 6030 PRINT "LETTE VERSTREUT HERUMLIEGEN." 
 6050 RETURN
 6070 PRINT "DER PLATZANWEISER WILL SIE  ","NICHT HINEINLASSEN, DA DAS PRO-",
 6090 PRINT "GRAMM BEREITS ANGEFANGEN HAT.","ER VERSPERRT IHNEN DEN WEG MIT"
 6110 PRINT "SEINER TASCHENLAMPE." 
 6130 RETURN
 6150 PRINT "SIE SIND VOR DEM KINO. DER  ","LAERM VON SCHUESSEN DRINGT AUS"
 6170 PRINT "DEM INNEREN." 
 6190 RETURN
 6210 CLS:BEEP:PRINT "**** HERZLICHEN GLUECKWUNSCH ****":PRINT:BEEP
 6230 PRINT "SIE KONNTEN AUS DER STADT","FLIEHEN. DAS IST WIRKLICH EIN"
 6250 PRINT "SELTENER FALL. GUT GEMACHT."
 6270 RETURN
 6290 RETURN
 6310 END
 6330 PAPER 1:INK 3:CLS:WINDOW 0,19,0,31
 6350 RESTORE 6370
 6370 DATA "EIN BRENNENDE LAMPE"
 6390 DATA "EINE LAMPE"
 6410 DATA "EINE LEITER"
 6430 DATA "EINEN HAMMER"
 6450 DATA "EINEN HAMMER"
 6470 DATA "EIN BUENDEL BANKNOTEN"
 6490 DATA "KANALDECKEL"
 6510 DATA "EIN SACK NAEGEL"
 6530 DATA "EINE EINTRITTSKARTE"
 6550 DATA "EINE UNGEHOBELTE LEITER"
 6570 DATA "ETWAS HOLZ"
 6590 DATA "01N   ","01NORD","02O   ","02OSTE","03S   ","03SUED"
 6610 DATA "04W   ","04WEST","05H   ","05HINA","06T   ","06HINU"
 6630 DATA "13NEHM","14STEL","14WERF","15HINE","14LEGE","16RAUS"
 6650 DATA "32TASC","16VERL","32LAMP","18LEIT","19HAMM","34BANK"
 6670 DATA "20GELD","22SACK","22NAEG","23KART","05DURC","15REIN"
 6690 DATA "29OEFF","05HOCH","30BAST","30BAUE","31ZUEN","35HABE"
 6710 DATA "17KAUF","33HOLZ","34KLAU","34RAUB","35TRAG","36AUFH","37SCHA"
 6730 DATA "00"
 6750 DATA "01040209041800"
 6770 DATA "00"
 6790 DATA "0302050500"
 6810 DATA "0604050700"
 6830 DATA "00"
 6850 DATA "0108031000"
 6870 DATA "030700"
 6890 DATA "011202100314040200"
 6910 DATA "040900"
 6930 DATA "00"
 6950 DATA "0209041800"
 6970 DATA "00"
 6990 DATA "010900"
 7010 DATA "031600"
 7030 DATA "011500"
 7050 DATA "00"
 7070 DATA "0112020200"
 7090 DATA "00"
 7110 DATA "A01.K02O."
 7130 DATA "A16H30.G0121."
 7150 DATA "G01.D01Q."
 7170 DATA "B06H10.D02J06."
 7190 DATA "A14E07.D07N."
 7210 DATA "A14F07.D08N."
 7230 DATA "A11.Q."
 7250 DATA "A19.Q."
 7270 DATA "A06.K03O."
 7290 DATA ".N."
 7310 DATA "1332B01.B01E03L."
 7330 DATA "1332A17C01C02.I02B02D03K18E10O."
 7350 DATA "1718B03.D05N."
 7370 DATA "1319B05.B05L."
 7390 DATA "1320B06.B06L."
 7410 DATA "2900A14E07.D04N."
 7430 DATA "2900A14.E07M."
 7450 DATA "1322B08.B08L."
 7470 DATA "1323B09.B09L."
 7490 DATA "1432B01.C01F03L."
 7510 DATA "1432B02.C02L."
 7530 DATA "1419B05.C05L."
 7550 DATA "1420B06.C06L."
 7570 DATA "1422B08.C08L."
 7590 DATA "1423B09.C09L."
 7610 DATA "0500A10C10.D10M."
 7630 DATA "0500A08C10.D10M."
 7650 DATA "0500A10.K11O."
 7670 DATA "0500A08.K19O."
 7690 DATA "0500A15.F02K14O."
 7710 DATA "0600A14.E02K15O."
 7730 DATA "3100D02.H01E03L."
 7750 DATA "3100B01.D11N."
 7770 DATA "1719B04B06.H04J06B05L."
 7790 DATA "1719B04B09.H04D06B05M."
 7810 DATA "3000B05B11B08.D06I10J08J11M."
 7830 DATA "1333B11.B11L."
 7850 DATA "1M3B11.C11L."
 7870 DATA "1500A02.K03O."
 7890 DATA "1500A12.K13O."
 7910 DATA "1500A18F10.K17O."
 7930 DATA "1600A03.K02O."
 7950 DATA "1600A13.K12O."
 7970 DATA "1600A17.K18O."
 7990 DATA "1500A05.K06O."
 8010 DATA "3400A03.D09M."
 8030 DATA "3400A13E08.D14M."
 8050 DATA "3400A13.E08D13I06B06M."
 8070 DATA "1500A18E10.D16M."
 8090 DATA "1318B10.B10L."
 8110 DATA "1418B10.C10L."
 8130 DATA "1300B03.D09M."
 8150 DATA "1332B02.B02L."
 8170 DATA "3500.A."
 8190 DATA "3600.P."
 8210 DATA "3700.O."
 8230 DATA "5000.N."
 8250 FOR N=1 TO 11:READ O$(N):NEXT N
 8270 FOR N=1 TO 43:READ V$(N):NEXT N
 8290 FOR N=1 TO 19:READ M$(N):NEXT N
 8310 FOR N=1 TO 10:READ C$(N):NEXT N
 8330 FOR N=1 TO 47:READ A$(N):NEXT N
 8350 RETURN
 8370 WINDOW 25,30,1,30:FOR I=0 TO 31:PRINT AT(24,I);"":PRINT AT(31,I);"":NEXT    
 8390 FOR Y=24 TO 31:PRINT AT(Y,0);"":PRINT AT(Y,31);"":NEXT Y:CLS:RETURN
 8410 FOR I=0 TO 30:PRINT AT(17,I);"":PRINT AT(22,I);"":NEXT         
 8430 FOR I=18 TO 22:PRINT AT(I,0);"":PRINT AT(I,31);"":NEXT  
 8450 FOR I=0 TO 1:PRINT AT(15,0);"":PRINT AT(15,30);""     
 8470 PRINT AT(15,1);STRING$(29," "):PAUSE 3 :PRINT AT(15,0);""   
 8490 PRINT AT(15,30);"":PRINT AT(15,6);"Was machen Sie nun ?":PAUSE 5:NEXT:I=0 
 8510 RETURN       
 8530 WINDOW 2,21,0,31:CLS:FOR I=0 TO 31:PRINT AT(3,I);"":PRINT AT(22,I);"" 
 8550 NEXT   
 8570 FOR I=3 TO 22:PRINT AT(I,0);"":PRINT AT(I,31);"":NEXT   
 8590 PRINT AT(5,5);"H E L P - M E N U E":FOR I=5 TO 23
 8610 PRINT AT(6,I);"":NEXT 
 8630 WINDOW 8,21,2,15:PRINT "Hinein":PRINT "Hinaus":PRINT "Hinunter":PRINT "Rein"
 8650 PRINT "Raus":PRINT "Durch":PRINT "Hoch":PRINT "Hinueber":PRINT "Nehme"
 8670 PRINT "Werfe":PRINT "Lege":PRINT "Bastle":WINDOW 8,21,12,30:PRINT "Stelle"
 8690 PRINT "Baue":PRINT "Zuende":PRINT "Habe":PRINT "Kaufe":PRINT "Klaue"   
 8710 PRINT "Raube":PRINT "Schaffe":PRINT "Trage":PRINT "Aufheben"
 8730 WINDOW 8,21,22,30:PRINT "NORD":PRINT "WEST":PRINT "SUED":PRINT "OST"
 8750 PRINT AT(19,25);">ENT<"                
 8770 I$=INKEY$:IF I$=""THEN PAUSE 5:PRINT AT(19,25);"<ENT>":PAUSE 5:GOTO 8750
 8790 WINDOW 3,22,0,31:CLS:RETURN
