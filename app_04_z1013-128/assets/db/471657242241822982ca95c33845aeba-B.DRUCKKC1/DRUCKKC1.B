Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10 CLS:CLEAR:PRINT AT(5,5);"Laden, Saven, Drucken"
   20 PRINT AT(7,5);"von BASIC-Textfeldern"
   30 PRINT AT(9,5);"bekannter Feldlaenge"
   35 PRINT AT(11,5);"(fuer den KC 85/1)" 
   36 PRINT AT(13,8);"(mit V24A3)"
   38 GOSUB 1230
   40 PRINT AT(15,1);"(C) Dr.Trog,H-J;Berlin,Febr.89":GOSUB 1330
   50 GOSUB 1230:CLS:VO=0
   60 PRINT "Feld ansehen   = 1":PRINT
   70 PRINT "Feld saven     = 2":PRINT
   80 PRINT "Feld laden     = 3":PRINT
   90 PRINT "Feld drucken   = 4":PRINT
  100 PRINT "Beenden        = 5":PRINT
  110 I$=INKEY$:P=VAL(I$)
  120 ON P GOTO 150,240,320,370,140
  130 IF P<1 OR P>5 THEN 110
  140 WINDOW: CLS:END
  150 GOSUB 1230:IF I>0 THEN I=1
  155 IF NN=0 THEN 50
  160 I=1:X=0:CLS:FOR I=1 TO NN:PRINT CHR$(A(I));:IF A(I)=13 THEN X=X+1
  170 IF X=15 THEN X=0:GOSUB 1330:ELSE 190
  190 IF I=NN THEN GOSUB 1330:ELSE 210
  210 NEXT I
  220 GOSUB 1330
  230 GOTO 50
  240 GOSUB 1230:CLS:IF NN=0 THEN 50
  245 GOSUB 490
  250 PRINT "Feldlaenge merken:";NN:PRINT:GOSUB 1330
  280 CSAVE*""+D$;A
  290 PRINT "(M)enue  (N)eu":PRINT
  291 I$=INKEY$:IF I$=""THEN 291
  292 IF I$="M"THEN 50
  293 IF I$="N"THEN 10
  300 GOTO 291
  320 CLEAR:GOSUB 1230:CLS:GOSUB 490
  330 INPUT "Feldlaenge?";NN:DIM A(NN):PRINT:PAUSE 5
  335 PRINT "Band ab !":PRINT
  340 GOSUB 1330:CLS:PRINT "laden von ";D$:PRINT
  350 CLOAD*""+D$;A
  360 GOTO 50
  370 XX=72:ST=1:ZV=0:GOSUB 1230:GOTO 500
  380 GOSUB 1200:X=0:FOR I=ST TO NN:PRINT CHR$(A(I));
  381 IF VO=1 THEN 390
  382 IF A(I)=13 THEN X=X+1
  390 IF A(I)=12 THEN X=XX:ZV=1
  400 IF X=XX THEN 410:ELSE 420
  410 IF NN-I<80 THEN 430:ELSE X=0:GOSUB 1200:GOTO 570
  420 NEXT I
  430 GOSUB 1200:I=0:CLS:PRINT:PRINT "DATEIENDE !":GOSUB 1330
  440 GOTO 500
  490 INPUT "Dateiname:";D$:RETURN
  500 CLS
  510 PRINT "Drucker neu    = N":PRINT:PRINT
  520 PRINT "        alt    = O":PRINT:PRINT
  530 PRINT "Beenden        = E":PRINT:PRINT
  540 PRINT "Neues Feld lad.= F":PRINT:PRINT
  550 PRINT "Feld kopieren  = K":PRINT
  560 PRINT:GOTO 580
  570 CLS:PRINT:PRINT "neue Seite ! -> S"
  580 I$=INKEY$:IF I$=""THEN 580
  590 IF I$="N"THEN 660
  600 IF I$="E"THEN WINDOW:CLS:GOSUB 1210:CLEAR:END
  610 IF I$="K"THEN 700
  620 IF I$="F"THEN 740
  630 IF I$="S"THEN 705
  640 IF I$="O"THEN 790
  650 GOTO 580
  660 PRINT
  670 INPUT "L.Rand:";L:GOSUB 1200:PRINT CHR$(27)"l"CHR$(L):GOSUB 1200
  680 INPUT "Zeichen/Zeile:";R:GOSUB 1200:PRINT CHR$(27)"Q"CHR$(L+R)
  690 PRINT CHR$(27)"N"CHR$(7):GOSUB 1200:GOTO 790
  700 GOSUB 1200:PRINT CHR$(12):GOSUB 1200:GOTO 790
  705 IF ZV=1 THEN 706:ELSE 710 
  706 ZV=0:GOTO 715
  710 GOSUB 1200:PRINT CHR$(12):GOSUB 1200
  715 PRINT "Druck >D<"
  740 I$=INKEY$:IF I$=""THEN 740
  750 IF I$="D"THEN GOSUB 1000:GOSUB 1200:GOTO 420
  770 IF I$="F"THEN GOSUB 1200:PRINT CHR$(12):GOSUB 1200:GOTO 50
  780 IF I$<>"F"OR I$<>"D"THEN 740
  790 GOSUB 900:GOSUB 1400:GOSUB 1090:GOSUB 1250
  795 PRINT "Druck START     = D"
  800 I$=INKEY$:IF I$=""THEN 800
  810 IF I$<>"D"THEN 800:ELSE 380
  900 PRINT "mit Seitennummer (N)":PAUSE 5
  910 I$=INKEY$:IF I$=""THEN 910
  920 IF I$<>"J"THEN N=0:RETURN
  930 INPUT "Seitennummer =";N
  945 IF N=0 THEN RETURN
  950 GOSUB 1200:PRINT CHR$(27)"f" "0";
  960 PRINT N:PRINT CHR$(10):GOSUB 1200
  970 RETURN
 1000 IF N=0 THEN RETURN
 1020 N=N+1:GOSUB 945
 1030 RETURN
 1090 IF VO=1 THEN RETURN
 1100 PRINT "Zeilenzahl aendern?(72) (N)":PAUSE 5
 1110 I$=INKEY$:IF I$=""THEN 1110
 1120 IF I$<>"J"THEN RETURN
 1130 INPUT "Zeilenzahl =";XX
 1140 RETURN
 1150 END
 1200 PRINT CHR$(16):RETURN
 1210 PRINT CHR$(16):PRINT CHR$(12):PRINT CHR$(27);CHR$(64):PRINT CHR$(16)
 1220 RETURN
 1230 WINDOW 0,4,0,39:PRINT "  Druck 89   Version KC 85/1"
 1231 PRINT:PRINT STRING$(38,"*")
 1235 WINDOW 5,23,0,39:RETURN
 1250 SE=1:PRINT:PRINT "Ab welcher Dateiseite":INPUT "drucken? Nr./(1): ";SE
 1260 IF SE>1 THEN 1270:ELSE ST=1:RETURN
 1270 X=0:X=XX*(SE-1):Y=0:ST=0:PRINT:PRINT "*** Moment bitte ***":PRINT
 1280 FOR I=1 TO NN:ST=ST+1
 1290 IF A(I)=13 THEN Y=Y+1
 1300 IF Y=X THEN 1320
 1310 NEXT
 1320 ST=ST+1:RETURN
 1330 I$=" ":PRINT AT(22,20);"ENTER"
 1340 I$=INKEY$:IF I$=""THEN 1340
 1350 RETURN
 1400 PRINT:PRINT "mit Festlegung der Zeilenzahl? N/(J)":PAUSE 5: VO=0
 1405 GOSUB 1340
 1410 IF I$="N"THEN 1420:ELSE RETURN
 1420 VO=1:RETURN
