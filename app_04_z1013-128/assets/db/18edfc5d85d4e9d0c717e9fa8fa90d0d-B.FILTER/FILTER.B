Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10 CLS
   20 PRINT "Dieses Programm simuliert":PRINT
   30 PRINT "den Frequenzgang passiver":PRINT        
   40 PRINT "R-C-L Netzwerke":PRINT
   50 PRINT:PRINT
   60 DEF FN LG(L)=LN(L)/LN(10)
   70 EP=2*PI
   80 INPUT "Anzahl der Elemente:";NR
   90 PRINT
  100 INPUT "Quellwiderstand (OHM):";SS   
  110 PRINT
  120 INPUT "Lastwiderstand (OHM):";RR 
  130 N=NR:EX=N+3
  140 DIM FG(EX),A$(EX),WR(EX)
  150 DIM WL(EX),WC(EX),D(30)
  160 PRINT:PRINT
  170 PRINT "Element-Anordnung und Art:":PRINT
  180 PRINT "L->Laengszweig":PRINT
  190 PRINT "Q->Querzweig":PRINT
  200 PRINT "R-L-C-K(Schwingkreis)":PRINT
  210 FOR N=1 TO NR
  220 PRINT N;" ";
  230 INPUT A$(N)
  240 NEXT N
  250 PRINT:PRINT
  260 PRINT "Werte der Elemente:":PRINT
  270 FOR N=1 TO NR
  280 PRINT N;" ";
  290 IF A$(N)="LR" THEN INPUT "Laengswiderstand  (OHM):";WR(N):FG(N)=1
  300 IF A$(N)="LL" THEN INPUT "Laengspule (H):";WL(N):FG(N)=2
  310 IF A$(N)="LC" THEN INPUT "Laengskondensator (F):";WC(N):FG(N)=3
  320 IF A$(N)="LK" THEN INPUT "Laengskreis(RP,L,C):";WR(N),WL(N),WC(N):FG(N)=4
  330 IF A$(N)="QR" THEN INPUT "Querwiderstand (OHM):";WR(N):FG(N)=5
  340 IF A$(N)="QL" THEN INPUT "Querspule (H):";WL(N):FG(N)=6
  350 IF A$(N)="QC" THEN INPUT "Querkondensator (F):";WC(N):FG(N)=7
  360 IF A$(N)="QK" THEN INPUT "Querkreis(RV,L,C):";WR(N),WL(N),WC(N):FG(N)=8
  370 NEXT N
  380 PRINT:PRINT
  390 INPUT "Startfrequenz:";F 
  400 PRINT
  410 INPUT "Schrittweite (Hz):";H
  420 CLS
  430 PRINT "Vierpolrechnung laeuft!":PRINT
  440 FOR Z=1 TO 30
  450 FF=F*EP    
  460 C1=1:C3=0:C5=0:C7=1:C2=0:C4=0      
  470 C6=0:C8=0
  480 FOR N=NR TO 1 STEP -1
  490 GOSUB 740
  500 A1=1:A3=0:A5=0:A7=1:A2=0
  510 A4=0:A6=0:A8=0
  520 ON FG(N) GOSUB 770,780,790,800,850,860,870,880
  530 GOSUB 650
  540 NEXT N
  550 GOSUB 740
  560 GOSUB 940
  570 PRINT "F=";F;"Hz","D=";D(Z);"dB"              
  580 IF D(Z)>MAX THEN MAX=D(Z)
  590 PRINT
  600 F=F+H
  610 IF Z=15 THEN INPUT "weiter->ENT.";Y       
  620 NEXT Z
  630 INPUT "graph.Ausgabe->ENT";Y
  640 GOTO 1120
  650 C1=A1*B1-A2*B2+A3*B5-A4*B6
  660 C2=A1*B2+A2*B1+A3*B6+A4*B5
  670 C3=A1*B3-A2*B4+A3*B7-A4*B8
  680 C4=A1*B4+A2*B3+A3*B8+A4*B7
  690 C5=A5*B1-A6*B2+A7*B5-A8*B6
  700 C6=A5*B2+A6*B1+A7*B6+A8*B5
  710 C7=A5*B3-A6*B4+A7*B7-A8*B8
  720 C8=A5*B4+A6*B3+A7*B8+A8*B7
  730 RETURN
  740 B1=C1:B2=C2:B3=C3:B4=C4
  750 B5=C5:B6=C6:B7=C7:B8=C8
  760 RETURN
  770 A3=WR(N):RETURN
  780 A4=FF*WL(N):RETURN
  790 A4=-1/(FF*WC(N)):RETURN
  800 G=1/WR(N):BL=-1/(FF*WL(N))
  810 BC=FF*WC(N)
  820 B=BC+BL:DR=G*G+B*B
  830 A3=G/DR:A4=-B/DR
  840 RETURN
  850 A5=1/WR(N):RETURN
  860 A6=-1/(FF*WL(N)):RETURN
  870 A6=FF*WC(N):RETURN
  880 R=WR(N):XL=FF*WL(N)
  890 XC=-1/(FF*WC(N)):X=XL+XC
  900 DR=R*R+X*X:A5=R/DR
  910 A6=-X/DR:RETURN
  920 PP=MM*NN-MI*NI
  930 PL= MM*NI+MI*NN:RETURN 
  940 W=SQR(RR/SS):MM=B1
  950 MI=B2:NN=W:NI=0:GOSUB 920
  960 V1=PP:I1=PL:W=1/SQR(SS*RR)
  970 MM=B3:MI=B4:NN=W
  980 GOSUB 920
  990 V2=PP:I2=PL
 1000 W=1/W:MM=B5:MI=B6:NN=W
 1010 GOSUB 920
 1020 V3=PP:I3=PL:W=SQR(SS/RR)
 1030 MM=B7:MI=B8:NN=W
 1040 GOSUB 920
 1050 V4=PP:I4=PL
 1060 MM=0.5:MI=0 
 1070 NN=V1+V2+V3+V4
 1080 NI=I1+I2+I3+I4
 1090 GOSUB 920
 1100 D(Z)=20*FN LG(SQR(PP*PP+PL*PL))
 1110 RETURN
 1120 CLS
 1130 FOR I=1 TO 30
 1140 D(I)=INT(D(I)/MAX*224)             
 1150 G=INT(D(I)/8)
 1160 R=D(I)/8-G
 1170 FOR J=31 TO 31-G STEP -1 
 1180 PRINT AT(J,I);CHR$(255)  
 1190 NEXT J
 1200 R=R*10
 1210 IF R>8.75 THEN Z$=CHR$(254):GOTO 1290
 1220 IF R>7.5 AND R<=8.75 THEN Z$=CHR$(253):GOTO 1290
 1230 IF R>6.25 AND R<=7.5 THEN Z$=CHR$(252):GOTO 1290
 1240 IF R>5 AND R<=6.25 THEN Z$=CHR$(251):GOTO 1290
 1250 IF R>3.75 AND R<=5 THEN Z$=CHR$(250):GOTO 1290
 1260 IF R>2.5 AND R<=3.75 THEN Z$=CHR$(249):GOTO 1290
 1270 IF R>1.25 AND R<=2.5 THEN Z$=CHR$(248):GOTO 1290
 1280 GOTO 1300
 1290 PRINT AT(30-G,I);Z$   
 1300 NEXT I
 1310 INPUT "weiter->ENT.";Y
 1320 CLS
 1330 PRINT
 1340 PRINT
 1350 PRINT
 1360 PRINT "neuer Freqenzbereich->(F)":PRINT
 1370 PRINT "Aenderung der BE-Werte->(E)"
 1380 PRINT
 1390 PRINT "neues Netzwerk->(N)"
 1400 PRINT
 1410 INPUT Y$
 1420 IF Y$="F" THEN CLS:GOTO 380
 1430 IF Y$="E" THEN CLS:GOTO 270
 1440 IF Y$="N" THEN CLEAR:GOTO 10
 1450 GOTO 1410
 1460 END
