<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="de" xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_02.jpg" alt="Screenshot 2" height="384"  >
<img src="screenshot_03.jpg" alt="Screenshot 3" height="384"  >
<img src="screenshot_04.jpg" alt="Screenshot 4" height="384"  >
</div>
<div class="filelist">2bc0 3ecb 0300 B ... <a href="adressen.bas.z80">ADRESSEN        </a></div>
<div class="text"><pre>1 CLEAR 10000:A1$="MIKRORECHNER robotron Z1013"
2 A2$="ADRESSENVERZEICHNIS"
16 REM INITIALWERTE =========IN
18 WINDOW:CLS:GOSUB 195
20 IZ=1:DIM C$(101,7),D$(7):LS$=STRING$(11," ")
22 C$(101,0)="NAME":C$(101,1)="VORNAME":C$(101,2)="STRASSE"
24 C$(101,3)="WOHNORT":C$(101,4)="PLZ    ":C$(101,5)="TELEFON"
26 C$(101,6)="BETRIEB":C$(101,7)="SONST."
90 GOSUB 100:GOTO 1000
96 REM UNTERPROGRAMME =======UP
98 REM TITELBILD ------------UP
100 PRINT AT(3,2);A1$:PRINT AT(2,0);"",STRING$(29,""),""
102 PRINT AT(3,0);"":PRINT AT(3,30);""
104 PRINT AT(4,0);"",STRING$(29,""),""
108 PRINT AT(8,0);STRING$(31,"")
110 FOR I=9 TO 13:PRINT AT(I,0);"":PRINT AT(I,30);"":NEXT I
112 PRINT AT(14,0);STRING$(31,"")
114 PRINT AT(11,25-LEN(A2$));A2$
118 PRINT AT(20,1);"H.-J. BACHMANN * TAURUSWEG 2":PRINT AT(22,8);"LEIPZIG 7063
119 REM &gt;ENTER&lt; ---------------------UP
120 PRINT AT(26,22);"&gt;ENTER&lt;":GOSUB 195
122 T$=INKEY$:IF T$=""THEN 122
124 IF T$=CHR$(13)THEN RETURN
126 GOSUB 130:GOTO 122
129 REM WIE BITTE ? -----------------UP
130 PRINT AT(24,15);"WIE BITTE ?":RETURN
139 REM KENNZIFFER ------------------UP
140 PRINT AT(25,15);"KENNZIFFER :"
141 T$=INKEY$:IF T$=""THEN 141
142 PRINT AT(25,28);T$
144 IF ASC(T$)&lt;48+KA OR ASC(T$)&gt;48+KE THEN GOSUB 130:GOTO 141
146 T=ASC(T$)-48:RETURN
149 REM (J)/N ABFRAGE ---------------UP
150 PRINT AT(24,(25-LEN(KY$)));KY$,":(J)/N":PRINT AT(25,25);LS$
151 T$=INKEY$:IF T$=""THEN 151
154 IF ASC(T$)=13 OR ASC(T$)=74 THEN T=1:RETURN
156 IF ASC(T$)=78 THEN T=2:RETURN
158 GOSUB 130:PAUSE 10:GOTO 150
159 REM J/(N) ABFRAGE ---------------UP
160 PRINT AT(24,(25-LEN(KY$)));KY$,":J/(N)":PRINT AT(22,15);LS$
161 T$=INKEY$:IF T$=""THEN 161
164 IF ASC(T$)=13 OR ASC(T$)=78 THEN T=2:RETURN
166 IF ASC(T$)=74 THEN T=1:RETURN
168 GOSUB 130:PAUSE 10:GOTO 160
169 REM VERZEICHNIS -----------------UP
170 PRINT AT(6,2);C$(101,0):PRINT AT(8,2);C$(101,1)
172 PRINT AT(10,2);C$(101,2):PRINT AT(12,2);C$(101,3)
174 PRINT AT(14,2);C$(101,4):PRINT AT(16,2);C$(101,5)
176 PRINT AT(18,2);C$(101,6):PRINT AT(20,2);C$(101,7):RETURN
179 REM SORTIEREN -------------------UP
180 CLS:GOSUB 195:PRINT AT(24,18);"BITTE WARTEN":FOR I=1 TO IZ-1:FOR J=1 TO I
181 IF C$(I+1,0)&gt;C$(J,0)THEN 186
182 IF C$(I+1,0)=C$(J,0)THEN IF C$(I+1,1)&gt;=C$(J,1)THEN 186
183 FOR L=0 TO 7:D1$(L)=C$(I+1,L):NEXT L
184 FOR K=I+1 TO J STEP-1:FOR L=0 TO 7:C$(K,L)=C$(K-1,L):NEXT L:NEXT K
185 FOR L=0 TO 7:C$(J,L)=D1$(L):NEXT L:J=I
186 NEXT J:NEXT I:PRINT AT(10,10);"            ":RETURN
189 REM KORRIGIEREN -----------------UP
190 WINDOW 6,24,10,31:FOR J=0 TO 7:INPUT C$(I1,J):PRINT:NEXT J:RETURN
194 REM CURSOR BESEITIGEN -----------UP
195 POKE DEEK(43),32:RETURN
999 REM HAUPTPROGRAMM ===============HP
1000 WINDOW:CLS:GOSUB 195
1005 PRINT AT(2,0);A2$:PRINT AT(3,0);STRING$(LEN(A2$),"")
1010 PRINT AT(7,0);"1 = DATEN EINGEBEN"
1020 PRINT AT(9,0);"2 = DATEN ABSPEICHERN "
1030 PRINT AT(11,0);"3 = DATEN EINLESEN"
1040 PRINT AT(13,0);"4 = VERZEICHNIS AENDERN"
1050 PRINT AT(15,0);"5 = DATEN LISTEN"
1051 PRINT AT(17,0);"6 = DATENLISTE EINSEHEN"
1052 PRINT AT(19,0);"7 = PROGRAMMENDE"
1060 KA=1:KE=7:GOSUB 140:CLS:GOSUB 195
1070 ON T GOTO 2000,3000,4000,5000,6000,7000,8000
1999 REM DATEN EINGEBEN ------------HP1
2000 PRINT AT(1,0);"DATEN EINGEBEN  --&gt;  MOMENT!"
2002 PRINT AT(2,0);STRING$(14,""):PRINT AT(1,15);"               "
2005 PRINT AT(1,17);"    SAETZE FREI"
2010 GOSUB 170
2020 WINDOW 6,24,10,31:CLS
2030 FOR I=IZ TO 100:CLS:C$(I,0)="ZZ":PRINT AT(1,16);(101-I):INPUT C$(I,0):PRINT
2032 FOR J=1 TO 7:C$(I,J)=" ":NEXT J
2040 IF C$(I,0)="ZZ"THEN IZ=I:I=100:NEXT I:GOSUB 180:GOTO 1000
2045 FOR J=1 TO 7:INPUT C$(I,J):PRINT
2050 NEXT J:NEXT I
2060 IZ=I:WINDOW:CLS:PRINT AT(4,0);"DATENSPEICHER UEBERFUELLT!"
2070 PAUSE 30:GOSUB 180:GOSUB 120:GOTO 1000
2999 REM DATEN ABSPEICHERN ---------HP2
3000 PRINT AT(2,2);"DATEN ABSPEICHERN":PRINT AT(3,2);STRING$(17,"")
3010 PRINT AT(5,0);"KASSETTE-AUFNAHME STARTEN"
3020 PRINT AT(7,0);"UND &gt;ENTER&lt; BETAETIGEN!":GOSUB 120
3030 WINDOW 10,23,2,31:CLS:C$(0,0)=STR$(IZ):CSAVE*"DATEN";C$
3040 GOSUB 120:WINDOW:GOTO 1000
3999 REM DATEN EINLESEN ------------HP3
4000 PRINT AT(2,2 );"DATEN EINLESEN":PRINT AT(3,2);STRING$(14,"")
4010 PRINT AT(5,2);"START TAPE !"
4020 PRINT AT(7,2);"UND BEI VORTON &gt;ENTER&lt; !":GOSUB 120
4030 WINDOW 10,23,2,31:CLS:CLOAD*"DATEN";C$:IZ=VAL(C$(0,0))
4040 GOSUB 120:WINDOW:GOTO 1000
4999 REM VERZEICHNIS AENDERN -------HP4
5000 PRINT AT(2,0);"VERZEICHNIS AENDERN":PRINT AT(3,0);STRING$(19,"")
5002 GOSUB 170:WINDOW 6,24,0,31:FOR I=0 TO 7:INPUT " ";C$(101,I):PRINT
5004 IF LEN(C$(101,I))&gt;7 THEN C$(101,I)=LEFT$(C$(101,I),7)
5006 NEXT I
5010 GOSUB 120:GOTO 1000
5999 REM DATEN AUSGEBEN ------------HP5
6000 PRINT AT(1,2);"DATEN AUSGEBEN":PRINT AT(2,2);STRING$(14,"")
6002 IF IZ=1 THEN 7120
6004 PRINT AT(5,2);"WAS MOECHTEN SIE WISSEN ?"
6006 PRINT STRING$(7,CHR$(10));"  ";
6008 CI$=" ":INPUT CI$:I1=1
6010 FOR I=I1 TO IZ-1:FOR J=0 TO 7
6012 IF INSTR(CI$,C$(I,J))&lt;&gt;0 THEN I1=I:I=IZ-1:J=7:NEXT J:NEXT I:GOTO 6020
6014 NEXT J:NEXT I
6015 WINDOW:CLS
6016 PRINT AT(8,0);"KEINE INFORMATION VORHANDEN !"
6018 GOSUB 120:GOTO 1000
6020 CLS:PRINT AT(1,2);"DATEN-AUSGABE"
6022 GOSUB 170
6024 WINDOW 6,24,10,31:CLS
6026 FOR I=0 TO 7:PRINT ": ";C$(I1,I):PRINT:NEXT I:GOSUB 195
6028 KY$="WEITER SUCHEN":GOSUB 150:IF T=1 THEN I1=I1+1:CLS:GOTO 6010
6029 KY$="  KORRIGIEREN":GOSUB 150:IF T=1 THEN GOSUB 190:GOSUB 180:GOTO 1000
6030 KY$="     LOESCHEN":GOSUB 160:IF T=2 THEN 1000
6032 C$(I1,0)="ZZ":FOR I=1 TO 7:C$(I1,I)=" ":NEXT I:GOSUB 180:IZ=IZ-1:GOTO 1000
6999 REM DATENLISTE EINSEHEN -------HP6
7000 I1=1
7020 CLS:PRINT AT(1,2);"DATENLISTE":PRINT AT(2,2);STRING$(10,"")
7025 IF IZ=1 THEN 7120
7030 PRINT AT(3,2);C$(101,0):PRINT AT(3,20);C$(101,1)
7040 PRINT STRING$(3,CHR$(10))
7050 FOR I=I1 TO I1+7:PRINT:PRINT "  ";
7070 PRINT C$(I,0);TAB(20)C$(I,1)
7075 IF I=IZ-1 THEN I=I1+7:NEXT I:WINDOW:GOSUB 120:GOTO 1000
7080 NEXT I
7090 KY$="FORTSETZEN":GOSUB 150
7100 IF T=2 THEN 1000
7110 I1=I1+8:GOTO 7020
7120 PRINT AT(3,0);"BITTE ERST DATEN EINGEBEN"
7130 PRINT AT(5,0);"ODER VON KASSETTE EINLESEN!"
7140 KY$="DATEN EINLESEN":GOSUB 150:IF T=2 THEN 1000
7150 CLS:GOTO 4000
7999 REM PROGRAMM BEENDEN ----------HP7
8000 PRINT AT(5,2);"ENDE DES PROGRAMMES":PRINT AT(6,2);STRING$(19,"")
8010 PRINT AT(8,2);"BITTE VERGESSEN SIE NICHT"
8020 PRINT AT(10,0);"DIE NEUEN DATEN ABZUSPEICHERN!"
8030 KY$="DATEN ABSPEICHERN?":GOSUB 150:CLS
8040 IF T=1 THEN GOSUB 195:GOTO 3000
8050 END
</pre>
</div>
</body>
</html>
