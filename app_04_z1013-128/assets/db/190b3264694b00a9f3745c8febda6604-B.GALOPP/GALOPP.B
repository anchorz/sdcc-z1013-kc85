Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

    2 CLEAR
    5 WINDOW
   10 CLS
   11 FOR I=4 TO 27:PRINT AT(6,I);"":PAUSE 1:NEXT I
   12 FOR I=7 TO 12:PRINT AT(I,27);"":PAUSE 1:NEXT I
   13 FOR I=27 TO 4 STEP-1:PRINT AT(13,I);"":PAUSE 1:NEXT I
   14 FOR I=12 TO 7 STEP-1:PRINT AT(I,4);"":PAUSE 1:NEXT I
   30 PAUSE 5:PRINT AT(8,10);"G A L O P P":PAUSE 5
   40 PRINT AT(11,7);"R E N N S P O R T":PAUSE 10
   50 PRINT AT(20,3);"WIR GEHEN NACH HOPPEGARTEN"
   55 PRINT AT(22,5);"UND WOLLEN BEIM WETTEN"
   60 PRINT AT(24,4);"UNSER GLUECK VERSUCHEN !":PAUSE 5
   62 PRINT AT(28,15);CHR$(25)," ",CHR$(16)
   65 PAUSE 70:CLS:Q$="":GOSUB 1400
   70 PRINT:PRINT "GEBEN SIE BITTE IHREN NAMEN UND"
   80 PRINT "DAS IHNEN ZUR VERFUEGUNG"
   90 PRINT "STEHENDE KAPITAL AN. DIE WETTE"
   95 PRINT "IST EINE SIEG-WETTE UND"
  100 PRINT "WIRD WIE FOLGT ABGESCHLOSSEN:":PRINT
  110 PRINT "WETTE ==> <pferd-nr.,einsatz>":PRINT
  120 PRINT "DER EINSATZ PRO WETTE KANN"
  130 PRINT "ZWISCHEN 1 UND 9999 LIEGEN."
  140 PRINT:PRINT "UND NUN VIEL SPASS !     "
  150 PRINT:GOSUB 1400
  160 PAUSE 100
  170 WINDOW:CLS
  180 Q$="":Q=15:GOSUB 1500
  190 WINDOW 16,31,0,31:CLS
  200 INPUT "WIEVIEL MANN WOLLEN MITWETTEN?";A
  210 IF A>8 THEN PRINT:PRINT "BITTE NICHT MEHR ALS 8 MANN.":PRINT:PAUSE 10:GOTO 190
  220 PRINT:PRINT "DARF ICH UM IHRE NAMEN BITTEN !":PRINT:PAUSE 5
  230 FOR I=1 TO A:PRINT "WETTSPIELER";I;": ";:INPUT "";N$(I):PAUSE 5:NEXT I
  240 CLS:PRINT "WIEVIEL GELD SETZEN SIE EIN ?":PRINT:PAUSE 5
  250 FOR I=1 TO A:PRINT N$(I);TAB(15);":";:INPUT "";W1(I):PAUSE 5:NEXT I
  260 RESTORE 1090:FOR I=1 TO 8:READ P$(I):NEXT I
  270 CLS:WINDOW 16,31,0,18 
  280 PRINT "PFERD   NR.  KURS|":PRINT STRING$(18,"-")
  290 E=0:T=1:R=0:FOR I=1 TO 8:B(I)=INT(10*RND(1)+1):R=R+B(I):NEXT I
  300 FOR I=1 TO 8:PRINT P$(I);TAB(8);I;TAB(10);INT(R/B(I));TAB(15);":1|":NEXT I
  310 WINDOW 16,31,19,31:PRINT "DIE WETTEN:":PRINT STRING$(12,"-")
  320 FOR I=1 TO A
  330 IF W1(I)=0 THEN E=E+1:W(I)=0:GOTO 380
  340 PAUSE 5:PRINT LEFT$(N$(I),2);".:";:INPUT "";T(I),W(I)
  345 IF T(I)<1 OR T(I)>8 THEN 1200
  350 IF W(I)<1 OR W(I)>9999 THEN 960
  360 IF W(I)>W1(I)THEN 990
  370 W1(I)=W1(I)-W(I)
  380 PAUSE 5:NEXT I
  390 IF E=A THEN 1040
  400 WINDOW 0,14,0,31:CLS
  405 PRINT AT(13,9);"GALOPPRENNBAHN":PAUSE 5
  406 Q$="":Q=0:GOSUB 1500:Q$="":Q=9:GOSUB 1500
  410 FOR I=0 TO 9:FOR J=1 TO 31 STEP 29:PRINT AT(I,J);"":NEXT J:NEXT I
  430 FOR I=1 TO 8:P(I)=-4832-(32*I):P1(I)=P(I)+31:POKE P(I),48+I:NEXT I
  440 FOR I=1 TO 500:NEXT I:BEEP
  450 FOR I=1 TO 8
  460 X(I)=I:V(X(I))=INT(100*RND(1)+1):IF V(X(I))<10 THEN V(X(I))=1:GOTO 530
  470 X=INT(R/B(I)+.5):IF V(X(I))<X+17 THEN V(X(I))=2:GOTO 530
  480 IF V(X(I))<X+37 THEN V(X(I))=3:GOTO 530
  490 IF V(X(I))<X+57 THEN V(X(I))=4:GOTO 530
  500 IF V(X(I))<X+77 THEN V(X(I))=5:GOTO 530
  510 IF V(X(I))<X+92 THEN V(X(I))=6:GOTO 530
  520 V(X(I))=7
  530 NEXT I
  540 FOR I=1 TO 8
  550 P(X(I))=P(X(I))+V(X(I)):IF P(X(I))<P1(X(I))THEN 620
  560 IF P1(X(I))<>P(X(I))-V(X(I))THEN POKE(P(X(I))-V(X(I))),32
  570 P(X(I))=P1(X(I))
  580 FOR J=1 TO T
  590 IF E(J)=P(X(I))THEN 620
  600 NEXT J
  610 E(T)=P(X(I)):T=T+1:BEEP
  620 NEXT I
  630 FOR I=1 TO 8
  640 FOR J=1 TO 8-I
  650 IF V(X(J))<V(X(J+1))THEN 670
  660 K=X(J):X(J)=X(J+1):X(J+1)=K
  670 NEXT J
  680 NEXT I
  690 FOR I=1 TO 8
  700 IF P(X(I))<P1(X(I))THEN POKE(P(X(I))-V(X(I))),32
  710 POKE P(X(I)),48+X(I)
  720 NEXT I
  730 IF T<9 THEN 450
  740 WINDOW 16,31,0,31:CLS:WINDOW 16,31,0,17
  750 PRINT "HIER DER EINLAUF:":PRINT STRING$(17,"-")
  760 FOR I=1 TO 8
  770 T=ABS(E(I)+4801)/32:PRINT I;P$(T);TAB(11);INT(R/B(T));TAB(15);":1"
  780 NEXT I
  790 WINDOW 16,31,18,31:PRINT "GEWONNEN HAT:":PRINT STRING$(13,"-"):PRINT
  800 FOR I=1 TO A
  810 IF T(I)<>ABS(E(1)+4801)/32 OR W(I)=0 THEN 830
  820 PRINT LEFT$(N$(I),2);".:";INT(R/B(T(I)))*W(I)
  821 W1(I)=W1(I)+INT(R/B(T(I)))*W(I)
  830 NEXT I
  840 WINDOW 0,14,0,31:CLS
  850 PRINT "IHR VERRMOEGEN BETRAEGT z.Zt.:"
  860 PRINT:FOR I=1 TO A:PRINT TAB(2);N$(I);TAB(14);":";W1(I):NEXT I
  870 PAUSE 100:CLS
  880 RESTORE 1110:FOR I=1 TO 5:READ B$:PRINT:PRINT TAB((31-LEN(B$))/2);B$;:NEXT I
  890 PRINT:PRINT:INPUT " ";X$
  900 IF LEFT$(X$,1)="N"OR LEFT$(X$,1)="n"THEN 940
  910 IF LEFT$(X$,1)="J"OR LEFT$(X$,1)="j"THEN 930
  920 GOTO 890
  930 FOR I=1 TO 8:E(I)=0:NEXT I:WINDOW 16,31,0,31:CLS:WINDOW 0,14,0,31:GOTO 270
  940 WINDOW:CLS:PRINT:PRINT "NA DANN SPAREN SIE FUER DAS"
  950 PRINT:PRINT "NAECHSTE MAL. AUF WIEDERSEHEN."
  955 Q$="":Q=7:GOSUB 1500:PRINT AT(9,15);CHR$(25)," ",CHR$(16)
  956 PRINT:PRINT:PRINT:PRINT:PRINT:END
  960 GOSUB 1030
  970 PRINT AT(2,3);"IHNEN NUR ZUR ERINNERUNG !"
  975 PRINT AT(3,3);STRING$(26,"")
  980 PRINT AT(6,0);"DER EINSATZ LIEGT VON 1 - 9999.":GOTO 340
  990 GOSUB 1030
 1000 PRINT AT(1,3);"ES TUT MIR SEHR LEID, SIE"
 1005 PRINT AT(3,2);"DARAUF HINWEISEN ZU MUESSEN,"
 1010 PRINT AT(5,9);"DASS NUR NOCH"
 1015 PRINT AT(7,11);W1(I)," MARK"
 1020 PRINT AT(9,0);"AUF IHREM KONTO ZU BUCHE STEHEN.":GOTO 340
 1030 FOR J=0 TO 9:PRINT AT(J,0);STRING$(32," "):NEXT J:RETURN
 1040 GOSUB 1030
 1050 PRINT AT(1,0);"SIE HABEN ALLE ALLES VERSPIELT."
 1060 PRINT AT(3,1);"WOLLEN SIE ES WIEDERGEWINNEN ?"
 1070 PRINT AT(5,0);"DANN GEHEN SIE ANS EINGEMACHTE"
 1080 PRINT AT(7,2);"UND FANGEN SIE VON VORNE AN.":PAUSE 50:GOTO 170
 1090 DATA "AGATHE","MEPHISTO","LANDLUFT","OEDIPUS","JESSICA"
 1100 DATA "HANNIBAL","MAINELKE","ALADIN"
 1110 DATA "DAS NEUE RENNEN BEGINNT GLEICH.",""
 1120 DATA "WOLLEN SIE WIEDER MITMACHEN ?",""
 1130 DATA "Ja oder Nein (J/N) ? "
 1200 GOSUB 1030:PRINT AT(2,4);"SIE SOLLEN EINGEBEN :" 
 1205 PRINT AT(3,4);STRING$(21,"")
 1210 PRINT AT(6,4);"nr.des pferdes,einsatz"
 1215 PRINT AT(8,9);"z.B.:  3,100 " 
 1220 GOTO 340
 1400 FOR M=0 TO 31:PRINT Q$;:NEXT M:RETURN
 1500 FOR M=0 TO 31:PRINT AT(Q,M);Q$:NEXT M:RETURN
