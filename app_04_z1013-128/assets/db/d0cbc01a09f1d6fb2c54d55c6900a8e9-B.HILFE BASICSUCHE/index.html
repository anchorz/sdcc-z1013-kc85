<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="de" xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_01.jpg" alt="Screenshot 1" height="384"  >
</div>
<div class="filelist">2bc0 3119 0302 B ... <a href="B.HILFE%20BASICSUCHE.z80">HILFE BASICSUCHE</a></div>
<div class="text">wird nach dem Programm ab Zeile 30000 geladen. 
Start: GOTO 30000
<pre>Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

30000 CLS:PRINT TAB(12);"HILFE":PRINT:ZM=0:GOTO 30038
30001!- Zeilenende erreicht -
30002 ZM=0:ZI=ZI+2:IF ZA$="" THEN NEXT:END
30003 PRINT ZC;ZA$:NEXT:RETURN
30004!--- FOR ---
30005 GOSUB 30021:ZA$=ZA$+"&gt;FOR "+ZB$:NEXT:END
30006!--- NEXT ---
30007 ZA$=ZA$+"&gt;NEXT ":GOSUB 30021
30008 IF ZB$&gt;""THEN ZA$=ZA$+ZB$
30009 NEXT:END
30010!--- GOTO ---
30011 GOSUB 30033:ZA$=ZA$+" GOTO "+ZB$:NEXT:RETURN 
30012!--- GOSUB ---
30013 GOSUB 30033:ZA$=ZA$+" GOSUB "+ZB$:NEXT:RETURN
30014!--- THEN ---
30015 GOSUB 30033:IF ZF=0 THEN NEXT:END
30016 ZA$=ZA$+" THEN "+ZB$:NEXT:END
30017!--- ELSE ---
30018 GOSUB 30033:IF ZF=0 THEN NEXT:END
30019 ZA$=ZA$+" ELSE "+ZB$:NEXT:END
30020!- Variablensuche -
30021 ZI=ZI+1:ZA=PEEK(ZI):ZB$="":IF ZA=0 THEN ZI=ZI-1:RETURN
30022 IF ZA=32 GOTO 30021
30023 IF ZA&lt;65 OR ZA&gt;90 THEN RETURN
30024 ZB$=ZB$+CHR$(ZA)
30025 ZI=ZI+1:ZA=PEEK(ZI)
30026 IF ZA=40 THEN ZB$=ZB$+"(":RETURN
30027 IF ZA=36 THEN ZB$=ZB$+"$":GOTO 30025
30028 IF(ZA&gt;64 AND ZA&lt;91)OR(ZA&gt;47 AND ZA&lt;58)THEN ZB$=ZB$+CHR$(ZA):GOTO 30025
30029 ZI=ZI-1:RETURN
30030!- Zeilennummer holen -
30031 ZC=DEEK(ZI):ZA$="":ZI=ZI+2:ZM=1:RETURN
30032!- Zahlensuche -
30033 ZB$="":ZF=0:FOR ZJ=0 TO 1 STEP 0:ZI=ZI+1:ZA=PEEK(ZI) 
30034 IF ZA=32 THEN NEXT
30035 IF ZA&lt;48 OR ZA&gt;57 THEN ZJ=1:ZI=ZI-1:NEXT:RETURN
30036 ZF=1:ZB$=ZB$+CHR$(ZA):NEXT:ZI=ZI-1:RETURN
30037!--- Main ---
30038 FOR ZI=11267 TO DEEK(11243)
30039 IF ZM=0 THEN GOSUB 30031:! Neue Zeile
30040 IF ZC&gt;29999 THEN END
30041 ZZ=PEEK(ZI):ZM=1
30042 IF ZZ=0 THEN 30002:!Zlende
30043 IF ZZ=129 THEN 30005:! FOR
30044 IF ZZ=130 THEN 30007:!NEXT
30045 IF ZZ=136 THEN 30011:!GOTO
30046 IF ZZ=140 THEN 30013:!GOSUB
30047 IF ZZ=169 THEN 30015:!THEN
30048 IF ZZ=212 THEN 30018:!ELSE
30049 NEXT
</pre>
</div>
</body>
</html>
