Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10!*************************
   20!* ANDREAS ZIERMANN      *
   30!* 4372 AKEN             *
   40!* DESSAUER LANDSTR.25/2 *
   50!*************************
   60!*VERBESSERT VON D.AMBRAS*
   70!*4073 HALLE/S.          *
   80!*HEIMSTAETTENWEG 63     *
   90!*************************
  100:
  110 JA=1400:WINDOW:CLS:A$=STRING$(128,""):FOR I=0 TO 7:PRINT AT(I*4,0);A$:NEXT
  120 FOR M=0 TO 7:READ TB$(M):NEXT
  130 DATA SIR,BARON,COUNT,MARQUIS,DUKE,GRAND DUKE,PRINCE,KING
  140 DATA SANTA PARAVIA,FIBONACCIO,TORRICELLA,MOLINETTO,FONTA,MARINETTO
  150 WINDOW 1,5,1,30:CLS:WINDOW 2,5,2,29:PRINT "Wieviele Personen wollen
  160 PRINT:PRINT "spielen (1-6) ?":WINDOW 4,4,18,26
  170 GOSUB 3960:SA=VAL(A$):IF SA<1 OR SA>6 THEN 170
  180 WINDOW 7,15,1,30:CLS:WINDOW 8,15,2,29
  190 FOR SP=1 TO SA:READ T$:CLS
  200 PRINT "Wer ist der Herrscher in"
  210 PRINT T$:INPUT ">";N$:PRINT:N$=LEFT$(N$,8)
  220 PRINT "Wie soll '"T$ "'":PRINT "genannt werden ?":U$="":INPUT ">";U$
  230 U$=LEFT$(U$,8)
  240 N$(SP)=" "+N$+" of "+U$:IF U$=""THEN N$(SP)=N$(SP)+T$
  250 S1(SP)=25:S2(SP)=10:S3(SP)=5:JU(SP)=2
  260 GD(SP)=1000:LA(SP)=10000:KO(SP)=5000
  270 LS(SP)=100:VO(SP)=2000
  280 AD(SP)=4:SO(SP)=30:GE(SP)=5:KA(SP)=25
  290 NEXT
  300 WINDOW 17,30,1,30:CLS:WINDOW 18,30,2,29:PRINT "Spielstaerke :"
  310 PRINT:PRINT "1.Anfaenger     3.Meister"
  320 PRINT:PRINT "2.mit Erfahrung 4.Koenner":PRINT:PRINT "5.-8.Wahnsinn":PRINT
  330 PRINT:PRINT "Spielstaerke :";:WINDOW 27,27,18,20
  340 GOSUB 3960:SS=VAL(A$):IF SS=0 OR A$=""OR SS>8 THEN 340
  350 SS=SS+4:TJ(0)=1:N$(0)=" PEPPONE of MONTERANE"
  360 FOR SP=1 TO SA:SU(SP)=INT(1413+SS*1.5+RND(1)*26+RND(1)*10):NEXT
  370 FOR SP=1 TO SA:IF SU(SP)<JA THEN 450
  380 IF SU(SP)=JA THEN 460
  390 IF VO(SP)<200 THEN SU(SP)=JA:GOTO 460
  400 WINDOW 1,30,1,30:CLS:GOSUB 880
  410 GOSUB 1290
  420 GOSUB 1850
  430 GOSUB 2410
  440 GOSUB 3010
  450 NEXT:JA=JA+1:GOTO 370
  460 WINDOW:CLS:PRINT:IF VO(SP)>2500 THEN 500
  470 PRINT "Hurra !":PRINT:PRINT TB$(TJ(SP));N$(SP)
  490 PRINT:PRINT "wurde ermordet.":PRINT:GOTO 520
  500 PRINT "Wir trauern um":PRINT:PRINT TB$(TJ(SP));NA$(SP):PRINT
  520 PRINT " ( geb. 1381, gest."JA ")"
  530 H$="Sie":IF TJ(SP)<8 THEN H$="Er"
  550 PRINT:PRINT:PRINT H$;" starb":PRINT
  570 IF VO(SP)>200 THEN 610
  580 PRINT "nach langjaehriger Tyrannei":PRINT
  600 PRINT "durch ein Attentat !":GOTO 750
  610 IF JA<1449 THEN 650
  620 PRINT "in hohem Alter nach einer":PRINT
  640 PRINT "langen Herrschaft.":GOTO 750
  650 ON(RND(1)*5)GOTO 690,700,710,740,750
  660 PRINT "waehrend eines eisigen":PRINT
  680 PRINT "Winters an einer Grippe.":GOTO 750
  690 PRINT "waehrend einer Pockenepedemie.":GOTO 750
  700 PRINT "durch verseuchtes Wasser":PRINT:PRINT "an Typhus.":GOTO 750
  710 PRINT "nach einem Raubueberfall":PRINT
  730 PRINT "waehrend einer Reise.":GOTO 750
  740 PRINT "durch vergiftetes Fleisch."
  750 Q$=INKEY$:IF Q$=""THEN 750
  760 CLS:GOSUB 3300:WINDOW:CLS
  770 VS=VS+1:IF VS<SA THEN 450
  780 PRINT "Das Spiel ist zu Ende,":PRINT
  800 PRINT "da alle Teilnehmer der Tod":PRINT:PRINT "ereilt hat."
  830 PRINT:PRINT:PRINT "Ich habe euch alle ueberlebt."
  860 PRINT:PRINT:PRINT "    ";TB$(1);N$(0)
  870 Q$=INKEY$:IF Q$<>""THEN END:ELSE 870
  880 Z=RND(1)*30:KO(SP)=INT(KO(SP)*(100-Z)/100):RV=INT(Z)
  890 WE=(INT(RND(1)*5)+INT(RND(1)*6)+2)/2
  900 H=LA(SP):M=(VO(SP)-100*MU(SP))*5:IF M<0 THEN M=0
  910 IF M<H THEN H=M
  920 M=KO(SP):IF M<H THEN H=M
  930 ER=H*(WE-.5):KO(SP)=KO(SP)+ER
  940 GOSUB 3180:FL=0
  950 IF ER<1 THEN Z=2:GOTO 980
  960 Z=VE*1/ER:IF Z>2 THEN Z=2
  970 IF Z<.8 THEN Z=.8
  980 LP=INT((3*WE+INT(RND(1)*6)+INT(RND(1)*6)+12)*Z)
  990 KP=INT((20-WE*3+INT(RND(1)*5)+INT(RND(1)*5))*4*Z)
 1000 WINDOW 2,30,2,29:PRINT "Man schreibt das Jahr"JA;CHR$(8)",";
 1010 PRINT B$(TJ(SP))N$(SP)".":PRINT AT(5,0);STRING$(32,""):WINDOW 7,30,2,29
 1020 IF FL>0 THEN 1060:FL=1
 1030 PRINT "Die Ratten frassen";RV;"%.":PRINT
 1040 ON WE GOSUB 3190,3200,3210,3220,3230
 1050 PRINT "  (";ER;"Sters)":PRINT AT(12,0);STRING$(32,""):WINDOW 14,30,2,30
 1060 PRINT "Kornreserven : ";INT(KO(SP));"Sters
 1070 PRINT "Kornverlangen: ";INT(VE);"Sters
 1080 PRINT "Kornpreis    : "KP/10 "(100St)
 1090 PRINT "Landpreis    : ";LP*.1;"(1 ha)":GD(SP)=INT(GD(SP)+.5)
 1100 PRINT "Goldreserven : ";GD(SP);"Florin  ":PRINT AT(20,0);STRING$(32,"")
 1110 WINDOW 22,29,2,29:PRINT "1  Kornkauf   2  Kornverkauf";
 1120 PRINT "3  Landkauf   4  Landverkauf":PRINT AT(25,0);STRING$(32,"")
 1130 WINDOW 27,30,2,29:CLS
 1140 PRINT "Was machen Sie ?"
 1150 KZ$=INKEY$:IF KZ$=""THEN 1150
 1160 IN=VAL(KZ$):IF IN<0 OR IN>4 THEN 1140
 1170 PRINT
 1180 IF IN>0 THEN ON IN GOSUB 3720,3750,3790,3820:GOTO 1000
 1190 CLS:PRINT "Wieviel Sters Korn geben Sie";
 1200 INPUT "ihren Untertanen ?";GG
 1210 FL=FL+1:IF FL=2 AND GG=0 THEN 1000
 1220 IF GG<KO(SP)*.2 THEN GOSUB 1250:GOTO 1190
 1230 IF GG>KO(SP)*.8 THEN GOSUB 1270:GOTO 1190
 1240 KO(SP)=KO(SP)-GG:RETURN
 1250 CLS:PRINT "Sie muessen mindestens 20%  des ";
 1260 PRINT "Korns verteilen !":PAUSE 40:RETURN
 1270 CLS:PRINT "Sie muessen mindestens 20%  des ";
 1280 PRINT "Korns als Notreserve    behalten !":PAUSE 40:RETURN
 1290 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
 1300 IF VE>GG THEN 1560
 1310 GR=6:SR=2:GOSUB 3860
 1320 IF S1(SP)+S2(SP)<35 THEN KA(SP)=INT(KA(SP)+1+RND(1)*4)
 1330 IF S3(SP)<RND(1)*20+1 THEN GOSUB 1840
 1340 IF VE*1.3>GG THEN 1400
 1350 H=VO(SP)/1000:Z=10*(GG-VE)/GG
 1360 H=Z*H*(1+RND(1)*25)+1+RND(1)*40:H=RND(1)*H+2:VO(SP)=INT(VO(SP)+H)
 1370 PRINT INT(H)"Menschen sind":PRINT "zugewandert."
 1380 H=RND(1)*(H/5)+1:IF H>50 THEN H=50
 1390 KA(SP)=KA(SP)+H:AD(SP)=AD(SP)+1:GE(SP)=GE(SP)+3
 1400 H=MA(SP)*75:IF H>0 THEN GD(SP)=GD(SP)+H
 1410 PRINT "Ihr Markt brachte ";H:PRINT "Florin ";
 1420 PRINT "an Zoll ein.":PRINT
 1430 H=MU(SP)*INT(60+RND(1)*460):IF H>0 THEN GD(SP)=GD(SP)+H
 1440 PRINT "Ihre Muehle machte einen    Gewinn von";
 1450 PRINT H;"Florin.":PRINT
 1460 H=SO(SP)*3:GD(SP)=GD(SP)-H:PRINT "Sie zahlten ihren Soldaten
 1470 PRINT CHR$(8)H "Florin als Sold."
 1480 PRINT
 1490 IF LA(SP)/320-20>SO(SP)THEN 1590
 1500 IF LA(SP)/250-20>SO(SP)THEN 1530
 1510 FOR M=1 TO SA:IF M<>SP AND SO(SP)<SO(M)/1.4 THEN 1590
 1520 NEXT
 1530 PRINT " "
 1540 I$=INKEY$:IF I$<>""THEN RETURN
 1550 GOTO 1540
 1560 H=(VE-GG)*100/VE-9:IF H>65 THEN H=65
 1570 IF H<0 THEN H=0
 1580 GR=2:SR=H+7:GOSUB 3860:GOTO 1400
 1590 REM
 1600 AG=0
 1610 FOR M=1 TO SA:IF SP=M THEN 1630
 1620 IF SO(M)>SO(SP)AND SO(M)>LA(M)/400 AND SO(M)=>SO(AG)THEN AG=M
 1630 NEXT:IF AG>0 THEN LV=(SO(AF)*200+LA(SP)/4)*(0.3+RND(1)*0.4):GOTO 1650
 1640 LV=1000*RND(1)*9000:IF LA(SP)>30000 THEN LV=LV+RND(1)*(LA(SP)-300005)
 1650 IF LV>LA(SP)-5000 THEN LV=LA(SP)/2-2500
 1660 IF LV<2 THEN 1530
 1670 LA(AG)=LA(AG)+LV:LA(SP)=LA(SP)-LV
 1680 H=RND(1)*40+2:IF H>SO(SP)-15 THEN H=SO(SP)-15
 1690 SO(SP)=SO(SP)-H
 1700 PRINT
 1710 PRINT "ALARM  !!!!!"
 1720 PRINT
 1730 PRINT TB$(TJ(AG));N$(AG):PRINT "greift an. Die Schlacht ging"
 1740 PAUSE 30
 1750 PRINT "leider verloren ..."
 1760 PRINT
 1770 PRINT "Es gab ";LV;" Hektar Land und"
 1780 PRINT
 1790 PRINT "       ";INT(H);"Soldaten Verlust
 1800 PRINT
 1810 PRINT "Erhoehen Sie ihre"
 1820 PRINT "Verteidigungsfaehigkeit !!!"
 1830 FOR M=1 TO 2000:NEXT:GOTO 1530
 1840 AD(SP)=INT(AD(SP)+RND(1)*2):GE(SP)=INT(GE(SP)+RND(1)*3):RETURN
 1850 OD=TJ(SP)/8-INT(TJ(SP)/8):OD=OD*8:WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
 1860 SN=(JU(SP)*300-500)*(OD+1)
 1870 Z=(150-S1(SP)-S2(SP)-S3(SP))/100:IF Z<1^E-2 THEN Z=1^E-2
 1880 A1=INT(((AD(SP)*100+GE(SP)*75+KA(SP)*20)*Z+LS(SP))*S1(SP)/100)
 1890 A2=INT(((AD(SP)*50+KA(SP)*25+LS(SP)*0.1)*Z*(5-JU(SP)))*S2(SP)/200)
 1900 A3=INT((AD(SP)*250+LS(SP)/5+10*JU(SP)*Z)*S3(SP)/100)
 1910 PRINT TB$(TJ(SP));N$(SP)
 1920 PRINT
 1930 PRINT "Es werden";A1+A2+A3+SN;"Florin an"
 1940 PRINT "Steuern erhoben.";
 1950 PRINT AT(7,0);STRING$(32,""): WINDOW 9,29,2,29
 1960 PRINT "Konsumsteuer  (";S1(SP);"%):";A1
 1970 PRINT
 1980 PRINT "Handelssteuer (";S2(SP);"%):";A2
 1990 PRINT
 2000 PRINT "Einkommensteuer(";S3(SP);"%):";A3
 2010 PRINT
 2020 ON JU(SP)GOSUB 3240,3250,3260,3270
 2030 PRINT
 2040 WINDOW 19,29,2,29
 2050 PRINT "Nummer zum Aendern (1-4)"
 2060 WINDOW 25,25,25,25
 2070 GOSUB 3960:I$=A$
 2080 IN=VAL(I$):IF IN>4 THEN 2070
 2090 WINDOW 25,29,2,29
 2100 IF IN>0 THEN ON IN GOTO 2270,2300,2330,2360
 2110 GD(SP)=GD(SP)+A1+A2+A3+SN
 2120 IF GD(SP)<0 THEN GD(SP)=GD(SP)*1.5
 2130 IF GD(SP)+2000+OD*1500=>0 THEN RETURN
 2140 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
 2150 PRINT TB$(TJ(SP));N$(SP);" !!!"
 2160 PRINT
 2170 PRINT "Durch ihre totale Misswirt-"
 2180 PRINT "schaft sind Sie jetzt bank- rott !!!"
 2190 PRINT
 2200 PRINT "Ihr Vermoegen wurde ent-    eignet"
 2210 PRINT "und zur Schuldtilgung       versteigert !!!"
 2220 MA(SP)=0:MU(SP)=O:PT(SP)=0:KT(SP)=0
 2230 GOSUB 3180 IF KO(SP)>2*VE THEN KO(SP)=2*VE
 2240 LA(SP)=6000:GD(SP)=100:LS(SP)=100
 2250 GOSUB 3960
 2260 RETURN
 2270 H=0:CLS:INPUT "Neue Konsumsteuer (0-100)";H
 2280 IF H<0 OR H>100 THEN H=25
 2290 S1(SP)=H:GOTO 1850
 2300 H=0:CLS:INPUT "Neue Handelssteuer (0-50)";H
 2310 IF H<0 OR H>50 THEN H=10
 2320 S2(SP)=H:GOTO 1850
 2330 H=0:CLS:INPUT "Neue Einkommensteuer (0-25)";H
 2340 IF H<0 OR H>25 THEN H=5
 2350 S3(SP)=H:GOTO 1850
 2360 CLS:PRINT "1  sehr fair  2  mild"
 2370 PRINT "3  streng     4  abscheulich";
 2380 INPUT "Welche Justiz praktizieren  Sie ?";H
 2390 IF H<0 OR H>4 THEN 1850
 2400 JU(SP)=H:GOTO 1850
 2410 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
 2420 PRINT "  STAATLICHE INVESTITIONEN"
 2430 PRINT
 2440 PRINT " ";TB$(TJ(SP));N$(SP);
 2450 PRINT AT(6,0);STRING$(32,""):WINDOW 8,29,2,29
 2460 H$="00 Fl"
 2470 PRINT "1  Marktplatz       :10";H$
 2480 PRINT "2  Kornmuehle       :20"H$
 2490 PRINT "3  Palast (Teilbau) :30"H$ 
 2500 PRINT "4  Kirche (Teilbau) :50"H$ 
 2510 PRINT "5  Militaer         : 500 Fl"
 2520 PRINT
 2530 PRINT
 2540 IF GD(SP)<0 THEN 2560
 2550 GOTO 2580
 2560 PRINT "Sie haben bereits ";-GD(SP);"       "
 2570 PRINT "Florin Schulden !!!":GOTO 2600
 2580 PRINT "In der Schatzkammer liegen     "
 2590 PRINT CHR$(8)GD(SP)" Florin.
 2600 PRINT
 2610 PRINT "Weitere Eingaben :"
 2620 PRINT
 2630 PRINT "6  Vergleich der Voelker"
 2640 PRINT "7  Vergleich Besitztuemer"
 2650 WINDOW 29,29,2,29
 2660 PRINT "Ihre Wahl :";:WINDOW 29,29,14,15
 2670 GOSUB 3960:IN=VAL(A$):IF IN>7 THEN 2670
 2680 M=GD(SP)+3500+2800*OD:IF IN=0 THEN RETURN
 2690 WINDOW 24,29,2,29:CLS:ON IN GOSUB 2700,2760,2800,2860,2920,3300,3520:GOTO 2410
 2700 PRINT " Wieviele Marktplaetze
 2710 INPUT " wollen Sie kaufen ?";H
 2720 IF H<1 THEN RETURN
 2730 IF M<H*1000 THEN 2970
 2740 MA(SP)=MA(SP)+H:GD(SP)=GD(SP)-H*1000
 2750 LS(SP)=INT(LS(SP)+H*10):KA(SP)=INT(KA(SP)+H*5):RETURN
 2760 INPUT " Wieviele Muehlen wollen Sie kaufen ?";H
 2770 IF H<1 THEN RETURN
 2780 IF M<H*2000 THEN 2970
 2790 MU(SP)=MU(SP)+H:GD(SP)=GD(SP)-H*2000:LS(SP)=INT(LS(SP)+H*25):RETURN
 2800 PRINT " Um wieviele Teile wollen
 2810 PRINT " Sie den Palast weiter-":INPUT " bauen ?";H
 2820 IF H<1 THEN RETURN
 2830 IF M<H*3000 THEN 2970
 2840 PT(SP)=PT(SP)+H:GD(SP)=GD(SP)-H*3000:LS(SP)=INT(LS(SP)+H*50)
 2850 FOR M=1 TO H:AD(SP)=AD(SP)+1+(INT(RND(1)*2)):NEXT M:RETURN
 2860 PRINT " Um wieviele Teile wollen Sie
 2870 INPUT " die Kirche weiterbauen ?";H
 2880 IF H<1 THEN RETURN
 2890 IF M<H*5000 THEN 2970
 2900 KT(SP)=KT(SP)+H:GD(SP)=GD(SP)-H*5000:LS(SP)=INT(LS(SP)+H*100)
 2910 FOR M=1 TO H:GE(SP)=GE(SP)+1+INT(RND(1)*6):NEXT:RETURN
 2920 PRINT " Wieviele Regimenter wollen
 2930 INPUT " Sie rekrutieren ?";H
 2940 IF H<1 OR H*20>VO(SP) THEN RETURN
 2950 IF M<H*500 THEN 2970
 2960 SO(SP)=SO(SP)+H*20:VO(SP)=VO(SP)-H*20:GD(SP)=GD(SP)-H*500:RETURN
 2970 PRINT
 2980 PRINT " Fuer so hohe Ausgaben       kriegen";
 2990 PRINT " Sie keinen Kredit !!!"
 3000 INPUT I$:IF I$<>"" OR I$="" THEN RETURN
 3010 REM
 3020 SU=0:M=MA(SP):GOSUB 3280
 3030 M=MU(SP):GOSUB 3280:M=PT(SP):GOSUB 3280:M=KT(SP):GOSUB 3280
 3040 M=GD(SP)/1000:GOSUB 3280:M=AD(SP)/5:GOSUB 3280
 3050 M=KA(SP)/50:GOSUB 3280:M=AD(SP)/5:GOSUB 3280
 3060 M=SO(SP)/50:GOSUB 3280:M=GE(SP)/10:GOSUB 3280
 3070 M=VO(SP)/2000:GOSUB 3280:M=LS(SP)/500:GOSUB 3280
 3080 TJ=SU/SS-JU(SP)-1:IF TJ>7 THEN TJ=7
 3090 IF JA+2=SJ(SP) THEN TJ(SP)=TJ(SP)+1
 3100 IF TJ>TJ(SP) AND 7 THEN TJ(SP)=TJ(SP) AND 8 OR TJ
 3110 IF(TJ(SP) AND 7)<7 THEN RETURN
 3120 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29:PRINT:PRINT
 3130 PRINT " Das Spiel ist zu Ende !!!
 3140 PRINT:PRINT
 3150 PRINT "  Es lebe King "N$(SP)"."
 3160 FOR Q9=1 TO 4000:NEXT
 3170 GOSUB 3300:GOSUB 3520:END
 3180 VE=AD(SP)*100+GE(SP)*40+KA(SP)*30+SO(SP)*10+VO(SP)*5:RETURN
 3190 PRINT "Duerre - Hungersnot droht":RETURN
 3200 PRINT "mieses Wetter-miese Ernte":RETURN
 3210 PRINT "normales Wetter-gute Ernte":RETURN
 3220 PRINT "gutes Wetter-schoene Ernte":RETURN
 3230 PRINT "Bombenwetter-grosse Ernte":RETURN
 3240 PRINT "Justiz (sehr fair)   :"SN:RETURN
 3250 PRINT "Justiz (mild)        :"SN:RETURN
 3260 PRINT "Justiz (streng)      :"SN:RETURN
 3270 PRINT "Justiz (abscheulich) :"SN:RETURN
 3280 SU=SU+M:IF M>10 THEN SU=SU-M+10
 3290 RETURN
 3300 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29:PRINT "  VERGLEICH DER VOELKER
 3310 A=0:PRINT " ***********************":PRINT
 3320 FOR M=1 TO SA
 3330 A=A+1
 3340 PRINT CHR$(8)STR$(A)"."TB$(TJ(M));N$(M)
 3350 NEXT
 3360 PRINT
 3370 PRINT "   Land   Gold  Soldaten"
 3380 A=0
 3390 FOR M=1 TO SA
 3400 A=A+1
 3410 PRINT CHR$(8)STR$(A)".";TAB(1)LA(M);TAB(10)GD(M);TAB(17)INT(SO(M))
 3420 NEXT
 3430 PRINT
 3440 PRINT "  Adel Klerus Haendler Volk"
 3450 A=0
 3460 FOR M=1 TO SA
 3470 A=A+1
 3480 PRINT CHR$(8)STR$(A)".";
 3490 PRINT TAB(1)INT(AD(M));TAB(8)GE(M);TAB(15)INT(KA(M));TAB(23)INT(VO(M));
 3500 PRINT CHR$(8)CHR$(8):NEXT
 3510 GOSUB 3960:CLS:RETURN
 3520 WINDOW 2,29,1,30:CLS:WINDOW 2,29,2,29
 3530 PRINT "  EIGENTUM DER HERRSCHAFTS-            HAEUSER" 
 3540 PRINT " *************************** ":A=0
 3550 FOR M=1 TO SA
 3560 A=A+1
 3570 PRINT CHR$(8)STR$(A)".";TB$(TJ(M));N$(M)
 3580 NEXT
 3590 PRINT:PRINT "   Land   Gold  Maerkte
 3600 A=0
 3610 FOR M=1 TO SA
 3620 A=A+1
 3630 PRINT CHR$(8)STR$(A)"."TAB(3)LA(M);TAB(10)GD(M);TAB(17)MA(M)
 3640 NEXT
 3650 PRINT
 3660 PRINT TAB(3)"Muehlen";TAB(11)"Palast";TAB(18)"Kirche"
 3670 A=0
 3680 FOR M=1 TO SA
 3690 A=A+1
 3700 PRINT CHR$(8)STR$(A)"."TAB(4)MU(M);TAB(12)PT(M);TAB(19)KT(M)
 3710 NEXT:GOSUB 3960:CLS:RETURN
 3720 CLS:H=0:INPUT "Wieviel Sters wollen Sie    kaufen ?";H
 3730 IF H<1 THEN RETURN
 3740 GD(SP)=GD(SP)-H*KP/1000:KO(SP)=KO(SP)+H:RETURN
 3750 CLS:H=0:INPUT "Wieviel Sters verkaufen     Sie ?";H
 3760 IF H<1 THEN RETURN
 3770 IF KO(SP)-H<VE(SP)/4 THEN 3750
 3780 GD(SP)=GD(SP)+H*KP/1000:KO(SP)=KO(SP)-H:RETURN
 3790 CLS:H=0:INPUT "Wieviel Land wollen Sie     kaufen ?";H
 3800 IF H<1 THEN RETURN
 3810 GD(SP)=GD(SP)-H*LP/10:LA(SP)=LA(SP)+H:RETURN
 3820 CLS:H=0:INPUT "Wieviel Land verkaufen      Sie ?";H
 3830 IF H<1 THEN RETURN
 3840 IF LA(SP)-H<5000 THEN 3820
 3850 GD(SP)=GD(SP)+H*LP/10:LA(SP)=LA(SP)-H:RETURN
 3860 H=INT((RND(1)*GR+1)*VO(SP)/100+2):VO(SP)=VO(SP)+H
 3870 PRINT INT(H)"Kinder wurden geboren.     "
 3880 H=INT((RND(1)*SR)*VO(SP)/100+2):VO(SP)=VO(SP)-H
 3890 PRINT INT(H)"Menschen starben.
 3900 IF JU(SP)<3 THEN 3950
 3910 H=INT((JU(SP)-2)^2*(2+RND(1))*VO(SP)/180+2):VO(SP)=VO(SP)-H
 3920 PRINT
 3930 PRINT INT(H)"Menschen flohen vor der
 3940 PRINT " Justiz."
 3950 PRINT AT(9,0);STRING$(32,""):WINDOW 11,29,2,29:RETURN
 3960 A$=INKEY$:IF A$=""THEN 3960:ELSE PRINT:PRINT A$;:RETURN
