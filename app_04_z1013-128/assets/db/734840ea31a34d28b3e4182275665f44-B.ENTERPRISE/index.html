<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="de" xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_01.jpg" alt="Screenshot 1" height="384"  >
<img src="screenshot_02.jpg" alt="Screenshot 2" height="384"  >
<img src="screenshot_03.jpg" alt="Screenshot 3" height="384"  >
<img src="screenshot_04.jpg" alt="Screenshot 4" height="384"  >
</div>
<div class="filelist">2bc0 57c6 0000 B ... <a href="B.ENTERPRISE.z80">ENTERPRISE      </a></div>
<div class="text"><pre>Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

    2 WINDOW
    4 CLS
    6 PRINT AT(13,5);"TOENE GEWUENSCHT ?
    8 PRINT AT(16,10);"JA =&gt; J"
   10 PRINT AT(0,0);" "
   12 I$=INKEY$
   14 IF I$=""THEN GOTO 12
   16 IF I$="J"THEN TE=1
   18 IF TE&lt;&gt;1 THEN GOSUB 584
   20 CLS
   22 PRINT AT(15,0);"         M  O  M  E  N  T
   24 PRINT AT(0,0);" " 
   26 IF TE=1 THEN GOSUB 566
   28 GOTO 522
   30 IF PEEK(4)&lt;&gt;13 THEN 30:ELSE RETURN
   32 WINDOW 18,31,0,31:CLS:WINDOW:PRINT AT(0,0);" "
   34 PRINT AT(18,22);STRING$(17," "):RETURN
   36 WINDOW 0,18,0,31:CLS:WINDOW:PRINT AT(0,0);" ":RETURN
   38 REM RAUMSEKTORKARTE ----UP
   40 PRINT AT(0,0);"  RAUMSEKTOR":PRINT AT(0,13);Z$(X),Y$(Y)
   42 FOR I=1 TO 8:PRINT AT(1,I*2-1);Y$(I):PRINT AT(I*2,0);Z$(I):NEXT I
   44 DOKE 24765,F+512*X+64*Y+9:CALL 24764:SB(X,Y)=1
   46 IF C&lt;&gt;7 THEN PRINT AT(Z*2+0,S*2-1);"":RETURN:ELSE RETURN
   48 REM OBJEKTZAEHLUNG ----UP
   50 B1=F+512*X+64*Y+9:DOKE 24728,B1:CALL 24727
   52 U=PEEK(8):W=PEEK(9):V=PEEK(10)
   54 K$(X,Y)=CHR$(U+48-7*(U&gt;9))+CHR$(W+48-7*(W&gt;9))+CHR$(V+48-7*(V&gt;9))
   56 RETURN
   58 REM COMPUTER-REPORT ----UP
   60 WINDOW 1,18,16,31:CLS:WINDOW:PRINT AT(0,0);" "
   62 CALL*5E00
   64 IF AD=1 THEN PRINT AT(10,17);"ANGEDOCKT!":P=8:E=5000
   66 PRINT AT(0,16);"",STRING$(14,""),"":FOR I=1 TO 15:PRINT AT(I,16);""
   68 PRINT AT(I,31);"":NEXT I:PRINT AT(3,16);"",STRING$(14,""),""
   70 PRINT AT(16,16);"",STRING$(14,""),""
   72 PRINT INK 2;AT(1,17);"COMPUTER -":PRINT AT(2,25);"REPORT"
   74 PRINT INK 6;AT(4,27);M:PRINT INK 7;AT(6,17);"ENERGIE"
   76 PRINT INK 6;AT(6,25);E:PRINT INK 7;AT(8,17);"TORPEDOS"
   78 PRINT AT(8,28);P:PRINT AT(12,17);"KLINGONEN"
   80 PRINT AT(12,27);K:PRINT AT(14,17);"BASISRAUMER"
   82 PRINT AT(14,28);R:PRINT AT(4,17);"STERNENTAG":RETURN
   84 REM KLINGONEN-BESCHUSS ---UP
   86 GOSUB 32:PRINT INK 2;AT(21,3);"KLINGONEN-BESCHUSS !!!":BEEP
   88 FOR TT=1 TO 5:CALL*5E00:NEXT TT
   90 IF E&gt;=300*W THEN 100
   92 PAUSE 15:GOSUB 32:PRINT AT(21,3);"SCHUTZSCHILD AUSGEFALLEN !!!"
   94 PRINT AT(23,3);"ENTERPRISE GETROFFEN !!!":FOR I=1 TO W
   96 CALL*5E00
   98 A=INT(6*RND(1)+1):R(A)=R(A)+INT(5*RND(1)+4):NEXT I:GOTO 104
  100 PRINT AT(23,0);"ENERGIESCHILD AUFGEBAUT:",300*W,"ERG"
  102 E=E-300*W
  104 PAUSE 15:GOSUB 32:RETURN
  106 PRINT AT(20,0);"-&gt;  NTRIEB      -&gt;  YPERANTRIEB"
  108 PRINT AT(21,0);"-&gt;  ORPEDO      -&gt;  LASMAWOLKE":C$=INKEY$
  110 PRINT AT(22,0);"-&gt;  MKREISSENS. -&gt;  TERNENKARTE"
  112 PRINT AT(23,0);"-&gt;  AUMSEKTOR   -&gt;  HECK ENT"
  114 PRINT AT(20,3);"A":PRINT AT(20,19);"H":PRINT AT(21,3);"T":PRINT AT(21,19);"P
  116 PRINT AT(22,3);"U":PRINT AT(22,19);"S":PRINT AT(23,3);"R":PRINT AT(23,19);"C
  118 PRINT AT(18,16);"KOMMANDO-EINGABE":C$=INKEY$:IF C$=""THEN C$=" "
  120 C=INSTR(C$,D$):IF C=0 THEN PAUSE 1:PRINT AT(18,16);STRING$(16," "):PAUSE 1
  122 IF C&lt;&gt;0 THEN RETURN:ELSE GOTO 118
  124 GOSUB 266:IF D=5 THEN GOSUB 32:RETURN:ELSE GOSUB 280:IF G=0 THEN RETURN
  126 A=Z+G*(D&lt;4)-G*(D&gt;6):B=S+G*(D=1 OR D=4 OR D=7)-G*(D=3 OR D=6 OR D=9)
  128 PAUSE 1:PRINT AT(2*Z+0,2*S-1);"":Z=Z+SGN(A-Z):S=S+SGN(B-S):E=E-5  
  130 IF E&lt;=0 THEN L=3:RETURN
  132 IF Z&gt;0 AND Z&lt;9 AND S&gt;0 AND S&lt;9 THEN 140
  134 IF Z=0 OR Z=9 THEN X=X+(A&lt;1)-(A&gt;8):Z=Z+8*(Z=9)-8*(Z=0):A=A+8*(A&gt;8)-8*(A&lt;1)
  136 IF S=0 OR S=9 THEN Y=Y+(B&lt;1)-(B&gt;8):S=S+8*(S=9)-8*(S=0):B=B+8*(B&gt;8)-8*(B&lt;1)
  138 IF X=0 OR X=9 OR Y=0 OR Y=9 THEN L=1:RETURN:ELSE GOSUB 36:GOSUB 40:GOSUB 50
  140 H=PEEK(F+512*X+64*Y+8*Z+S):IF H&lt;&gt;166 THEN GOSUB 288:RETURN
  142 PRINT AT(2*Z+0,2*S-1);"":IF A&lt;&gt;Z OR B&lt;&gt;S THEN 128:ELSE GOSUB 32:RETURN
  144 REM HYPERANTRIEB ----UP
  146 GOSUB 266:IF D=5 THEN GOSUB 32:RETURN:ELSE GOSUB 280:IF G=0 THEN RETURN
  148 X=X+G*(D&lt;4)-G*(D&gt;6):Y=Y+G*(D=1 OR D=4 OR D=7)-G*(D=3 OR D=6 OR D=9)
  150 IF X&lt;1 OR X&gt;8 OR Y&lt;1 OR Y&gt;8 THEN L=1:RETURN
  152 E=E-100*G*(1-(D=1 OR D=3 OR D=7 OR D=9)):IF E&lt;0 THEN E=0:L=2:RETURN
  154 GOSUB 32:GOSUB 36:GOSUB 40:GOSUB 50
  156 H=PEEK(F+512*X+64*Y+8*Z+S):IF H&lt;&gt;166 THEN GOSUB 288
  158 RETURN
  160 REM TORPEDO ----UP
  162 GOSUB 32:IF P=0 THEN PRINT AT(22,1);"KEIN TORPEDO AN BORD!":GOTO 182
  164 GOSUB 266:IF D=5 THEN 164:ELSE P=P-1
  166 A=Z:B=S:FOR I=1 TO 7:A=A+(D&lt;4)-(D&gt;6):B=B+(D=1 OR D=4 OR D=7)-(D=3 OR D=6 OR D=9)
  168 IF A=0 OR A=9 OR B=0 OR B=9 THEN I=8:GOTO 180:ELSE PRINT INK 2;AT(2*A+0,2*B-1);""
  170 PAUSE 2:PRINT INK 5;AT(2*A+0,2*B-1);"":H=PEEK(F+512*X+64*Y+8*A+B)
  172 IF H=166 THEN 180:ELSE POKE F+512*X+64*Y+8*A+B,166:I=9
  174 IF H=75 THEN K=K-1:PRINT AT(22,2);"KLINGONE VERNICHTET!!!"
  176 FOR TT=1 TO 3:CALL*5E00:NEXT TT
  178 IF H=66 THEN R=R-1:PRINT AT(22,1);"BASIS ZERSTOERT - AUSGEZEICHNET
  180 NEXT I:IF I=9 THEN PRINT AT(22,2);"VERSCHWENDUNG!
  182 PAUSE 15:GOSUB 50:GOSUB 32:RETURN
  184 GOSUB 32:PRINT AT(21,2);"ENERGIE-MENGE":WINDOW 23,23,2,17:CLS:C$=INKEY$
  186 INPUT "-&gt;";A:IF A&lt;0 OR A&gt;E THEN 186
  188 E=E-A:IF W=0 THEN GOSUB 32:PRINT AT(21,2);"VERSCHWENDUNG!":RETURN
  190 B=0:FOR I=1 TO 8:FOR J=1 TO 8:H=PEEK(F+512*X+64*Y+8*I+J)
  192 IF H=75 THEN B=B+ABS(I-Z)+ABS(J-S)
  194 NEXT J:NEXT I:B=A-100*B:IF B&lt;0 THEN GOSUB 32:PRINT AT(21,2);ABS(B):K=K-1
  196 IF B&lt;0 THEN PRINT AT(21,8);"ERG ZU WENIG !!!":PAUSE 25:GOSUB 32:RETURN
  198 PRINT AT(21,1);"PLASMA VERNICHTET KLINGONEN !!!":FOR I=1 TO 8:FOR J=1 TO 8
  200 H=F+512*X+64*Y+8*I+J:IF PEEK(H)=75 THEN K=K-1:PRINT AT(2*I+0,2*J-1);""
  202 IF PEEK(H)=75 THEN POKE H,166:FOR TT=1 TO 3:CALL*5E00:NEXT TT
  204 NEXT J:NEXT I:GOSUB 32:GOSUB 50:RETURN
  206 FOR I=X-1 TO X+1:FOR J=Y-1 TO Y+1:IF I=0 OR I=9 OR J=0 OR J=9 THEN 212
  208 IF K$(I,J)&lt;&gt;" "THEN 212
  210 O=X:Q=Y:X=I:Y=J:GOSUB 50:X=O:Y=Q
  212 NEXT J:NEXT I:WINDOW 25,31,10,31:CLS:PRINT AT(25,12);"UMKREIS-SENSORENBILD"
  214 O=19:FOR I=X-1 TO X+1:Q=19:FOR J=Y-1 TO Y+1:IF I=0 OR I=9 OR J=0 OR J=9 THEN 220
  216 IF I=X AND J=Y THEN PRINT AT(O+8,Q-6);"*KB":GOTO 220
  218 PRINT AT(O+8,Q-6);K$(I,J)
  220 Q=Q+5:NEXT J:O=O+2:NEXT I:AX$=INKEY$
  222 IF INKEY$=""THEN 222:ELSE GOSUB 32:RETURN
  224 WINDOW:CLS:PRINT AT(0,0);" ":PRINT AT(0,0);"",STRING$(27,""),""
  226 FOR I=1 TO 21:PRINT AT(I,0);CHR$(161):NEXT I:PRINT AT(2,0);"",STRING$(31,"")
  228 PRINT AT(20,0);"",STRING$(30,""),"":PRINT AT(21,31);CHR$(161)
  230 PRINT AT(22,0);"",STRING$(30,""),"":PRINT AT(21,10);"STERNEN-KARTE"
  232 PRINT AT(21,5);"*KB":FOR I=1 TO 8
  234 PRINT AT(1,4*I-2);Y$(I):NEXT I 
  236 FOR I=1 TO 8:FOR J=1 TO 8:IF K$(I,J)=" "THEN PRINT AT(2*I+2,4*J-3);"XXX":GOTO 240
  238 IF X=I AND Y=J THEN PRINT AT(I*2+1,J*4-4);"":PRINT AT(I*2+2,J*4-3);"T"
  240 PRINT AT(2*I+2,4*J-3);K$(I,J):IF X=I AND Y=J THEN PRINT AT(I*2+3,J*4-4);""
  242 NEXT J:NEXT I:PRINT AT(25,22);"&gt;ENTER&lt;":GOSUB 30:CLS:GOSUB 40:RETURN
  244 GOSUB 32:IF C=1 THEN PRINT AT(21,0);"ANTRIEBS-SYSTEME AUSGEFALLEN !!!"
  246 IF C=2 THEN PRINT AT(21,2);"HYPERRAUM-ANTRIEB DEFEKT !!!"
  248 IF C=3 THEN PRINT AT(21,0);"ZIELCOMPUTER ARBEITET NICHT !!!"
  250 IF C=4 THEN PRINT AT(21,2);"PLASMA-ERZEUGER INAKTIV !!!"
  252 IF C=5 THEN PRINT AT(21,0);"UMKREIS-SENSOREN BESCHAEDIGT !!!"
  254 IF C=6 THEN PRINT AT(21,0);"NAVIG.-COMPUTER UNBRAUCHBAR !!!"
  256 IF C=7 THEN PRINT AT(21,2);"DATENSPEICHER GESPERRT !!!"
  258 IF R(C)=1 THEN C$="":ELSE C$="E"
  260 INK 3:PRINT AT(23,2);"REPARATUR-DAUER",R(C),"STERNENTAG",C$:BEEP:PAUSE 25
  262 GOSUB 32:RETURN
  264 REM RICHTUNGSWAHL ----UP
  266 GOSUB 32:PRINT AT(19,22);"Q W E":PRINT INK 2;AT(20,23);""
  268 PRINT AT(21,22);"A   D":PRINT AT(21,23);" ":PRINT AT(21,24);""
  270 PRINT INK 2;AT(22,23);"":PRINT AT(23,22);"Y X C"
  272 C$=INKEY$:PRINT AT(21,3);"RICHTUNGS-EINGABE!"
  274 C$=INKEY$:IF C$=""THEN C$=" " 
  276 D=INSTR(C$,E$):IF D=0 THEN 274:ELSE GOSUB 32:CALL*5E00:RETURN
  278 REM ENTFERNUNGSWAHL ----UP
  280 GOSUB 32:C$=INKEY$:PRINT INK 4;AT(21,3);"ENTFERNUNGS-EINGABE (0...7)!"
  282 C$=INKEY$:IF C$=""THEN C$=" "
  284 G=ASC(C$)-48:IF G&lt;0 OR G&gt;7 THEN 282:ELSE GOSUB 32:CALL*5E00:RETURN
  286 REM KOLLISION ----UP
  288 GOSUB 32:PRINT AT(10,17);"ALARMSTUFE":PRINT AT(21,3);"KOLLISION MIT "  
  290 FOR TT=1 TO 15:CALL*5E00:NEXT TT
  292 IF H=42 THEN PRINT AT(10,28);"1":PRINT AT(21,17);"STERN":A=.3:B=3
  294 IF H=75 THEN PRINT AT(10,28);"2":PRINT AT(21,17);"KLINGONEN":A=.5:B=5:K=K-1
  296 IF H=66 THEN PRINT AT(10,28);"3":PRINT AT(21,17);"BASISRAUMER":A=.7:B=6:R=R-1
  298 POKE F+512*X+64*Y+8*Z+S,166:FOR I=1 TO 7:R(I)=R(I)-(A&gt;RND(1))*INT(B*RND(1)+B)
  300 PRINT AT(2*Z+0,2*S-1);"":BEEP:PAUSE 3:PRINT AT(2*Z+0,2*S-1);"":PAUSE 3
  302 NEXT I:GOSUB 50:PRINT AT(21,3);"HINDERNIS VERNICHTET!      "
  304 PRINT INK 4;AT(23,3);"ENTERPRISE BESCHAEDIGT!!!":PAUSE 20:GOSUB 32:RETURN
  306 REM ANDOCK-KONTROLLE ----UP
  308 A=0:FOR I=Z-1 TO Z+1:FOR J=S-1 TO S+1:IF I=0 OR I=9 OR J=0 OR J=9 THEN 312
  310 IF PEEK(F+512*X+64*Y+8*I+J)=66 THEN A=1
  312 NEXT J:NEXT I:IF A=1 THEN E=5000:P=8:FOR I=1 TO 6:R(I)=0:NEXT I:AD=1:ELSE AD=0
  314 IF AD=1 THEN PRINT INK 6;AT(Z*2+0,S*2-1);"":RETURN:ELSE RETURN
  316 REM RAUMSEKTOR-BILD ---UP
  318 GOSUB 32:PRINT INK 4;AT(20,2);"SEKTOR-KOORDINATEN ?":WINDOW 22,22,2,30
  320 INPUT "--&gt; ";A$:IF LEN(A$)&lt;&gt;2 THEN 320
  322 X1=INSTR(LEFT$(A$,1),X$):Y1=INSTR(RIGHT$(A$,1),S$):IF X1=0 OR Y1=0 THEN 320
  324 IF SB(X1,Y1)=1 THEN 328:ELSE GOSUB 32
  326 PRINT AT(21,2);"SEKTOR NICHT ERFORSCHT!":PAUSE 30:GOSUB 32:RETURN
  328 X2=X:Y2=Y:X=X1:Y=Y1:GOSUB 40:X=X2:Y=Y2:GOSUB 30:C=0:GOSUB 40:C=7
  330 RETURN
  332 REM CHECK ENTERPRISE ----UP
  334 CLS:PRINT AT(0,0);" ":PRINT AT(1,3);"SYSTEM-CHECK ENTERPRISE"
  336 PRINT AT(4,0);"BORDSYSTEM       ZUSTAND   TAGE"
  338 PRINT AT(7,0);"ANTRIEB":PRINT AT(9,0);"HYPERANTRIEB"
  340 PRINT AT(11,0);"TORPEDO":PRINT AT(13,0);"PLASMAWOLKE"
  342 PRINT AT(15,0);"UMKREIS-SENSOREN":PRINT AT(17,0);"STERNENKARTE"
  344 PRINT AT(19,0);"RAUMSEKTOR-BILD":FOR I=1 TO 7
  346 IF R(I)=0 THEN PRINT AT(I*2+5,17);"BEREIT"
  348 IF R(I)&lt;&gt;0 THEN PRINT AT(I*2+5,17);"DEFEKT   ",R(I)
  350 NEXT I:PRINT AT(24,22);"&gt;ENTER&lt;":GOSUB 30:CLS:GOSUB 40:RETURN
  352 CLEAR 6144:WINDOW:CLS:PRINT AT(0,0);" "
  354 CALL*5E00
  356 I=RND(-ABS(DEEK(30)/65536))
  358 DATA "        "
  360 DATA "           "
  362 DATA "                "
  364 DATA "          " 
  366 PRINT AT(05,05);"BITTE 2 1/2 MIN WARTEN!!"
  368 GOSUB 370:GOTO 382
  370 RESTORE 358:FOR I=15 TO 18:READ A$:PRINT AT(I,1);A$:NEXT I
  372 PRINT AT(14,23);"":PRINT AT(19,17);"":INK 5
  374 PRINT AT(13,0);"",STRING$(30,""),""
  376 FOR I=14 TO 19:PRINT AT(I,0);"":PRINT AT(I,31);"":NEXT I
  378 PRINT AT(20,0);"",STRING$(30,""),""
  380 INK 3:RETURN
  382 DIM Y$(8),Z$(8)
  384 DATA A,1,B,2,C,3,D,4,E,5,F,6,G,7,H,8
  386 RESTORE 384:FOR I=1 TO 8:READ Y$(I),Z$(I):NEXT I:X$="12345678":S$="ABCDEFGH"
  388 DEF FN T(I)=INT(RND(1)*I+1)
  390 D$="AHTPUSRC":E$="QWEASDYXC"
  392 DIM K$(8,8),R(8),SB(8,8):FOR I=1 TO 8:FOR J=1 TO 8:K$(I,J)=" ":NEXT J:NEXT I
  394 PRINT AT(31,10);"ZIEL: 32696"
  396 F=28600:FOR I=F TO F+4096:POKE I,166:IF KK=800 THEN KK=0
  398 PRINT AT(31,25);I
  400 KK=KK+1:NEXT I:F=F-1
  402 PRINT AT(31,10);"                     ":PRINT AT(5,11);"  "
  404 CALL*5E00
  406 FOR I=1 TO 4
  408 A=FN T(4096):IF PEEK(F+A)&lt;&gt;166 THEN 408
  410 POKE F+A,66:NEXT I
  412 FOR I=1 TO 30
  414 A=FN T(4096):IF PEEK(F+A)&lt;&gt;166 THEN 414
  416 POKE F+A,75:NEXT I
  418 GOTO 432
  420 POKE 25137,32:POKE 25138,139:POKE 25139,3:RESTORE 428
  422 FOR II=25140 TO 25230 STEP 9:POKE II,63:POKE II+3,103:POKE II+6,143
  424 FOR J=2 TO 8 STEP 3:READ A:POKE II+J,A:NEXT J:NEXT II:DOKE 24985,603
  426 RETURN
  428 DATA 32,160,32,32,160,32,32,207,32,134,180,133,135,181,132,32,197,32
  430 DATA 32,197,32,32,197,32,144,197,145,137,197,200,179,161,176
  432 FOR I=1 TO 500:IF I/150=INT(I/150)THEN GOSUB 420
  434 A=FN T(4096):IF PEEK(F+A)&lt;&gt;166 THEN 434
  436 POKE F+A,42:NEXT I
  438 F=F-584
  440 X=FN T(8):Y=FN T(8):Z=FN T(8):S=FN T(8)
  442 IF PEEK(F+512*X+64*Y+8*Z+S)&lt;&gt;166 THEN 440
  444 WINDOW 14,19,1,30:CLS:PRINT AT(14,3);"FUER WIEVIELE STERNENTAGE"
  446 IF XXX=0 THEN WINDOW 4,6,0,31:CLS:XXX=1:GOTO 444
  448 CALL*5E00
  450 PRINT AT(16,2);"MOECHTEN SIE VORRAETE LADEN ?":WINDOW 18,18,10,18:A=0
  452 INPUT "--&gt;";M:IF M&gt;60 AND M&lt;100 AND M=INT(M)THEN 464
  454 IF M&lt;&gt;INT(M)THEN PRINT AT(17,1);"  ES GIBT NUR GANZE RATIONEN!  ":GOTO 450
  456 IF M&lt;=50 AND A=0 THEN PRINT AT(17,1);"  DAMIT KOMMEN SIE KAUM AUS!   ":A=1
  458 IF M&lt;=50 AND A=1 THEN GOTO 450
  460 IF M&lt;=60 THEN PRINT AT(18,2);"WENN SIE UNBEDINGT WOLLEN!":PAUSE 30:GOTO 464
  462 IF M&gt;99 THEN PRINT AT(17,1);"SOVIEL TRAEGT ENTERPRISE NICHT!":GOTO 452
  464 WINDOW:CLS:PRINT AT(0,0);" ":P=8:E=5000:K=30:R=4
  466 GOSUB 40:GOSUB 50:GOSUB 60
  468 IF W&lt;&gt;0 THEN GOSUB 86:GOSUB 60
  470 I=0:GOSUB 106
  472 CALL*5E00
  474 IF C&lt;&gt;6 AND C&lt;&gt;7 AND C&lt;&gt;3 THEN M=M-1
  476 FOR I=1 TO 8:R(I)=R(I)+(R(I)&lt;&gt;0):NEXT I
  478 L=0:ON-C*(R(C)=0)-9*(R(C)&lt;&gt;0)GOSUB 124,146,162,184,206,224,318,334,244
  480 IF L&lt;&gt;1 AND V&lt;&gt;0 THEN GOSUB 308:L=0:ELSE AD=0
  482 IF R(1)&gt;9 AND R(2)&gt;9 AND R(3)&gt;9 AND R(4)&gt;9 AND R(5)&gt;9 AND R(6)&gt;9 AND R(7)&gt;9 THEN L=4
  484 IF L=0 AND M&lt;&gt;0 AND K&lt;&gt;0 AND E&lt;&gt;0 THEN GOSUB 50:GOSUB 60:GOTO 468
  486 GOSUB 60:BEEP:PAUSE 05:CLS:PRINT AT(0,0);" ":GOSUB 370:PAUSE 05
  488 WINDOW 14,19,1,30:CLS:PRINT AT(14,4);" ":INK 2:BEEP:IF K&lt;&gt;0 THEN 494
  490 PRINT AT(15,1);"SIE HABEN DIE GALAXIS BEFREIT!"
  492 PRINT AT(18,1);"DIE ERDE IST STOLZ AUF SIE !!!":WINDOW:END
  494 FOR TT=1 TO 40:CALL*5E00:NEXT TT:IF M&lt;&gt;0 THEN 500
  496 PRINT AT(15,1);"IHRE VORRAETE SIND ERSCHOEPFT!"
  498 PRINT AT(18,1);"DIE MISSION IST GESCHEITERT !!":WINDOW:END
  500 IF L&lt;&gt;1 THEN 506:ELSE PRINT AT(14,2);"SIE MISERABLER FEIGLING SIND"
  502 PRINT AT(16,2);"AUS DER GALAXIS GEFLOHEN !"
  504 PRINT AT(18,2);"DIE ERDE VERACHTET SIE !!!":WINDOW:END
  506 IF L&lt;&gt;2 THEN 510:ELSE PRINT AT(15,2);"DIE ENERGIE REICHTE NICHT AUS!"
  508 PRINT AT(18,2);"IM HYPERRAUM VERSCHOLLEN !!!":WINDOW:END
  510 IF L&lt;&gt;3 THEN 514:ELSE PRINT AT(15,2);"SIE DRIFTEN ANTRIEBSLOS IM ALL"
  512 PRINT AT(18,2);"KEINE RETTUNG MEHR MOEGLICH!":WINDOW:END
  514 IF L&lt;&gt;4 THEN 518:ELSE PRINT AT(15,2);"SAEMTLICHE BORDSYSTEME DEFEKT!"
  516 PRINT AT(18,2);"SIE HABEN KEINE CHANCE MEHR!":WINDOW:END
  518 PRINT AT(15,2);"ENERGIE RESTLOS VERBRAUCHT !!!"
  520 PRINT AT(18,1);"AUSFALL LEBENSERHALTUNGSSYSTEME":WINDOW:END
  522 REM BALA51
  524 CALL*5E00
  526 DATA 197,213,229,245,33,154,2,78,13,113,32,99,54,16,35,78,35,70,0,120
  528 DATA 183,40,96,205,143,2,35,94,53,35,22,237,126,18
  530 DATA 62,112,22,233,18,35,16,240,46,155,78,35,70,203,121,40,10
  532 DATA 62,33,184,40,11,4,4,4,24,3,5,5,5,112,40,52,12,12,12,43,113,24,10
  534 DATA 197,213,229,245,33,17,2,34,2,2,33,155,2,78,35,70,35,205,143,2
  536 DATA 94,35,22,237,26,119,35,126,18,22,233,62,96,18
  538 DATA 35,16,239,62,183,211,129,62,50,211,129,241,225,209,193,251,237,77
  540 DATA 175,129,200,248,35,35,35,13,32,250,201
  542 DATA 33,0,0,175,79,87,95,6,64,126,254,42,32,1,20,254,66,32,1,12
  544 DATA 254,75,32,1,28,35,16,237,33,8,0,114,35,115,35,113,201
  546 DATA 33,0,91,17,65,236,14,8,6,8,126,35,18,203,146,254,42,32,2,62,112
  548 DATA 254,66,32,2,62,48,254,75,32,2,62,16,254,166,32,2,62,64,254,207
  550 DATA 32,2,62,80,18,19,62,64,18,203,210,16,2,24,6,62,160,18,19,24,204
  552 DATA 13,200,229,33,17,0,25,6,8,54,161,203,148,54,64,203,212
  554 DATA 35,35,16,244,17,16,0,25,235,225,24,173
  556 S=0:C=25000:RESTORE 526:FOR I=C TO C+136:READ B:POKE I,B:S=S+B:NEXT I 
  558 C=24727:FOR I=C TO C+127:READ B:POKE I,B:S=S+B:NEXT I
  560 IF S&lt;&gt;22550 THEN PRINT "LADEFEHLER!";S:END
  562!CLS:PRINT"BITTE MIT &gt;RUN&lt; STARTEN":DELETE10010,10530
  564 GOTO 352
  566 REM UP BEEP
  568 DATA 33,58,16,14,250,203,255
  570 DATA 211,2,70,16,254,203,191
  572 DATA 211,2,70,16,254,13,32
  574 DATA 239,201
  576 RESTORE 568
  578 FOR I=24064 TO I+22
  580 READ BEE:POKE I,BEE:NEXT I
  582 RETURN
  584 REM KEIN BEEP
  586 POKE 24064,201
  588 RETURN
</pre>
</div>
</body>
</html>
