Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10 POKE 10846,32:WINDOW:CLS:CLEAR:GOSUB 30000
   20 WINDOW:CLS
   30 PRINT STRING$(64,CHR$(199))
   40 PRINT "        Textadventure von":PRINT
   50 PRINT "      Klaus Rothe,Ottendorf":PRINT
   60 PRINT STRING$(32,"~")
   70 PRINT "      Zusammengestellt  von":PRINT
   80 PRINT "  Dietmar Neumann,Finsterwalde":PRINT
   90 PRINT STRING$(64,CHR$(199)):PAUSE 20
  100 PRINT AT(18,3);"M e n u e":PRINT AT(19,3);"~~~~~~~~~"
  110 PRINT AT(22,3);"Alzan        -  1"
  120 PRINT AT(24,3);"Wueste       -  2":PRINT AT(26,3);"Champion     -  3" 
  130 PRINT AT(28,3);"E N D E      -  0":PRINT AT(30,3);"Auswahl:"
  140 IF INKEY$="1" THEN 3800
  150 IF INKEY$="2" THEN 8260
  160 IF INKEY$="3" THEN 210
  170 IF INKEY$="0" THEN 190
  180 GOTO 140
  190 WINDOW:CLS:CLEAR:POKE 10846,95
  200 END
  210 WINDOW:CLS:CLEAR 280 
  220 DIM PF$(53),X(53),Y(53),PW(53),PS(53),PN(53),BP(5),BV(5),RN$(27),RW(27)
  230 DIM RX(27):GOSUB 2600:K=1
  240 PRINT:PRINT:PRINT:PRINT "Ein Gesellschaftsspiel fuer die ganze ";
  250 PRINT "Familie von ":PRINT:PRINT
  260 PRINT "Michael Siedentopf"
  270 PRINT "Fr.-A.-Sorge-Str. 33"
  280 PRINT "Dresden"
  290 PRINT "8 0 2 7"
  300 PRINT
  310 PRINT "version for Z 1013 K.Rothe/88":PRINT:PRINT:PRINT:PAUSE 2
  320 PRINT "Bitte warten!":PRINT:PRINT:A$=CHR$(16):B$=CHR$(25):GOSUB 470:CLS
  330 WINDOW 5,9,1,31:PRINT "Wieviel Spieler nehmen teil?":PRINT "Maximal 4!"
  340 FOR I=1 TO 4:BV(I)=500000:NEXT I
  350 PRINT:INPUT "--->";PE:IF PE<1 OR PE>4 THEN CLS:PRINT "maximal 4":GOTO 350
  360 SZ=PE:CLS
  370 FOR I=1 TO SZ:PRINT "Spieler";I;", wie heissen Sie?" 
  380 PRINT:INPUT "--->";SN$:CLS:SN$(I)=LEFT$(SN$,20):NEXT I 
  390 PRINT "Wieviel Runden werden gespielt?"
  400 PRINT "Maximal 25!":PRINT:INPUT "--->";CG
  410 IF CG>25 OR CG<1 THEN CLS:GOTO 390
  420 CLS
  430 WINDOW:FOR OO=1 TO CG
  440 FOR O=1 TO SZ:GOSUB 670:NEXT O,OO
  450 GOSUB 2480
  460 END
  470 FOR J=1 TO 52:PRINT A$;:PRINT " ";:PRINT B$;:READ PF$(J)
  480 X(J)=INT(RND(1)*10+1):Y(J)=INT(RND(1)*(21-X(J))+X(J))
  490 PW(J)=(X(J)+Y(J))/2*10000
  500 PN(J)=INT(RND(1)*5):BP(PN(J))=BP(PN(J))+1
  510 IF BP(PN(J))>8 AND PN(J)<>0 THEN BP(PN(J))=8:GOTO 500
  520 NEXT J
  530 FOR J=0 TO 26:READ RN$(J),RW(J),RX(J):NEXT J
  540 DIM RH(27):FOR J=0 TO 26:READ RH(J):NEXT J:RETURN
  550 PRINT AT(4,0);"Nr. Name   Wert   Nr. Name  Wert"
  560 PRINT AT(7,0);STRING$(31,""):FOR T=4 TO 19:PRINT AT(T,16);"":NEXT T    
  570 PRINT AT(7,16);"" 
  580 PRINT AT(5,11);"i.TM":PRINT AT(5,28);"i.TM":N=9:QD=0:VV=0     
  590 FOR J=1 TO 52:IF PN(J)<>I THEN NEXT J:ELSE GOTO 610
  600 RETURN
  610 IF PW(J)<=RX(K)*10000 THEN VV=VV+1
  620 INK 7:QD=QD+1:IF QD/2=INT(QD/2)THEN 640
  630 PRINT AT(N,0);J:PRINT AT(N,4);PF$(J):PRINT AT(N,11);PW(J)/1000:NEXT J     
  640 PRINT AT(N,17);J:PRINT AT(N,21);PF$(J):PRINT AT(N,28);PW(J)/1000
  650 IF J>52 THEN RETURN
  660 N=N+1:NEXT J:RETURN
  670 PRINT AT(1,6);"*  STALL ",SN$(O),"  *":PRINT AT(2,0);STRING$(31,"")
  680 WINDOW 3,23,0,3:PRINT AT(6,3);"Besitz:",BP(O),"Pferde"
  690 PRINT AT(7,10);BV(O),"Mark"
  700 PRINT AT(11,3);"Was wollen Sie tun? ":PRINT AT(19,3);"Rennen starten  -3"
  710 PRINT AT(17,3);"Verkaufen       -1":PRINT AT(18,3);"Kaufen          -2"
  720 PRINT AT(20,3);"Morgenarbeit    -4"
  730 PRINT AT(15,0);STRING$(31,""):PRINT AT(22,1);STRING$(30,"")           
  740 WINDOW 1,22,0,0:PRINT STRING$(22,""):WINDOW 1,22,31,31  
  750 PRINT STRING$(22,""):WINDOW 24,31,0,31:PRINT STRING$(224,"");  
  760 WINDOW 11,11,23,25:INPUT "";PE :IF PE<1 OR PE>4 THEN 760:ELSE WINDOW
  770 PZ=PE:CLS:II$=INKEY$:ON PZ GOSUB 810,930,1060,1460:WINDOW:CLS
  780 PD=INT(RND(O)*10+1)
  790 ON PD GOSUB 3450,3550,3630
  800 FOR J=1 TO SZ:BV(J)=BV(J)-500*BP(J):NEXT J:RETURN
  810 WINDOW: I=O:CLS:PRINT AT(22,0);STRING$(224,"")
  820 GOSUB 550
  830 WINDOW 23,27,1,30:CLS:WINDOW 24,26,2,30
  840 PRINT "Welches Pferd verkaufen ?":PRINT "Bitte Nummer eingeben! "
  850 INPUT "";TN:CLS
  860 IF TN<0 OR TN>52 THEN PRINT "Dieses Pferd gibt es nicht!":PAUSE 10:GOTO 900
  870 INK 2:IF PN(TN)<>I THEN PRINT PF$(TN);" haben Sie nicht!":PAUSE 10:GOTO 900
  880 CLS
  890 PN(TN)=0:BP(O)=BP(O)-1:BV(O)=BV(O)+PW(TN):PRINT PF$(TN);" wurde verkauft!
  900 PRINT "Wollen Sie noch verkaufen?":PRINT "(Y/N)";
  910 INPUT "";PE$:IF PE$="Y"THEN WINDOW 2,21,0,31:CLS:II$=INKEY$:GOTO 820
  920 IF PE$="N"THEN RETURN:ELSE GOTO 910
  930 WINDOW:CLS:PRINT AT(23,0);STRING$(224,""):PRINT "Zum Verkauf stehen:"    
  940 WINDOW 2,21,0,31:CLS:I=0:GOSUB 550
  950 WINDOW 24,28,1,30:CLS:WINDOW 25,27,2,30
  960 PRINT AT(21,3);"Sie haben",BV(O),"Mark."
  970 PRINT "Welches Pferd wird gewuenscht":PRINT:PRINT "Bitte Nummer eingeben!"
  980 INPUT "";TN:CLS
  990 IF TN<0 OR TN>52 THEN PRINT "Dieses Pferd gibt es nicht!":PAUSE 10:GOTO 1030
 1000 IF PN(TN)<>0 THEN PRINT PF$(TN);" ist nicht verkaeuflich!":PAUSE 9:GOTO 1030
 1010 IF BV(O)<PW(TN)THEN PRINT "Soviel Geld haben Sie nicht!":PAUSE 9:GOTO 1030
 1020 PN(TN)=O:BP(O)=BP(O)+1:BV(O)=BV(O)-PW(TN):PRINT "Sie kauften ";PF$(TN)
 1030 PAUSE 9:PRINT "Wollen Sie noch kaufen? Y/N"
 1040 INPUT "";PE$:IF PE$="Y"THEN II$=INKEY$:GOTO 940
 1050 IF PE$="N"THEN RETURN:ELSE GOTO 1040
 1060 WINDOW:CLS:WINDOW 26,31,0,31:K=INT(RND(1)*27) 
 1070 PRINT "Es wird gelaufen:":PRINT RN$(K):PRINT:PRINT "Wert:";RW(K);
 1080 IF RH(K)=0 THEN PRINT " ":ELSE PRINT " (Huerdenrennen)" 
 1090 PRINT "Startberechtigt sind Pferde bis zu einem Wert von ";RX(K)*10000;" M
 1100 WINDOW 0,23,0,31
 1110 FOR I=1 TO SZ:CLS:GOSUB 550:PRINT AT(0,3);"STALL ",SN$(I)
 1120 PRINT AT(2,0);STRING$(31,"")      
 1130 FOR P=I*2-1 TO I*2:GOSUB 1790:NEXT P:WINDOW 4,14,0,31:NEXT I
 1140 WINDOW:CLS:GOSUB 1580
 1150 PRINT AT(1,4);"---->  WETTBANK <----":FOR J=1 TO 8:LH=LH(J)
 1160 PRINT AT(J+2,1);J:PRINT AT(J+2,4);PF$(LH):QT(J)=INT(100*10000/PW(LH))
 1170 IF PN(LH)=0 OR PN(LH)>SZ THEN 1210
 1180 OI=PW(LH)/10000:RD=INT(RND(OI)*5-2)
 1190 OI=OI+RD:IF OI<1 THEN OI=1
 1200 QT(J)=INT(100/OI)
 1210 PRINT AT(J+2,20);"1:",QT(J):NEXT J
 1220 WINDOW 15,25,1,31:CLS:GOSUB 3680
 1230 FOR J=1 TO SZ:PRINT SN$(J):PRINT "Wollen Sie wetten?(Y/N)":BQ(J)=0:BE(J)=0
 1240 INPUT "";PE$:IF PE$="N"THEN CLS:GOTO 1350
 1250 IF PE$>"Y"THEN 1240
 1260 PRINT:PRINT "Auf welche Startnummer?"
 1270 INPUT "";PE:IF PE<1 OR PE>8 THEN 1270
 1280 QZ=PE-48:BQ(J)=QZ:PRINT:II$=INKEY$
 1290 PRINT "Wieviel wollen Sie wagen?":PRINT "Hoechsteinsatz 10000 Mark!"
 1300 PRINT "Sie haben";BV(J);"Mark.":INPUT TN
 1310 IF TN<0 THEN PRINT "Bloedsinn!":PAUSE 15:CLS:GOTO 1290
 1320 IF TN>BV(J)THEN PRINT "Soviel haben Sie nicht!":PAUSE 15:CLS:GOTO 1290
 1330 IF TN>10000 THEN PRINT "Kein Hassard bitte!":PAUSE 15:CLS:GOTO 1290
 1340 BE(J)=TN:BV(J)=BV(J)-TN:CLS
 1350 NEXT J
 1360 WINDOW:CLS:PRINT AT(0,2);RN$(K):FOR J=0 TO 31:PRINT AT(2,J);""
 1370 PRINT AT(11,J);"":NEXT J:PRINT AT(22,0);STRING$(224,CHR$(RND(1)*31))
 1380 IF RH(K)=0 THEN 1400
 1390 FOR J=5 TO 31 STEP 10:FOR I=3 TO 10:PRINT INK 1;AT(I,J);"":NEXT I,J
 1400 GOSUB 1650:GOSUB 1960
 1410 FOR J=1 TO 8:LH=LH(J):PS(LH)=PS(LH)+1
 1420 IF PS(LH)<3 THEN NEXT J:RETURN
 1430 X(LH)=INT(RND(J)*10+1):Y(LH)=INT(RND(J)*(21-X(LH))+X(LH)):PS(LH)=-1
 1440 PW(LH)=(Y(LH)+X(LH))*5000:NEXT J
 1450 WINDOW:CLS:RETURN
 1460 INK 7:WINDOW:CLS:PRINT AT(0,1);"STALL ",SN$(O):I=O:GOSUB 550
 1470 PRINT AT(22,0);STRING$(224,""):WINDOW 23,27,1,30:PAUSE 9:CLS   
 1480 PRINT:PRINT "Morgenarbeit":WINDOW 25,27,1,30
 1490 PRINT "Wieviel Pferde sollen arbeiten ":PRINT "(Max.3)";
 1500 INPUT "";PE:IF PE<1 OR PE>3 THEN 1500:ELSE TZ=PE   
 1510 FOR J=1 TO TZ:INK 4
 1520 CLS:PRINT "Nummer des Pferdes eingeben!":INPUT "";A(J)
 1530 IF A(J)<0 OR A(J)>52 THEN CLS:GOTO 1520
 1540 IF PN(A(J))<>I THEN PRINT PF$(A(J));" haben Sie nicht!":PAUSE 10:GOTO 1520
 1550 CLS:NEXT J:WINDOW:CLS:GOSUB 3200
 1560 BV(I)=BV(I)-TZ*1000:WINDOW:CLS
 1570 RETURN
 1580 FOR J=SZ+1 TO 4:IF SZ=4 THEN RETURN
 1590 FOR P=2*J-1 TO 2*J
 1600 LH(P)=INT(RND(J)*52+1):LH=LH(P)
 1610 IF PN(LH(P))<SZ+1 AND PN(LH(P))<>0 THEN 1600
 1620 IF PW(LH)>RX(K)*10000 THEN X(LH)=RX(K)-4:Y(LH)=RX(K)+4:PW(LH)=RX(K)*10000
 1630 FOR Z=1 TO P-1:IF LH(Z)=LH(P)THEN 1600:ELSE NEXT Z
 1640 NEXT P:NEXT J:RETURN
 1650 FOR J=1 TO 8:PRINT AT(J+2,0);CHR$(J+48):LX(J)=0:NEXT J:V=0    
 1660 PRINT AT(19,22);"ACHTUNG":PAUSE 4:PRINT AT(22,0);STRING$(224,CHR$(18))     
 1670 PRINT AT(19,22);"FERTIG ":PAUSE 4:PRINT AT(22,0);STRING$(224,CHR$(29))
 1680 PRINT AT(19,22);"JOCKEYS !":PRINT AT(22,0);STRING$(224,CHR$(14))
 1690 FOR J=1 TO 8:IF LH(J)=0 THEN 1770:ELSE LH=LH(J)
 1700 IF LX(J)>=0 AND LX(J)<120 THEN PRINT AT(J+2,INT(LX(J)/3));" " 
 1710 G=INT(RND(J)*(10-X(LH)))
 1720 IF LX(J)>=0 THEN LX(J)=LX(J)+INT(RND(J)*(Y(LH)+G-X(LH))+X(LH)):ELSE 1770
 1730 IF LX(J)>=90 THEN V=V-1:LX(J)=V:PRINT AT(J+2,31);CHR$(64-V):GOTO 1770
 1740 IF PEEK((J+2)*32+INT(LX(J)/3)-5120)=ASC("")THEN LX(J)=LX(J)-11
 1750 IF LX(J)<0 THEN LX(J)=0
 1760 PRINT AT(J+2,INT(LX(J)/3));CHR$(J+48)
 1770 NEXT J:PAUSE 6:IF V>-8 THEN 1690
 1780 RETURN
 1790 IF VV<2 THEN GOSUB 1880:RETURN:ELSE WINDOW 21,23,1,30
 1800 PRINT AT(20,0);STRING$(160,""):PAUSE 5:CLS:WINDOW 22,23,2,30
 1810 PAUSE 10:CLS:PRINT " Startnummer des Pferdes?";:INPUT "";TN:LH(P)=0:CLS
 1820 IF TN<0 OR TN>52 THEN CLS:GOTO 1810
 1830 IF PN(TN)<>I THEN PRINT PF$(TN);" haben Sie nicht!":GOTO 1810
 1840 IF PW(TN)>RX(K)*10000 THEN PRINT PF$(TN);" nicht startberechtigt":GOTO 1810
 1850 FOR Z=1 TO P-1:IF LH(Z)=TN THEN PRINT PF$(TN);" schon genannt!":GOTO 1810
 1860 NEXT Z:BV(I)=BV(I)-RW(K)/100
 1870 LH(P)=TN:RETURN
 1880 VV=VV+1
 1890 LH(P)=INT(RND(J)*52+1):LH=LH(P)
 1900 IF PN(LH(P))<>0 AND PN(LH(P))<SZ+1  THEN 1890
 1910 IF PW(LH)>RX(K)*10000 THEN PW(LH)=RX(K)*10000:X(LH)=RX(K)-4:Y(LH)=RX(K)+4
 1920 IF P=1 THEN 1940
 1930 FOR Z=1 TO P-1:IF LH(Z)=LH(P)THEN 1890:ELSE NEXT Z
 1940 RETURN
 1950 PRINT AT(2,2);STRING$(29,"-")
 1960 FOR J=1 TO 8:LX(J)=-LX(J):NEXT J:WINDOW:CLS:PRINT AT(1,2);"'Grosser Einlauf'"
 1970 PRINT AT(2,0);STRING$(31,"-"):PRINT AT(11,0);STRING$(31,"-")
 1980 FOR J=1 TO 8:PRINT AT(LX(J)+2,0);LX(J):LH=LH(J)
 1990 FOR HH=1 TO SZ:IF BQ(HH)=J THEN PRINT AT(LX(J)+2,12);HH,"*"
 2000 NEXT HH
 2010 PRINT AT(LX(J)+2,0);PF$(LH):PRINT AT(LX(J)+2,13);"1:",QT(J):NEXT J
 2020 PRINT AT(16,2);"P r e i s v e r g a b e":PRINT AT(17,2);STRING$(23,"")   
 2030 FOR J=1 TO 8 
 2040 ON LX(J)GOSUB 2120,2150,2170,1570,1570,1570,2190,2210
 2050 GOSUB 1570
 2060 NEXT J
 2070 FOR J=1 TO SZ
 2080 IF BQ(J)=JJ THEN BV(J)=QT(JJ)*BE(J)+BV(J)
 2090 NEXT J
 2100 FOR J=1 TO SZ:PRINT AT(19+J,2);SN$(J),"-",BV(J):NEXT J
 2110 PRINT AT(31,25);"<ENTER>":I$=INKEY$:IF I$=CHR$(13)THEN RETURN:ELSE GOTO 2110
 2120 LH=LH(J):JJ=J:Y(LH)=Y(LH)+3:PW(LH)=(Y(LH)+X(LH))*5000
 2130 BV(PN(LH))=BV(PN(LH))+RW(K)
 2140 PRINT AT(LX(J)+2,23);RW(K):RETURN
 2150 LH=LH(J):BV(PN(LH))=BV(PN(LH))+RW(K)/4:Y(LH)=Y(LH)+2
 2160 PW(LH)=(Y(LH)+X(LH))*5000:PRINT INK 7;AT(LX(J)+2,23);RW(K)/4:RETURN
 2170 LH=LH(J):BV(PN(LH))=BV(PN(LH))+RW(K)/16:Y(LH)=Y(LH)+1
 2180 PW(LH)=(Y(LH)+X(LH))*5000:PRINT INK 7;AT(LX(J)+2,23);RW(K)/16:RETURN
 2190 IF X(LH(J))>1 THEN X(LH(J))=X(LH(J))-1
 2200 PW(LH(J))=(Y(LH(J))+X(LH(J)))*5000:RETURN
 2210 LH=LH(J):IF X(LH)+2<Y(LH)THEN X(LH)=X(LH)+2
 2220 PW(LH)=(Y(LH)+X(LH))*5000:RETURN
 2230 DATA "POND","KARO","BELINE","BOOST","CINDER"
 2240 DATA "CALL","DREAM","DAKKA","ELEVAT","ECLIP"
 2250 DATA "FAME","FLORAD","GINGER","GALAX","JESICA"
 2260 DATA "COLIN","ISABEL","MORE","JASPI","JEREMI"
 2270 DATA "NIGHT","KISS","GRAND","LOPEZ","DEAR"
 2280 DATA "WIND","WORLD","ONE","TONI","OLD","PALISA" 
 2290 DATA "POLO","MIREK","QUEEN","DAY"
 2300 DATA "RICA","SHANG","SILVER","AMOR","TEST"
 2310 DATA "ULTRA","FINISH","SALLY","VESTA","SOLO"
 2320 DATA "WONDER","X-RAY","XAVER","YESTER","YELLOW"
 2330 DATA "ZACHAR","ZEPHIR"
 2340 DATA "^Preis von Hamburg^",4000,8,"*Grosser Preis von Hannover*",8000,8
 2350 DATA "#Grosser Preis von Muenchen#",4000,8,"-Preis von Koeln-",8000,8
 2360 DATA "= Price of Everton =",8000,9,"+Liverpool-Stakes+",12000,9
 2370 DATA "~ Preis von Berlin ~",12000,9,": Triumph :",16000,9
 2380 DATA "<Stella-Stakes>",16000,9,"Grosser Preis von Baden-Baden",20000,10
 2390 DATA "_ Grand National _",24000,11,"| Queens Cup |",24000,11
 2400 DATA "* Union *",28000,11,"=Henckel-Rennen=",28000,11
 2410 DATA "+Oestereichisches Derby+",32000,12,"^ Evidence-Cup ^",32000,12
 2420 DATA "* Oaks *",36000,12,"- 1000 Guineen -",40000,12
 2430 DATA "- 2000 Guineen -",48000,14,"% St. Leger %",52000,14
 2440 DATA "-Preis von Deutschland-",52000,14,"* Deutsches Derby *",60000,15
 2450 DATA "< Ascot-Derby >",60000,15,"# Preis von Europa #",60000,15
 2460 DATA "* Grand Prix de Triomphe *",72000,15
 2470 DATA "!Goldene Peitsche!",80000,17,"Washington D.C. International",100000
 2480 DATA 20,0,0,0,0,1,1,0,0,1,0,1,1,0,0,0,1,0,0,0,0,0,0,0,0,0,0,0
 2490 WINDOW:CLS:PRINT AT(0,5);"A U S W E R T U N G":PAUSE 20:WINDOW 2,31,0,31
 2500 FOR I=1 TO 4:PRINT STRING$(224,"");:NEXT:FOR I=1 TO 4 
 2510 PRINT STRING$(224,"");:NEXT:FOR I=1 TO 4:PRINT STRING$(224,"");:NEXT  
 2520 W=15:X=15:Y=15:Z=15                    
 2530 WINDOW W,X,Y,Z:IF Z<29 THEN W=W-.5:X=X+1:Y=Y-1:Z=Z+1:CLS:GOTO 2530
 2540 !FORJ=1TO52
 2550!BV(PN(J))=BV(PN(J))+PW(J):NEXTJ
 2560 FOR K=1 TO SZ
 2570 PRINT AT(K+9,3);SN$(K)
 2580 PRINT AT(K+9,19);BV(K):NEXT K
 2590 GOSUB 3440:GOTO 20
 2600 WINDOW:CLS
 2610 FOR I=0 TO 31: PRINT AT(I,0);STRING$(31,""):NEXT I:WINDOW 25,29,3,28
 2620 CLS:PAUSE 10:FOR I=25 TO 29:PRINT AT(I,3);STRING$(25,(CHR$(255))):NEXT I      
 2630 WINDOW 26,28,6,24:CLS:PRINT AT(27,7);" C H A M P I O N "
 2640 WINDOW:PAUSE 9    
 2650 PRINT AT(12,3);""
 2660 PRINT AT(13,3);"   "
 2670 PRINT AT(14,3);"  "
 2680 PRINT AT(15,3);"   "
 2690 PRINT AT(16,3);"   "
 2700 PRINT AT(17,3);"  "
 2710 PRINT AT(18,3);"  "
 2720 PRINT AT(19,3);"  "
 2730 PRINT AT(20,3);"   "
 2740 PRINT AT(21,2);"   "
 2750 PRINT AT(22,2);"DERBY"
 2760 PRINT AT(23,2);""
 2770 PAUSE 5
 2780 PRINT AT(13,11);""
 2790 PRINT AT(14,11);""
 2800 PRINT AT(15,11);"     "
 2810 PRINT AT(16,11);"       "
 2820 PRINT AT(17,11);"       "
 2830 PRINT AT(18,11);"      "
 2840 PRINT AT(19,11);"   OAK   "
 2850 PRINT AT(20,11);"   S   "
 2860 PRINT AT(21,11);"       "
 2870 PRINT AT(22,11);"       "
 2880 PRINT AT(23,11);"      "
 2890 PAUSE 5
 2900 PRINT AT(13,23);""
 2910 PRINT AT(14,23);"     "
 2920 PRINT AT(15,23);"     "
 2930 PRINT AT(16,23);""
 2940 PRINT AT(17,23);"UNION"
 2950 PRINT AT(18,23);""
 2960 PRINT AT(19,23);"     "
 2970 PRINT AT(20,23);"     "
 2980 PRINT AT(21,23);"     " 
 2990 PRINT AT(22,23);"     "
 3000 PRINT AT(23,23);"    "
 3010 PAUSE 5
 3020 PRINT AT(1,12);""
 3030 PRINT AT(2,12);"  S  "
 3040 PRINT AT(3,12);"  T  "
 3050 PRINT AT(4,12);"LEGER"        
 3060 PRINT AT(5,12);""
 3070 PRINT AT(6,12);"     " 
 3080 PRINT AT(7,12);"     "
 3090 PRINT AT(8,12);"    " 
 3100 PRINT AT(9,12);"    "
 3110 PRINT AT(10,12);"     "
 3120 PRINT AT(11,12);"  "
 3130 A$=CHR$(144)
 3140 PAUSE 5:FOR I=0 TO 32:PRINT AT(0,0);STRING$(31,A$ )
 3150 PRINT AT(31,0);STRING$(31,A$):FOR M=0 TO 31:PRINT AT(M,0);A$:NEXT M
 3160 FOR M=0 TO 31:PRINT AT(M,31);A$:NEXT M :A$=CHR$(208+I):NEXT I:WINDOW
 3170 WINDOW 0,31,0,0:PRINT SPC(31):WINDOW
 3180 WINDOW 31,31,0,31:PRINT SPC(31):WINDOW
 3190 QQ=RND(1):CLS:RETURN:ELSE 3190
 3200 PRINT AT(1,4);"M O R G E N A R B E I T"
 3210 PRINT AT(3,0);STRING$(32,""):PRINT AT(7,0);STRING$(32,"")
 3220 PRINT AT(15,0);"Nr  Name    Sollz. Istz.   X   Y"
 3230 FOR J=1 TO TZ:PRINT AT(J+16,0);A(J):PRINT AT(J+16,4);PF$(A(J))
 3240 ZL(J)=INT(240/(X(A(J))+Y(A(J)))):PRINT AT(J+16,26);X(A(J))
 3250 PRINT AT(J+16,13);ZL(J):ZV(J)=0:PRINT AT(J+16,29);Y(A(J)):NEXT J
 3260 FOR J=1 TO TZ:PRINT AT(J+3,0);J:NEXT J:AB=0       
 3270 AB=AB+1:FOR J=1 TO TZ:A=A(J)
 3280 IF ZV(J)>=0 AND ZV(J)<90 THEN PRINT AT(J+3,0);STRING$(32," ")
 3290 G=INT(RND(J)*(10-X(A)))
 3300 IF ZV(J)>=0 THEN ZV(J)=ZV(J)+INT(RND(J)*(Y(A)-X(A)+G)+X(A)):ELSE GOTO 3330
 3310 IF ZV(J)>=90 THEN ZV(J)=-AB:PRINT;AT(J+3,31);CHR$(J+48):GOTO 3330
 3320 PRINT AT(J+3,INT(ZV(J)/3));CHR$(J+48)
 3330 NEXT J:TN=0:PAUSE 5
 3340 FOR J=1 TO TZ:IF ZV(J)<0 THEN TN=TN+1:NEXT J
 3350 IF TN<TZ THEN 3270
 3360 FOR J=1 TO TZ:ZV(J)=-ZV(J):PRINT AT(J+16,19);ZV(J)
 3370 IF ZV(J)<ZL(J)-ZL(J)*0.1 THEN Y(A(J))=Y(A(J))+1
 3380 IF ZV(J)<ZL(J)-ZL(J)*0.2 THEN Y(A(J))=Y(A(J))+2
 3390 IF ZV(J)>ZL(J)+ZL(J)*0.1 THEN X(A(J))=X(A(J))-1
 3400 IF ZV(J)>ZL(J)+ZL(J)*0.2 THEN X(A(J))=X(A(J))-2
 3410 PRINT AT(J+16,26);X(A(J))
 3420 PRINT AT(J+16,29);Y(A(J)):NEXT J
 3430 PRINT AT(31,25);"<ENTER>"
 3440 I$=INKEY$:IF I$=CHR$(13) THEN RETURN:ELSE 3440
 3450 CLS:IF BP(O)>=8 THEN RETURN
 3460 PRINT AT(10,2);"Auf Grund des verminderten"
 3470 PRINT AT(11,2);"Trainingsbestandes von nur":PRINT AT(13,13);BP(O)
 3480 EK=2^(9-BP(O)):EK=EK*1000
 3490 PRINT AT(15,0);"Pferden wird Ihr Etat um",EK
 3500 PRINT AT(16,8);"Mark gekuerzt":PAUSE 45
 3510 BV(O)=BV(O)-EK
 3520 CLS
 3530 RETURN
 3540 RETURN
 3550 CLS
 3560 IF BP(O)<8 THEN RETURN
 3570 PRINT AT(10,3);"Auf Grund des erhoehten"
 3580 PRINT AT(11,3);"Trainingsaufkommens von":PRINT AT(13,13);BP(O)
 3590 PRINT AT(15,3);"Pferden erhalten Sie einen"
 3600 EK=(BP(O)-7)*4000:PRINT AT(16,3);"Etatzuschuss von"
 3610 PRINT AT(18,10);EK,"Mark."
 3620 PAUSE 45:BV(O)=BV(O)+EK:CLS:RETURN
 3630 CLS:IF BP(O)<6 THEN RETURN
 3640 PRINT AT(10,0);"Fuer hervorragende Leistungen"
 3650 PRINT AT(12,0);"beim Vollbluttraining erhalten"
 3660 PRINT AT(14,3);"Sie 10000 Mark Praemie!":PAUSE 45
 3670 BV(O)=BV(O)+10000:CLS:RETURN
 3680 PRINT AT(15,1);STRING$(31,""):PRINT AT(30,1);STRING$(31,"")     
 3690 WINDOW 15,31,1,1:PRINT STRING$(15,""):WINDOW 15,31,31,31           
 3700 PRINT STRING$(15,""):WINDOW:WINDOW 17,29,3,30:RETURN
 3800 REM ALZAN ADVENTURE
 3810 WINDOW 0,31,0,31:CLS:FOR B=0 TO 1:WINDOW 10,31,1,31
 3820 PRINT AT(16,6);"version for Z-1013":PRINT AT(17,9);"c/1988 rothe":PAUSE 10
 3830 PRINT "         "      
 3840 PRINT "                     "          
 3850 PRINT "                     "   
 3860 PRINT "                     "  
 3870 PRINT "           "  
 3880 PRINT "                     "  
 3890 PRINT "                     "  
 3900 PRINT "                     "     
 3910 PRINT "                     "     
 3920 PRINT "            "  
 3930 PRINT:PRINT " ***  A D V E N T U R E  ***":PAUSE 10:PRINT
 3940 Z=9:Y=19:I=0:L=1 :FOR M=0 TO 30 :WINDOW Z,Y,I,L:CLS:L=L+1:NEXT:PAUSE 1 :NEXT B
 3950 CLEAR 3072:WINDOW:CLS       
 3960 DIM C(5),O(11),S(10),A$(47),M$(19),O$(11),P$(2),V$(43):GOSUB 7020
 3970 R=1
 3980 A=47:O=11:V=43
 3990 DATA 0,0,3,3,0,0,0,15,16,0,4
 4000 RESTORE 3990
 4010 FOR X=1 TO O
 4020 READ O(X)
 4030 NEXT X
 4040 X=RND(-ABS(DEEK(30))/3600)
 4050 IF S(2)=0 THEN 4120
 4060 IF C(2)<>0 THEN C(2)=C(2)-1
 4070 IF S(3)<>0 THEN 4120
 4080 PRINT "ES IST DUNKEL. SIE SOLLTEN BES-","SER FUER ETWAS LICHT SORGEN,"
 4090 PRINT "SONST KOENNTE ES SCHWIERIGKEITEN","GEBEN."
 4100 IF C(3)<>0 THEN C(3)=C(3)-1
 4110 GOTO 4240
 4120 REM RAUMBESCHREIBUNG
 4130 PRINT
 4140 ON R GOSUB 6370,6460,6500,6530,6560,6590,6610,6630,6660,6680,6710,6750
 4150 IF R>12 THEN ON R-12 GOSUB 6770,6800,6830,6860,6890,6930,6960
 4160 F=0
 4170 FOR X=1 TO O
 4180 IF O(X)<>R THEN 4230
 4190 IF F<>0 THEN 4220
 4200 PRINT "FERNER GIBT ES HIER:":PRINT STRING$(20,"")   
 4210 F=1 
 4220 PRINT "   ";O$(X)
 4230 NEXT X
 4240 REM BEFEHLSVERARBEITUNG
 4250 T=1
 4260 GOTO 4540
 4270 IF C(1)<>0 THEN C(1)=C(1)-1
 4280 IF C(4)<>0 THEN C(4)=C(4)-1
 4290 PRINT AT(15,5);" <?> ---->  H E L P .":GOSUB 6420
 4300 PRINT AT(15,1);STRING$(29,"")   
 4310 WINDOW 20,21,1,30:CLS:GOSUB 8060:INPUT "  >";Y$:IF Y$="?"THEN GOSUB 8120
 4320 WINDOW 3,16,0,31:CLS:Y=0
 4330 P$(2)="00"
 4340 FOR W=1 TO 2
 4350 GOSUB 5600
 4360 IF Y>=LEN(Y$)THEN 4400
 4370 IF P$(W)="00"THEN 4350
 4380 NEXT W
 4390 W=W-1
 4400 IF P$(W)<>"00"THEN 4430
 4410 WINDOW 20,21,1,30:CLS:PRINT AT(20,9);"PARDON ?":WINDOW 3,16,0,31
 4420 GOTO 4050
 4430 REM FESTSTELLEN OB STANDORTS-
 4440 Z=1:IF P$(2)<>"00"AND P$(1)>P$(2)THEN N$=P$(1):P$(1)=P$(2):P$(2)=N$
 4450 T$=MID$(M$(R),Z,2)
 4460 IF T$="00"THEN 4520
 4470 IF T$<>P$(1)THEN 4500
 4480 R=VAL(MID$(M$(R),Z+2,2))
 4490 GOTO 4050
 4500 Z=Z+4
 4510 GOTO 4450
 4520 T=0
 4530 M=0
 4540 REM FESTSTELLEN OB BEDINGUNGEN              ERFUELLT WERDEN MUESSEN
 4550 C=0
 4560 C=C+1
 4570 IF T=0 THEN 4600
 4580 E$=C$(C)
 4590 GOTO 4710
 4600 IF A>=C THEN 4670
 4610 IF M<>0 THEN 4240
 4620 WINDOW 19,21,1,30:CLS       
 4630 PRINT " SIE KOENNEN NICHT";
 4640 IF VAL(P$(1))<13 THEN PRINT " DIESEN WEG","           GEHEN";
 4650 PRINT ".":PAUSE 30:CLS: WINDOW 3,17,0,31
 4660 GOTO 4050
 4670 IF LEFT$(A$(C),2)<>P$(1)THEN 4560
 4680 Y$=MID$(A$(C),3,2)
 4690 IF Y$<>"00"AND Y$<>P$(2)THEN 4560
 4700 E$=MID$(A$(C),5)
 4710 REM ABFRAGEN DER BEDINGUNGSTABELLE
 4720 E=1
 4730 IF MID$(E$,E,1)="."THEN 4880
 4740 P=ASC(MID$(E$,E,1))-64
 4750 N=VAL(MID$(E$,E+1,2))
 4760 ON P GOSUB 4800,4810,4820,4830,4840,4850,4860,4870
 4770 IF K=0 THEN 4560
 4780 E=E+3
 4790 GOTO 4730
 4800 K=-(N=R):RETURN
 4810 K=-(O(N)=R OR O(N)<0):RETURN
 4820 K=-(O(N)<>R AND O(N)>=0):RETURN
 4830 K=-(O(N)<0):RETURN
 4840 K=S(N):RETURN
 4850 K=-(S(N)=0):RETURN
 4860 K=-(C(N)=1):RETURN
 4870 K=-(INT(RND(1)*100+1<=N)):RETURN
 4880 REM DURCHFUEHREN DER AKTIONEN
 4890 M=1
 4900 E=E+1
 4910 IF MID$(E$,E,1)="."THEN 4560
 4920 P=ASC(MID$(E$,E,1))-64
 4930 IF MID$(E$,E+1,1)<>"."THEN N=VAL(MID$(E$,E+1,2))
 4940 B=0
 4950 ON P GOSUB 5000,5100,5200,5270,5310,5330,5350,5380,5420,5440,5470,5490
 4960 IF P>12 THEN ON P-12 GOSUB 5500,5520,5540,5560,5590
 4970 ON B GOTO 4050,4240,4270
 4980 E=E+3
 4990 GOTO 4910
 5000 PRINT
 5010 PRINT "SIE TRAGEN:"
 5020 F=1
 5030 FOR X=1 TO O
 5040 IF O(X)>=0 THEN 5070
 5050 PRINT "   ";O$(X)
 5060 F=0
 5070 NEXT X
 5080 IF F=1 THEN PRINT "   NICHTS."
 5090 B=1:RETURN
 5100 IF S(1)<4 THEN 5140
 5110 PRINT "SO VIEL KOENNEN SIE NICHT TRAGEN"
 5120 B=1
 5130 RETURN
 5140 IF O(N)=-1 THEN 5180
 5150 O(N)=-1
 5160 S(1)=S(1)+1
 5170 RETURN
 5180 PRINT "SIE HABEN ES BEREITS."
 5190 GOTO 5120
 5200 IF O(N)=-1 THEN 5240
 5210 PRINT "SIE HABEN ES NICHT."
 5220 B=1
 5230 RETURN
 5240 O(N)=R
 5250 S(1)=S(1)-1
 5260 RETURN
 5270 PRINT
 5280 ON N GOSUB 5970,5990,6030,6050,6070,6090,6110,6130,6150,6170,6190,6210
 5290 IF N>12 THEN ON N-12 GOSUB 6230,6270,6290,6310
 5300 RETURN
 5310 S(N)=1
 5320 RETURN
 5330 S(N)=0
 5340 RETURN
 5350 C(N)=VAL(MID$(E$,E+3,2))
 5360 E=E+2
 5370 RETURN
 5380 X=O(N)
 5390 O(N)=O(N+1)
 5400 O(N+1)=X
 5410 RETURN
 5420 O(N)=R
 5430 RETURN
 5440 IF O(N)<0 THEN S(1)=S(1)-1
 5450 O(N)=0
 5460 RETURN
 5470 R=N
 5480 RETURN
 5490 PRINT " OKEY."
 5500 B=2
 5510 RETURN
 5520 B=3
 5530 RETURN
 5540 B=1
 5550 RETURN
 5560 PRINT "SIE WOLLEN AUFGEBEN ?"
 5570 INPUT W$
 5580 IF LEFT$(W$,1)<>"J"THEN 5540
 5590 GOTO 7010
 5600 REM SCHLUESSELWORT ABTRENNEN
 5610 W$=""
 5620 P$(W)="00"
 5630 GOSUB 5840
 5640 IF D=1 THEN RETURN
 5650 FOR Q=1 TO 4
 5660 W$=W$+MID$(Y$,Y,1)
 5670 GOSUB 5790
 5680 IF D=1 THEN 5720
 5690 NEXT Q
 5700 GOSUB 5790
 5710 IF D=0 THEN 5700
 5720 W$=W$+STRING$(4-LEN(W$)," "):IF W$="    "THEN RETURN
 5730 FOR Q=1 TO V
 5740 IF W$=MID$(V$(Q),3)THEN 5770
 5750 NEXT Q
 5760 RETURN
 5770 P$(W)=LEFT$(V$(Q),2)
 5780 RETURN
 5790 Y=Y+1
 5800 D=-(Y>LEN(Y$))
 5810 IF D=1 THEN RETURN
 5820 D=-(MID$(Y$,Y,1)=" ")
 5830 RETURN
 5840 Y=Y+1
 5850 D=-(Y>LEN(Y$))
 5860 IF D=1 THEN RETURN
 5870 IF MID$(Y$,Y,1)=" "THEN 5840
 5880 RETURN
 5890 CLS
 5900 FOR N=1 TO 50
 5910 PRINT N;V$(N)
 5920 NEXT N
 5930 GOTO 10
 5940 REM AKTIONSMELDUNGEN
 5950 REM MELDUNG 1 BEWIRKT
 5960 REM SPRUNG ZUR ZEILE 7010
 5970 PRINT "ACH DU LIEBE GUETE, SCHON TOT!"
 5980 RETURN
 5990 PRINT TAB(8);"---HOPPLA---"
 6000 PRINT "EL GRABBO, DER DIEB DER STADT,","SCHNAPPT SICH IHR GELD UND"
 6010 PRINT "VERSCHWINDET IM DICHTEN NEBEL."
 6020 RETURN
 6030 PRINT "HALTET DEN DIEB, RUFT DER PLATZ-";"ANWEISER, ABER SIE ENTKOMMEN" 
 6040 RETURN
 6050 PRINT "DER DECKEL IST BEREITS OFFEN !! "
 6060 RETURN
 6070 PRINT "ES KOSTET MEHR, ALS SIE AUF-    ";"BRINGEN KOENNEN."
 6080 RETURN
 6090 PRINT "SEHR WOHL, MEIN HERR!
 6100 RETURN
 6110 PRINT "DER KANALDECKEL IST OFFEN."
 6120 RETURN
 6130 PRINT "DER KANALDECKEL IST VERSCHLOSSEN."
 6140 RETURN
 6150 PRINT "DER LADENBESITZER IST GROESSER","ALS SIE..."
 6160 RETURN
 6170 PRINT "SIE BRAUCHEN EINE LEITER, UM","UEBER DIESE MAUER ZU KOMMEN."
 6180 RETURN
 6190 PRINT "ES IST BEREITS AN!"
 6200 RETURN
 6210 PRINT "WAS FUER EIN GEISTESBLITZ!"
 6220 RETURN
 6230 PRINT "SIE UEBERWAELTIGEN HEIMLICH","DIE WACHEN UND KOENNEN SICH"
 6240 PRINT "NOCH EIN BUENDEL BANKNOTEN  ","SCHNAPPEN,KEINER HAT ETWAS"
 6250 PRINT "GEMERKT (LUSTIGES VOELKCHEN","DIESE ALZANER)."
 6260 RETURN
 6270 PRINT "SIE HABEN ALLES GENOMMEN, WAS","HIER WAR."
 6280 RETURN
 6290 PRINT "ICH SEHE KEINE LAMPE...?"
 6300 RETURN
 6310 PRINT "DAS KINO IST FUER EINEN PRIVA-  ";"TEN ZWECK GEMIETET !";:PAUSE 10
 6320 RETURN
 6330 RETURN
 6340 REM STANDORT BESCHREIBUNGEN
 6350 REM STANDORT 1 BEWIRKT
 6360 REM SPRUNG ZUR ZEILE 8010
 6370 PRINT "  +++ WILLKOMMEN IN ALZAN +++":PAUSE 20 
 6380 PRINT "SIE MUESSEN DIE MAUERN UEBERWIN-";"DEN, UM AUS DIESER STADT VON",
 6390 PRINT "DIEBEN UND HALSABSCHNEIDERN ZU","    E N T K O M M E N !!"
 6400 IF INKEY$<>CHR$(13)THEN 6400
 6410 WINDOW 2,16,0,31:CLS:  GOSUB 8040
 6420 FOR I=1 TO 3 :PRINT AT(31-(5*RND(1)),30*RND(1)+1);CHR$(14)
 6430 PRINT AT(31-(5*RND(1)),30*RND(1)+1);CHR$(23)
 6440 FOR M=0 TO 49:PRINT AT(31-(5*RND(1)),30*RND(1)+1);" ":NEXT M,I:WINDOW 3,16,0,31
 6450 RETURN
 6460 PRINT "SIE SIND IN DER HAUPTSTRASSE","VOR EINEM KURZWARENLADEN. DIE"
 6470 PRINT "HAUPTSTRASSE VERLAEUFT IN OST-","WEST-RICHTUNG. EINE KLEINE"      
 6480 PRINT "GASSE FUEHRT NEBEN DEM LADEN","NACH NORDEN.";
 6490 RETURN
 6500 PRINT "SIE SIND IM LADEN. DER LADEN-   ";"BESITZER SIEHT VERSCHLAGEN AUS."
 6510 PRINT "ABER ER HAT VIELE SCHOENE WAREN ";"IM SCHAUFENSTER."              
 6520 RETURN
 6530 PRINT "SIE SIND IN EINER SCHMALEN GAS-","SE HINTER GROSSEN HAEUSERN."
 6540 PRINT "UNTER EINER FEUERLEITER STEHEN","VIELE VOLLE MUELLTONNEN." 
 6550 RETURN
 6560 PRINT "SIE SIND AUF DER FEUERLEITER.","SIE FUEHRT ZU EINER TUER IN DER"
 6570 PRINT "HAEUSERN." 
 6580 RETURN
 6590 PRINT "SIE SIND UEBER EINE VERSTECKTE","TREPPE IN DEN LADEN GELANGT." 
 6600 RETURN
 6610 PRINT "SIE BEFINDEN SICH AUF SCHLEICH-","WEGEN ZWISCHEN DEN HAEUSERN." 
 6620 RETURN
 6630 PRINT "JETZT SIND SIE AN DER STADT-","MAUER. HIER BEFINDET SICH EINE"
 6640 PRINT "UNBENUTZTE TUER IN DER MAUER." 
 6650 RETURN
 6660 PRINT "SIE SIND AN EINER KREUZUNG.   " 
 6670 RETURN
 6680 PRINT "SIE SIND AN DER STADTMAUER. DER","NEBEL IST SO DICK, DASS SIE"
 6690 PRINT "NICHT WEIT SEHEN KOENNEN." 
 6700 RETURN
 6710 PRINT "SIE STUERZEN VON DER MAUER -","GENAU AUF DIE SEE-KLIPPEN 100"
 6720 PRINT "METER TIEF UNTEN. MACHT NICHTS.","VIELLEICHT HABEN SIE NAECHSTES"
 6730 PRINT "MAL MEHR GLUECK!"
 6740 RETURN
 6750 PRINT "SIE SIND VOR DER STADTBANK.   "  
 6760 RETURN
 6770 PRINT "IN DER BANK BEFINDEN SICH VIELE","WAECHTER DIE ZIEMLICH GELANG-" 
 6780 PRINT "WEILT DREINSCHAUEN." 
 6790 RETURN
 6800 PRINT "SIE SIND IN EINER SACKGASSE","ANGELANGT, DOCH DA BEFINDET"
 6810 PRINT "SICH EIN KANALDECKEL IN DER","STRASSE..." 
 6820 RETURN
 6830 PRINT "SIE SIND IN EINER KLEINEN   ","NISCHE UNTERHALB(DER EINSTIEGS-",
 6840 PRINT "OEFFNUNG. EIN GANG FUEHRT NACH","SUEDEN." 
 6850 RETURN
 6860 PRINT "DER GANG FUEHRT ZU EINER ALTEN","GRABSTAETTE, IN DER VIELE SKE-",
 6870 PRINT "LETTE VERSTREUT HERUMLIEGEN." 
 6880 RETURN
 6890 PRINT "DER PLATZANWEISER WILL SIE  ","NICHT HINEINLASSEN, DA DAS PRO-",
 6900 PRINT "GRAMM BEREITS ANGEFANGEN HAT.","ER VERSPERRT IHNEN DEN WEG MIT"
 6910 PRINT "SEINER TASCHENLAMPE." 
 6920 RETURN
 6930 PRINT "SIE SIND VOR DEM KINO. DER  ","LAERM VON SCHUESSEN DRINGT AUS"
 6940 PRINT "DEM INNEREN." 
 6950 RETURN
 6960 CLS:BEEP:PRINT "**** HERZLICHEN GLUECKWUNSCH ****":PRINT:BEEP
 6970 PRINT "SIE KONNTEN AUS DER STADT","FLIEHEN. DAS IST WIRKLICH EIN"
 6980 PRINT "SELTENER FALL. GUT GEMACHT."
 6990 RETURN
 7000 RETURN
 7010 GOTO 10
 7020 PAPER 1:INK 3:CLS:WINDOW 0,19,0,31
 7030 RESTORE 7040
 7040 DATA "EIN BRENNENDE LAMPE"
 7050 DATA "EINE LAMPE"
 7060 DATA "EINE LEITER"
 7070 DATA "EINEN HAMMER"
 7080 DATA "EINEN HAMMER"
 7090 DATA "EIN BUENDEL BANKNOTEN"
 7100 DATA "KANALDECKEL"
 7110 DATA "EIN SACK NAEGEL"
 7120 DATA "EINE EINTRITTSKARTE"
 7130 DATA "EINE UNGEHOBELTE LEITER"
 7140 DATA "ETWAS HOLZ"
 7150 DATA "01N   ","01NORD","02O   ","02OSTE","03S   ","03SUED"
 7160 DATA "04W   ","04WEST","05H   ","05HINA","06T   ","06HINU"
 7170 DATA "13NEHM","14STEL","14WERF","15HINE","14LEGE","16RAUS"
 7180 DATA "32TASC","16VERL","32LAMP","18LEIT","19HAMM","34BANK"
 7190 DATA "20GELD","22SACK","22NAEG","23KART","05DURC","15REIN"
 7200 DATA "29OEFF","05HOCH","30BAST","30BAUE","31ZUEN","35HABE"
 7210 DATA "17KAUF","33HOLZ","34KLAU","34RAUB","35TRAG","36AUFH","37SCHA"
 7220 DATA "00"
 7230 DATA "01040209041800"
 7240 DATA "00"
 7250 DATA "0302050500"
 7260 DATA "0604050700"
 7270 DATA "00"
 7280 DATA "0108031000"
 7290 DATA "030700"
 7300 DATA "011202100314040200"
 7310 DATA "040900"
 7320 DATA "00"
 7330 DATA "0209041800"
 7340 DATA "00"
 7350 DATA "010900"
 7360 DATA "031600"
 7370 DATA "011500"
 7380 DATA "00"
 7390 DATA "0112020200"
 7400 DATA "00"
 7410 DATA "A01.K02O."
 7420 DATA "A16H30.G0121."
 7430 DATA "G01.D01Q."
 7440 DATA "B06H10.D02J06."
 7450 DATA "A14E07.D07N."
 7460 DATA "A14F07.D08N."
 7470 DATA "A11.Q."
 7480 DATA "A19.Q."
 7490 DATA "A06.K03O."
 7500 DATA ".N."
 7510 DATA "1332B01.B01E03L."
 7520 DATA "1332A17C01C02.I02B02D03K18E10O."
 7530 DATA "1718B03.D05N."
 7540 DATA "1319B05.B05L."
 7550 DATA "1320B06.B06L."
 7560 DATA "2900A14E07.D04N."
 7570 DATA "2900A14.E07M."
 7580 DATA "1322B08.B08L."
 7590 DATA "1323B09.B09L."
 7600 DATA "1432B01.C01F03L."
 7610 DATA "1432B02.C02L."
 7620 DATA "1419B05.C05L."
 7630 DATA "1420B06.C06L."
 7640 DATA "1422B08.C08L."
 7650 DATA "1423B09.C09L."
 7660 DATA "0500A10C10.D10M."
 7670 DATA "0500A08C10.D10M."
 7680 DATA "0500A10.K11O."
 7690 DATA "0500A08.K19O."
 7700 DATA "0500A15.F02K14O."
 7710 DATA "0600A14.E02K15O."
 7720 DATA "3100D02.H01E03L."
 7730 DATA "3100B01.D11N."
 7740 DATA "1719B04B06.H04J06B05L."
 7750 DATA "1719B04B09.H04D06B05M."
 7760 DATA "3000B05B11B08.D06I10J08J11M."
 7770 DATA "1333B11.B11L."
 7780 DATA "1M3B11.C11L."
 7790 DATA "1500A02.K03O."
 7800 DATA "1500A12.K13O."
 7810 DATA "1500A18F10.K17O."
 7820 DATA "1600A03.K02O."
 7830 DATA "1600A13.K12O."
 7840 DATA "1600A17.K18O."
 7850 DATA "1500A05.K06O."
 7860 DATA "3400A03.D09M."
 7870 DATA "3400A13E08.D14M."
 7880 DATA "3400A13.E08D13I06B06M."
 7890 DATA "1500A18E10.D16M."
 7900 DATA "1318B10.B10L."
 7910 DATA "1418B10.C10L."
 7920 DATA "1300B03.D09M."
 7930 DATA "1332B02.B02L."
 7940 DATA "3500.A."
 7950 DATA "3600.P."
 7960 DATA "3700.O."
 7970 DATA "5000.N."
 7980 FOR N=1 TO 11:READ O$(N):NEXT N
 7990 FOR N=1 TO 43:READ V$(N):NEXT N
 8000 FOR N=1 TO 19:READ M$(N):NEXT N
 8010 FOR N=1 TO 10:READ C$(N):NEXT N
 8020 FOR N=1 TO 47:READ A$(N):NEXT N
 8030 RETURN
 8040 WINDOW 25,30,1,30:FOR I=0 TO 31:PRINT AT(24,I);"":PRINT AT(31,I);"":NEXT    
 8050 FOR Y=24 TO 31:PRINT AT(Y,0);"":PRINT AT(Y,31);"":NEXT Y:CLS:RETURN
 8060 FOR I=0 TO 30:PRINT AT(17,I);"":PRINT AT(22,I);"":NEXT         
 8070 FOR I=18 TO 22:PRINT AT(I,0);"":PRINT AT(I,31);"":NEXT  
 8080 FOR I=0 TO 1:PRINT AT(15,0);"":PRINT AT(15,30);""     
 8090 PRINT AT(15,1);STRING$(29," "):PAUSE 3 :PRINT AT(15,0);""   
 8100 PRINT AT(15,30);"":PRINT AT(15,6);"Was machen Sie nun ?":PAUSE 5:NEXT:I=0 
 8110 RETURN       
 8120 WINDOW 2,21,0,31:CLS:FOR I=0 TO 31:PRINT AT(3,I);"":PRINT AT(22,I);"" 
 8130 NEXT   
 8140 FOR I=3 TO 22:PRINT AT(I,0);"":PRINT AT(I,31);"":NEXT   
 8150 PRINT AT(5,5);"H E L P - M E N U E":FOR I=5 TO 23
 8160 PRINT AT(6,I);"":NEXT 
 8170 WINDOW 8,21,2,15:PRINT "Hinein":PRINT "Hinaus":PRINT "Hinunter":PRINT "Rein"
 8180 PRINT "Raus":PRINT "Durch":PRINT "Hoch":PRINT "Hinueber":PRINT "Nehme"
 8190 PRINT "Werfe":PRINT "Lege":PRINT "Bastle":WINDOW 8,21,12,30:PRINT "Stelle"
 8200 PRINT "Baue":PRINT "Zuende":PRINT "Habe":PRINT "Kaufe":PRINT "Klaue"   
 8210 PRINT "Raube":PRINT "Schaffe":PRINT "Trage":PRINT "Aufheben"
 8220 WINDOW 8,21,22,30:PRINT "NORD":PRINT "WEST":PRINT "SUED":PRINT "OST"
 8230 PRINT AT(19,25);">ENT<"                
 8240 I$=INKEY$:IF I$=""THEN PAUSE 5:PRINT AT(19,25);"<ENT>":PAUSE 5:GOTO 8230
 8250 WINDOW 3,22,0,31:CLS:RETURN
 8260 WINDOW:CLS:GOSUB 12080
 8270 GOSUB 10820:WINDOW 0,19,0,31:CLS
 8280 IF NV=1 THEN 8320
 8290 WINDOW 0,31,0,31
 8300 ON OZ GOSUB 8620,9050,8740,8880,8830,8790,8990,8970
 8310 IF OZ> 8 THEN GOSUB 8660
 8320 WINDOW 30,30,0,31: PRINT "WAS NUN";  
 8330 PRINT AT(0,0);"SIE HABEN NOCH",WV,"LITER WASSER."
 8340 GOSUB 8490:WV=WV-1:IF WV=0 THEN PRINT "IHR WASSERVORRAT GING ZUENDE,"
 8350 IF WV=0 THEN PRINT "SIE SIND VERDURSTET.":PAUSE 55:GOTO 10
 8360 IF TZ=0 THEN NV=1:GOTO 8280
 8370 IF TZ=1 OR TZ>23 THEN GOSUB 8560:NV=0:ELSE GOSUB 8590:NV=1
 8380 IF(TZ=1 AND RV=0)OR(TZ>1 AND TZ<24 AND GV=0)THEN NV=1:GOTO 8280
 8390 ON TZ GOSUB 9680,9620,9570,9500,9450,9350,9390,9390,9350,9370
 8400 IF TZ>10 THEN ON TZ-10 GOSUB 9320,9240,9240,9240,9200,9370,9160,9160,9160,9110
 8410 IF TZ>20 THEN ON TZ-20 GOSUB 9110,9100,9100,9680,9680,9680,9680
 8420 GOTO 8280
 8430 PRINT "SIE BESITZEN:":KK=0:K=PV:GOSUB 8440:RETURN
 8440 FG=32:FOR I=1 TO 6
 8450 IF K<FG THEN H(I)=0:GOTO 8480
 8460 K=K-FG:H(I)=1
 8470 IF KK=0 THEN PRINT D$(I)
 8480 FG=FG/2:NEXT:RETURN
 8490 INPUT I$:IL=LEN(I$):I1$="":TZ=0:FOR I=1 TO IL:A$=MID$(I$,I,1)
 8500 IF A$=" "THEN 8520
 8510 I1$=I1$+A$
 8520 NEXT I:I1$=I1$+" ":WINDOW 28,31,0,31:CLS
 8530 FOR I=1 TO 27:IS=INSTR(H$(I),I1$):IF IS=0 THEN NEXT I:ELSE 8550
 8540 PRINT "PARDON, DASS VERSTEHE ICH NICHT.":PAUSE 25:CLS:RETURN
 8550 TZ=I:RETURN
 8560 RV=0:FOR I=1 TO 4:IS=INSTR(R$(I),I1$):IF IS=0 THEN NEXT I:ELSE 8580
 8570 PRINT "BITTE HIMMELSRICHTUNG ANGEBEN!":PAUSE 25:CLS:RETURN
 8580 RV=I:RETURN
 8590 GV=0:FOR I=1 TO 16:IS=INSTR(G$(I),I1$):IF IS=0 THEN NEXT I:ELSE 8610
 8600 PRINT "DAS GEHT NICHT!":PAUSE 25:CLS:RETURN 
 8610 GV=I:RETURN
 8620 CLS:FOR I=1 TO 12:PRINT AT(I+6,0);E$(I+13):NEXT     
 8630!
 8640 PRINT AT(29,0);"SIE SIND IN IHREM BASISLAGER."
 8650 RETURN
 8660 CLS:VG=OZ/4-INT(OZ/4):IF VG=0 THEN VG=1
 8670 VG=(VG-.25)*28+1:FOR I=0 TO 6:PRINT AT(I+2,0);A$(VG+I):NEXT
 8680 IF OZ<9 THEN RETURN
 8690 PRINT AT(28,0);"IN DER WUESTE"
 8700 IF OZ=14 THEN K=DV(14):KK=1:GOSUB 8440:IF H(3)=1 THEN 8720
 8710 RETURN
 8720 PRINT AT(29,0);"Hier liegt ein weisses Gewand."
 8730 RETURN
 8740 GOSUB 8660:FOR I=1 TO 15:PRINT AT(I+5,16);B$(30+I):PRINT AT(I+12,25);B$(I+15)  
 8750 NEXT:PRINT AT(28,1);"DER WEISSE OBELISK"   
 8760 K=DV(3):KK=1:GOSUB 8440
 8770 IF H(4)=1 THEN PRINT AT(29,1);"Im Sand liegt ein Kompass."
 8780 RETURN
 8790 CLS:FOR I=1 TO 13:PRINT AT(I+5,0);E$(I):NEXT
 8800 PRINT AT(28,1);"EINE GEISTERSTADT"
 8810 IF RB=0 THEN PRINT AT(29,0);"Du wirst von Raeubern ueberfallen."
 8820 RETURN
 8830 GOSUB 8660:FOR I=1 TO 11:PRINT AT(I+8,10);E$(I+25):NEXT
 8840 PRINT AT(28,1);"Eine SPHINX bewacht diesen Ort."
 8850 IF SV=0 THEN PRINT AT(29,0);"Sie toetet Dich als Eindringling":PAUSE 50     
 8860 GOTO 10
 8870 RETURN
 8880 CLS:GOSUB 8660:FOR I=7 TO 22:PRINT AT(I-4,18);C$(I):NEXT
 8890 FOR I=15 TO 29:PRINT AT(I-10,0);B$(I):PRINT AT(I-13,6);B$(I):NEXT
 8900 FOR I=23 TO 28:PRINT AT(I-1,22);C$(I):PRINT AT(I-3,12);C$(I)
 8910 PRINT AT(I-1,0);C$(I):NEXT I
 8920 PRINT AT(28,0);"In der Karawanserei gibt es"
 8930 PRINT AT(29,1);"Kamele zu kaufen." 
 8940 K=DV(4):KK=1:GOSUB 8440
 8950 IF H(6)=1 THEN PRINT AT(31,0);"auch ein Schwert wird angeboten."
 8960 RETURN
 8970 CLS:GOSUB 8660:FOR I=1 TO 6:PRINT AT(I+15,18);C$(I):NEXT
 8980 PRINT AT(29,1);"Im Sand liegen einige Gerippe.":RETURN
 8990 CLS:GOSUB 8660:FOR I=7 TO 22:PRINT AT(I-3,12);C$(I):NEXT
 9000 FOR I=32 TO 38:PRINT AT(I-20,20);C$(I):NEXT
 9010 PRINT AT(27,1);"DAS HAUS DES EINSIEDLERS"
 9020 PRINT AT(28,0);"der Einsiedler steht in der Tuer "
 9030 IF SV=0 THEN PRINT AT(29,1);"und bittet um eine Gabe."
 9040 RETURN
 9050 CLS:GOSUB 8660:FOR I=23 TO 28:PRINT AT(I-1,22);C$(I):NEXT:PAUSE 1
 9060 FOR I=15 TO 29:PRINT AT(I-1,6);B$(I):PRINT AT(I-14,25);B$(I)
 9070 PRINT AT(I-12,0);B$(I):NEXT:FOR J=1 TO 10:PRINT AT(J+10,17);B$(J):NEXT
 9080 PRINT AT(29,6);"EINE OASE MIT BRUNNEN"
 9090 RETURN
 9100 WINDOW 0,19,0,31:CLS:PRINT:PRINT:GOSUB 8430:RETURN
 9110 IF OZ<>3 OR GV<>2 THEN PRINT "DAS GEHT NICHT !":RETURN
 9120 K=PV:KK=1:GOSUB 8440
 9130 IF H(2)=0 THEN PRINT "WELCHEN SCHLUESSEL?":RETURN
 9140 IF OB=0 THEN GOSUB 10520
 9150 IF OB=1 THEN GOSUB 10570
 9160 IF OZ=8 AND GV=13 OR GV=14 THEN GOSUB 10790:RETURN
 9170 IF OZ=3 AND GV=11 THEN GOSUB 10660:RETURN
 9180 IF OZ=5 AND GV=12 THEN GOSUB 10750:RETURN
 9190 PRINT "HIER GIBT ES NICHTS ZU SEHEN.":RETURN
 9200 IF OZ<>3 OR GV<>1 THEN PRINT "DAS GEHT NICHT. TUT MIR LEID.":RETURN
 9210 K=PV:KK=1:GOSUB 8440
 9220 IF H(1)=0 THEN PRINT "WELCHEN DIAMANTEN?":RETURN
 9230 OB=1:PRINT "ERLEDIGT!!":PV=PV-32:PRINT AT(14,17);"":RETURN        
 9240 IF OZ<>6 OR(GV<>9 AND GV<>10)THEN PRINT "DIES IST UNMOEGLICH.":RETURN
 9250 IF TZ=12 AND GV<>10 THEN PRINT "WEN WOLLEN SIE VERTEIDIGEN?":RETURN
 9260 IF TZ>12 AND GV<>9 THEN PRINT "WAS HABEN SIE VOR?":RETURN
 9270 K=PV:KK=1:GOSUB 8440
 9280 IF H(6)=0 THEN PRINT "Sie hatten keine Waffe bei sich..."
 9290 IF H(6)=0 THEN PRINT "und wurden leider getoetet !":PAUSE 50:GOTO 10
 9300 RB=1:PRINT "Sie haben die Raeuber besiegt !"
 9310 RETURN
 9320 K=PV:KK=1:GOSUB 8440
 9330 IF H(GV)=0 THEN PRINT "DAS HABEN SIE NICHT.":RETURN
 9340 PRINT "OKAY!!":PV=PV-GV(GV):DV(OZ)=DV(OZ)+GV(GV):RETURN
 9350 IF OZ<>5 OR GV<>8 THEN PRINT "DAS IST UNNOETIG.":RETURN
 9360 SV=2:PRINT " Die SPHINX ist gereizt gestimmt.":RETURN
 9370 IF OZ<>2 OR GV<>7 THEN PRINT "DAS GEHT NICHT.":RETURN
 9380 WV=30:PRINT "Sie haben wieder Wasservorrat.":RETURN
 9390 IF OZ<>5 AND(GV<>5 OR GV<>2)THEN PRINT "PARDON, ICH VERSTEHE NICHT.":RETURN
 9400 K=DV(5):KK=1:GOSUB 8440:IF H(2)=0 THEN PRINT "ES IST NICHTS DA!":RETURN
 9410 K=PV:KK=1:GOSUB 8440:IF H(5)=0 THEN PRINT "WEN WOLLEN SIE BELADEN?":RETURN
 9420 IF SV<2 THEN PRINT " Die SPHINX toetet den Dieb.":PAUSE 50:GOTO 15000
 9430 PV=PV+16:DV(5)=DV(5)-16
 9440 PRINT "Der Schluessel gehoert nun Ihnen":RETURN
 9450 IF OZ<>7 OR GV<>3 THEN PRINT "DAS IST NICHT DRIN.":RETURN
 9460 K=PV:KK=1:GOSUB 8440
 9470 IF H(3)=0 THEN PRINT "WELCHES GEWAND?":RETURN
 9480 EV=1:PRINT AT(28,0);"Der Einsiedler ist nun bereit"
 9490 PRINT AT(29,0);"Dir zu antworten.":RETURN
 9500 IF OZ<>7 OR GV<>15 THEN PRINT "DAS KOENNEN SIE NICHT TUN.":RETURN    
 9510 IF EV=0 THEN PRINT AT(28,0); "Der Einsiedler schweigt verbissen":RETURN
 9520 WINDOW:CLS:WINDOW 13,20,0,31      
 9530 SV=1:PRINT "DER EINSIEDLER VERRAET DIR"
 9540 PRINT "EINEN ZAUBERSPRUCH. AUSSERDEM "
 9550 PRINT "RAET ER, DEN WEISSEN OBELISK ZU" 
 9560 PRINT "SUCHEN":PRINT "DORT SOLL DER SCHATZ RUHEN!":PAUSE 50:RETURN
 9570 IF OZ<>4 OR(GV<>5 AND GV<>6)THEN PRINT "SO GEHT ES NICHT.":RETURN
 9580 K=DV(OZ):KK=1:GOSUB 8440
 9590 IF H(GV)=0 THEN PRINT "Das ist bereits ausverkauft !":RETURN
 9600 PRINT "Der Haendler gibt es Ihnen.":PV=PV+GV(GV)
 9610 DV(OZ)=DV(OZ)-GV(GV):RETURN
 9620 IF GV=2 THEN PRINT "Den Schluessel koennen Sie nicht allein tragen.": RETURN
 9630 IF GV>6 THEN PRINT "DAS IST NICHT MOEGLICH.":RETURN
 9640 IF GV>4 THEN PRINT "Dagegen hat der Haendler etwas!":RETURN
 9650 K=DV(OZ):KK=1:GOSUB 8440
 9660 IF H(GV)=0 THEN PRINT "DAS GIBT ES HIER GAR NICHT.":RETURN
 9670 PRINT "OKAY!":PV=PV+GV(GV):DV(OZ)=DV(OZ)-GV(GV):RETURN
 9680 NV=0:ON OZ GOTO 9710,9760,9800,9850,9910,9950,10020,10100
 9690 IF OZ>8 THEN ON OZ-8 GOTO 10140,10180,10240,10280,10330,10380,10410,10450
 9700 RETURN
 9710 ON RV GOTO 9720,9730,9740,9750
 9720 OZ=10:RETURN
 9730 PRINT "DORTHIN BRAUCHEN SIE NICHT GEHEN.":PAUSE 40:CLS:RETURN  
 9740 OZ=9:RETURN
 9750 OZ=11:RETURN
 9760 ON RV GOTO 9770,9780,9730,9790
 9770 OZ=5:RETURN
 9780 OZ=9:RETURN
 9790 OZ=3:RETURN
 9800 ON RV GOTO 9810,9820,9830,9840
 9810 OZ=13:RETURN
 9820 OZ=10:RETURN
 9830 OZ=2:RETURN
 9840 OZ=12:RETURN
 9850 ON RV GOTO 9860,9870,9880,9890
 9860 OZ=14:RETURN
 9870 OZ=11:RETURN
 9880 OZ=12:RETURN
 9890 BEEP:PRINT "Sie fielen in ein Loch voll":PRINT "    T R E I B S A N D "
 9900 PRINT "....Ruhen Sie in Frieden !":PAUSE 50:GOTO 10
 9910 ON RV GOTO 9920,9930,9730,9940
 9920 OZ=15:RETURN
 9930 OZ=2:RETURN
 9940 OZ=13:RETURN
 9950 IF RB=1 THEN ON RV GOTO 9980,9990,10000,10010
 9960 PRINT "Die Raeuber haben Sie nicht"
 9970 PRINT "einfach gehen lassen.":PRINT "Nun ist's aus.":PAUSE 50:GOTO 10
 9980 OZ=7:RETURN
 9990 OZ=12:RETURN
10000 OZ=13:RETURN
10010 OZ=14:RETURN
10020 ON RV GOTO 9730,10030,10040,10070
10030 OZ=6:RETURN
10040 BEEP:BEEP:PRINT "Es war einmal ein Loewe ...":PAUSE 20:PRINT             
10050 PRINT "Er hatte eine hungrige Familie":PAUSE 20:PRINT
10060 PRINT "Sie wurden leider sein Opfer (!)":PAUSE 50:GOTO 10
10070 BEEP:BEEP:PRINT "Bis heute weiss leider keiner...":PAUSE 20
10080 PRINT "wohin Sie sich in der":PAUSE 20:PRINT
10090 PRINT "Wueste verirrt haben.":PAUSE 50:GOTO 10
10100 K=PV:KK=1:GOSUB 8440
10110 IF H(4)=1 THEN ON RV GOTO 10070,10130,9890,9730
10120 PRINT "SIE LAUFEN IM KREISE!":PAUSE 10:RETURN
10130 OZ=15:RETURN
10140 ON RV GOTO 10150,9730,10160,10170
10150 OZ=3:RETURN
10160 OZ=2:RETURN
10170 OZ=1:RETURN
10180 ON RV GOTO 10190,10200,10210,9730
10190 OZ=3:RETURN
10200 OZ=1:RETURN
10210 BEEP:PRINT "Man fand Sie drei Wochen spaeter":PAUSE 20
10220 PRINT "tot unter einem Felsen.":PAUSE 20:PRINT
10230 PRINT "Sie sind leider abgestuerzt !":PAUSE 50:GOTO 10
10240 ON RV GOTO 10250,9730,10260,10270
10250 OZ=10:RETURN
10260 OZ=1:RETURN
10270 OZ=4:RETURN
10280 ON RV GOTO 10290,10300,10310,10320
10290 OZ=6:RETURN
10300 OZ=11:RETURN
10310 OZ=3:RETURN
10320 OZ=4:RETURN
10330 ON RV GOTO 10340,10350,10360,10370
10340 OZ=16:RETURN
10350 OZ=3:RETURN
10360 OZ=5:RETURN
10370 OZ=6:RETURN
10380 ON RV GOTO 9730,10390,10400,9890
10390 OZ=4:RETURN
10400 OZ=6:RETURN
10410 ON RV GOTO 10420,10430,9730,10440
10420 OZ=8:RETURN
10430 OZ=5:RETURN
10440 OZ=16:RETURN
10450 ON RV GOTO 10460,10490,10500,10510
10460 PRINT "Sie wurden in der Wueste" 
10470 PRINT "vom einzigen Auto in der Woche":PRINT "ueberfahren.":PAUSE 50
10480 GOTO 10
10490 OZ=13:RETURN
10500 OZ=15:RETURN
10510 OZ=6:RETURN
10520 WINDOW 0,19,0,31:CLS
10530 PRINT "unter Ihnen oeffnet":PRINT "sich ploetzlich der Boden":PAUSE 20:PRINT
10540 PRINT "und Sie stuerzen in eine Grube":PAUSE 20:PRINT
10550 PRINT "in der giftige Schlangen schon":PRINT
10560 PRINT "auf Sie warten... ":PAUSE 50:GOTO 10
10570 GOSUB 12460:PAUSE 30:WINDOW:CLS:WINDOW 5,31,0,31
10580 PRINT "WIE VON GEISTERHAND BEWEGT,":PAUSE 20:PRINT
10590 PRINT "SINKT DER WEISSE OBELISK":PAUSE 20:PRINT "LANGSAM IN DEN SAND EIN." 
10600 PRINT:PAUSE 20:PRINT "AN SEINER STELLE ERSCHEINT":PAUSE 20
10610 PRINT "KURZE ZEIT DANACH DER SCHATZ,":PAUSE 20:PRINT: PRINT "NACH DEM SIE SO"
10620 PRINT "LANGE GESUCHT HABEN. ":PAUSE 20:PRINT:PRINT "  SOLCH EIN GLUECK":PAUSE 2
10630 PRINT "HAT MAN IM LEBEN NUR EINMAL !":PAUSE 30:PRINT
10640 PRINT "SIE SOLLTEN ALLEN":PAUSE 30
10650 PRINT "EINEN AUSGEBEN , MEIN FREUND !!":PAUSE 50:PRINT "(c) RO":END 
10660 WINDOW 0,19,0,31:CLS
10670 PRINT:PRINT: PRINT "SIE SEHEN DEN WEISSEN OBELISKEN."
10680 PRINT "AUF SEINER VORDERSEITE IST DAS"
10690 PRINT "BILD EINER GOTTHEIT DARGESTELLT."
10700 PRINT "ALLERDINGS FEHLT DIESEM GOTT"
10710 PRINT "EINES SEINER DIAMANTENEN AUGEN."
10720 PRINT "DER WEIT GEOEFFNETE MUND":PRINT "ERINNERT AN EIN SCHLUESSELLOCH"
10730 PRINT "ABER FUER EINEN GIGANTISCHEN":PRINT "S C H L U E S S E L .
10740 RETURN
10750 K=DV(5):KK=1:GOSUB 8440
10760 IF H(2)=0 THEN PRINT "HIER GIBT ES NUR DIE SPHINX .":RETURN
10770 PRINT AT(28,0);"DIE SPHINX BEWACHT EINEN GROSSEN"
10780 PRINT AT(29,0);"STEINERNEN SCHLUESSEL.":RETURN
10790 K=DV(8):KK=1:GOSUB 8440
10800 IF H(1)=0 THEN PRINT "HIER IST NICHTS MEHR ZU SEHEN.":RETURN
10810 PRINT "Im Sand liegt ein Diamant.":RETURN
10820 DATA "                                 " 
10830 DATA "                    " 
10840 DATA "                               " 
10850 DATA "                     "
10860 DATA "                                 "
10870 DATA "                          "
10880 DATA "                         "
10890 DIM A$(28):RESTORE 10820:FOR I=1 TO 7:READ A$(I):NEXT
10900 DATA "","","  "
10910 DATA "    ","","  "
10920 DATA "   "," "," "
10930 DATA "   "," "," "
10940 DATA "     ",""," "
10950 DATA "","   "," "
10960 DATA "","  ",""
10970 DATA "","  ",""
10980 DATA "","  ",""
10990 DATA "","  ",""
11000 DATA "","  ",""    
11010 DATA "","  ","" 
11020 DATA "","  ",""
11030 DATA ""," ",""
11040 DATA ""," ",""
11050 DIM B$(45):FOR I=1 TO 15:READ B$(I),B$(I+15),B$(I+30):NEXT I
11060 DATA "          "
11070 DATA "        "
11080 DATA "       "
11090 DATA "       "
11100 DATA "   "
11110 DATA "      "
11120 DATA "            ","  "
11130 DATA "          ","     "
11140 DATA "        ","    "
11150 DATA "      ","    "
11160 DATA "    ","    "
11170 DATA "  ","    "
11180 DATA "",""
11190 DATA "",""
11200 DATA "        ",""
11210 DATA "        ","  "
11220 DATA "        "," "
11230 DATA "        "," "
11240 DATA "     "," "
11250 DATA "     "," "
11260 DATA "     ","  "
11270 DATA "",""
11280 DIM C$(38):FOR I=1 TO 6:READ C$(I):NEXT
11290 FOR I=7 TO 22:READ C$(I),C$(I+16):NEXT I
11300 DATA "                         "
11310 DATA "                  "  
11320 DATA "            " 
11330 DATA "           " 
11340 DATA "       "
11350 DATA "      "
11360 DATA "      "
11370 DATA "      "
11380 DATA "            "
11390 DATA "             "
11400 DATA "           "
11410 DATA "              "
11420 DATA "         "
11430 DIM E$(40):FOR I=1 TO 13:READ E$(I):NEXT
11440 DATA "                   "
11450 DATA "           "
11460 DATA "   "
11470 DATA ""
11480 DATA ""
11490 DATA ""
11500 DATA ""
11510 DATA ""
11520 DATA "        " 
11530 DATA ""
11540 DATA "              "
11550 DATA ""
11560 FOR I=14 TO 25:READ E$(I):NEXT
11570 DATA "   "
11580 DATA "     " 
11590 DATA "     "
11600 DATA "      "
11610 DATA "       "
11620 DATA "          "
11630 DATA "               "
11640 DATA "                 "
11650 DATA "      "
11660 DATA "              "
11670 DATA "    "
11680 FOR I=26 TO 36:READ E$(I):NEXT
11690 DATA "                                 "
11700 DATA "                          "
11710 DATA "                                "
11720 DATA "                             "
11730 DATA "                             "
11740 DATA "                               "
11750 DATA "                            "
11760 DATA "                                       "
11770 DATA "                                       "
11780 DATA "                     "
11790 DATA "                        "
11800 DATA "                       "
11810 DATA "                  "
11820 DATA "                               "
11830 DATA "                        "
11840 DATA "                             "
11850 DATA "                          "
11860 DATA "                              "
11870 DATA "                                  "
11880 DATA "                        "
11890 DATA "     "
11900 FOR I=8 TO 28:READ A$(I):NEXT
11910 WV=30:SV=0:RB=0:OB=0:DIM G$(16),D$(16),H$(27),H(16),GV(16),DV(16):OZ=1
11920 DATA "NORD","SUED","OST","WEST":FOR I=1 TO 4:READ R$(I):H$(I+23)=R$(I):NEXT
11930 DATA "GEHE","NEHME","KAUFE","FRAGE","GEBE","SAGE","LADE","BELADE","ANTW" 
11940 DATA "SCHOEPF","WERFE","VERTEI","VERTREI","TOETE","SETZE","FUELLE"
11950 DATA "SCHAU","SEHE","BLICK","FUEHRE","SCHLUES","HABE","BESITZ"
11960 FOR I=1 TO 23:READ H$(I):NEXT
11970 DATA "DIAMANT","EINEN GROSSEN DIAMANTEN",32
11980 DATA "SCHLUESSEL","EINEN STEINERNEN SCHLUESSEL"
11990 DATA 16,"GEWAND","EIN WEISSES GEWAND",8,"KOMPASS","EINEN KOMPASS",4
12000 DATA "KAMEL","EIN GUTES KAMEL",2,"SCHWERT","EIN SCHARFES SCHWERT",1
12010 DATA "WASSER","",0,"SPRUCH","",0,"RAEUBER","",0,"MICH","",0
12020 DATA "OBELISK","",0,"SPHIN","",0,"GERIPP","",0,"SAND","",0,"EINSIED","",0
12030 DATA "WAS","",0
12040 FOR I=1 TO 16:READ G$(I),D$(I),GV(I):NEXT
12050 DV(3)=4:DV(4)=3:DV(5)=16:DV(8)=32:DV(14)=8 
12060 PV=0:RETURN
12070 NEW
12080 PRINT
12090 FOR I=0 TO 31: PRINT STRING$(31,"."):NEXT
12100 I=2:K=1 
12110 FOR K=1 TO 13 :WINDOW I,I+1,K,K+1
12120 ON K GOSUB 12140,12150,12160,12170,12180,12190,12200,12210,12220
12130 PAUSE 5:NEXT :GOTO 12230
12140 PRINT "R":RETURN
12150 PRINT "O":RETURN
12160 PRINT " ":RETURN
12170 PRINT "1":RETURN
12180 PRINT "1":RETURN
12190 PRINT "/":RETURN
12200 PRINT "8":RETURN
12210 PRINT "8":RETURN
12220 PRINT "*":RETURN
12230 WINDOW 5,26,1,29:CLS:PAUSE 20
12240 PRINT:PRINT "   TEXTADVENTURE - WUESTE":PAUSE 10
12250 PRINT AT(11,3);" Game :M. Siedentopf"
12260 PRINT AT(15,4);"Version Z1013 : K. Rothe":PAUSE 10
12270 PRINT AT(22,4);"ausfuehrl. Erlaeuterung":PRINT AT(23,4);"ueber EDITEX File
12280 IF INKEY$<>CHR$(13)THEN 12280
12290 WINDOW 9,26,2,29:CLS:PRINT "Gehen Sie auf die Suche":PRINT
12300 PRINT "nach dem in der Wueste":PRINT "RUROMI verborgenen Schatz .":PRINT
12310 PRINT "Wagen Sie das Abenteuer,":PRINT "von dem vor Ihnen":PRINT
12320 PRINT "bisher keiner zurueckkehrte."
12330 IF INKEY$<>CHR$(13)THEN 12330
12340 CLS:PRINT "Lassen Sie sich fesseln":PRINT "      von diesem":PRINT     
12350 PRINT "  TEXTADVENTURE - GAME,":PRINT:PRINT "Das Ihnen ein Raetsel nach "
12360 PRINT "dem anderen aufgeben wird !":PRINT "Aber verzweifeln Sie nicht ,";
12370 PRINT "wenn Sie beim ersten Anlauf":PRINT "scheitern !"
12380 PRINT "Nur Phantasie kann Ihnen ":PRINT "helfen und natuerlich die"
12390 PRINT "ausfuehrliche Beschreibung"
12400 PRINT "ueber Textfile EDITEX."
12410 IF INKEY$<>CHR$(13)THEN 12410
12420 FOR I=0 TO 500:PRINT AT((RND(1)*31),(RND(1)*31));"":NEXT:WINDOW:CLS      
12430 CLS:PRINT " Es geht sofort los ..."
12440!
12450 RETURN
12460 FOR K=1 TO 16   
12470 FOR I=1 TO K:PRINT AT(I+3,16);"       ":NEXT
12480 FOR J=1 TO 15-K:PRINT AT(J+K+3,16);B$(30+J):NEXT
12490 NEXT
12500 F$(1)=""
12510 F$(2)="      "
12520 F$(3)=""
12530 F$(4)="    "
12540 F$(5)=""
12550 F$(6)=""
12560 F$(7)=""
12570 FOR K=1 TO 7
12580 FOR I=1 TO K:PRINT AT(I-K+14,16);F$(I):NEXT I
12590 PAUSE 1:NEXT K:RETURN
30000 PRINT:PRINT:INPUT "Wer will was von mir ?" ;N$
30010 CLS:WINDOW 8,31,14,31
30020 RESTORE 30070
30030 FOR I=1 TO 63
30040 READ X,Y,Z
30050 PRINT AT(X,Y);CHR$(Z)
30060 NEXT
30070 DATA 7,3,129,7,4,139,7,5,202,7,6,202,7,7,139,7,8,128
30080 DATA 8,3,190,8,8,143
30090 DATA 9,2,174,9,3,142,9,4,129,9,5,128,9,6,129,9,7,128,9,8,191,9,9,173
30100 DATA 10,2,171,10,3,159,10,5,133,10,6,132,10,8,192,10,9,172
30110 DATA 11,3,145,11,4,213,11,5,171,11,6,172,11,8,144,11,9,18
30120 DATA 12,4,191,12,5,183,12,6,183,12,7,142
30130 DATA 18,3,129,18,4,139,18,5,202,18,6,202,18,7,139,18,8,128
30140 DATA 19,3,190,19,8,143
30150 DATA 20,2,174,20,3,142,20,4,129,20,5,128,20,6,129,20,7,128,20,8,191
30160 DATA 20,9,173,21,2,171,21,3,159,21,5,133,21,6,132,21,8,192,21,9,172
30170 DATA 22,3,145,22,5,174,22,6,173,22,8,144,22,9,18
30180 DATA 23,4,191,23,5,183,23,6,183,23,7,142
30190 PAUSE 5
30200 FOR I=1 TO 22
30210 PRINT AT(11,5);CHR$(129) 
30220 PRINT AT(11,6);CHR$(128)
30230 IF I=4 THEN PRINT "Hallo ";N$:PRINT:PRINT:PRINT
30240 IF I=10 THEN PRINT "Moechtest Du":PRINT
30250 IF I=20 THEN PRINT "mit mir spielen ?":PRINT:PRINT:PRINT
30260 PRINT AT(11,5);"~~"
30270 PRINT AT(11,6);CHR$(129)
30280 PRINT AT(11,5);CHR$(128)
30290 PRINT AT(11,5);"--"
30300 PRINT AT(11,5);CHR$(129)
30310 PRINT AT(11,6);CHR$(128)
30320 PRINT AT(11,5);CHR$(171)
30330 PRINT AT(11,6);CHR$(172)
30340 NEXT I
30350 FR$=INKEY$
30360 IF FR$="J" THEN 30390
30370 IF FR$="N" THEN 30610
30380 GOTO 30350
30390 WINDOW 15,30,0,10
30400 FOR I=1 TO 14
30410 PRINT "          ":PAUSE 1
30420 NEXT I
30430 PRINT AT(15,3);"Schoen"
30440 PRINT AT(11,5);CHR$(129)
30450 PRINT AT(11,6);CHR$(128)
30460 PAUSE 10 
30470 FOR I=1 TO 20
30480 PRINT AT(11,5);"~~"
30490 PRINT AT(11,5);CHR$(148)
30500 PRINT AT(11,6);CHR$(151)
30510 PRINT AT(11,5);CHR$(129)
30520 PRINT AT(11,6);CHR$(128)
30530 PRINT AT(11,5);CHR$(171)
30540 PRINT AT(11,6);CHR$(172)
30550 IF I=3 THEN PRINT "Es":PRINT
30560 IF I=8 THEN PRINT "geht":PRINT
30570 IF I=12 THEN PRINT "gleich":PRINT
30580 IF I=15 THEN PRINT "los !"
30590 NEXT I:PAUSE 20
30600 RETURN
30610 WINDOW 1,15,0,10
30620 FOR I=1 TO 14
30630 PRINT "          ":PAUSE 1
30640 NEXT I
30650 PRINT AT(27,0);"Dann eben nicht !"
30660 PRINT AT(22,5);"~~"
30670 PAUSE 40
30680 WINDOW:CLS:POKE 10846,95:END
