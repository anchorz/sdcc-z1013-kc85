1 PRINT AT(0,0);" ":CLEAR 1000:LO$=STRING$(224," "):GOTO 10000
10 WINDOW:GOTO 3700
20 WINDOW 22,31,0,31:FOR X=22 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT:RETURN
30 IF Y>28 THEN Y=3:V=4:FOR I=1 TO 3:PRINT AT(23+I,0);I:NEXT I:RETURN:ELSE RETURN
40 SK(Z)=H:IF INT(H/10)<>FB AND H<100 THEN SK(Z)=-H
50 R=FN B(SK(Z)):IF R<5 THEN E(R)=E(R)+1:RETURN:ELSE RETURN
60 GOTO 3790
70 IF X<100 THEN RETURN
80 IF X>200 THEN C((258-X)/10)=1:BF=BF+1:R1=R1+1
90 RETURN
100 IF X<100 THEN RETURN
110 IF 100<X AND X<200 THEN C(10*P+112-X)=1
120 RETURN
130 FOR J=1 TO 12:IF FN B(B(Z,J))=I AND FN A(B(Z,J))<7 THEN RETURN:ELSE NEXT J
140 IF Z=SP THEN PRINT INK 6;" * ";:ELSE PRINT INK 6;Z;
150 Q$=RIGHT$(STR$(SK(Z)),2)
160 Z1=VAL(LEFT$(Q$,1)):IF Z1=0 THEN PRINT "      ";:RETURN
170 PAPER 7:ON 5-Z1 GOTO 180,190,200,210
180 B$="Ê":INK 8:GOTO 220
190 B$="Ì":INK 8:GOTO 220
200 B$="Ë":INK 2:GOTO 220
210 B$="É":INK 2
220 IF F2=1 THEN F2=0:INK 3:PRINT "      ";:RETURN
230 Z1=VAL(RIGHT$(Q$,1))
240 ON 9-Z1 GOTO 250,260,270,280,290,300,310,320
250 A$="B":GOTO 330
260 A$="A":GOTO 330
270 A$="10":GOTO 330
280 A$="K":GOTO 330
290 A$="D":GOTO 330
300 A$="9":GOTO 330
310 A$="8":GOTO 330
320 A$="7":GOTO 330
330 PRINT INK 7;"®­";A7$;A5$;
340 PRINT INK 7;"«øøø¬";A8$;A9$;
350 PRINT A$;A3$;A1$;:IF A$="10"THEN PRINT A1$;
360 PRINT B$;A3$;A3$;A2$;B$;A3$;A1$;
370 IF A$="10"THEN PRINT A1$;
380 PRINT A$;A2$;A8$;:INK 3:PAPER 1:RETURN
390 Q$=RIGHT$(STR$(B(Z,T)),2):GOSUB 160:RETURN
400 RETURN
410 IF E(FN B(H))>2 THEN RETURN:ELSE R=RND(1):IF R>.5 THEN RETURN
420 GOSUB 1990:IF H>0 THEN RETURN:ELSE GOSUB 1970:RETURN
430 PRINT AT(22,0);"ICH MISCHE DIE KARTEN"
440 ZV=ZV+1:IF ZV=4 THEN ZV=1
450 FOR I=1 TO 32:K(I)=1:NEXT I
460 FOR Z=1 TO 3:FOR T=1 TO 10
470 FOR I=INT(RND(1)*31)+1 TO 32:IF K(I)=0 THEN NEXT I:GOTO 470
480 G=(I-1)/4:W=FN C(G):IF FN A(W)=8 THEN W=W+200
490 K(I)=0:B(Z,T)=W:NEXT T,Z
500 Z1=1:FOR I=1 TO 32:IF K(I)=0 THEN NEXT I:GOTO 530
510 G=(I-1)/4:W=FN C(G):IF FN A(W)=8 THEN W=W+200
520 R(Z1)=W:Z1=2:NEXT I:Z1=0
530 Z=1:GOSUB 2760
540 GOSUB 2820:GOSUB 2100:GOSUB 2240:Z=ZV+1:IF Z=4 THEN Z=1
550 ZF=0:FOR I=1 TO 4:E(I)=0:A(I)=1:NEXT I
560 GOSUB 2640:FT=P:PR=0::FOR RD=1 TO 10:GOSUB 960
570 PRINT:PRINT STRING$(6,CHR$(10))
580 PRINT "DIESER STICH GEHT AN SPIELER" Z:PRINT AT(31,0);" ":PAUSE 12 
590 FB=0:NEXT RD:IF HF=1 THEN GOSUB 20:GOSUB 2910:GOSUB 920
600 FOR X=2 TO 29 STEP 7:PRINT AT(X,0);LO$:NEXT:PRINT AT(0,0);STRING$(32,CHR$(160))
605 WINDOW 2,31,0,31:FOR X=2 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT
610 PRINT "WERTUNG:" Q;A1$;". SPIEL ("INT((Q-1)/3+1);". RUNDE )":PRINT
620 PRINT STRING$(32,CHR$(160))
630 FOR I=1 TO 2:Z1=VAL(RIGHT$(STR$(HR(I)),1)):GOSUB 3290:PT(SP)=PT(SP)+W:NEXT I
640 INK 7:PRINT:PRINT "EINZELSPIELER WAR SPIELER" SP;A1$;".":E=S3(SP)
650 PRINT:IF SP=1 THEN PRINT "SIE ERREICHTEN" PT(SP)"PUNKTE!"
660 IF SP<>1 THEN PRINT "ER ERREICHTE" PT(SP)"PUNKTE!"
670 PRINT:IF SF=1 AND PT(SP)>90 THEN FG=1:GOTO 700
680 IF SF=0 AND PT(SP)>60 THEN FG=1:GOTO 700
690 FG=0:PRINT "SPIELER HAT VERLOREN ";
700 IF ZA=1 THEN X$="MIT":ELSE X$="OHNE"
710 PRINT X$;E-1:PRINT:PRINT "SPIEL";E; 
720 IF HF=1 THEN E=E+1:PRINT "HAND";E;
730 IF(PT(SP)>89 OR PT(SP)<31)AND SF=0 THEN E=E+1:PRINT "SCHNEIDER";E:PRINT
740 IF SF=1 THEN E=E+2:PRINT "SCHNEIDER";E-1:PRINT:PRINT "ANGESAGT";E;
750 IF ZF=0 THEN E=E+1:PRINT "SCHWARZ";E;
760 IF GR=1 THEN GR=0:S2(SP)=24*E:GOTO 780
770 S2(SP)=E*R2(SP)
780 PRINT AT(20,0);STRING$(32,CHR$(158)):PRINT INK 6;AT(22,0);"SPIELSTAND"
790 P=FT:GOSUB 2630:PRINT AT(20,25);"E "
800 IF S2(SP)>=GW THEN 860
810 PRINT:PRINT:IF SP=1 THEN PRINT "SIE HABEN SICH ";:GOTO 830
820 PRINT:PRINT "SPIELER" SP"HAT SICH ";
830 PRINT "UEBERREIZT."
840 S2(SP)=S2(SP)+R2(SP):IF S2(SP)<GW THEN 840
850 S2(SP)=(HF-2)*S2(SP):GOTO 865
860 IF(PT(SP)<61)OR(PT(SP)<91 AND SF=1)THEN S2(SP)=(HF-2)*S2(SP):HF=0:SF=0
865 PRINT AT(24,0);STRING$(32,CHR$(158))
870 PRINT AT(25,0);"SPIELER 1      2      3    SPIEL"
880 GP(SP)=GP(SP)+S2(SP):FOR I=0 TO 2:PRINT AT(27,I*7+6);GP(I+1):NEXT I
890 PRINT AT(27,26);S2(SP)
900 Z=Z-1:IF Z=0 THEN Z=3
910 FOR I=1 TO 3:PT(I)=0:NEXT I:Q=Q+1:GOSUB 920:FOR X=0 TO 31 STEP 7:PRINT AT(X,0);LO$
912 NEXT:WINDOW:PRINT AT(0,0);" ":HF=0:SF=0:V=0:GOTO 430
920 BEEP:PRINT AT(31,0);"'ENTER'oder'N'fuer anderes Spiel"
930 P$=INKEY$:IF P$="N"THEN RUN:ELSE:IF P$><CHR$(13)THEN 930
950 RETURN
960 VMH=1:FB=0:GOSUB 20:ON Z GOTO 970,980,980
970 GOSUB 3370:GOSUB 140:GOTO 1080
980 IF SP=Z THEN 1020
990 GOSUB 1910:IF YF=1 THEN 1070:ELSE GOSUB 2020:IF H=0 THEN GOSUB 1880:GOTO 1070
1000 IF FN A(H)=6 THEN GOSUB 2050
1010 GOTO 1070
1020 IF R1=3 AND ZA=1 THEN H1=(22+RD)*10:ELSE H1=200
1030 GOSUB 1890:IF YF=1 THEN 1070:ELSE H=0:GOSUB 1910
1040 IF YF=0 OR(FN A(H)=6 AND(A(FN B(H))=1 OR E(FN B(H))>3))THEN GOSUB 2020
1050  IF H=0 OR(FN A(H)=6 AND(A(FN B(H))=1 OR E(FN B(H))>3))THEN 1060:ELSE GOTO 1070
1060 GOSUB 1880:IF YF=0 THEN GOSUB 2050
1070 SK(Z)=H:GOSUB 50:GOSUB 140:IF RD=2 THEN R1=0
1080 VMH=2:FB=INT(ABS(SK(Z)/10)):ZW=Z:Z=Z+1:IF Z=4 THEN Z=1
1090 IF Z=1 THEN GOSUB 3370:GOSUB 140:GOTO 1320
1100 IF Z=SP THEN 1250
1110 IF SK(SP)>0 THEN 1180
1120 IF FB>9 THEN GOSUB 1830:IF YF=0 THEN GOSUB 1880:IF YF=0 THEN GOSUB 2050
1130 IF FB>9 THEN 1160
1140 GOSUB 1970:IF H>0 AND FN A(H)<>6 THEN 1160 ELSE GOSUB 1990
1150 IF H=0 THEN GOSUB 1910:IF FN A(H)=6 OR YF=0 THEN GOSUB 1880:IF YF=0 THEN GOSUB 2050
1160 GOSUB 40
1170 GOSUB 140:GOTO 1320
1180 IF SK(SP)>100 THEN 1230
1190 GOSUB 1970:IF H>SK(SP)THEN 1160
1200 IF H>0 THEN GOSUB 1990:GOTO 1160
1210 GOSUB 1850:IF YF=0 THEN GOSUB 2050:IF H=0 THEN GOSUB 1830
1220 GOTO 1160
1230 H1=SK(SP):GOSUB 1890:IF YF=0 THEN GOSUB 1880:IF YF=0 THEN GOSUB 2050
1240 GOTO 1160
1250 IF FB>9 THEN 1300
1260 GOSUB 1970:IF H>SK(ZW)AND FN A(H)<>6 THEN 1160
1270 IF H>0 THEN GOSUB 1990:GOTO 1160
1280 GOSUB 1850:IF YF=1 THEN 1160
1290 GOSUB 2050:GOTO 1160
1300 H1=SK(ZW):GOSUB 1890:IF YF=0 THEN GOSUB 1880:IF YF=0 THEN GOSUB 2050
1310 GOTO 1160
1320 VMH=3:ZW=Z:Z=Z+1:IF Z=4 THEN Z=1
1330 IF Z=1 THEN GOSUB 3370:GOSUB 140:GOTO 1740
1340 IF Z=SP THEN 1530
1350 GOSUB 1700:IF H1=SK(SP)THEN 1420
1360 IF FB<10 THEN 1390
1370 GOSUB 1850:IF YF=1 THEN 1510 ELSE GOSUB 1880:IF YF=0 THEN GOSUB 1910
1380 IF YF=1 THEN 1510:ELSE GOSUB 2020:GOTO 1510
1390 GOSUB 1970:IF FN A(H)=7 THEN GOSUB 410
1400 IF H=0 THEN GOSUB 1910:IF YF=0 THEN GOSUB 2020:IF H=0 THEN GOSUB 1880
1410 GOTO 1510
1420 IF FB<9 THEN 1460
1430 H1=SK(SP):GOSUB 1890:IF YF=0 THEN GOSUB 1880:IF YF=0 THEN GOSUB 2050
1440 IF H>100 AND H<H1 AND FN A(H)>5 THEN 1450:ELSE GOTO 1510
1450 H1=200:GOSUB 1890:IF FN B(H)=21 THEN 1510 ELSE GOSUB 1880:GOTO 1510
1460 GOSUB 1970:IF H>SK(SP)THEN 1510
1470 IF H>0 THEN GOSUB 1990:GOTO 1510
1480 IF SK(SP)>100 THEN H1=SK(SP):GOSUB 1890:IF YF=1 THEN 1510
1490 IF SK(SP)>100 THEN GOSUB 2050:IF YF=1 THEN 1510
1500 GOSUB 1850:IF YF=0 THEN H1=200:GOSUB 1890:IF YF=0 THEN GOSUB 2050
1510 GOSUB 40
1520 GOSUB 140:GOTO 1740
1530 IF FB>9 THEN 1640
1540 GOSUB 1970:GOSUB 1700:IF H>H1 THEN GOTO 1510
1550 IF H>0 THEN GOSUB 1990:GOTO 1510
1560 GOSUB 1720:IF H1>100 THEN 1600:ELSE GOSUB 1850:IF YF=1 THEN GOTO 1510
1570 IF SS<9 THEN 1590:ELSE H1=200:GOSUB 1890:IF YF=0 THEN GOSUB 2050
1580 GOTO 1510
1590 GOSUB 2050:IF H=0 THEN GOSUB 1880:GOTO 1510:ELSE GOTO 1510
1600 IF SS>9 THEN GOSUB 1890:IF YF=0 THEN GOSUB 2050:GOTO 1510
1610 IF SS>9 THEN 1510
1620 GOSUB 2050:IF H=0 OR FN A(H)>5 THEN GOSUB 1880:IF YF=0 THEN GOSUB 2050
1630 GOTO 1510
1640 GOSUB 1720:IF SS>10 THEN 1680
1650 GOSUB 1880:IF YF=0 THEN GOSUB 2050:GOTO 1510
1660 IF FN A(H)<6 THEN 1510
1670 GOSUB 1830:IF YF=0 THEN GOSUB 1880:GOTO 1510:ELSE GOTO 1510
1680 GOSUB 1700:GOSUB 1890:IF YF=0 THEN GOSUB 1880:IF YF=0 THEN GOSUB 2050
1690 GOTO 1510
1700 H1=0:FOR I=1 TO 3:IF H1<SK(I)THEN H1=SK(I)
1710 NEXT I:RETURN
1720 SS=0:FOR I=1 TO 3:Z1=FN A(SK(I)):GOSUB 3290:SS=SS+W
1730 NEXT I:RETURN
1740 GOSUB 400:FOR Z1=1 TO 3:FOR I=1 TO 10:IF ABS(SK(Z1))=B(Z1,I)THEN B(Z1,I)=0
1750 NEXT I,Z1:IF SK(1)>SK(2)AND SK(1)>SK(3)THEN Z=1:GOTO 1770
1760 GOTO 1780
1770 IF INT(ABS(SK(1)/10))<>FB AND SK(1)<100 THEN 1780:ELSE GOTO 1790
1780 IF SK(2)>SK(3)THEN Z=2:GOTO 1790:ELSE Z=3
1790 IF Z<>SP THEN ZF=1
1800 SS=0:FOR I=1 TO 3:Z1=FN A(SK(I)):GOSUB 3290
1810 IF FN A(SK(I))=7 AND SK(I)<100 THEN A(FN B(SK(I)))=0
1820 SS=SS+W:SK(I)=0:NEXT I:PT(Z)=PT(Z)+SS:RETURN
1830 YF=0:H=200:FOR I=1 TO 10:IF H<B(Z,I)THEN H=B(Z,I):YF=1
1840 NEXT I:RETURN
1850 YF=0:H=0:FOR I=1 TO 12:IF H<B(Z,I)AND B(Z,I)<200 THEN H=B(Z,I):YF=1
1860 NEXT I:IF H<100 THEN YF=0
1870 RETURN
1880 H1=100
1890 H=300:YF=0:FOR I=1 TO 10:IF H>B(Z,I)AND B(Z,I)>H1 THEN H=B(Z,I):YF=1
1900 NEXT I:RETURN
1910 YF=0:FOR I=1 TO 4:F(I)=0:NEXT I:FOR I=1 TO 12
1920 IF B(Z,I)=0 OR B(Z,I)>100 THEN NEXT I:GOTO 1940
1930 E=INT(B(Z,I)/10):F(E)=F(E)+1:NEXT I
1940 FOR I=1 TO 4:IF F(I)<>1 THEN NEXT I:RETURN
1950 FOR J=1 TO 12:IF INT(B(Z,J)/10)=I THEN H=B(Z,J):YF=1:RETURN
1960 NEXT J
1970 H=0:FOR J=1 TO 12:IF H<B(Z,J)AND INT(B(Z,J)/10)=FB THEN H=B(Z,J):R=J
1980 NEXT J:J=R:RETURN
1990 H=300:FOR J=1 TO 12:IF H>B(Z,J)AND INT(B(Z,J)/10)=FB THEN H=B(Z,J):R=J
2000 NEXT J:IF H=300 THEN H=0
2010 J=R:RETURN
2020 H1=0:H=0:FOR I=1 TO 10:IF H1<B(Z,I)-10*INT(B(Z,I)/10)AND B(Z,I)<100 THEN 2040
2030 NEXT I:H1=0:RETURN
2040 H=B(Z,I):H1=B(Z,I)-10*INT(B(Z,I)/10):GOTO 2030
2050 H1=10:H=1000:FOR I=1 TO 10
2060 IF H1>VAL(RIGHT$(STR$(B(Z,I)),1))AND B(Z,I)<100 AND B(Z,I)>0 THEN 2090
2070 NEXT I:IF H=1000 THEN H=0
2080 RETURN
2090 H=B(Z,I):H1=VAL(RIGHT$(STR$(B(Z,I)),1)):GOTO 2070
2100 FOR Z=2 TO 3:R1=0:R2(Z)=0:R3=0:R5(Z)=0
2110 BF=0:SP=Z:GOSUB 2690:IF S3(Z)>4 THEN S3(Z)=4
2120 FOR I=1 TO 4:K(I)=0:NEXT I
2130 FOR I=1 TO 10:IF B(Z,I)<100 THEN FB=INT(B(Z,I)/10):K(FB)=K(FB)+1
2140 NEXT I:FOR I=1 TO 4:IF R3<K(I)THEN R3=K(I):R2(Z)=I
2150 NEXT I:R2(Z)=8+R2(Z):R4=R2(Z)*S3(Z):R8=0
2160 FOR J=1 TO 10:IF FN A(B(Z,J))=7 THEN R8=R8+1:NEXT J:ELSE NEXT J
2170 X=R1+R3:IF X>5 THEN:GOTO 2220
2180 IF X=5 THEN IF R8>1 OR(R8>0 AND R1>1)THEN 2220
2190 IF X=4 THEN IF R8>2 OR(R1>1 AND R8>1)THEN 2220
2200 IF X>3 THEN R4=18:GOTO 2220
2210 R6=0:S2(Z)=0:GOTO 2230
2220 R6=R4:S2(Z)=R4
2230 RESTORE:S1(Z)=R6:NEXT Z:RETURN
2240 PG=ZV:Z1=ZV:GOSUB 2250:PH=Z1:GOSUB 2250:SP=Z1:Y=0:R4=18:GOTO 2270
2250 FOR I=1 TO 3:PRINT AT(21+I,0);I:NEXT I:Z1=Z1+1:IF Z1<4 THEN RETURN
2260 Z1=1:RETURN
2270 Y=Y+3:GOSUB 30:GOSUB 2320:IF H=0 THEN 2290
2280 GOSUB 2380:IF H=1 THEN 2270
2290 SP=PG:Y=Y+3:GOSUB 30:GOSUB 2320:IF H=0 THEN SP=PH:GOTO 2430
2300 GOSUB 2380:IF H=1 THEN 2290
2310 GOTO 2430
2320 IF SP=1 THEN 2350
2330 IF R4<S1(SP)+.5 THEN H=1:PRINT AT(21+V+SP,Y);R4:READ R4:RETURN
2340 PRINT AT(21+V+SP,Y);" WEG":H=0:SP=PG:RETURN
2350 PRINT AT(22+V,Y);R4:PRINT INK 6;    
2360 GOSUB 3620:IF P$="J"THEN H=1:READ R4:RETURN
2370 H=0:ZW=R4:PRINT AT(22+V,Y);" WEG":RETURN
2380 IF PH=1 THEN 2410
2390 IF R4<=S1(PH)THEN PRINT AT(21+PH+V,Y+1);"JA":H=1:ZW=R4:RETURN
2400 PRINT AT(21+PH+V,Y+1);"WEG":H=0:ZW=R4:PH=SP:SP=PG:RETURN
2410 PRINT INK 6;:GOSUB 3610:IF P$="J"THEN H=1:PRINT AT(22+V,Y+1);"JA":RETURN 
2420 H=0:ZW=R4:PRINT AT(22+V,Y);" WEG":PH=SP:RETURN
2430 IF R4>18 THEN 2460
2440 IF SP=1 THEN PRINT AT(22,Y+3);"18":GOSUB 3610:IF P$="J"THEN 2460
2450 GOSUB 20:PRINT AT(27,2);"ALLE PASSEN - NEUES SPIEL":PAUSE 50:GOTO 430
2460 BEEP:PRINT AT(31,0);"SPIEL GEHT AN SPIELER",SP
2470 IF SP=1 THEN PAUSE 9:GOSUB 2880:RETURN
2480 FOR I=1 TO 2:B(SP,I+10)=R(I):NEXT I
2490 FOR I=1 TO 4:K(I)=0:NEXT I:FOR I=1 TO 12:FB=INT(B(SP,I)/10)
2500 IF FB<10 THEN K(FB)=K(FB)+1
2510 NEXT I:F1=0:FB=0:FOR I=1 TO 4:E(I)=K(I):IF F1<=K(I)THEN FB=I:F1=K(I)
2520 NEXT I:P=FB:R1=0:GOSUB 2630:GOSUB 3090:S1(SP)=8
2530 S2(SP)=S3(SP)*S1(SP):E(P)=0:Z9=Z:Z=SP:FF=FB:FOR L=1 TO 2:GOSUB 1910
2540 IF YF=1 AND FN A(H)<7 THEN 2570:ELSE FOR I=1 TO 4:IF E(I)<>2 THEN NEXT I:GOTO 2560
2550 E(I)=1:FB=I:GOSUB 1970:IF FN A(B(Z,J))<7 THEN 2570:ELSE GOSUB 1990:GOTO 2570
2560 J=INT(RND(1)*12)+1:IF B(Z,J)>100 OR FN A(B(Z,J))=7 THEN 2560
2570 HR(L)=B(Z,J):B(Z,J)=0:I=4:J=12:NEXT:IF L<3 THEN NEXT L
2580 FB=FF:Z=Z9:Z1=0:FOR I=1 TO 12:IF B(SP,I)>0 THEN Z1=Z1+1:K(Z1)=B(SP,I)
2590 NEXT I:FOR I=1 TO 10:B(SP,I)=K(I):NEXT I:FOR I=11 TO 12:B(SP,I)=0:NEXT I:RETURN
2600 IF SP=1 AND HF=1 AND P>3 THEN RETURN
2610 IF SP<>1 AND P>3 THEN RETURN
2620 WINDOW 0,31,0,31:GOSUB 2820:RETURN
2630 PRINT AT(20,9);"SPIELER",SP,"SPIELT "
2640 IF P=1 THEN PRINT AT(20,27);"KARO":RETURN
2650 IF P=2 THEN PRINT AT(20,27);"HERZ":RETURN
2660 IF P=3 THEN PRINT AT(20,27);"PIK":RETURN
2670 IF P=4 THEN PRINT AT(20,27);"KREUZ":RETURN
2680 PRINT AT(20,27);"GRAND":RETURN
2690 FOR I=1 TO 12:C(I)=0:NEXT I:FOR I=1 TO 12:X=B(SP,I):GOSUB 70:NEXT I
2700 S3(SP)=2:FOR I=2 TO 4:IF C(I)=C(I-1)THEN S3(SP)=S3(SP)+1:NEXT I
2710 ZA=0:IF C(1)=1 THEN ZA=1
2720 IF S3(SP)<5 THEN RETURN
2730 FOR I=1 TO 12:X=B(SP,I):GOSUB 100:NEXT I
2740 FOR I=5 TO 12:IF C(I)=C(I-1)THEN S3(SP)=S3(SP)+1:NEXT I
2750 RETURN
2760 FOR T=1 TO 10
2770 FOR I=1 TO 10:IF Z1<B(Z,I)THEN Z1=B(Z,I)
2780 NEXT I:K(T)=Z1:FOR I=1 TO 10:IF B(Z,I)=Z1 THEN B(Z,I)=0
2790 NEXT I:Z1=0:NEXT T
2800 FOR J=1 TO 10:B(Z,J)=K(J):NEXT J:RETURN
2810 WINDOW 0,31,0,31:CLS
2820 PRINT:PRINT:Z=1:FOR T=1 TO 9
2830 IF T=6 THEN PRINT:PRINT:PRINT STRING$(5,CHR$(10))
2840 PRINT CHR$(9);:GOSUB 390:NEXT T
2850 T=10:PRINT CHR$(9);:GOSUB 390
2852 FOR X=1 TO 5:PRINT AT(0,X*6-4);X:PRINT AT(18,X*6-4);X+5:NEXT
2855 PRINT AT(18,26);" 0 "
2860 GOSUB 20
2870 PRINT:PRINT: PRINT STRING$(8,CHR$(10)):PRINT STRING$(5,CHR$(10))
2872 PRINT AT(31,0);" ":RETURN
2880 PRINT AT(29,2);"HAND ":GOSUB 3620
2890 IF P$="J"THEN HF=1:FOR I=1 TO 2:HR(I)=R(I):NEXT I:GOTO 3040
2900 Z=1:GOSUB 20
2910 BEEP:PRINT INK 6;"SKAT":FOR I=1 TO 2:B(1,10+I)=R(I):NEXT I
2920 Z=1:FOR T=11 TO 12:PRINT T;:GOSUB 390:NEXT T:IF HF=1 THEN RETURN
2930 WINDOW 22,31,19,31:K(1)=13:K(2)=13
2940 PRINT "DRUECKEN":PRINT:FOR I=1 TO 2
2950 INPUT "KARTE";K(I):PRINT
2960 IF K(I)=0 THEN K(I)=10
2970 IF K(I)=K(I-1)THEN 2950
2980 IF K(I)<1 OR K(I)>12 THEN 2950
2990 HR(I)=B(1,K(I)):NEXT I
3000 Z1=0:FOR I=1 TO 2:B(1,K(I))=0:NEXT I:FOR I=1 TO 12
3010 IF B(1,I)<>0 THEN Z1=Z1+1:K(Z1)=B(1,I) 
3020 NEXT I:FOR I=1 TO 10:B(1,I)=K(I):NEXT I
3030 F4=1:Z=1:!IFP<4THENGOSUB2420
3040 GOSUB 20:PRINT "WAS IST TRUMPF?":PRINT:PRINT "1. KARO     2. HERZ"
3050 PRINT:PRINT "3. PIK      4. KREUZ    5. GRAND":PRINT
3060 P$=INKEY$:P=VAL(P$)
3070 IF P>5 OR P<1 THEN 3060
3080 PRINT "NR." P:IF P=5 THEN 3190
3090 FOR Z=1 TO 3:FOR T=1 TO 10
3100 IF B(Z,T)<100 AND INT(B(Z,T)/10)=P THEN B(Z,T)=100+B(Z,T)
3110 NEXT T,Z
3120 FOR T=11 TO 12:IF B(SP,T)<100 AND INT(B(SP,T)/10)=P THEN B(SP,T)=100+B(SP,T)
3130 NEXT T:Z=1:GOSUB 2760
3140 GOSUB 2690:S2(1)=(P+8)*S3(1)
3150 GOSUB 2600:SF=0:IF HF=0 THEN 3180
3160 F4=0:PRINT AT(27,0);"SCHNEIDER ANSAGEN " 
3170 GOSUB 3620:IF P$="J"THEN SF=1
3180 GOSUB 3220:RETURN
3190 GR=1:GOSUB 2690:IF S3(SP)>4 THEN S3(SP)=4
3200 S2(SP)=24*S3(SP):Z=1:IF HF=0 THEN GOSUB 2760
3210 GOSUB 3150:RETURN
3220 Z1=1:R2(SP)=8+P:IF P=5 THEN R2(SP)=24
3230 FOR T=1 TO 4:K(T)=0:NEXT T
3240 FOR T=1 TO 4:FOR I=1 TO 10:IF B(SP,I)=258-10*T THEN K(T)=1
3250 NEXT I,T:FOR T=1 TO 3:IF K(T)=K(T+1)THEN Z1=Z1+1:NEXT T
3260 IF HF=1 THEN Z1=Z1+1
3270 R5(SP)=Z1:S2(SP)=(R5(SP)+1)*R2(SP)
3280 GW=ZW:RETURN
3290 ON Z1 GOTO 3300,3300,3300,3310,3320,3330,3340,3350
3300 W=0:RETURN
3310 W=3:RETURN
3320 W=4:RETURN
3330 W=10:RETURN
3340 W=11:RETURN
3350 W=2:RETURN
3370 IF PR=1 OR RD=10 THEN 3670
3371 PRINT AT(26,19);"KARTE Nr. ?"
3380 P$=INKEY$
3390 IF P$<"0"OR P$>"9"THEN 3380:ELSE BEEP
3400 PRINT AT(26,19);"           "
3410 P=VAL(P$):IF P=0 THEN P=10
3420 IF B(1,P)=0 THEN 3370
3430 IF SK(2)=0 AND SK(3)=0 THEN 3530
3440 GE=2:IF SK(2)=0 THEN GE=3
3450 F=0:FOR G=1 TO 10
3460 IF B(1,G)>100 THEN F=F+1
3470 NEXT G
3480 IF F>0 AND B(1,P)<100 AND SK(GE)>100 THEN 3520
3490 F=0:FOR G=1 TO 10:IF B(1,G)>100 THEN NEXT G
3500 IF INT(B(1,G)/10)=INT(SK(GE)/10)AND INT(B(1,P)/10)<>INT(SK(GE)/10)THEN 3520
3510 NEXT G:GOTO 3530
3520 PRINT AT(18,12);"BEDIENEN ! ":PAUSE 20:GOTO 3370
3530 SK(1)=B(1,P)
3540 IF FB>0 AND INT(SK(1)/10)<>FB AND SK(1)<100 THEN SK(1)=-SK(1)
3550 X=P*6-5:PX=2:IF INT(P)>5 THEN X=P*6-35:PX=10
3560 FOR I=PX TO(PX+6):PRINT AT(I,X);STRING$(5," "):NEXT
3570 R=FN B(SK(1)):IF R<5 THEN E(R)=E(R)+1
3580 RETURN
3590 DATA 20,22,23,24,27,30,33,35,36,40,44,45,46,48,50,54,55,60,63,66,70
3600 DATA 72,77,80,81,84,88,90,96,99,100,108,110,120,144,168
3610!
3620 PRINT AT(29,12);"(J/N)? " 
3630 P$=INKEY$:IF P$="J"THEN PRINT AT(29,12);" JA   ":BEEP:RETURN
3640 IF P$<>"N"THEN 3630
3650 PRINT AT(29,12);"NEIN  ":BEEP:RETURN
3660 PR=1:PRINT AT(18,20);"          "
3670 FOR P=1 TO 9:IF B(1,P)=0 THEN NEXT P
3680 GOTO 3430:NEXT P
3690 P=10:IF B(1,P)=0 THEN RETURN:ELSE GOTO 3430
3700 CLS:PRINT INK 7;AT(22,0);"ICH MISCHE DIE  KARTEN":PRINT AT(0,0);" "
3710 A1$=CHR$(8):A2$=CHR$(9):A3$=CHR$(10):A4$=CHR$(11):AB$=STRING$(22,A2$)
3720 A5$=STRING$(5,A1$)+A3$:A6$=A5$+"   À":A7$=STRING$(5,A6$)
3730 A8$=STRING$(5,A4$):A9$=STRING$(4,A1$):AA$=STRING$(32,A2$)
3740 DIM B(3,12),PT(3),K(32),R(2),HR(2),SK(3),R5(3),R2(3),S1(3),S2(3)
3750 DIM S3(3),GP(3),C(12),F(4),A(4),E(4):ZV=INT(RND(1)*3)+1:Q=1  
3760 DEF FN A(H)=ABS(H)-10*INT(ABS(H)/10):DEF FN B(H)=INT(ABS(H)/10)
3770 DEF FN C(G)=(INT(G)+1)+10*((G-INT(G))*4+1):GOTO 450
3780 CLS
3790 X=2:FOR I=1 TO 10
3800 FOR Z=1 TO 3
3810 PRINT AT(X,8*Z+9);B(Z,I):NEXT Z
3820 X=X+1:NEXT I:PRINT AT(14,30);HR(1);HR(2):RETURN
3830 FOR SP=1 TO 3:GOSUB 2690:FOR J=1 TO 8:PRINT;C(J);
3840:NEXT J
3850 PRINT "    ";S3(SP);:PRINT:NEXT SP
3860 FOR I=1 TO 10:C(I)=0:NEXT I:RETURN
3870 REM *** P A R A V I A ****
3950 GOSUB 7930:RESTORE 3980
3960 JAHR=1400
3970 FOR M=0 TO 7:READ TB$(M):NEXT
3980 DATA SIR,BARON,COUNT,MARQUIS,DUKE,GRAND DUKE,PRINCE,KING
3990 DATA "SANTA PARAVIA","FIBONACCIO","TORRICELLA","MOLINETTO","FONTA"    
4000 DATA "MARINETTO"
4010 PRINT "WIEVIELE PERSONEN WOLLEN SPIELEN":PRINT:PRINT "( 1 bis 6 ) ";
4020 INPUT SA
4030 IF SA<1 OR SA>6 THEN 4020:ELSE:PRINT
4040 PRINT "WENN DIE STADT NICHT WIE":PRINT:PRINT "ANGEGEBEN ";
4050 PRINT "HEISSEN SOLL,DANN    ":PRINT:PRINT "GEBEN SIE NICHTS," 
4060 PRINT:PRINT "SONST DEN NAMEN EIN.":PRINT
4070 FOR SP=1 TO SA:READ T$
4080 PRINT "WER IST DER BEHERRSCHER VON"
4090 PRINT: PRINT T$;:PRINT " (max.10Buchstaben)          ":INPUT N$:PRINT 
4100 PRINT "WOLLEN SIE '" T$"'":PRINT:PRINT "UMBENNEN";:U$="":INPUT U$:PRINT
4110 PRINT:N$(SP)=N$+" OF "+U$:IF U$="" THEN N$(SP)=N$(SP)+T$  
4120 S1(SP)=25:S2(SP)=10:S3(SP)=5:JU(SP)=2
4130 GD(SP)=1000:LA(SP)=10000:KO(SP)=5000
4140 LS(SP)=100:VO(SP)=2000
4150 AD(SP)=4:SO(SP)=30:GE(SP)=5:KA(SP)=25
4160 PRINT "+ +":PRINT:NEXT
4170 PRINT "SPIELSTAERKE :"
4180 PRINT:PRINT "1.ANFAENGER   3.MEISTER "
4190 PRINT:PRINT "2.MIT ERFAHRUNG 4.KOENNER":PRINT:PRINT "5.-8.WAHNSINN"
4200 PRINT:PRINT "SPIELSTAERKE :";:PRINT:INPUT S$:PRINT
4210 SS=VAL(S$):IF SS=0 OR S$=""OR SS>8 THEN 4210
4220 SS=SS+4:TJ(0)=1:N$(0)=" PEPPONE OF MONTERANE"
4230 FOR SP=1 TO SA:SU(SP)=INT(1413+SS*1.5+RND(1)*26+RND(1)*10):NEXT
4240 REM
4250 FOR SP=1 TO SA:IF SU(SP)<JA THEN 4330
4260 IF SU(SP)=JA THEN 4340
4270 IF VO(SP)<200 THEN SU(SP)=JA:GOTO 4340
4280 WINDOW:FOR X=0 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT:GOSUB 4800
4290 GOSUB 5200
4300 WINDOW:FOR X=0 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT:GOSUB 5790 
4310 WINDOW:FOR X=0 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT:GOSUB 6380
4320 GOSUB 6960
4330 NEXT:JA=JA+1:GOTO 4240
4340 PRINT:WINDOW:CLS
4350 PRINT "HURRA!!":PRINT:PRINT TB$(TJ(SP)):PRINT:PRINT N$(SP) 
4360 PRINT
4370 PRINT "WURDE ERMORDET":PRINT:GOTO 4400
4380 PRINT "WIR TRAUERN UM";TB$(TJ(SP)):PRINT:PRINT NA$(SP)
4390 PRINT
4400 PRINT "GEB.1381,GEST.";JA
4410 H$="SIE":IF TJ(SP)<8 THEN H$="ER"
4420 PRINT
4430 PRINT H$;" STARB
4440 PRINT
4450 IF VO(SP)>200 THEN 4490
4460 PRINT "NACH LANGJAEHRIGER TYRANNEI"
4470 PRINT
4480 PRINT "DURCH EIN ATTENTAT!":GOTO 4630
4490 IF JA<1449 THEN 4530
4500 PRINT "IN HOHEM ALTER NACH EINER"
4510 PRINT
4520 PRINT "LANGEN HERRSCHAFT":GOTO 4630
4530 ON(RND(1)*5)GOTO 4570,4580,4590,4620,4630
4540 PRINT "AN EINER GRIPPE,WAEHREND EINES"
4550 PRINT
4560 PRINT "EISIGEN WINTERS":GOTO 4630
4570 PRINT " WAEHREND EINER POCKEN-EPEDEMIE.":GOTO 4630
4580 PRINT "DURCH VERSEUCHTES WASSER AN":PRINT:PRINT "TYPHUS.":GOTO 4630
4590 PRINT "BEI EINEM RAUBUEBERFALL WAEHREND"   
4600 PRINT
4610 PRINT "EINER REISE.":GOTO 4630
4620 PRINT "DURCH VERGIFTETES FLEISCH."
4630 INPUT Q$:IF Q$=""THEN 4640:GOTO 4630
4640 GOSUB 7250
4650 VS=VS+1:IF VS<SA THEN 4330
4660 PRINT "DAS SPIEL IST ZU ENDE,DA ALLE"
4670 PRINT " "
4680 PRINT "TEILNEHMER DER TOD EREILT HAT."
4690 PRINT " "
4700 PRINT " "
4710 PRINT " ICH HABE EUCH ALLE UEBERLEBT."
4720 PRINT " "
4730 PRINT " "
4740 PRINT TB$(1):PRINT:PRINT N$(0)
4750 BEEP:PAUSE 44:PRINT:PRINT:PRINT "WUENSCHEN SIE EIN NEUES SPIEL ?"
4760 PRINT:PRINT:INPUT I$:IF I$="J"THEN RUN:ELSE:IF I$><"N"THEN 4750
4770 WINDOW:CLS:END
4780 REM*************************       * PROGRAMMENDE **********
4790 INPUT Q$:IF Q$<>""THEN GOSUB 7250:GOTO 4790
4800 Z=RND(1)*30:KO(SP)=INT(KO(SP)*(100-Z)/100):RV=INT(Z)
4810 WE=(INT(RND(1)*5)+INT(RND(1)*6)+2)/2
4820 H=LA(SP):M=(VO(SP)-60*MU(SP))*5:IF M<0 THEN M=0
4830 IF M<H THEN H=M
4840 M=KO(SP):IF M<H THEN H=M
4850 ER=H*(WE-.5):KO(SP)=KO(SP)+ER
4860 GOSUB 7130:FLAG=0
4870 IF ERNTE<1 THEN Z=2:GOTO 4900
4880 Z=VE*1/ER:IF Z>2 THEN Z=2
4890 IF Z<.8 THEN Z=.8
4900 LP=INT((3*WE+INT(RND(1)*6)+INT(RND(1)*6)+12)*Z)
4910 KP=INT((20-WE*3+INT(RND(1)*5)+INT(RND(1)*5))*4*Z)
4920 WINDOW:PRINT "MAN SCHREIBT DAS JAHR";JA:PRINT
4930 PRINT TB$(TJ(SP)):PRINT:PRINT N$(SP):PRINT
4940 IF FLAG>0 THEN 4980:FLAG=1
4950 PRINT "DIE RATTEN FRASSEN";RV;"% DER":PRINT:PRINT "RESERVEN WEG." 
4960 PRINT:ON WE GOSUB 7140,7150,7160,7170,7180
4970 PRINT "(";ER;"STERS)":PRINT
4980 PRINT "KORNRESERVEN : ";KO(SP);"STER              "
4990 PRINT "KORNVERLANGEN: ";VE;"STER                  "
5000 PRINT "KORNPREIS    : ";KP:PRINT
5010 PRINT "LANDPREIS    : ";LP*.1;"P.HEKTAR":PRINT
5020 PRINT "GOLD         : ";GD(SP);"FLORIN                    "
5030 PRINT "1.KORN KAUFEN  2.KORN VERKAUFEN "
5040 PRINT "3.LAND KAUFEN  4.LAND VERKAUFEN"
5050 WINDOW 28,31,0,31:CLS
5060 PRINT "WAS MACHEN SIE?  (0=NICHTS)":INPUT IN
5070 IF IN<0 OR IN>4 THEN 5060
5080 PRINT
5090 IF IN>0 THEN ON IN GOSUB 7670,7700,7740,7770:GOTO 4920
5100 PRINT "WIEVIEL KORN VERTEILEN SIE AN   "
5110 INPUT "IHRE UNTERTANEN";GG
5120 FLAG=FLAG+1:IF FLAG=2 AND GG=0 THEN 4920
5130 IF GG<KO(SP)*.2 THEN GOSUB 5160:GOTO 5100
5140 IF GG>KO(SP)*.8 THEN GOSUB 5180:GOTO 5100
5150 KO(SP)=KO(SP)-GG:RETURN
5160 PRINT "SIE MUESSEN MINDESTENS 20% DES"
5170 PRINT "KORNS VERTEILEN!":BEEP:PAUSE 20:PRINT:RETURN
5180 PRINT "SIE MUESSEN MINDESTENS 20% DES"
5190 PRINT "KORNS ALS NOTRESERVE BEHALTEN!":BEEP:PAUSE 20:PRINT:PRINT:RETURN
5200 WINDOW:FOR X=0 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT
5210 IF VE>GG THEN 5510
5220 GR=6:SR=2:GOSUB 7820
5230 IF S1(SP)+S2(SP)<35 THEN KA(SP)=INT(KA(SP)+1+RND(1)*4)
5240 IF S3(SP)<RND(1)*20+1 THEN GOSUB 5780
5250 IF VE*1.3>GG THEN 5320
5260 H=VO(SP)/1000:Z=10*(GG-VE)/GG
5270 H=Z*H*(1+RND(1)*25)+1+RND(1)*40:H=RND(1)*H+2:VO(SP)=INT(VO(SP)+H)
5280 PRINT
5290 PRINT INT(H);"MENSCHEN SIND ZUGEWANDERT."
5300 H=RND(1)*(H/5)+1:IF H>50 THEN H=50
5310 KA(SP)=KA(SP)+H:AD(SP)=AD(SP)+1:GE(SP)=GE(SP)+3
5320 PRINT
5330 H=MA(SP)*75:IF H>0 THEN GD(SP)=GD(SP)+H
5340 PRINT "IHR MARKT BRACHTE";H:PRINT
5350 PRINT "FLORIN AN ZOLL EIN."
5360 H=MU(SP)*INT(60+RND(1)*460):IF H>0 THEN GD(SP)=GD(SP)+H
5370 PRINT
5380 PRINT "IHRE MUEHLE MACHTE EINEN GEWINN "
5390 PRINT "VON ";H;"FLORIN."
5400 PRINT
5410 H=SO(SP)*3:GD(SP)=GD(SP)-H:PRINT "SIE ZAHLTEN IHREN SOLDATEN";H:PRINT
5420 PRINT "FLORIN ALS SOLD."
5430 PRINT
5440 IF LA(SP)/320-20>SO(SP) THEN 5540
5450 IF LA(SP)/250-20>SO(SP) THEN 5480
5460 FOR M=1 TO SA:IF M<>SP AND SO(SP)<SO(M)/1.4 THEN 5540
5470 NEXT
5480 PRINT " "
5490 INPUT " ";I$:RETURN
5500 GOTO 5490
5510 H=(VE-GG)*100/VE-9:IF H>65 THEN H=65
5520 IF H<0 THEN H=0
5530 GR=2:SR=H+7:GOSUB 7820:GOTO 5320
5540 REM
5550 AG=0
5560 FOR M=1 TO SA:IF SP=M THEN 5580
5570 IF SO(M)>SO(SP) AND SO(M)>LA(M)/400 AND SO(M)=>SO(AG) THEN AG=M
5580 NEXT:IF AG>0 THEN LV=(SO(AG)*200+LA(SP)/4)*(0.3+RND(1)*0.4):GOTO 5600
5590 LV=1000*RN(1)*9000:IF LA(SP)>30000 THEN LV=LV+RND(1)*(LA(SP)-300005)
5600 IF LV>LA(SP)-5000 THEN LV=LA(SP)/2-2500
5610 IF LV<2 THEN 5480
5620 LA(AG)=LA(AG)+LV:LA(SP)=LA(SP)-LV
5630 H=RND(1)*40+2:IF H>SO(SP)-15 THEN H=SO(SP)-15
5640 SO(SP)=SO(SP)-H
5650 PRINT
5660 PRINT "ALARM  !!!!!"
5670 PRINT
5680 PRINT TB$(TJ(AG)):PRINT:PRINT N$(AG);" GREIFT AN"
5690 PAUSE 50
5700 PRINT "DIE SCHLACHT GING LEIDER":PRINT:PRINT "VERLOREN ..."
5710 PRINT
5720 PRINT "ES GAB ";LV;" HEKTAR LAND UND":PRINT
5730 PRINT "       ";INT(H);"SOLDATEN VERLUST
5740 PRINT
5750 PRINT "ERHOEHEN SIE IHRE"
5760 PRINT:PRINT "VERTEIDIGUNGSFAEHIGKEIT !!!"
5770 PAUSE 29:GOTO 5480
5780 AD(SP)=INT(AD(SP)+RND(1)*2):GE(SP)=INT(GE(SP)+RND(1)*3):RETURN
5790 OD=TJ(SP)/8-INT(TJ(SP)/8):OD=OD*8
5800 SN=(JU(SP)*300-500)*(OD+1)
5810 Z=(150-S1(SP)-S2(SP)-S3(SP))/100:IF Z<1^E-2 THEN Z=1^E-2
5820 A1=INT(((AD(SP)*100+GE(SP)*75+KA(SP)*20)*Z+LS(SP))*S1(SP)/100)
5830 A2=INT(((AD(SP)*50+KA(SP)*25+LS(SP)*0.1)*Z*(5-JU(SP)))*S2(SP)/200)
5840 A3=INT((AD(SP)*250+LS(SP)/5+10*JU(SP)*Z)*S3(SP)/100)
5850 WINDOW
5860 PRINT
5870 PRINT TB$(TJ(SP)):PRINT:PRINT N$(SP)
5880 PRINT
5890 PRINT
5900 PRINT "ES WURDEN ";A1+A2+A3+SN;"FLORIN":PRINT:PRINT "AN STEUERN ";
5910 PRINT "ERHOBEN."
5920 PRINT:PRINT "STEUERN :":PRINT
5930 PRINT "KONSUM ";S1(SP);"%     :";A1;"FLORIN            "
5940 PRINT "HANDEL ";S2(SP);"%     :";A2;"FLORIN            "
5950 PRINT "EINKOMMEN ";S3(SP);"%   :";A3;"FLORIN            "
5960 ON JU(SP)GOSUB 7190,7200,7210,7220
5970 PRINT
5980 WINDOW 22,31,0,31:CLS
5990 PRINT "NUMMERN ZUM AENDERN DER STEUERN ":PRINT " 1-4  , SONST 0 ."
6000 PRINT 
6010 I$="0":INPUT I$:IF I$=""THEN I$="0"
6020 IN=VAL(I$):IF IN>4 THEN 6010
6030 IF IN>0 THEN ON IN GOTO 6230,6260,6290,6320
6040 GD(SP)=GD(SP)+A1+A2+A3+SN
6050 IF GD(SP)<0 THEN GD(SP)=GD(SP)*1.5
6060 IF GD(SP)+2000+OD*1500=>0 THEN RETURN
6070 WINDOW:CLS:PRINT
6080 PRINT TB$(TJ(SP)):PRINT:PRINT N$(SP);" !!!"
6090 PRINT
6100 PRINT
6110 PRINT "DURCH IHRE TOTALE MISSWIRTSCHAFT":PRINT "SIND";
6120 PRINT "SIE JETZT BANKROTT !!!"
6130 PRINT
6140 PRINT
6150 PRINT "IHR VERMOEGEN WURDE ENTEIGNET" 
6160 PRINT
6170 PRINT "UND VERSTEIGERT !!!"
6180 MA(SP)=0:MU(SP)=O:PT(SP)=0:KT(SP)=0
6190 GOSUB 7130 IF KO(SP)>2*VE THEN KO(SP)=2*VE
6200 LA(SP)=6000:GD(SP)=100:LS(SP)=100
6210 INPUT I$
6220 RETURN
6230 INPUT "NEUE KONSUMSTEUER (0-100)";H
6240 IF H<0 OR H>100 THEN H=25
6250 S1(SP)=H:GOTO 5790
6260 INPUT "NEUE HANDELSSTEUER (0-50)";H
6270 IF H<0 OR H>50 THEN H=10
6280 S2(SP)=H:GOTO 5790
6290 INPUT "NEUE EINKOMMENSTEUER(0-25)";H
6300 IF H<0 OR H>25 THEN H=5
6310 S3(SP)=H:GOTO 5790
6320 PRINT:PRINT:PRINT:PRINT "1. SEHR FAIR             2. MILD
6330 PRINT "3. STRENG         4. ABSCHEULICH"
6340 PRINT
6350 INPUT "WELCHE JUSTIZ WUENSCHEN SIE";H
6360 IF H<0 OR H>4 THEN 5790
6370 JU(SP)=H:GOTO 5790
6380 WINDOW
6390 PRINT "STAATLICHE INVESTITIONEN VON    "
6400 PRINT TB$(TJ(SP)):PRINT:PRINT N$(SP)
6410 H$="00 Fl."
6420 PRINT
6430 PRINT "1.MARKTPLATZ          :10";H$:PRINT
6440 PRINT "2.KORNMUEHLE          :20";H$:PRINT
6450 PRINT "3.PALAST(TEILW.)      :30";H$:PRINT
6460 PRINT "4.KATHEDRALE(TEILW.)  :50";H$:PRINT
6470 PRINT "5.SOLDATEN REKRUTIEREN: 5";H$:PRINT
6480 PRINT
6490 IF GD(SP)<0 THEN 6510
6500 GOTO 6530
6510 PRINT "SIE HABEN BEREITS ";-GD(SP);"                 "
6520 PRINT "FLORIN SCHULDEN!!!              ":GOTO 6550
6530 PRINT "IN DER SCHATZKAMMER LIEGEN          "
6540 PRINT GD(SP);" FLORIN.                       "
6550 PRINT "WEITERE EINGABEN: ";
6560 PRINT " JAHRESENDE 8."
6570 PRINT "        VERGLEICH DER VOELKER 6."
6580 PRINT "   VERGLEICH DER BESITZTUEMER 7."
6590 WINDOW 27,31,0,31:CLS
6600 INPUT " IHRE WAHL ?";I$
6610 IF I$="" THEN I$="8"
6620 IN=VAL(I$):IF IN>8 OR IN<1 THEN 6600
6630 M=GD(SP)+3500+2800*OD:IF IN=8 THEN RETURN
6640 ON IN GOSUB 6650,6710,6750,6810,6870,7250,7460:GOTO 6380
6650 PRINT "WIEVIELE MARKTPLAETZE WOLLEN":PRINT
6660 INPUT "SIE KAUFEN";H
6670 IF H<1 THEN RETURN
6680 IF M<H*1000 THEN 6920
6690 MA(SP)=MA(SP)+H:GD(SP)=GD(SP)-H*1000
6700 LS(SP)=INT(LS(SP)+H*10):KA(SP)=INT(KA(SP)+H*5):RETURN
6710 PRINT "WIEVIELE MUEHLEN WOLLEN SIE ":PRINT:INPUT "KAUFEN ?";H
6720 IF H<1 THEN RETURN
6730 IF M<H*2000 THEN 6920
6740 MU(SP)=MU(SP)+H:GD(SP)=GD(SP)-H*2000:LS(SP)=INT(LS(SP)+H*25):RETURN
6750 PRINT "UM WIEVIELE TEILE WOLLEN SIE DEN"
6760 INPUT "PALAST WEITERBAUEN";H
6770 IF H<1 THEN RETURN
6780 IF M<H*3000 THEN 6920
6790 PT(SP)=PT(SP)+H:GD(SP)=GD(SP)-H*3000:LS(SP)=INT(LS(SP)+H*50)
6800 FOR M=1 TO H:AD(SP)=AD(SP)+1+(INT(RND(1)*2)):NEXT M:RETURN
6810 PRINT "WIEVIELE TEILE WOLLEN SIE DIE   "
6820 INPUT "KATHEDRALE WEITERBAUEN";H
6830 IF H<1 THEN RETURN
6840 IF M<H*5000 THEN 6920
6850 KT(SP)=KT(SP)+H:GD(SP)=GD(SP)-H*5000:LS(SP)=INT(LS(SP)+H*100)
6860 FOR M=1 TO H:GE(SP)=GE(SP)+1+INT(RND(1)*6):NEXT:RETURN
6870 PRINT "WIEVIELE EINHEITEN WOLLEN SIE   "
6880 INPUT "REKRUTIEREN";H
6890 IF H<1 OR H*20>VO(SP) THEN RETURN
6900 IF M<H*500 THEN 6920
6910 SO(SP)=SO(SP)+H*20:VO(SP)=VO(SP)-H*20:GD(SP)=GD(SP)-H*500:RETURN
6920 PRINT
6930 PRINT "FUER SO HOHE AUSGABEN KRIEGEN   "
6940 PRINT "SIE KEINEN KREDIT MEHR !!!"
6950 INPUT I$:RETURN
6960 REM
6970 SU=0:M=MA(SP):GOSUB 7230
6980 M=MU(SP):GOSUB 7230:M=PT(SP):GOSUB 7230:M=KT(SP):GOSUB 7230
6990 M=GD(SP)/1000:GOSUB 7230:M=AD(SP)/5:GOSUB 7230
7000 M=KA(SP)/50:GOSUB 7230:M=AD(SP)/5:GOSUB 7230
7010 M=SO(SP)/50:GOSUB 7230:M=GE(SP)/10:GOSUB 7230
7020 M=VO(SP)/2000:GOSUB 7230:M=LS(SP)/500:GOSUB 7230
7030 TJ=SU/SS-JU(SP)-1:IF TJ>7 THEN TJ=7
7040 IF JA+2=SJ(SP) THEN TJ(SP)=TJ(SP)+1
7050 IF TJ>TJ(SP) AND 7 THEN TJ(SP)=TJ(SP) AND 8 OR TJ
7060 IF(TJ(SP) AND 7)<7 THEN RETURN
7070 WINDOW:CLS:PRINT:PRINT:PRINT:PRINT STRING$(32,"-")
7080 PRINT:PRINT "   DAS SPIEL IST ZU ENDE!!"
7090 PRINT:PRINT
7100 PRINT "ES LEBE KING ":PRINT:PRINT N$(SP)
7110 PAUSE 70:BEEP
7120 GOSUB 7250:GOSUB 7460:END
7130 VE=AD(SP)*100+GE(SP)*40+KA(SP)*30+SO(SP)*10+VO(SP)*5:RETURN
7140 PRINT "DUERRE          HUNGERSNOT DROHT":RETURN
7150 PRINT "MISES WETTER     SCHLECHTE ERNTE":RETURN
7160 PRINT "NORMALES WETTER   MITTLERE ERNTE":RETURN
7170 PRINT "GUTES WETTER       SCHOENE ERNTE":RETURN
7180 PRINT "EXCELLENTES WETTER  GROSSE ERNTE":RETURN
7190 PRINT "JUSTIZ(SEHR FAIR):";SN;"FLORIN":RETURN
7200 PRINT "JUSTIZ (MILD)    :";SN;"FLORIN":RETURN
7210 PRINT "JUSTIZ (STRENG)  :";SN;"FLORIN":RETURN
7220 PRINT "JUSTIZ (GRAUSAM) :";SN;"FLORIN":RETURN
7230 SU=SU+M:IF M>10 THEN SU=SU-M+10
7240 RETURN
7250 WINDOW:CLS:PRINT "EIGENTUM DER LAENDER":PRINT STRING$(32,"-")
7260 A=0
7270 FOR M=1 TO SA
7280 A=A+1
7290 PRINT TB$(TJ(M));" ";N$(M):PRINT
7300 NEXT
7310 PRINT
7320 PRINT TAB(0)"LAND";TAB(8)"GOLD";TAB(23)"SOLDATEN"
7330 A=0
7340 FOR M=1 TO SA
7350 A=A+1
7360 PRINT TAB(0)LA(M);TAB(8)GD(M);TAB(23)INT(SO(M)) 
7370 NEXT
7380 PRINT
7390 PRINT TAB(0)"ADEL";TAB(8)"KLERUS";TAB(16)"HAENDLER";TAB(27)"VOLK" 
7400 A=0
7410 FOR M=1 TO SA
7420 A=A+1
7430 PRINT TAB(0)INT(AD(M));TAB(8)GE(M);TAB(17)INT(KA(M));TAB(25)INT(VO(SP))
7440 NEXT
7450 INPUT I$:WINDOW:CLS:RETURN
7460 WINDOW:CLS
7470 PRINT "EIGENTUM DER HERSCHAFTSHAEUSER":PRINT STRING$(32,"-")
7480 A=0
7490 FOR M=1 TO SA
7500 A=A+1
7510 PRINT TB$(TJ(M));" ";N$(M)
7520 PRINT:NEXT
7530 PRINT
7540 PRINT TAB(0)"LAND";TAB(10)"GOLD";TAB(20)"MAERKTE"
7550 A=0
7560 FOR M=1 TO SA
7570 A=A+1
7580 PRINT TAB(0)LA(M);TAB(10)GD(M);TAB(20)MA(M)
7590 NEXT
7600 PRINT
7610 PRINT TAB(0)"MUEHLEN";TAB(10)"PALAST";TAB(20)"KATHEDRALE"
7620 A=0
7630 FOR M=1 TO SA
7640 A=A+1
7650 PRINT TAB(0)MU(M);TAB(10)PT(M);TAB(20)KT(M)
7660 NEXT:INPUT I$:WINDOW:CLS:RETURN
7670 PRINT "WIEVIEL KORN WOLLEN SIE KAUFEN":PRINT:INPUT H
7680 IF H<1 THEN RETURN
7690 GD(SP)=GD(SP)-H*KP/1000:KO(SP)=KO(SP)+H:RETURN
7700 PRINT "WIEVIEL KORN WOLLEN SIE":PRINT:INPUT "VERKAUFEN";H
7710 IF H<1 THEN RETURN
7720 IF KO(SP)-H<VE(SP)/4 THEN PRINT "SIE WOLLEN WOHL EINE REVOLUTION":GOTO 7700
7730 GD(SP)=GD(SP)+H*KP/1000:KO(SP)=KO(SP)-H:RETURN
7740 PRINT "WIEVIEL LAND WOLLEN SIE KAUFEN":PRINT:INPUT H
7750 IF H<1 THEN RETURN
7760 GD(SP)=GD(SP)-H*LP/10:LA(SP)=LA(SP)+H:RETURN
7770 PRINT "WIEVIEL LAND WOLLEN SIE":PRINT:INPUT "VERKAUFEN";H
7780 IF H<1 THEN RETURN
7790 IF LA(SP)-H>5000 THEN 7810:ELSE:PRINT:PRINT "SOVIEL LAND KOENNEN SIE NICHT"     
7800 PRINT "VERKAUFEN !":BEEP:PAUSE 36:PRINT:PRINT:GOTO 7770
7810 GD(SP)=GD(SP)+H*LP/10:LA(SP)=LA(SP)-H:RETURN
7820 H=INT((RND(1)*GR+1)*VO(SP)/100+2):VO(SP)=VO(SP)+H
7830 PRINT INT(H);"MENSCHEN WURDEN GEBOREN"
7840 PRINT
7850 H=INT((RND(1)*SR)*VO(SP)/100+2):VO(SP)=VO(SP)-H
7860 PRINT INT(H);"MENSCHEN STARBEN ."
7870 IF JU(SP)<3 THEN RETURN
7880 H=INT((JU(SP)-2)^2*(2+RND(1))*VO(SP)/180+2):VO(SP)=VO(SP)-H
7890 PRINT
7900 PRINT INT(H);"MENSCHEN FLOHEN VOR DER"
7910 PRINT "ABSCHEULICHEN JUSTIZ."
7920 RETURN
7930 FOR X=0 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT:WINDOW:PRINT
7940 PRINT "    ANDREAS GRIMM PRESENTS":PRINT
7950 PRINT:PRINT:PRINT STRING$(32,"+"):PRINT
7960 PRINT "        SANTA PARAVIA"
7970 PRINT:PRINT:PRINT STRING$(32,"+"):PRINT
7980 PRINT " IN EINER UEBERARBEITUNG FUER":PRINT
7990 PRINT
8000 PRINT "       robotron Z-1013"
8010 PRINT:PRINT
8020 PRINT "      VON MARIO LIPPERT"
8030 PRINT AT(21,0);" ":PAUSE 66:FOR X=0 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT:WINDOW 
8040 RETURN
10000 WINDOW:FOR X=0 TO 31 STEP 7:PRINT AT(X,0);LO$:NEXT
10010 WINDOW 1,31,2,2:ML$="* *   M L  *   S O F T   * *"
10020 FOR X=1 TO 29:PRINT MID$(ML$,X,1);:NEXT:WINDOW 1,31,29,29
10030 FOR X=1 TO 29:PRINT MID$(ML$,X,1);:NEXT:WINDOW:PRINT AT(0,0);" "
10040 FOR X=0 TO 31:PRINT AT(X,0);CHR$(161):PRINT AT(X,31);CHR$(161)
10050 PRINT AT(X,4);CHR$(161):PRINT AT(X,27);CHR$(161):NEXT
10060 FOR X=2 TO 29 STEP 3:PRINT AT(0,X);CHR$(160):PRINT AT(31,X);CHR$(160):NEXT
10065 PAUSE 10
10070 PRINT AT(6,5);"P R E S E N T I E R T":PRINT AT(8,5);STRING$(22,CHR$(158)) 
10071 PRINT AT(4,5);STRING$(22,CHR$(248))
10075 PAUSE 10
10080 BEEP:PAUSE 3:BEEP:PAUSE 1:BEEP:PAUSE 2:BEEP:PAUSE 4:BEEP:PAUSE 2:BEEP:PAUSE 12
10090 PRINT AT(12,9);"P A R A V I A":PRINT AT(14,12);"S K A T"
10100 PRINT AT(10,8);STRING$(16,CHR$(130)):PRINT AT(16,8);STRING$(16,CHR$(131)) 
10105 PAUSE 5
10110 PRINT AT(19,6);"Intelligente  Spiele":PRINT AT(21,6);"fuer Klein und Gross
10119 PRINT AT(25,5);STRING$(22,CHR$(248)):PAUSE 12
10120 ML$="ML-GAMES>>>>>>>>>>>>":Z=6:W=1:FOR X=1 TO LEN(ML$):FOR Y=25 TO Z STEP-1
10130 BEEP:PRINT AT(29,Y);MID$(ML$,X,1)+" ":PRINT AT(2,Y);MID$(ML$,X,1)+" "
10140 NEXT:Z=Z+1:W=W+1:IF W<21 THEN NEXT
10150 PAUSE 33:PRINT AT(19,6);"Welches Spiel ?      "
10160 PRINT AT(21,6);"PARAVIA (1)         ":PRINT AT(23,6);"oder SKAT (2)?      
10170 X$=INKEY$:IF X$=""THEN 10170:ELSE:IF VAL(X$)<1 OR VAL(X$)>2 THEN 10170
10180 IF X$="1"THEN 3870
10190 IF X$="2"THEN 10
10200! E N D E  ***************
