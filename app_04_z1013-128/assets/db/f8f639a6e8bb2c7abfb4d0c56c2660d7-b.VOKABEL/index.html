<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="de" xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_01.png" alt="Screenshot 1" height="384"  />
</div>
<div class="filelist">1000 25cd 0000 b ... <a href="b.VOKABEL.z80">VOKABEL         </a></div>
<div class="text">Vor dem Start END H.(3FFF)-64 eingeben und
ggf. die Daten [4d7c7fcb4a0b28fa1399a3a26b002563] laden

<pre>Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

    5 OUTC.12
    6 OUT(1)=207;OUT(1)=0
    8 IF@(7)=4729G.20
   10 @(7)=0
   15 P.;P."BITTE ERST MIT &gt;END H.(3FFF)-64&lt;"
   16 P."ERWEITERN";P.
   17 P."UND ERNEUT MIT &gt;RUN&lt; STARTEN !";P.;P.;TAB(20);P."DANKE",
   18 @(7)=4729
   19 P.;P.;STOP
   20 OUTC.12
   27 P.;P.;P.
   30 P."      VOKABELLERNPROGRAMM"
   31 P."      "
   35 P.;P.;TAB(30)
   36 P."         DEUTSCH - ENGLISCH"
   37 P.;TAB(30)
   38 P."         ENGLISCH - DEUTSCH"
   40 P.;E=H.(2700)
   48 P.;P.;P.
   50 P.;P."WOLLEN SIE EINE NEUE VOKABEL-";P.
   52 P."DATEI AUFBAUEN ? (J/ENT):",
   60 W=INC.;OUTC.W;P.;P.
   70 IFW=13G.160
   90 P."MAX.SATZLAENGE (VOKABEL+UEBER-"
   92 P.;INP."SETZUNG) ? "V
  101 IFV&gt;0IFV&lt;258G.105
  102 G.90
  105 Q=H.(3F00)-E;Q=Q/V
  106 OUTC.12
  107 P.;P.;P."DAMIT SIND MAX. ",#2,Q," SAETZE";P.;P."SPEICHERBAR"
  108 P.;P.;P.
  109 P."BITTE WARTEN !"
  110 U=E+(Q*V)
  120 F.I=E TO U
  130 POKE I,32
  140 N.I
  150 POKE E,'*'
  160 F.K=E TO U STEP V
  170 IF PEEK(K)='*' GOTO 190
  180 N.K
  190 A=(K-E)/V
  200 P."VON ",#4,Q,"DATENSAETZEN"
  210 P."SIND",#4,A,"ERFASST."
  230 P.;P."AUSWAHL DURCH KENNZIFFER"
  250 P.;P."AUFBAU............0"
  260 P.;P."ANZEIGEN..........1"
  270 P.;P."SUCHEN............2"
  280 P.;P."SORTIEREN.........3"
  290 P.;P."VOKABELLERNEN.....4"
  300 P.;P."AENDERN...........5"
  310 P.;P."STATUS............6"
  312 P.;P."DATEN RETTEN......7"
  315 P.;P."DATEN EINLESEN....8"
  318 P.
  320 INP."EINGABE KENNZIFFER"W
  325 IF W&lt;0 GOTO 320
  330 IF W&gt;8 GOTO 320
  340 GOTO (W*300+400)
  400 REM DATEIAUFBAU
  410 IF K=U P."DATEI VOLL !";P.;GOTO 190
  420 X=K
  430 GOSUB 8000
  435 K=K+V
  440 POKE K,'*';GOTO 190
  700 REM ANZEIGEN
  705 IF K=E P."DATEI LEER !";P.;GOTO 190
  710 GOSUB 5000
  720 GOSUB 5200 
  730 GOSUB 7000
  740 IF Y=B GOTO 1890
  750 X=X+V
  760 Z=Z+1;Y=Y+1
  770 IF Z=4 GOSUB 1850
  780 IF X&gt;=K GOTO 1890
  790 GOTO 730
 1000 REM SUCHEN
 1020 F.S=U+1TOU+1+V/2
 1030 POKE S,32
 1040 N.S
 1050 P."DEUTSCHES WORT :"
 1060 G=V/2;I=U+1
 1070 GOSUB 10000
 1080 F.J=E TO K STEP V
 1090 M=0
 1100 F.I=0TOV/2
 1105 IF PEEK(J+I)=32 GOTO 1130
 1106 IFPEEK(J+I)=35G.1130
 1110 IFPEEK(U+1+I)=PEEK(J+I)G.1170
 1120 N.I
 1130 IF M=I GOTO 1200
 1140 N.J
 1150 P."NICHT GEFUNDEN";P.;GOTO190
 1170 M=M+1;GOTO 1120
 1200 P."GEFUNDEN"
 1210 X=J
 1215 P=(J-E)/V+1
 1220 GOSUB 7000
 1240 P.;GOTO 190
 1300 REM SORTIEREN
 1350 F.J=E TO K-(2*V) STEP V
 1360 F.I=J+V TO K-V STEP V
 1370 IF PEEK(J)&gt;PEEK(I) GOTO 1450
 1380 N.I
 1385 OUT(0)=1;OUT(0)=0
 1390 N.J
 1400 GOTO 190
 1450 F.S=0 TO V-1
 1460 F=PEEK(J+S);POKE(J+S),PEEK(I+S);POKE(I+S),F
 1470 N.S
 1480 GOTO 1380
 1600 REM LERNEN
 1605 OUTC.12;P.;P.;P.
 1610 P."AUSWAHL DURCH KENNZIFFER"
 1620 P.;P.;P.
 1630 P."DEUTSCH - ENGLISCH .......1"
 1640 P.;P."ENGLISCH - DEUTSCH .......2"
 1650 P.;P.
 1660 INP."KENNZIFFER"C
 1665 IFC&gt;2G.1660
 1666 IFC&lt;1G.1660
 1670 OUTC.12;P.;P.;P.
 1680 GOS.5020
 1698 OUTC.12;P.;P.;P.;P.
 1700 P."VOKABEL :";P.
 1710 D=E+(P-1)*V
 1715 D=(C-1)*V/2+D
 1716 D=D+C-1
 1720 F.F=DTOD+V/2-1
 1725 IFPEEK(F)=35G.1750
 1730 OUTC.(PEEK(F))
 1740 N.F
 1745 IFC=1P."KEIN #";ST.
 1750 P.;P.
 1760 P."UEBERSETZUNG ?";P.
 1770 M=0
 1800 F.S=U+1TOU+V/2+1
 1805 PO.S,32;N.S
 1810 G=V/2;I=U+1;GOS.10000
 1815 O=I-U-1
 1820 C=-C+3
 1825 D=E+(P-1)*V+((C-1)*(V/2))+C-1
 1830 F.I=0TOO-1
 1835 IFPEEK(U+1+I)#PEEK(D+I)G.1855
 1838 M=M+1
 1840 N.I
 1842 IFPEEK(D+I)#32IFPEEK(D+I)#35G.1855
 1845 G.1860
 1850 TAB(20);P."&gt;ENTER&lt;",W=INC.;Z=0;RETURN
 1855 P.;P."FALSCH !";G.4000
 1860 P.;P."RICHTIG !"
 1865 P.;P.;P."WEITER.....1"
 1866 P.;P."ENDE.......2"
 1868 P.;P.;P."KENNZIFFER:",
 1869 O=INC.;IFO&lt;75IFO&gt;72G.1871
 1870 G.1869
 1871 O=O-72;OUTC.O+48
 1872 IFO=2G.230
 1873 IFP&lt;AIFP&lt;QP=P+1;C=-C+3;G.1698
 1874 P.;P."DATEI ENDE !";G.190
 1890 TAB(15);P."ENDE &gt;ENTER&lt;",;W=INC.
 1892 P.
 1895 GOTO 190
 1900 REM AENDERN
 1920 GOSUB 5000
 1930 GOSUB 8000
 1940 GOTO 190
 2200 REM STATUS
 2210 P."DATEISTATUS";P.
 2220 F.I=E TO U STEP V
 2230 WORD(I);P." ",
 2240 OUTC.(PEEK(I));OUTC.(PEEK(I+1))
 2250 TAB(1)
 2260 N.I
 2270 P.
 2280 GOTO 190
 2500 REM DATEN RETTEN
 2510 F.J=HEX(F0) TO HEX(FF)
 2520 POKE J,32
 2530 N.J
 2535 P."FILENAME:",
 2540 I=HEX(F0);G=16
 2550 GOSUB 10000
 2560 POKE HEX(E0),E
 2570 POKE HEX(E1),E/256
 2580 POKE HEX(E2),K
 2590 POKE HEX(E3),K/256
 2600 CALL HEX(A0C)
 2610 CALL HEX(10C)
 2620 CALL HEX(A19)
 2630 CALL HEX(10C)
 2640 GOTO 200
 2800 REM
 2810 CALL HEX(A0C)
 2820 CALL HEX(10F)
 2830 F.I=HEX(F0) TO HEX(FF)
 2840 OUTC.(PEEK(I))
 2850 N.I
 2860 CALL HEX(A19)
 2870 CALL HEX(10F)
 2880 P.;GOTO 160
 4000 REM
 4010 P.;P.
 4020 P."WEITER.................1"
 4022 P.
 4025 P."ENDE...................2";P.
 4030 P."RICHTIGE UEBERSETZUNG..3";P.
 4032 P."NEUER VERSUCH..........4";P.
 4035 P.
 4040 P."KENNZIFFER:",
 4050 O=INC.;O=O-72;IFO&gt;0IFO&lt;5G.4060
 4055 G.4050
 4060 OUTC.O+48
 4065 C=-C+3
 4070 IFO=2P.;P.;G.230
 4080 IFO=1IFP&lt;AP=P+1;G.1698
 4090 IFO=4G.1698
 4098 P.;P.;P."RICHTIGE UEBERSETZUNG :"
 4100 P.
 4110 F.O=DTOD-1+V/2
 4120 IFPEEK(O)=35G.4160
 4130 OUTC.(PEEK(O))
 4150 N.O
 4160 REM
 4200 C=-C+3;G.1865
 5000 REM ABFRAGE VON
 5010 P.;Z=0
 5020 INP."AB POS."P;Y=P-1
 5025 IF Y&lt;0 GOTO 5020
 5030 X=E+(Y*V)
 5040 IF X&gt;=K GOTO 5020
 5041 IF W=1 GOTO5050
 5042 F.Y=H.(3096) TO H.(309F)
 5043 POKE Y,32;N.Y
 5050 RETURN
 5200 REM ABFRAGE BIS
 5220 INP."BIS POS."W;B=W-1
 5230 IF B&gt;A-1 GOTO 5220
 5240 IF B&lt;Y GOTO 5220
 5241 F.Y=H.(3096) TO H.(309F)
 5242 POKE Y,32;N.Y
 5243 Y=P-1
 5250 RETURN
 7000 REM
 7010 P.
 7020 P."LFD.NR.",#4,P," ",
 7030 P.;P.;P."DEUTSCH  :",
 7040 G=V/2;I=X
 7050 GOSUB 10300
 7060 P.;P."ENGLISCH :",
 7070 I=X+V/2+1
 7080 GOSUB 10300
 7090 P.;P=P+1;RETURN
 8000 REM
 8010 P."DEUTSCHES WORT :",
 8020 G=V/2
 8022 I=X
 8030 GOSUB 10000
 8032 POKEI,35
 8040 P."ENGLISCHES WORT:",
 8050 G=V/2;I=X+V/2+1
 8060 GOSUB 10000
 8065 RETURN
10000 REM EINGABE DATENSATZ
10010 Z=1;H=I+G
10020 IF PEEK(I)=32 GOTO 10100
10030 IF PEEK(I)='*' GOTO 10100
10040 OUTC.(PEEK(I));I=I+1
10045 IF I=H GOTO 10100
10050 IFPEEK(I)#32IFPEEK(I)#35G.10040
10100 W=INC.
10110 IF W=8 I=I-1;GOTO 10200
10120 IF W=9 I=I+1;GOTO 10250
10130 IF W=13 OUTC.W;RETURN
10140 POKE I,W
10150 I=I+1
10160 OUTC.W
10170 IF I#H GOTO 10100
10180 P.;RETURN
10200 IF I&lt;Z I=I+1;GOTO 10100
10210 GOTO 10160
10250 IF I&gt;H I=I-1;GOTO 10100
10260 GOTO 10160
10300 REM AUSGABE DATENSATZ
10310 F.J=I TO I+G-1
10315 IFPEEK(J)=35RETURN
10320 OUTC.(PEEK(J))
10330 N.J
10340 RETURN
</pre>
</div>
</body>
</html>
