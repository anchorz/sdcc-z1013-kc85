Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

    1 RUN 75
    2 IF N<3 THEN RETURN
    3 FOR K=N TO 3 STEP-1
    4 Z=3:IF K<4 THEN 8
    5 FOR I=4 TO K:0=P(Z):P=P(I)
    6 IF G(0,0)*G(0,0)+B(0,0)*B(0,0)<G(P,P)*G(P,P)+B(P,P)*B(P,P)THEN Z=I
    7 NEXT 
    8 P=P(Z):P(Z)=P(K):P(K)=P
    9 V=G(P,P)*B(P,P)+B(P,P)*B(P,P):IF V<P1 THEN PRINT "PIVOT";P;V
   10 V=1/V:FOR J=1 TO K-1:Z=P(J):IF G(Z,P)=0 AND B(Z,P)=0 THEN 16
   11 FOR I=1 TO K-1:0=P(I):IF G(P,0)=0 AND B(P,0)=0 THEN 15
   12 X=G(Z,P)*G(P,0)-B(Z,P)*B(P,0):Y=G(Z,P)*B(P,0)+B(Z,P)*G(P,0)
   13 G(Z,0)=G(Z,0)-(X*G(P,P)+Y*B(P,P))*V
   14 B(Z,0)=B(Z,0)-(Y*G(P,P)-X*B(P,P))*V
   15 NEXT
   16 NEXT:NEXT:RETURN
   17 B(L,L)=B(L,L)+B1:B(L,M)=B(L,M)-B1:B(M,L)=B(M,L)-B1:B(M,M)=B(M,M)+B1
   18 RETURN
   19 G(L,L)=G(L,L)+G1:G(L,M)=G(L,M)-G1:G(M,L)=G(M,L)-G1:G(M,M)=G(M,M)+G1
   20 RETURN
   21 G(L,J)=G(L,J)-X:G(L,K)=G(L,K)+X:G(M,J)=G(M,J)+X:G(M,K)=G(M,K)-X
   22 RETURN
   23 FOR I=1 TO A:FOR J=1 TO A:G(I,J)=0:B(I,J)=0:NEXT:NEXT 
   24 IF Z(1)=0 THEN 26
   25 FOR I=1 TO Z(1):G1=R(I,1):L=R(I,2):M=R(I,3):GOSUB 19:NEXT  
   26 IF Z(2)=0 THEN 28
   27 FOR I=1 TO Z(2):B1=C(I,1)*F3:L=C(I,2):M=C(I,3):GOSUB 17:NEXT 
   28 IF Z(3)=0 THEN 31
   29 FOR I=1 TO Z(3):L=L(I,3):M=L(I,4):B1=L(I,1)/F3:G1=-B1/L(I,2)  
   30 GOSUB 19:GOSUB 17:NEXT 
   31 IF Z(4)=0 THEN 34
   32 FOR I=1 TO Z(4):J=I(I,2):K=I(I,3):L=I(I,4):M=I(I,5):X=I(I,1)
   33 GOSUB 21:NEXT 
   34 RETURN
   35 PRINT:INPUT "* INPUT: ";C:PRINT TAB(14);CHR$(11);:INPUT " (";C0
   36 PRINT TAB(18);CHR$(11);")"
   37 PRINT:INPUT "* OUTPUT:";D:PRINT TAB(14);CHR$(11);:INPUT " (";D0
   38 PRINT TAB(18);CHR$(11);")":PRINT
   39 INPUT "* RG(kOhm):";R1:PRINT TAB(14);CHR$(11);:PRINT:INPUT "* RL(kOhm):";R2
   40 INPUT "*Frequenz(Mhz): ";F2:F3=F2/F0   
   41 GOSUB 23
   42 P(1)=C:P(2)=D:P(N+1)=C0:Z=0:FOR I=3 TO N
   43 Z=Z+1:IF Z=C OR Z=D OR Z=C0 THEN 43
   44 P(I)=Z:NEXT 
   45 IF C0=D0 THEN 48
   46 FOR J=1 TO N+1:G(D0,J)=G(D0,J)+G(D,J):B(D0,J)=B(D0,J)+B(D,J):NEXT 
   47 FOR I=1 TO N+1:G(I,D0)=G(I,D0)+G(I,D):B(I,D0)=B(I,D0)+B(I,D):NEXT 
   48 GOSUB 2
   49 G1=G(C,C):G2=G(C,D):G3=G(D,C):G4=G(D,D)
   50 B1=B(C,C):B2=B(C,D):B3=B(D,C):B4=B(D,D)
   51 PRINT:INPUT "*Parameter?(Y/U/P/E/A/F/Ycorr)";X$:IF X$="Y"THEN 58
   52 IF X$="U"OR X$="P"THEN 65
   53 IF X$="E"THEN 70
   54 IF X$="A"THEN 73
   55 IF X$="F"THEN 40
   56 IF X$="Ycorr"THEN 128
   57 GOTO 85
   58 PRINT "G11=";G1;TAB(16);"G12=";G2:PRINT "G21=";G3;TAB(16);"G22=";G4
   59 PRINT "B11=";B1;TAB(16);"B12=";B2:PRINT "B21=";B3;TAB(16);"B22=";B4:GOTO 51
   60 X1=G2*G3-B2*B3:Y1=G2*B3+B2*G3:V1=G1+1/R1:V2=G4+1/R2:RETURN
   61 V=ATN(X/Y)*180/PI:IF X<0 THEN V=V+180
   62 W=X*X+Y*Y:X=SQR(W):Y=10*LN(X)/LN(10):RETURN
   63 W=(X+U)*(X+U)+Y*Y:X=((X-U)+(X+U)+Y*Y)/W:Y=(Y*(X+U)-Y*(X-U))/W
   64 GOSUB 61:PRINT "RF=";X:PRINT " G=";U;"(mS)":PRINT "PH=";V;" GRD":RETURN
   65 GOSUB 60:U=V1*V2-B1*B4-X1:V=B1*V2+V1*B4-Y1:W=U*U+V*V
   66 X=-(G3*U+B3*V)/R1/W:Y=(G3*V-B3*U)/R1/W:GOSUB 61:IF X$="P"THEN 68
   67 PRINT "VU=";X;" (";2*Y;" dB)":PRINT "Ph=";V;" GRD":GOTO 51
   68 PRINT "VP=";10*LN(4*W*R1/R2)/LN(10);" dB" 
   69 PRINT " S=";2*V1*V2/(SQR(X1*X1+Y1*Y1)+X1):GOTO 51
   70 GOSUB 60:V=V2*V2+B4*B4:X=G1-(X1*V2+Y1*B4)/V:Y=B1-(Y1*Y2-X1*B4)/V
   71 PRINT "GE=";X:PRINT "BE=";Y:U=1/R1
   72 GOSUB 63:GOTO 51
   73 GOSUB 60:V=V1*V1+B1*B1:X=G4-(X1*V1+Y1*B1)/V:Y=B4-(Y1*V1-X1*B1)/V
   74 PRINT "GA=";X:PRINT "BA=";Y:U=1/R2:GOTO 72
   75 CLS:PRINT TAB(4);"*** KESA-L***"
   76 M$="RCLI":DIM Z(LEN(M$))
   77 PRINT:PRINT "  MODELLBIBLIOTHEK = ";M$:PRINT
   78 INPUT " KNOTENZAHL:";A 
   79 INPUT "*  BE-TYPEN:";B$
   80 INPUT "*   F0(Mhz):";F0:F1=2*PI*F0
   81 FOR I=1 TO LEN(B$):FOR J=1 TO LEN(M$)
   82 IF MID$(B$,I,1)=MID$(M$,J,1)THEN GOSUB 88
   83 NEXT:NEXT 
   84 DIM G(A,A):DIM B(A,A):DIM P(A):P1=1E-12:N=A-1
   85 PRINT:INPUT "* FORTSETZUNG ?   (K/E/H)";X$:IF X$="E"THEN RUN 75
   86 IF X$="K"THEN 120
   87 GOTO 35
   88 PRINT:PRINT "* ANZAHL ";MID$(B$,I,1);":";:INPUT "";Z(J)
   89 ON J GOSUB 91,96,103,112:FOR K=1 TO Z(J):ON J GOSUB 93,98,105,114
   90 NEXT:RETURN
   91 DIM R(Z(1),3) 
   92 PRINT "Nr";TAB(4);"Wert(Kohm)";TAB(25);"K   K":RETURN
   93 PRINT "R";K;TAB(8);:INPUT "";R(K,1):PRINT TAB(24);CHR$(11);:R(K,1)=1/R(K,1)
   94 INPUT "";R(K,2):PRINT TAB(27);CHR$(11);:INPUT ",";R(K,3):RETURN
   95 PRINT "R";I;TAB(5);1/R(I,1);TAB(24);R(I,2);TAB(27);",";R(I,3):RETURN     
   96 DIM C(Z(2),3)
   97 PRINT "Nr";TAB(4);"Wert(pF)";TAB(25);"K   K":RETURN
   98 PRINT "C";K;TAB(8);:INPUT "";C(K,1):PRINT TAB(24);CHR$(11);
   99 C(K,1)=F1*C(K,1)*1E-3:INPUT "";C(K,2):PRINT TAB(27);CHR$(11);
  100 INPUT ",";C(K,3):RETURN
  101 PRINT "C";I;TAB(5);C(I,1)*1E3/F1;TAB(24);C(I,2);TAB(26);",";C(I,3)
  102 RETURN
  103 DIM L(Z(3),4) 
  104 PRINT "Nr";TAB(6);"Wert(uH/QL)";TAB(25);"K   K":RETURN
  105 PRINT "L";K;TAB(9);:INPUT "";L(K,1):PRINT TAB(13);CHR$(11);"/";
  106 L(K,1)=-1E3/F1/L(K,1)
  107 INPUT "";L(K,2):PRINT TAB(24);CHR$(11);:INPUT "";L(K,3)
  108 PRINT TAB(27);CHR$(11);:INPUT ",";L(K,4)
  109 RETURN
  110 PRINT "L";I;TAB(5);-1E3/F1/L(I,1);"/";L(I,2);TAB(24);L(I,3)
  111 PRINT TAB(27);",";L(I,4):RETURN
  112 DIM I(Z(4),5)
  113 PRINT "Nr";TAB(4);"S(mA/V)";TAB(18);"S+ S- K+ K-":RETURN
  114 PRINT "I";K;TAB(5);:INPUT "";I(K,1):PRINT TAB(17);CHR$(11);
  115 INPUT "";I(K,2):PRINT TAB(19);CHR$(11);:INPUT ",";I(K,3)
  116 PRINT TAB(23);CHR$(11);:INPUT "";I(K,4):PRINT TAB(25);CHR$(11);
  117 INPUT ",";I(K,5):RETURN
  118 PRINT "I";I;TAB(5);I(I,1);TAB(17);I(I,2);TAB(20);",";I(I,3);TAB(23);
  119 PRINT I(I,4);TAB(26);",";I(I,5):RETURN
  120 X$="":PRINT:PRINT "* TYP ?(";B$;")";:INPUT "";X$
  121 FOR J=1 TO LEN(M$):IF X$=MID$(M$,J,1)THEN 124
  122 NEXT:IF X$=""THEN 35
  123 X$="X":GOTO 120
  124 PRINT:ON J GOSUB 92,97,104,113:FOR I=1 TO Z(J)
  125 ON J GOSUB 95,101,110,118:NEXT
  126 INPUT "* INDEX ?";K:IF K=0 THEN 120
  127 ON J GOSUB 93,98,105,114:GOTO 126
  128 CLS:PRINT "Y-PARAMERTER(mS)"
  129 INPUT "G11=";G1:PRINT TAB(17);CHR$(11);:INPUT "G12=";G2
  130 INPUT "G21=";G3:PRINT TAB(17);CHR$(11);:INPUT "G22=";G4;
  131 INPUT "B11=";B1:PRINT TAB(17);CHR$(11);:INPUT "B12=";B2
  132 INPUT "B21=";B3:PRINT TAB(17);CHR$(11);:INPUT "B22=";B4
  133 PRINT:INPUT "RG(Kohm)=";R1:PRINT:PRINT CHR$(11);:INPUT "RL(Kohm)=";R2
  134 GOTO 51
