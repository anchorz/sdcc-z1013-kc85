<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="de" xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_01.png" alt="Screenshot 1" height="384"  />
<img src="screenshot_02.png" alt="Screenshot 2" height="384"  >
</div>
<div class="filelist">2bc0 41bf 0300 B ... <a href="ganymed.z80">GANYMED         </a></div>
<div class="text">braucht ein paar Sekunden ehe die Initialisierung beendet ist. Warten!

Instruktionen:
-------------------------------

SIE SIND AN BORD VON FLUG 08/15
DER NIOBSUCHFLOTTE,

IHRE AUFGABE IST
MOEGLICHST VIELE NIOBKRISTALLE
ZU FINDEN UND ZURUECKZUKEHREN .

GANYMED IST EIN LABYRINTH
VON 400 RAEUMEN.

ES KANN FOLGENDE UEBERRASCHUNGEN

GEBEN:1. VERSCHLOSSENE RAEUME

      2. MATERIEBEAMER DIE SIE
         WEGTRANSPORTIEREN"

      3. MONSTER DIE
         UNBERECHENBAR SIND.

      4. FALLGRUBEN, DURCH DIE
         SIE HERAUSKOMMEN

UND NATUERLICH AUCH KRISTALLE.
AUCH MONSTER KOENNEN KRISTALLE
GEBEN !!

IHR SAUERSTOFF REICHT FUER
100 RAUME.

SIE BRAUCHEN MINDESTENS 55
KRISTALLE FUER DEN RUCKFLUG,
VON DENEN JEDES KRISTALL AUF DER

ERDE 1,5 MILL. DOLLAR BRINGT.      
ICH WUENSCHE IHNEN VIEL GLUECK !
          
<pre>10 DIM C$(40):DIM A(400):BORDER 1:PAPER 1:INK 3
20 WINDOW:CLS
30 TT=0
40 CR=0:LL=100
50 DEF FN ZU(X)=INT(400*RND(I))+1
60 PRINT:PRINT "********************************"
70 PRINT:PRINT TAB(12)"GANYMED":PRINT
80 PRINT "********************************"
83 GOSUB 1430
90 PRINT "WIRD EINE ANLEITUNG BENOETIGT ?":PRINT:PRINT TAB(13);"(Y/N)?"               
95 A$=INKEY$:IF A$="" GOTO 95
100 IF A$="N"THEN 170
110 CLS
120 FOR I=1 TO 28:READ C$:PAUSE 1
130 PRINT C$:PRINT
140 IF I&gt;15 THEN PAUSE 35
150 NEXT I:INPUT "START MIT &gt;ENTER&lt; !";A:CLS
170 PRINT "SIE SIND JETZT AUF GANYMED":PRINT "GELANDET":PRINT
180 PRINT "SIE SIND IN RAUM ",
190 OU=FN ZU(I) 
200 IF A(OU)&lt;&gt;ASC(" ")GOTO 190
210 OT=FN ZU(I)
220 IF A(OT)&lt;&gt;ASC(" ")THEN 210
230 E=FN ZU(I)
240 IF A(E)&lt;&gt;ASC(" ") THEN 230
250 A(E) = ASC("Ä"):PRINT E  
260 OG=ASC("Ä")
270 IF INKEY$=" " THEN RETURN
280 PRINT
290 PRINT "HIER IST EINE KARTE DER RAUEME !"
300 PAUSE 36
310 GOSUB 330
320 GOTO 560
330 PAPER 7:CLS:PAPER 1:WINDOW 1,20,0,19:CLS:WINDOW:REM KARTE ZEIGEN
340 FOR I=1 TO 400
350 PRINT;INK 5;CHR$(A(I));
360 IF 20*INT(I/20)=I THEN PRINT
370 A$=INKEY$:IF A$=" "THEN A$="":PAPER 1:RETURN
380 A$=""
390 NEXT I
400 WINDOW 1,31,20,31:CLS
410 PRINT;INK 2;" LEGENDE :"
420 PRINT
430 PRINT;INK 7;"Ä SIE":PRINT
440 PRINT "@ BEAMEN":PRINT
450 PRINT "ÿ VERSPERRT    ":PRINT 
460 PRINT "Å FALLGRUBE":PRINT   
470 PRINT "Ê MONSTER":PRINT
480 PRINT;INK 2;"* KRISTALL":PRINT
490 PRINT "   N":PRINT
500 PRINT " W ¦ O" 
510 PRINT:PRINT "   S"
520 FOR I=1 TO 3:PRINT AT(INT(E/20),E-1-20*INT(E/20));" " :PAUSE 10
530 PAPER 1:PRINT;INK 2;AT(INT(E/20),E-1-20*INT(E/20));"Ä":PAUSE 3:NEXT I 
540 WINDOW:CLS
550 RETURN
560 REM DIALOG
570 CLS
580 PRINT:PRINT:PRINT "KRISTALLANZAHL :";CR
590 PRINT:PRINT;INK 6;"SAUERSTOFF FUER";LL;"RAEUME"  
600 PRINT
610 FOR I=0 TO 31
620 PRINT "*";
630 NEXT I:PRINT
640 PRINT "SIE BEFINDEN SICH IN RAUM:";E
650 FOR I=0 TO 37:PRINT "*";:NEXT I:PRINT
660 PRINT:PRINT "LAGEMELDUNG DES COMPUTERS"
670 PRINT:PRINT "NORDEN :",CHR$(A(E-20))
680 PRINT "SUEDEN :",CHR$(A(E+20))
690 PRINT "OSTEN  :",CHR$(A(E+1))
700 PRINT "WESTEN :",CHR$(A(E-1))
710 PRINT:PRINT "WELCHE RICHTUNG WOLLEN SIE GEHEN ?":PRINT
720 PRINT "(N/S/O/W) ?":OG =E
730 A$=INKEY$
740 IF LEN(A$)=0 THEN 730
750 BEEP:PRINT
760 IF A$="N" AND A(E-20)&lt;&gt;ASC("ÿ")THEN E=E-20
770 IF A$="S" AND A(E+20)&lt;&gt;ASC("ÿ")THEN E=E+20
780 IF A$="O" AND A(E+1)&lt;&gt;ASC("ÿ")THEN E=E+1
790 IF A$="W" AND A(E-1)&lt;&gt;ASC("ÿ")THEN E=E-1
800 IF A$="*" THEN GOSUB 330
810 A$=""
820 IF A(E)= ASC("*") THEN GOSUB 940
830 IF A(E)= ASC("@") THEN GOTO 990
840 IF A(E)= ASC("Ê") THEN GOSUB 1270
850 IF A(E)= ASC("Å") THEN GOTO 1050
860 LL=LL-1
870 IF LL=50 OR LL=20 THEN GOSUB 2220
880  IF LL=0 THEN 1960
890 A(OG)=ASC(" ")
900 A(E)= ASC("Ä")
910 IF RND(2)&gt;.8 THEN GOSUB 330
920 IF E=OT OR E=OU THEN 1670
930 GOTO 560
940 REM KRISTALL GEFUNDEN
950 X=INT(RND(3)*3)+2:CR=CR+X:INK 2
960 PRINT "SIE HABEN";X;" KRISTALLE GEFUNDEN,GESAMT :";CR
970 PAUSE 31:INK 3
980 CLS:RETURN
990 REM BEAMSTRAHL
1000 INK 6:PRINT "SIE WURDEN WEGGEBEAMT !":PRINT
1010 E=FN ZU(I)
1020 IF A(E)=ASC("ÿ")THEN 1010
1030 PRINT "SIE BEFINDEN SICH JETZT IN RAUM :";E
1040 PAUSE 40:CLS:INK 3:GOTO 830 
1050 REM FALLGRUBE
1060 CLS
1070 PRINT:PRINT "SIE SIND IN EINE FALLGRUBE":PRINT "  GEFALLEN !":PAUSE 15
1080 FOR I=1 TO 31
1090 X$="«"
1100 INK 2:PRINT STRING$(32,X$)
1110 PRINT SPC(32)
1120 NEXT I:INK 3
1130 X=INT(RND(3)*10)+1
1140 IF X&gt;8 THEN CLS:PRINT:PRINT "ABER EIN MONSTER HAT SIE GERETTET !":PRINT
1150 IF X&gt;8 THEN Y=INT(RND(3)*CR)+1:PRINT:PRINT "ES VERLANGT DAFUER";Y;
1160 IF X&gt;8 THEN PRINT " KRISTALLE ":CR=CR-Y
1170 IF X&gt;8 THEN PAUSE 25:GOTO 860
1180 CLS
1190 BORDER 5:PAPER 5:PRINT:PRINT "SIE SIND AUSSERHALB VON GANYMED .":PRINT
1200 PRINT:PRINT "ENDSTAND :";CR;" KRISTALLE"
1210 PRINT:IF CR&lt;55 THEN PRINT "DAS REICHT ABER NICHT,LEBEN SIE WOHL!":GOTO 2160
1220 PRINT:PRINT "DAS GENUEGT ZUM RUECKFLUG !"
1230 PRINT:PRINT "SIE ERHALTEN FUER DIE KRISTALLE";1.5*CR;"MILL.":PRINT "
1235 PRINT "DOLLAR":PRINT
1240 IF CR&gt;TT THEN TT=CR
1250 PRINT:PRINT "DER BESTE ERHIELT BISHER";1.5*TT;"MILL":PRINT
1255 PRINT "DOLLAR":PRINT
1260 GOTO 2160
1270 REM BEGEGNUNG MIT MONSTER
1280 K=INT(RND(3)*4)+1:INK 6
1290 PRINT "SIE SIND EINEM MONSTER BEGEGNET":PRINT
1300 PAUSE 40
1310 IF K&lt;4 THEN PRINT "ES HAT SIE GESEHEN !":PRINT
1320 PAUSE 25:  IF K&lt;4 THEN GOSUB 2000
1330 PRINT "ABER ES IST FREUNDLICH "
1340 IF K=2 THEN S=INT(RND(3)*30)+1:PRINT "UND GIBT IHNEN";S;" KRISTALLE":PRINT
1350 IF K=2 THEN CR=CR+S
1360 IF K=3 THEN PRINT "UND NIMMT NUR"
1370 IF K=3 AND CR&gt;0 THEN S=INT(RND(0)*CR):ELSE S=0
1380 IF K=3 THEN PRINT;S;" KRISTALLE !":CR=CR-S
1390 IF K&gt;3 THEN PRINT "ES SCHLAEFT !"
1400 PRINT "GESAMTSTAND";CR;"KRISTALLE"
1410 PAUSE 40:INK 3
1420 RETURN
1430 PRINT:REM INIT-TEIL
1440 FOR I=1 TO 400
1450 A(I)=ASC(" ")
1460 IF I&lt;=20 OR I&gt;=380 THEN A(I)=ASC("ÿ")
1470 IF I=20*INT(I/20)THEN A(I)=ASC("ÿ")
1480 NEXT I
1490 FOR I=21 TO 381 STEP 20:A(I)=ASC("ÿ"):NEXT I
1500 FOR I=20 TO 380 STEP 20:A(I)=ASC("ÿ"):NEXT I
1510!HINDERNISSE!
1520 FOR I=1 TO 40
1530 C=FN ZU(I)
1540 IF A(C)=ASC(" ") THEN A(C)=ASC("*"):NEXT I:FOR I=1 TO 25
1550 C=FN ZU (I)
1560 IF A(C)=ASC(" ") THEN A(C)=ASC("Ê")
1570 C=FN ZU(I)
1580 IF A(C)=ASC(" ") THEN A(C)=ASC("@")
1590 C=FN ZU(I)
1600 IF A(C)=ASC(" ")THEN A(C)=ASC("Å")
1610 NEXT I
1620 FOR I=1 TO 80
1630 C=FN ZU(I)
1640 IF A(C)=ASC(" ") THEN A(C)=ASC("ÿ")
1650 NEXT I
1660 RETURN
1670 REM GEHEIMGANG
1680 PAPER 6:CLS:PRINT " SIE SIND IN EINEM GEHEIMGANG !"
1690 PAUSE 20:GOTO 1230
1700 REM INSTRUKTIONEN
1710 DATA "INSTRUKTIONEN ZU GANYMED "
1720 DATA ""
1730 DATA "-------------------------------" 
1740 DATA "SIE SIND AN BORD VON FLUG 08/15"
1750 DATA "DER NIOBSUCHFLOTTE,","IHRE AUFGABE IST"
1760 DATA "MOEGLICHST VIELE NIOBKRISTALLE" 
1770 DATA "ZU FINDEN UND ZURUECKZUKEHREN ."
1780 DATA "GANYMED IST EIN LABYRINTH"
1790 DATA "VON 400 RAEUMEN."
1800 DATA "ES KANN FOLGENDE UEBERRASCHUNGEN"
1810 DATA "GEBEN:1.ÿ  VERSCHLOSSENE RAEUME
1820 DATA "      2.@  MATERIEBEAMER DIE SIE
1830 DATA "           WEGTRANSPORTIEREN"
1840 DATA "      3.Ê  MONSTER DIE 
1850 DATA "           UNBERECHENBAR SIND."
1860 DATA "      4.Å  FALLGRUBEN, DURCH DIE"
1870 DATA "           SIE HERAUSKOMMEN
1890 DATA "UND NATUERLICH AUCH KRISTALLE."
1900 DATA "AUCH MONSTER KOENNEN KRISTALLE"
1905 DATA "GEBEN !! "
1910 DATA "IHR SAUERSTOFF REICHT FUER "
1915 DATA "100 RAUME."
1920 DATA "SIE BRAUCHEN MINDESTENS 55","KRISTALLE FUER DEN RUCKFLUG,"         
1930 DATA "VON DENEN JEDES KRISTALL AUF DER"
1940 DATA "ERDE 1,5 MILL. DOLLAR BRINGT."
1950 DATA "ICH WUENSCHE IHNEN VIEL GLUECK !"
1960 PRINT;INK 2;"IHRE LEBENSERHALTUNGSSYSTEM IST"
1965 PRINT "AUF NULL"
1970 PRINT "IRGENDWANN WIRD EIN MONSTER SIE"
1972 PRINT "BEERDIGEN"
1990 GOTO 2160
2000 CLS
2010 X$="_":PRINT "ES KOMMT AUF SIE ZU !!":PRINT
2020 PRINT AT(10,0);STRING$(32,X$)
2030 PRINT AT(9,31);"Ä"
2040 IF K=1 THEN FOR I=0 TO 30
2050 IF K&lt;&gt;1 THEN FOR I=0 TO 29
2060 PRINT AT(9,I);" Ê"
2070 PAUSE 2
2080 NEXT I
2090 IF K&lt;&gt;1 THEN RETURN
2100 FOR I=1 TO 30
2110 PRINT STRING$(32,X$)
2120 NEXT I
2130 CLS
2140 PRINT "ES HAT SIE GEFRESSEN":PRINT
2150 GOTO 2160
2160 PRINT:PRINT "WOLLEN SIE NEU BEGINNEN(Y/N)?"    
2170 A$=INKEY$:IF A$=""THEN 2170
2180 IF A$="Y"THEN GOTO 20
2190 CLS
2200 PRINT AT(10,15);"NA DANN NICHT !"
2210 PAUSE 45:CLS:END
2220 FOR I=1 TO 10
2240 PRINT AT(20,2);" ":PAUSE 3
2250 NEXT I
2260 RETURN
</pre>
</div>
</body>
</html>
