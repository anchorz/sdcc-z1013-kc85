<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="de" xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><video height="400" controls autoplay loop>
<source src="animation.mp4" type="video/mp4">
</video>
<img src="screenshot_01.png" alt="Screenshot 1" height="384"  />
</div>
<div class="filelist">2bc0 352d 0000 B ... <a href="frogger.z80">FROGGER         </a></div>
<div class="text">Das ist das Froesche-Schnecken-
spiel !


  Geschicklichkeit ist gefragt!


* * ADAPTET FROM PC ZX 81 * *


         Aufgepasst!
                      
<pre>10 GOTO 100
15 IF N=0 THEN RETURN
20 FOR B=0 TO 3:IF PEEK(Z+N*X+M+B)=Y THEN A=1
25 NEXT
30 IF A=1 THEN PU=PU+10:BEEP:BEEP:BEEP:PAUSE 2
35 RETURN
40 IF N=21 OR A=1 THEN RETURN
45 FOR B=0 TO 3:IF PEEK(Z+(N+2)*X+M+B)=Y THEN A=1
50 NEXT:IF A=1 THEN PU=PU+10:BEEP:BEEP:BEEP:PAUSE 2
55 RETURN
60 IF M=-1 OR A=1 THEN RETURN
65 FOR B=0 TO 2:IF PEEK(Z+(N+B)*X+M)=Y THEN A=1
70 NEXT:IF A=1 THEN PU=PU+10:BEEP:BEEP:BEEP:PAUSE 2
75 RETURN
80 IF M=28 OR A=1 THEN RETURN
85 FOR B=0 TO 2:IF PEEK(Z+(N+B)*X+M+3)=Y THEN A=1
90 NEXT:IF A=1 THEN PU=PU+10:BEEP:BEEP:BEEP:PAUSE 2
95 RETURN
100 WINDOW:CLS:PRINT TAB(6)"FROGGER AND SNAIL":PAUSE 9
105 PRINT TAB(6)"-------------------":PAUSE 9
110 PRINT:PRINT:PRINT;"Das ist das Froesche-Schnecken- spiel !":PAUSE 9
115 PRINT:PRINT:PRINT TAB(2)"Geschicklichkeit ist gefragt!":PRINT:PRINT
120 PRINT AT(15,8);" Aufgepasst! ":PAUSE 5
125 PRINT AT(11,0);"* * ADAPTET FROM PC ZX 81 * *":PAUSE 15
130 PRINT AT(19,6);" Jetzt geht's los ! ":PAUSE 40
135!++++++++++++++++++++++++
140!Angepasst an Z 1013 von+  
141!JOACHIM NOLTE  2/87    +
145!DAHLIENWEG 26          +
150!DRESDEN                +
155!8028                   +   
160!++++++++++++++++++++++++
165!**************************!
170!*                        *!
175!*  ADAPTET FROM ZX 81    *!
180!*  by Juergen Schmidt    *!
185!*     8023 DRESDEN       *!
190!*  Hechtstrasse 133     *!
195!*                       *!
200!*   VERSION 2.1         *!
205!*                       *!
210!*************************!
215 CLS
220 PRINT:PRINT:PRINT:INK 1:PRINT TAB(8)"FROGGER AND SNAIL":BEEP:PAUSE 2:BEEP
225 PAUSE 2:BEEP:PAUSE 2
230 PRINT:PRINT:PRINT "Man muss versuchen,saemtliche"
235 PRINT:PRINT "Schnecken"
240 PRINT:PRINT:PRINT TAB(4)"mit dem FROSCH aufzufressen"
245 PRINT:PRINT:PRINT "* * FROSCHSTEUERUNG mit":PRINT "    KURSORTASTEN * * "
250 PRINT:PRINT:PRINT "* * VIEL  SPASS  BEIM  JAGEN * *":PAUSE 60
255 PRINT:X=32:Y=36:Z=-5120
260 R=0:PU=0:BORDER 2:PAPER 4
265 N=9:M=16
270 FOR I=1 TO 10
275 CLS
280 R=R+1:PRINT AT(0,0);R,".RUNDE!"
285 L=0:FOR C=1 TO 80
290 PRINT AT(N,M);"±  °"
295 PRINT AT(N+1,M);"  "
300 PRINT AT(N+2,M);" °± ":PAUSE 1
305 PRINT AT(N,M);"    "
310 PRINT AT(N+1,M);"   "
315 PRINT AT(N+2,M);"   "
320 T$=INKEY$
325 A=0
330 IF T$=CHR$(11)THEN N=N-1:GOSUB 15
335 IF T$=CHR$(10)THEN N=N+1:GOSUB 40
340 IF T$=CHR$(8) THEN M=M-1:GOSUB 60
345 IF T$=CHR$(9) THEN M=M+1:GOSUB 80
350 PRINT AT(0,21);"PUNKTE:",PU
355 IF N&lt;1 THEN N=1
360 IF N&gt;20 THEN N=20
365 IF M&lt;0 THEN M=0
370 IF M&gt;28 THEN M=28
375 L=L+1:IF L&lt;&gt;5 THEN 400
380 L=0
385 J=INT(RND(1)*22)+1
390 K=INT(RND(1)*31)
395 PRINT AT(J,K);"$"
400 NEXT C
405 NEXT I
410 PRINT AT(6,9);"YOUR SCORE:",PU:PAUSE 50
415 PRINT:PRINT:PRINT:PRINT "Moechtest Du noch mal spielen   (J/N) ";
420 INPUT Q$
425 IF Q$="J"THEN 260
430 WINDOW:CLS:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
435 PRINT:PRINT:INK 2:PRINT TAB(10)"G A M E":PAUSE 3
440 PRINT:PRINT:PRINT TAB(15)"O V E R":PAUSE 5:PRINT:PRINT:PRINT:PRINT
445 PRINT:PRINT:PRINT:PRINT:PRINT
450 CLS
455 PRINT AT(7,12);"G A M E":PAUSE 5:WINDOW
460 PRINT AT(16,19);"O V E R !":PAUSE 19
465 PRINT AT(11,17);"E N D E":PAUSE 15
470 CLS:PRINT AT(9,9);"G  A  M  E":PAUSE 4
475 PRINT AT(15,19);"O  V E  R":PAUSE 9:WINDOW:END
</pre>
</div>
</body>
</html>
