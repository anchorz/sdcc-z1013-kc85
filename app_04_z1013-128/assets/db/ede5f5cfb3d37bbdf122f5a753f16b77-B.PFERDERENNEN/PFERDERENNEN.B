Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10 WINDOW:CLS:Z=1:CLEAR 100
   12 PRINT AT(10,0); "DAS 'PFERDERENNEN' IST EIN WETT-SPIEL"
   14 PRINT AT(11,6);"FUER DIE GESAMTE FAMILIE !":PAUSE 60:CLS
   16 PRINT AT(3,10);"WAS SIE BRAUCHEN ?":PAUSE 50
   18 PRINT AT(3,10);"WAS SIE BRAUCHEN :":PAUSE 9:PRINT AT(4,10);STRING$(17,"_")
   20 WINDOW 8,31,1,31:PRINT ""
   22 PRINT "  GLUECK UND GELD  "
   24 PRINT ""
   26 PAUSE 20:WINDOW 13,31,0,31:PRINT "...HABEN SIE BEIDES,KANN ES LOS-GEHEN."
   28 PAUSE 20:PRINT:PRINT "BIS ZU 8 PERSONEN KOENNEN MIT-  SPIELEN."
   30 PRINT:PRINT " DIE WETTE IST EINE SIEG",
   32 PRINT "WETTE UND WIRD WIE FOLGT ABGESCHLOSSEN:"
   34 PAUSE 40:PRINT:PRINT "WETTE==> <pferde-nummer,einsatz>"
   36 PAUSE 40:PRINT:PRINT "DER EINSATZ PRO WETTE KANN"
   38 PRINT " ZWISCHEN 1 UND 9999 LIEGEN."
   40 PAUSE 50:PRINT:PRINT "... UND NUN VIEL SPASS BEIM":PAUSE 50:WINDOW:CLS
   42 PRINT AT(15,2);" P F E R D E R E N N E N "
   44 PRINT AT(17,2);STRING$(27,""):PAUSE 40:CLS
   46 PRINT AT(16,0);STRING$(31,"=")
   48 WINDOW 17,31,0,31:CLS
   50 INPUT "WIEVIEL PERS. WOLLEN  MITWETTEN ?";A
   52 IF A>8 THEN PRINT:PRINT "BITTE NICHT MEHR ALS 8 PERSONEN.":PRINT:GOTO 50
   54 PRINT:PRINT "DARF ICH UM IHRE NAMEN BITTEN !":PRINT
   56 FOR I=1 TO A:PRINT "WETTSPIELER";I;": ";:INPUT "";N$(I):NEXT I
   58 CLS:PRINT "WIEVIEL GELD WOLLEN SIE VERSPIELEN ?":PRINT
   60 FOR I=1 TO A:PRINT N$(I);TAB(15);":";:INPUT "";W1(I):NEXT I
   62 RESTORE 234:FOR I=1 TO 8:READ P$(I):NEXT I
   64 CLS:WINDOW 17,31,0,20
   66 PRINT "PFERD    NR.   KURS":PRINT STRING$(21,"-")
   68 E=0:T=1:R=0:FOR I=1 TO 8:B(I)=INT(10*RND(1)+1):R=R+B(I):NEXT I
   70 FOR I=1 TO 8:PRINT P$(I);TAB(9);I;TAB(13);INT(R/B(I));TAB(17);":1":NEXT I
   72 WINDOW 17,31,21,31:PRINT "Ihre Wette":PRINT STRING$(11,"-"):PRINT
   74 FOR I=1 TO A
   76 IF W1(I)=0 THEN E=E+1:GOTO 86
   78 PRINT N$(I);TAB(9);":";:INPUT "";T(I),W(I)
   80 IF W(I)<1 OR W(I)>9999 THEN 208
   82 IF W(I)>W1(I)THEN 214
   84 W1(I)=W1(I)-W(I)
   86 NEXT I
   88 IF E=A THEN 224
   90 WINDOW 0,10,0,31:CLS
   92 FOR I=1 TO 11:FOR J=6 TO 6:PRINT AT(I,J);"":NEXT J:NEXT I
   94 PRINT AT(0,13);"GALOPPRENNBAHN":PRINT AT(0,0);" ":WINDOW 1,10,0,31
   96 FOR I=1 TO 8:P(I)=-4760-(32*I):P1(I)=P(I)+39:POKE P(I),48+I:NEXT I
   98 FOR I=1 TO 500:NEXT I:BEEP
  100 FOR I=1 TO 8
  102 X(I)=I:V(X(I))=INT(100*RND(1)+1):IF V(X(I))<10 THEN V(X(I))=1:GOTO 116
  104 X=INT(R/B(I)+.5):IF V(X(I))<X+17 THEN V(X(I))=2:GOTO 116
  106 IF V(X(I))<X+37 THEN V(X(I))=3:GOTO 116
  108 IF V(X(I))<X+57 THEN V(X(I))=4:GOTO 116
  110 IF V(X(I))<X+77 THEN V(X(I))=5:GOTO 116
  112 IF V(X(I))<X+92 THEN V(X(I))=6:GOTO 116
  114 V(X(I))=7
  116 NEXT I
  118 FOR I=1 TO 8
  120 P(X(I))=P(X(I))+V(X(I)):IF P(X(I))<P1(X(I))THEN 134
  122 IF P1(X(I))<>P(X(I))-V(X(I))THEN POKE(P(X(I))-V(X(I))),32
  124 P(X(I))=P1(X(I))
  126 FOR J=1 TO T
  128 IF E(J)=P(X(I))THEN 134
  130 NEXT J
  132 E(T)=P(X(I)):T=T+1:BEEP
  134 NEXT I
  136 FOR I=1 TO 8
  138 FOR J=1 TO 8-I
  140 IF V(X(J))<V(X(J+1))THEN 144
  142 K=X(J):X(J)=X(J+1):X(J+1)=K
  144 NEXT J
  146 NEXT I
  148 FOR I=1 TO 8
  150 IF P(X(I))<P1(X(I))THEN POKE(P(X(I))-V(X(I))),32
  152 POKE P(X(I)),48+X(I)
  154 NEXT I
  156 IF T<9 THEN 100
  158 WINDOW 17,31,0,31:CLS:WINDOW 17,31,0,20
  160 PRINT "HIER DER EINLAUF:":PRINT STRING$(21,"-")
  162 FOR I=1 TO 8
  164 T=ABS(E(I)+4721)/32:PRINT I;P$(T);TAB(12);INT(R/B(T));TAB(16);":1"
  166 NEXT I
  168 WINDOW 17,31,21,31:PRINT "ES HABEN GEWONNEN:":PRINT STRING$(11,"-")
  170 FOR I=1 TO A
  172 IF T(I)<>ABS(E(1)+4721)/32 THEN 176
  174 PRINT N$(I);TAB(9);":";INT(R/B(T(I)))*W(I):W1(I)=W1(I)+INT(R/B(T(I)))*W(I)
  176 NEXT I
  178 WINDOW 0,11,0,31:CLS
  180 PRINT "SIE BESITZEN z.Zt. FOLGENDES VERMOEGEN:"
  182 FOR I=1 TO A:PRINT:PRINT TAB(5);N$(I);TAB(20);":";W1(I);:NEXT I
  184 PRINT TAB(33);:INPUT "=>";X$:CLS
  186 GOSUB 238
  188 X$="J"
  190 INPUT "";X$
  192 IF LEFT$(X$,1)="N"OR LEFT$(X$,1)="n"THEN 200
  194 IF LEFT$(X$,1)="J"OR LEFT$(X$,1)="j"THEN 198
  196 GOTO 190
  198 FOR I=1 TO 8:E(I)=0:NEXT I:WINDOW 17,31,0,31:CLS:WINDOW 0,11,0,31:GOTO 64
  200 CLS:PRINT:PRINT:PRINT:PRINT "NA DANN SPAREN SIE WEITER..."
  202 PRINT:PRINT "FUER DAS NAECHSTE MAL !":PRINT AT(4,0);" ":PAUSE 80 
  204 WINDOW:CLS:PRINT AT(0,0);" ":PRINT AT(10,14);"AUF WIEDERSEHEN":PAUSE 60:CLS
  206 END
  208 GOSUB 222
  210 PRINT AT(2,1);"IHNEN NUR ZUR ERINNERUNG !":PRINT AT(3,1);STRING$(25,"")
  212 PRINT AT(6,0);"DER EINSATZ LIEGT ZWISCHEN 1 UND 9999.":GOTO 78
  214 GOSUB 222
  216 PRINT AT(2,1);"ES TUT MIR SEHR LEID...":PAUSE 50
  217 PRINT AT(4,1);"SIE DARAUF HINWEISEN ZU MUESSEN" :PAUSE 40
  218 PRINT AT(6,1);"DASS NUR NOCH":PRINT AT(6,18);W1(I)
  220 PRINT AT(8,1);"AUF IHREM KONTO ZU BUCHE STEHEN.":GOTO 78
  222 FOR J=0 TO 9:PRINT AT(J,0);STRING$(40," "):NEXT J:RETURN
  224 GOSUB 222
  226 PRINT AT(3,1);"SIE HABEN ALLE IHR GELD":PRINT AT(5,1);"V E R S P I E L T "
  228 PRINT AT(7,1);"WOLLEN SIE ES WIEDERGEWINNEN ?"
  230 PRINT AT(9,1);"DANN GEHEN SIE ANS EINGEMACHTE":PRINT AT(11,1);"U  N  D   "
  232 PRINT AT(13,1);"FANGEN SIE VON VORNE AN.":END
  234 DATA "AGATHE","MEPHISTO","LANDLUFT","OEDIPUS","JESSICA"
  236 DATA "HANNIBAL","MAINELKE","ALADIN"
  237 END
  238 Z=Z+1:PRINT:PRINT:PRINT "DAS";Z;".RENNEN BEGINNT GLEICH.":PAUSE 20
  240 PRINT:PRINT:PRINT "WOLLEN SIE WIEDER MITMACHEN?":PAUSE 30
  242 PRINT:PRINT:PRINT "HABEN SIE NOCH GELD (J/N)"
  244 RETURN
