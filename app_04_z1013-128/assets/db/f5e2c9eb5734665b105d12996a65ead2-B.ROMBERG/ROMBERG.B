Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

  100 CLS
  110 PRINT "HABEN SIE DIE FKT. EINGEGEBEN ? (J/N)":BEEP
  120 N$=INKEY$:IF N$="J" THEN 150
  130 IF N$="N" THEN 750
  140 GOTO 120
  150 PRINT:PRINT:BEEP:INPUT "UNTERE GRENZE             a=";A
  160 PRINT:BEEP:INPUT "OBERE GRENZE              b=";B 
  170 IF A=B THEN 680
  180 IF B<A THEN 710
  190 PRINT:BEEP:INPUT "INTEGRATIONSSCHRITTWEITE h1=";H
  200 IF H<=0 THEN 710
  210 IF (B-A)/H<>INT((B-A)/H) THEN 710
  220 PRINT:BEEP:INPUT "FEHLER                   ep=";EP
  230 IF EP<=0 THEN 710
  240 PRINT:PRINT:PRINT
  250 PRINT "  X";
  260 PRINT TAB(8) "F(X)";
  270 PRINT TAB(15)"Z-ZUWACHS";
  280 PRINT TAB(30)"Z"
  290 PRINT STRING$ (39,"-")
  300 PRINT A;
  310 X=A
  320 GOSUB 790
  330 PRINT TAB(6)Y;
  340 PRINT TAB(27)"0"
  350 S=0
  360 DIM A(20,20)
  370 FOR L=0 TO ((B-A)/H)-1
  380 X=A+(L*H)
  390 GOSUB 790
  400 Z=(1/2)*Y
  410 X=A+((L+1)*H)
  420 PRINT X;
  430 GOSUB 790
  440 PRINT TAB(6)Y;
  450 Z=Z+(1/2*Y)
  460 FOR I=0 TO 20
  470 A(I,0)=Z
  480 IF I=0 THEN 550
  490 FOR J=1 TO (2^I)-1  STEP 2
  500 X=A+(L*H)+(J*(H/(2^I)))
  510 GOSUB 790
  520 A(I,0)=A(I,0)+Y
  530 NEXT J
  540 Z=A(I,0)
  550 A(I,0)=A(I,0)*(H/(2^I))
  560 IF I=0 THEN 620
  570 FOR K=1 TO I
  580 A(I,K)=(2^(2*K)*A(I,K-1)-A(I-1,K-1))/(2^(2*K)-1)
  590 IF K=I THEN 620
  600 IF ABS(A(I,K)-A(I-1,K))<EP THEN 630
  610 NEXT K
  620 NEXT I
  630 PRINT TAB(15) A(I,K);
  640 S=S+A(I,K) 
  650 PRINT TAB(26) S
  660 NEXT L
  670 GOTO 690
  680 S=0
  690 PRINT:PRINT:PRINT:PRINT "WERT DES INTEGRALS : ";S:BEEP
  700 GOTO 740
  710 PRINT AT(17,8);"F E H L E I N G A B E  ":BEEP:BEEP:BEEP:BEEP:BEEP
  720 PRINT AT(20,4);"MACHEN SIE SICH BITTE MIT DEN"
  730 PRINT AT(22,1);"DEN UNTERLAGEN ZUM PROGRAMM VERTRAUT !"
  740 END
  750 PRINT:PRINT:PRINT "GEBEN SIE DIE FUNKTION EIN  !"
  760 PRINT:PRINT "MUSTER :  2000 Y=EXP(X)  "
  770 PRINT:PRINT "STARTEN SIE DAS PROGRAMM NEU  !":BEEP
  780 PRINT:PRINT:PRINT:END
  790 Y=SIN(X)
  800 RETURN
