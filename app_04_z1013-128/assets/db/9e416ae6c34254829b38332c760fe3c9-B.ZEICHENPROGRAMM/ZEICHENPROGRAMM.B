Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

  100 RESTORE 10000
  110 FOR I=16128 TO 16158:READ A:POKE I,A:NEXT I
  900 REM Erklaerung!!
  905 WINDOW:CLS
  910 PRINT "     Zeichenprogramm"
  915 PRINT "     ~~~~~~~~~~~~~~~"
  920 PRINT:PRINT
  930 PRINT "  Bedeutung der Tasten:"
  940 PRINT:PRINT "   -E- links oben"
  950 PRINT "   -F- oben"
  960 PRINT "   -G- rechts oben"
  970 PRINT "   -O- rechts"
  980 PRINT "   -W- rechts unten"
  985 PRINT "   -V- unten"
  990 PRINT "   -U- links unten"
  995 PRINT "   -M- links"
  996 PRINT:PRINT  
  997 PRINT "   \   |   /" 
 1000 PRINT "    E  F  G"
 1001 PRINT "     \ | / "
 1002 PRINT "   -M--N--O-"
 1003 PRINT "     / | \" 
 1004 PRINT "    U  V  W"
 1005 PRINT "   /   |   \":PRINT:PRINT:PRINT:PAUSE 100
 1006 INPUT "Alles begriffen ? *ENTER*"; :CLS:PRINT:PRINT "FUNKTIONSTASTEN:"
 1007 PRINT:PRINT:PRINT " Taste -T- Neues Zeichen"
 1009 PRINT:PRINT " Taste -S- dient zum Steuern" 
 1010 PRINT:PRINT " Taste -Z- dient zum Zeichnen"
 1011 PRINT:PRINT " Taste -@- Ende"
 1012 PRINT:PRINT " Taste -X- Load von Kassette"
 1014 PRINT:PRINT " Taste -$- Save auf Kassette"
 1015 PAUSE 60:PRINT:PRINT:PRINT:PRINT
 1016 PRINT "Ich wuensche Dir viel Spass":PAUSE 40 
 1017 PRINT:PRINT:PRINT:PRINT:INPUT "                *ENTER*";
 1020 REM ZEICHENPROGRAMM
 1040 X=-5086:CLS:S=1:PRINT AT(0,0);CHR$(32) 
 1050 REM RAHMEN DES BILDSCHIRMES
 1060 REM IN UP PUNKTSTEUERUNG EINFUEGEN
 1070 FOR I=0 TO 31:POKE(-5120+I),46 :POKE(-4192+I),46
 1080 POKE(-4192-(32*I)),46 :POKE(-4161-(32*I)),46 :NEXT I
 1090 GOTO 1120
 1100 IF PEEK(Y)=46 THEN Y=X
 1110 RETURN
 1112 T$=INKEY$
 1115 IF T$="T"THEN GOSUB 1360
 1116 IF T$="X"THEN CALL*3F10
 1117 IF T$="$"THEN CALL*3F00
 1120 P=V:POKE X,P
 1140 IF T$="U"THEN Z=31
 1150 IF T$="V"THEN Z=32
 1160 IF T$="W"THEN Z=33
 1170 IF T$="M"THEN Z=-1
 1180 IF T$="N"THEN Z=0
 1190 IF T$="O"THEN Z=1
 1200 IF T$="E"THEN Z=-33
 1210 IF T$="F"THEN Z=-32
 1220 IF T$="G"THEN Z=-31
 1230 Y=X+Z
 1250 GOSUB 1100
 1260 IF T$="@" THEN 1350:REM ENDE
 1270 IF T$="S" THEN S=1:REM STEUERN
 1280 IF T$="Z" THEN S=2:REM  ZEICHNEN 
 1290 IF S=1 THEN 1310
 1300 IF S=2 THEN 1320
 1310 Z=0:POKE Y,P:POKE X,32:GOTO 1330
 1320 Z=0:POKE Y,P 
 1330 X=Y
 1340 GOTO 1112
 1350 WINDOW:END
 1360 WINDOW 30,31,0,31:CLS
 1370 INPUT "Neues Zeichen:";V:PRINT AT(30,20);CHR$(V)
 1380 RETURN: WINDOW:
10000 DATA 33,0,236,34,27,0,33,255,239,34,29,0,231,8,201,0
10010 DATA 33,0,236,34,27,0,33,255,239,34,29,0,231,9,201
