Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10 CLEAR 255:WINDOW:RANDOMIZE
   20 CLS:PRINT:PRINT
   30 FOR B=1 TO 3
   40 PRINT "  "
   50 NEXT B:FOR B=1 TO 7
   60 PRINT "             "
   70 NEXT B:FOR B=1 TO 3
   80 PRINT "          "
   90 NEXT B:FOR B=1 TO 7
  100 PRINT "             "
  110 NEXT B:FOR B=1 TO 3
  120 PRINT "  "
  130 NEXT B
  140 PRINT:PRINT:PRINT:PRINT
  150 PRINT "DAS GROSSE SPIEL UMS GROSSE GELD"
  160 POKE DEEK(43),32:GOSUB 6240:CLS
  170 GOSUB 6200:PRINT "Geben  Sie  bitte  das  aktuelle";
  180 PRINT "Jahr an ?":PRINT STRING$(12," ");:BEEP:CALL*F6B8
  190 INPUT "";RZ:IF RZ<1988 OR RZ>2050 THEN CLS:GOTO 170
  200 GOSUB 6200:PRINT " Wieviele wollen mitpumpen       (2-6 Pumper)? ";:BEEP
  210 SZ$=INKEY$:POKE DEEK(43),32:FOR B=1 TO 5:NEXT
  220 IF SZ$<"2"OR SZ$>"6"THEN:POKE DEEK(43),127:FOR B=1 TO 5:NEXT:GOTO 210
  230 CALL*F6C5:PRINT SZ$:GOSUB 6200:SZ=VAL(SZ$)
  240 PRINT "    EINEN MOMENT BITTE !":POKE DEEK(43),32:BEEP
  250!---DIM FUER OELFELD---
  260 DIM VG$(6),BF$(12),BM(12),BP(12),BT(12),AB(12),F(12),FB(12),FM(12),GF(12)
  270 DIM GG(12),PA(12),TA(12)
  280!---DIM FUER BOHRGESELLSCHAFT---
  290 DIM B$(3),BG(3),BD(3),BB(3)
  300!---DIM FUER PUMPENFIRMEN---
  310 DIM PF$(3),PF(3),P(3),PP(3)
  320!---DIM FUER LKW-FIRMEN---
  330 DIM TG$(4),TP(4),TW(4),TB(4),LP(4)
  340!---DIM FUER SPIELER---
  350 DIM SN$(SZ),KS(SZ),BK(SZ),RP(32)
  360 BF$(1)="BENZINACKER"
  370 BF$(2)="EVERSPRUDEL"
  380 BF$(3)="TIEFLOCH   "
  390 BF$(4)="DOLLARSRITZ"
  400 BF$(5)="HEAVYPUMP  "
  410 BF$(6)="LINEONPIPE "
  420 BF$(7)="HAPPYBOHR  "
  430 BF$(8)="IMMERDRUCK "
  440 BF$(9)="LUCKY HOLE "
  450 BF$(10)="BLACK GOLD "
  460 BF$(11)="OVERBUBBLE "
  470 BF$(12)="DAUERSPRIT "
  480 VG$(1)="DENVERCREAM"
  490 VG$(2)="EWINGOIL   "
  500 VG$(3)="SELL & HOPP"
  510 VG$(4)="NEVER&AGAIN"
  520 VG$(5)="SALATOIL AG"
  530 VG$(6)="SWIMMINOIL "
  540 PF$(1)="BAY.PUMPE EG   "
  550 PF$(2)="DT.PUMPENGLUECK"
  560 PF$(3)="BOHR & PUMP KG "
  570 TG$(1)="RAFF UND GIER  "
  580 TG$(2)="KNALTEX GMBH   "
  590 TG$(3)="NEVER COME BACK"
  600 TG$(4)="OIL ON THE ROAD"
  610 FOR T=1 TO 12
  620 BP(T)=INT(RND(1)*70000)+30000
  630 FM(T)=INT((BP(T)-(RND(1)*9999)+1)*10)
  640 BT(T)=INT(RND(1)*3666)+1:NEXT T
  650 K=INT(RND(1)*30000)+70000+(6-SZ)*10000
  660 L1=INT(RND(1)*6)+1
  670 FM(L1*2)=1200:FM(L1*2-1)=1400
  680 FOR T=1 TO 3
  690 PF(T)=INT(RND(1)*80000)+20000
  700 NEXT T
  710 FOR T=1 TO 4
  720 TP(T)=INT(RND(1)*55000)+25000
  730 TW(T)=INT(TP(T)/10000)*3:NEXT T
  740 RP(1)=INT(RND(1)*7)+7
  750 FOR T=2 TO 32
  760 RP(T)=RP(T-1)+(INT(RND(1)*14)-7)
  770 IF RP(T)<0 THEN RP(T)=1:GOTO 760
  780 IF RP(T)>30 THEN RP(T)=30
  790 NEXT T
  800 A1$=" Sie muessen leider drauflegen"
  810 A2$=" Ihr Vorhaben ist gelungen !"
  820 A3$="% erwischt"
  830 DEF FN Z(X)=INT(RND(1)*X)+1
  840 CLS:POKE DEEK(43),32
  850 PRINT AT(8,4);"WIR BEFINDEN UNS IM JAHR"
  860 B=INT(RZ/1000):T=6:GOSUB 6510
  870 B=INT((RZ-INT(RZ/1000)*1000)/100):T=10:GOSUB 6510
  880 B=INT((RZ-INT(RZ/100)*100)/10):T=14:GOSUB 6510
  890 B=INT(RZ-INT(RZ/10)*10):T=18:GOSUB 6510
  900 PRINT AT(28,1);"DAS SPIEL ENDET NACH ABSCHLUSS"
  910 PRINT AT(30,8);"DES JAHRES"+STR$(RZ+31):BEEP:PAUSE 50
  920 B$(1)="DEEP THROAT INC    "
  930 B$(2)="TIEF UND SCHNELL AG"
  940 B$(3)="TURBO & DRILL GMBH "
  950 FOR T=1 TO 3
  960 BG(T)=INT(RND(1)*50000)+10000
  970 NEXT T
  980 A4$="Was wollen Sie verkaufen ?"
  990 CLS:PRINT:PRINT:PRINT:PRINT
 1000 PRINT "  "SZ "Pumper spielen mit !":PRINT
 1010 PRINT "Nun noch die Namen:                      (maximal 12 Buchstaben)"
 1020 PRINT:FOR T=1 TO SZ:BEEP
 1030 INPUT "      ";SN$(T):IF SN$(T)=""THEN SN$(T)="UNBEKANNT"
 1040 IF LEN(SN$(T))>12 THEN SN$(T)=LEFT$(SN$(T),12)
 1050 KS(T)=K:NEXT T:GOSUB 6200:V=2
 1060 PRINT "Viel Spass beim GELDMACHEN und  VERLIEREN":PRINT:BEEP
 1070 PRINT "  Jeder Spieler hat"K "$              Startkapital"
 1080 POKE DEEK(43),32:PAUSE 70:CLS
 1090 PRINT:PRINT:PRINT "Ihnen stehen folgende Wege offen":PRINT:K=0
 1100 PRINT " 1. OELFELDER erwerben":PRINT:BEEP
 1110 PRINT " 2. PUMPENHERSTELLER werden":PRINT:BEEP
 1120 PRINT " 3. TANKWAGENHERSTELLER werden":PRINT:BEEP
 1130 PRINT " 4. BOHRGESELLSCHAFTER werden":PRINT:BEEP
 1140 PRINT " 5. SABOTAGE betreiben":PRINT:BEEP
 1150 PRINT " 6. Sowie alles zusammen":PRINT:BEEP
 1160 PRINT " 7. Oder garnichts von Allen":PRINT:BEEP
 1170 PRINT:PRINT "Sieger ist logischerweise Der":PRINT:BEEP
 1180 PRINT "mit dem meisten Kapital am":PRINT:BEEP
 1190 PRINT "Ende des Spiel's":PRINT:BEEP
 1200 PRINT:GOSUB 6220
 1210 CLS:GOSUB 6200
 1220 PRINT ""
 1230 PRINT "  DIE RAFFINERIEOELPREISENT- "
 1240 PRINT "                             "
 1250 PRINT "WICKLUNG DER NAECHSTEN JAHRE "
 1260 PRINT ""
 1270 POKE DEEK(43),32:GOSUB 6370:ZB=22
 1280 FOR T=0 TO 31
 1290 ZX=INT(RP(T+1)/4)
 1300 FOR ZY=0 TO ZX-1
 1310 PRINT AT((ZB-ZY),T);CHR$(255):NEXT ZY
 1320 ZY=RP(T+1)-(ZX*4)
 1330 IF ZY=1 THEN PRINT AT((ZB-ZX),T);CHR$(249)
 1340 IF ZY=2 THEN PRINT AT((ZB-ZX),T);CHR$(251)
 1350 IF ZY=3 THEN PRINT AT((ZB-ZX),T);CHR$(253)
 1360 RP(T+1)=RP(T+1)/10
 1370 HLOAD(25-ZX)*5,3:NEXT T:PAUSE 50
 1380 R=R+1:CLS:GOSUB 6440:GOSUB 6390:GOSUB 6390:FOR S=1 TO SZ
 1390 CLS
 1400 PRINT "";
 1410 PRINT "Entscheidungsmoeglichkeit fuer";
 1420 PRINT ""SN$(S)" $="KS(S):PRINT AT(2,31);""
 1430 PRINT "":PRINT
 1440 PRINT "Einkauf von :"
 1450 PRINT "  A = BOHRGESELLSCHAFT"
 1460 PRINT "  B = PUMPENGESELLSCHAFT"
 1470 PRINT "  C = TANKWAGENFIRMA"
 1480 PRINT "  D = OELFELD"
 1490 PRINT "  E = BOHRGESTAENGE"
 1500 PRINT "  F = PUMPE"
 1510 PRINT "  G = TANKWAGEN":PRINT
 1520 PRINT "Sonstiges :"
 1530 PRINT "  H = VERKAUF"
 1540 PRINT "  I = SABOTAGE BETREIBEN"
 1550 PRINT "  J = PREISFESTLEGUNG"
 1560 PRINT "  K = NAECHSTER SPIELER"
 1570 PRINT:GOSUB 6220:Z2=ASC(S$)-64:IF Z2<1 OR Z2>11 THEN 1570
 1580 ON Z2 GOSUB 4170,2660,3160,2470,4370,4610,4820,6720,3730,5010,1600
 1590 NEXT S:GOTO 1610
 1600 RETURN
 1610 FOR S=1 TO SZ
 1620 FOR I=1 TO 12:GOSUB 2130:NEXT I
 1630 KS(S)=INT(KS(S)):NEXT S
 1640 CLS:GOSUB 2410
 1650 GOSUB 5990:RZ=RZ+1:IF R<32 THEN 1380
 1660 CLS
 1670 PRINT " D A S"
 1680 PRINT:PRINT "     S P I E L"
 1690 PRINT:PRINT "         I S T"
 1700 PRINT:PRINT "             V O R R U E B E R":POKE DEEK(43),32
 1710 FOR T=1 TO 5:GOSUB 6390:NEXT T
 1720 PRINT:PRINT STRING$(32,"-")
 1730 PRINT " HIER DAS GESAMTERGEBNIS :"
 1740 PRINT:PRINT "SPIELER     BANKKREDIT  KAPITAL"
 1750 PRINT ""
 1760 FOR Z0=1 TO SZ
 1770 PRINT SN$(Z0)" "BK(Z0)" ";INT(KS(Z0)-(BK(Z0)*1.6))
 1780 PRINT:NEXT Z0
 1790 PRINT STRING$(32,"-"):PRINT:POKE DEEK(43),32
 1800 FOR T=1 TO 3:GOSUB 6390:NEXT T:GOSUB 6220:CLS
 1810 PRINT:PRINT:PRINT " ICH HOFFE SIE HATTEN VIEL"
 1820 PRINT:PRINT " FREUDE UND VERBLEIBE ALS
 1830 PRINT:PRINT "            IHR OELY .":PRINT:PRINT
 1840 PRINT "P.S.  KAM EIN OELPRINZ GEFLOGEN"
 1850 POKE DEEK(43),32:GOSUB 6320:WINDOW:CLS:END
 1860 CLS
 1870 PRINT ""
 1880 PRINT "          "
 1890 PRINT " OELFELD  ";TAB(10);BF$(I)
 1900 PRINT "          "
 1910 PRINT ""
 1920 PRINT "          "
 1930 PRINT " BESITZER ";TAB(8);
 1940 IF GG(I)<FM(I)THEN GF(I)=GF(I)+(8000*(PA(I)))
 1950 PRINT SN$(S):GG(I)=GG(I)+(8000*(PA(I)))
 1960 PRINT "          "
 1970 PRINT "               JAHR"RZ
 1980 PRINT ""
 1990 PRINT:PRINT "RAFFINERIEABNAHMEPREIS="RP(R)"$"
 2000 PRINT:PRINT "PUMPENANZAHL :"PA(I)
 2010 PRINT:PRINT "VERSANDMENGE :"GF(I)
 2020 PRINT:PRINT "TANKWAGEN    :"TA(I)
 2030 PRINT:PRINT "LKWKAPAZITAET:"TA(I)*7000
 2040 PRINT:PRINT "KAPITAL      :"KS(S)
 2050 IF GG(I)>FM(I)THEN PRINT:PRINT "Q U L L E    E R S C H O E P F T"
 2060 IF GF(I)<=0 THEN PRINT:GOSUB 6220:RETURN
 2070 IF TA(I)=0 OR PA(I)=0 THEN PRINT:GOSUB 6220:RETURN
 2080 PRINT:M=0:CALL*F6B8:PRINT "Wieviel Liter sollen weg ";:BEEP:INPUT M
 2090 CALL*F6C5:IF M>GF(I)OR M>TA(I)*7000 THEN PRINT " Z U V I E L":GOTO 2080
 2100 KS(S)=KS(S)+(M*(RP(R))):GF(I)=GF(I)-M
 2110 PRINT "KAPITAL      :"KS(S)"$"
 2120 PRINT:GOSUB 6220:RETURN
 2130 IF FB(I)<>S THEN RETURN
 2140 IF F(I)=1 THEN 1860
 2150 CLS
 2160 PRINT "  BOHRUNG AUF FELD"
 2170 PRINT ""
 2180 PRINT "  "BF$(I)
 2190 PRINT ""
 2200 PRINT "  BESITZER:"SN$(S)"
 2210 PRINT ""
 2220 PRINT
 2230 PRINT "Ihr OELFELDLAGERVERWALTER teilt"
 2240 PRINT "Ihnen folgende Daten mit :"
 2250 IF BM(I)<=0 THEN PRINT "BOHRUNG UNMOEGLICH,DA KEIN      GESTAENGE";
 2260 IF BM(I)<=0 THEN PRINT " MEHR VORHANDEN, WIR   BRAUCHEN NACHSCHUB !"
 2270 IF BM(I)<=0 THEN 2290
 2280 BM(I)=BM(I)-500:AB(I)=AB(I)+500-FN Z(30)
 2290 PRINT "BISHERIGE BOHRTIEFE     :"AB(I)"m"
 2300 PRINT "BOHRGESTAENGE NOCH FUER :"BM(I)"m"
 2310 IF AB(I)>=BT(I)THEN PRINT "WIR SIND FUENDIG !":F(I)=1:GOSUB 5960
 2320 PRINT:GOSUB 6220:RETURN
 2330 PRINT:FOR DD=1 TO 12:PRINT DD " ";
 2340 IF FB(DD)=S THEN PRINT BF$(DD):GOTO 2360
 2350 PRINT
 2360 NEXT DD
 2370 PRINT:CALL*F6B8:PRINT "Fuer welches OELFELD ?";:BEEP:INPUT "";D:CALL*F6C5
 2380 IF D=0 THEN RETURN
 2390 IF FB(D)<>S THEN 2370
 2400 RETURN
 2410 PRINT:PRINT "JAHR:"RZ
 2420 PRINT STRING$(32,"-")
 2430 PRINT "SPIELER     KAPITAL"
 2440 PRINT STRING$(32,"-"):PRINT
 2450 FOR Z8=1 TO SZ:PRINT SN$(Z8),KS(Z8)
 2460 NEXT Z8:PRINT:GOSUB 6220:RETURN
 2470 CLS
 2480 PRINT "    "
 2490 PRINT "         OELFELDANGEBOTE    "
 2500 PRINT "    "
 2510 PRINT:PRINT "GESELLSCHAFT   OELFELD   PREIS $"
 2520 FOR T=1 TO 6
 2530 PRINT VG$(T);" ";CHR$(64+T*2-1);"=";BF$(T*2-1);
 2540 IF FB(T*2-1)>0 THEN PRINT " VERK.  ":ELSE PRINT BP(T*2-1)
 2550 PRINT TAB(12);CHR$(64+T*2);"=";BF$(T*2);
 2560 IF FB(T*2)>0 THEN PRINT " VERK.  ":ELSE PRINT BP(T*2)
 2570 NEXT T
 2580 PRINT SN$(S)" Welches Feld soll "
 2590 PRINT "gekauft werden ? _";:BEEP
 2600 S$=INKEY$:IF S$<"A"THEN 2600:ELSE PRINT CHR$(8);S$;:POKE DEEK(43),32
 2610 B=ASC(S$)-64
 2620 IF B>12 THEN RETURN
 2630 IF FB(B)>0 THEN PRINT CHR$(8)+"_";:POKE DEEK(43),32:GOTO 2600
 2640 FB(B)=S:KS(S)=KS(S)-BP(B):PAUSE 30
 2650 RETURN
 2660 CLS
 2670 GOSUB 5960:PRINT " P U M P E"
 2680 PRINT " & P U M P E N V E R E I -"
 2690 PRINT " N I G U N G S G E S E L L -"
 2700 PRINT " S C H A F T
 2710 PRINT STRING$(32,""):T=0:PRINT
 2720 PRINT
 2730 IF PP(1)>0 AND PP(2)>0 AND PP(3)>0 THEN 2820
 2740 PRINT "Wir koennen Ihnen folgende":PRINT
 2750 PRINT "Angebote unterbreiten :":PRINT:PRINT
 2760 PRINT "1. "PF$(1)"    ";:IF PP(1)>0 THEN PRINT "VERKAUFT":ELSE PRINT PF(1)
 2770 PRINT
 2780 PRINT "2. "PF$(2)"    ";:IF PP(2)>0 THEN PRINT "VERKAUFT":ELSE PRINT PF(2)
 2790 PRINT
 2800 PRINT "3. "PF$(3)"    ";:IF PP(3)>0 THEN PRINT "VERKAUFT":ELSE PRINT PF(3)
 2810 PRINT:GOTO 2870
 2820 PRINT:PRINT "Leider muessen wir Ihnen mit-"
 2830 PRINT:PRINT "teilen , dass zur Zeit keine"
 2840 PRINT:PRINT "Pumpenherstellungsfirma zum"
 2850 PRINT:PRINT "Verkauf ansteht ."
 2860 PRINT:GOSUB 6220:RETURN
 2870 PRINT:PRINT "Wenn Sie eine Fabrik kaufen"
 2880 PRINT:PRINT "wollen , druecken Sie die"
 2890 PRINT:PRINT "betreffende Zahl , wenn Sie"
 2900 PRINT:PRINT "nicht wollen druecken Sie 'X'";:BEEP
 2910 CALL*F6B8:S$=INKEY$:CALL*F6C5:IF S$=""THEN 2910:ELSE PRINT S$
 2920 IF S$="1"AND PP(1)=0 THEN KS(S)=KS(S)-PF(1):PP(1)=S:GOTO 2980
 2930 IF S$="2"AND PP(2)=0 THEN KS(S)=KS(S)-PF(2):PP(2)=S:GOTO 3040
 2940 IF S$="3"AND PP(3)=0 THEN KS(S)=KS(S)-PF(3):PP(3)=S:GOTO 3100
 2950 PRINT:PRINT "Schade - das wir nicht ins"
 2960 PRINT:PRINT "Geschaeft kommen"
 2970 PAUSE 50:RETURN
 2980 CLS:GOSUB 6200
 2990 PRINT "Sie sind nun Besitzer der Firma"
 3000 PRINT:PRINT PF$(1)" .":PRINT
 3010 CALL*F6B8:PRINT "Legen Sie den Pumpenpreis fest !";:BEEP:INPUT "";P(1)
 3020 CALL*F6C5:IF P(1)>60000 THEN PRINT "  Zu hoch !":GOTO 3010
 3030 GOTO 3150
 3040 CLS:GOSUB 6200
 3050 PRINT "Sie sind nun Besitzer der Firma"
 3060 PRINT:PRINT PF$(2)" .":PRINT
 3070 CALL*F6B8:PRINT "Legen Sie den Pumpenpreis fest !";:BEEP:INPUT "";P(2)
 3080 CALL*F6C5:IF P(2)>60000 THEN PRINT "  Zu hoch !":GOTO 3070
 3090 GOTO 3150
 3100 CLS:GOSUB 6200
 3110 PRINT "Sie sind nun Besitzer der Firma"
 3120 PRINT:PRINT PF$(3)" .":PRINT
 3130 CALL*F6B8:PRINT "Legen Sie den Pumpenpreis fest !";:BEEP:INPUT "";P(3)
 3140 CALL*F6C5:IF P(3)>60000 THEN PRINT "  Zu hoch !":GOTO 3130
 3150 PRINT:PRINT "   ALLES KLAR ?":POKE DEEK(43),32:PAUSE 40:RETURN
 3160 CLS
 3170 PRINT "      TANKWAGENFIRMEN"
 3180 PRINT "  OIL " 
 3190 PRINT "       VERKAUF"
 3200 PRINT " "
 3210 PRINT ""
 3220 PRINT "Nr.    Firma        LKW   Preis"
 3230 PRINT ""
 3240 FOR T=1 TO 4
 3250 PRINT T " "TG$(T)" "TW(T)" ";
 3260 IF TB(T)>0 THEN PRINT " VERK.":GOTO 3280
 3270 PRINT TP(T)
 3280 PRINT
 3290 NEXT T
 3300 PRINT:PRINT " kein Kauf ='X'";:BEEP
 3310 CALL*F6B8:S$=INKEY$:CALL*F6C5:IF S$=""THEN 3310:ELSE PRINT S$
 3320 B=VAL(S$):IF TB(B)>0 THEN RETURN
 3330 IF B>4 OR B=0 THEN RETURN
 3340 KS(S)=KS(S)-TP(B):TB(B)=S
 3350 PRINT:PRINT "Ihr Kapital betraegt jetzt      "KS(S):PRINT
 3360 CALL*F6B8:PRINT "Ihr Tankwagenpreis";:BEEP:INPUT "";LP(B):CALL*F6C5
 3370 IF LP(B)>60000 THEN PRINT "unrealistisch !":GOTO 3360
 3380 RETURN
 3390 CLS:PRINT:PRINT:PRINT:PRINT "HIER FAELLT DIE ENTSCHEIDUNG !  "
 3400 W3=INT(RND(1)*10)+1
 3410 PRINT TAB(12)""
 3420 PRINT TAB(12)" + 50 % "
 3430 PRINT TAB(12)" - 20 % "
 3440 PRINT TAB(12)" + 40 % "
 3450 PRINT TAB(12)" - 10 % "
 3460 PRINT TAB(12)" + 30 % "
 3470 PRINT TAB(12)" - 30 % "
 3480 PRINT TAB(12)" + 10 % "
 3490 PRINT TAB(12)" - 40 % "
 3500 PRINT TAB(12)" + 20 % "
 3510 PRINT TAB(12)" - 50 % "
 3520 PRINT TAB(12)""
 3530 PRINT:PRINT "   DRUECKEN SIE IM RICHTIGEN    "
 3540 PRINT "   MOMENT EINE TASTE"
 3550 E=0:FOR T=6 TO 15
 3560 PRINT AT(T,22);"<":HLOAD T,3
 3570 E=E+1:IF INKEY$<>""THEN T=15:NEXT T:GOTO 3590
 3580 PRINT AT(T,22);" ":NEXT T:GOTO 3550
 3590 PRINT:PRINT "Sie haben die"
 3600 ON E GOTO 3610,3620,3630,3640,3650,3660,3670,3680,3690,3700
 3610 PRINT "+50"A3$:PRINT A1$:E=50:GOTO 3710
 3620 PRINT "-20"A3$:PRINT A2$:E=-20:GOTO 3710
 3630 PRINT "+40"A3$:PRINT A1$:E=40:GOTO 3710
 3640 PRINT "-10"A3$:PRINT A2$:E=-10:GOTO 3710
 3650 PRINT "+30"A3$:PRINT A1$:E=30:GOTO 3710
 3660 PRINT "-30"A3$:PRINT A2$:E=-30:GOTO 3710
 3670 PRINT "+10"A3$:PRINT A1$:E=10:GOTO 3710
 3680 PRINT "-40"A3$:PRINT A2$:E=-40:GOTO 3710
 3690 PRINT "+20"A3$:PRINT A1$:E=20:GOTO 3710
 3700 PRINT "-50"A3$:PRINT A2$:E=-50
 3710 PRINT:IF E<0 THEN GOSUB 6380:ELSE GOSUB 6420
 3720 GOSUB 6220:RETURN
 3730 CLS 
 3740 FOR T=1 TO 15
 3750 HLOAD 2*T,3:PRINT TAB(T);"SABOTAGEAKTION"
 3760 NEXT T:FOR T=15 TO 1 STEP-1:HLOAD 2*T,3:PRINT TAB(T);"SABOTAGEAKTION":NEXT T
 3770 POKE DEEK(43),32:PAUSE 20
 3780 CLS:PRINT:PRINT TAB(10);SN$(S):PRINT
 3790 PRINT "Folgende Sabotagemoeglichkeiten stehen zur Verfuegung :":PRINT
 3800 PRINT "1. AGENTEN ANWERBEN UND FREMDES    OELFELD DURCH IHN UNBRAUCH-"
 3810 PRINT "   BAR MACHEN LASSEN":PRINT
 3820 PRINT "2. PUMPENFIRMA DURCH SABOTAGE      AUSSER BETRIEB SETZEN"
 3830 PRINT
 3840 PRINT "3. TANKWAGENFIRMA DURCH BESTECH-   UNG DER KREDITGEBER";
 3850 PRINT " KONKURS     GEHEN LASSEN":PRINT
 3860 W4=FN Z(40000)
 3870 PRINT "4. BOHRFIRMA DURCH OELUEBER-       SCHWEMMUNG AUSSER KRAFT"
 3880 PRINT "   SETZEN":PRINT
 3890 PRINT "LIEBER DOCH NICHTS MACHEN ='X'":W3=INT(RND(1)*40000)+40000
 3900 PRINT:CALL*F6B8:GOSUB 6220:CALL*F6C5
 3910 ON VAL(S$)GOTO 3930,5340,5520,5800
 3920 RETURN
 3930 CLS:PRINT "HEY, HIER IST AGENT DIABOLO     HUGGI BAER"
 3940 PRINT "WELCHES OELFELD SOLL ICH SABO-  TIEREN ?":PRINT
 3950 FOR T=1 TO 12:PRINT T;BF$(T):NEXT T
 3960 PRINT:CALL*F6B8:PRINT " Oelfeldnummer ?";:BEEP:INPUT "";N:CALL*F6C5
 3970 IF N<1 OR N>12 THEN RETURN
 3980 PRINT "So. Fuer die SABOTAGE des OEL-  FELDES fallen folgende Unkosten"
 3990 PRINT "an :":PRINT
 4000 PRINT "SPESEN,SCHMIERGELDER etc="W3
 4010 PRINT "KORKEN,SPRENGSTOFF etc  ="W4
 4020 PRINT "GESAMT                  ="W3+W4
 4030 PRINT "Die Chancen einer erfolgreichen"
 4040 PRINT "Durchfuehrung liegen bei 33% .  "
 4050 PRINT "Soll ich zur Tat schreiten (J/N)":BEEP
 4060 S$=INKEY$:IF S$<>"N"AND S$<>"J"THEN 4060:ELSE PRINT S$
 4070 IF S$="N"THEN RETURN
 4080 KS(S)=KS(S)-W3-W4:W3=0:W4=0
 4090 W3=FN Z(3):T1=0
 4100 IF W3=1 THEN BP(N)=FN Z(50000)+30000:W4=FN Z(200000):FM(N)=W4:T1=2
 4110 IF W3=1 THEN F(N)=0:BT(N)=FN Z(4500):FB(N)=0:PA(N)=0:TA(N)=0:AB(N)=0
 4120 IF W3=1 THEN BM(N)=0:GF(N)=0:GG(N)=0
 4130 PRINT:GOSUB 6220
 4140 IF W3=1 THEN PRINT:PRINT "G E L U N G E N !":GOSUB 6370
 4150 IF W3<>1 THEN PRINT:PRINT "ANSCHLAG MISSLUNGEN":PRINT "PECH GEHABT":GOSUB 6420
 4160 PAUSE 20:RETURN
 4170 CLS
 4180 PRINT ""
 4190 PRINT "                             "
 4200 PRINT "  BOHRGESELLSCHAFTSANGEBOTE  "
 4210 PRINT "                             "
 4220 PRINT " "
 4230 FOR T=1 TO 3:IF BB(T)=0 THEN 4250
 4240 NEXT T:PRINT "KEINE MEHR DA":PAUSE 50:RETURN
 4250 T=0:FOR T=1 TO 3
 4260 IF BB(T)>0 THEN 4280
 4270 PRINT T ""B$(T);BG(T)"$":PRINT
 4280 NEXT T:PRINT:PRINT
 4290 PRINT "Welche Gesellschaft wollen Sie":PRINT:PRINT "kaufen ? ";:BEEP
 4300 CALL*F6B8:S$=INKEY$:CALL*F6C5:IF S$=""THEN 4300:ELSE PRINT S$:PRINT
 4310 Z1=VAL(S$):IF Z1=0 OR BB(Z1)>0 THEN RETURN
 4320 PRINT "Sie sind nun Inhaber der Gesell-":PRINT "schaft "B$(Z1)".":PRINT
 4330 PRINT "Legen Sie nun Ihren Bohrge-":PRINT
 4340 PRINT "staengepreis fuer 500m fest !":PRINT:BEEP
 4350 CALL*F6B8:INPUT BD(Z1):CALL*F6C5
 4360 KS(S)=KS(S)-BG(Z1):BB(Z1)=S:RETURN
 4370 CLS
 4380 PRINT:PRINT "FOLGENDE BOHRGESTAENGE WERDEN   ANGEBOTEN :"
 4390 PRINT:PRINT "FIRMA           PREIS FUER 500m"
 4400 PRINT:PRINT STRING$(32,"-")
 4410 FOR Z=1 TO 3:PRINT B$(Z);
 4420 IF BB(Z)>0 THEN PRINT " "BD(Z)"$":GOTO 4440
 4430 PRINT "KEIN ANGEBOT"
 4440 PRINT:NEXT Z
 4450 IF BB(1)=0 AND BB(2)=0 AND BB(3)=0 THEN 4460:ELSE 4480
 4460 PRINT:PRINT "ES GIBT NOCH KEINE FIRMA, DIE   GESTAENGE VERKAUFT !"
 4470 PAUSE 50:RETURN
 4480 PRINT:CALL*F6B8:PRINT "Einkauf bei welcher Firma  ?";:BEEP
 4490 S$=INKEY$:IF S$=""THEN 4490:ELSE CALL*F6C5:PRINT S$:CC=VAL(S$)
 4500 IF CC=0 THEN RETURN
 4510 IF BD(CC)=0 THEN PRINT "LIEGT NOCH KEIN ANGEBOT VOR":GOTO 4480
 4520 PRINT:CALL*F6B8:PRINT "Wieviele 500m-Einheiten wollen  Sie ? ";
 4530 BEEP:INPUT "";C:CALL*F6C5:IF C=0 THEN RETURN
 4540 GOSUB 2330
 4550 IF D=0 THEN RETURN
 4560 KS(S)=KS(S)-(C*(BD(CC)))
 4570 FOR K=1 TO SZ:IF BB(CC)=S THEN KS(S)=KS(S)+(.2*(C*(BD(CC)))):GOTO 4590
 4580 IF BB(CC)=K THEN KS(K)=KS(K)+(C*(BD(CC)))
 4590 NEXT K:BM(D)=BM(D)+C*500:GOSUB 2410
 4600 D=0:DD=0:C=0:CC=0:K=0:RETURN
 4610 CLS:PRINT STRING$(32,"-"):PRINT
 4620 PRINT "    PUMPENVERKAEFERANGEBOTE     "
 4630 PRINT STRING$(32,"-"):PRINT
 4640 PRINT "GESELLSCHAFT    PUMPENPREIS IN $"
 4650 PRINT STRING$(32,"-"):PRINT
 4660 FOR Z=1 TO 3:PRINT Z;PF$(Z);
 4670 IF PP(Z)=0 THEN PRINT " KEIN ANGEBOT":GOTO 4690
 4680 PRINT P(Z)
 4690 PRINT:NEXT Z:PRINT
 4700 CALL*F6B8:PRINT "Kauf bei welcher Firma ? ";:BEEP
 4710 S$=INKEY$:IF S$=""THEN 4710:ELSE CALL*F6C5:PRINT S$:CC=VAL(S$)
 4720 IF CC=0 THEN RETURN
 4730 IF PP(CC)=0 THEN PRINT "KEIN ANGEBOT DA":GOTO 4700
 4740 CALL*F6B8:PRINT "Kauf von wieviel Pumpen ? ";:BEEP:INPUT "";C:CALL*F6C5
 4750 IF C=0 THEN RETURN
 4760 GOSUB 2330:IF D=0 THEN RETURN
 4770 KS(S)=KS(S)-(C*P(CC))
 4780 FOR K=1 TO SZ:IF PP(CC)=S THEN KS(S)=KS(S)+(.2*(C*P(CC))):GOTO 4800
 4790 IF PP(CC)=K THEN KS(K)=KS(K)+(C*P(CC))
 4800 NEXT K:PA(D)=PA(D)+C:GOSUB 2410
 4810 D=0:DD=0:C=0:CC=0:K=0:RETURN
 4820 CLS:PRINT STRING$(32,"*"):PRINT
 4830 PRINT "** TANKWAGENVERKAUFSANGEBOTE  **"
 4840 PRINT STRING$(32,"*")
 4850 FOR Z=1 TO 4:PRINT Z;TG$(Z);TW(Z);
 4860 IF TB(Z)=0 THEN PRINT " KEIN":PRINT TAB(23)"ANGEBOT":GOTO 4880
 4870 PRINT LP(Z)
 4880 PRINT:NEXT Z:PRINT
 4890 CALL*F6B8:PRINT "LKW-Kauf bei welcher Firma ? ";:BEEP
 4900 S$=INKEY$:IF S$=""THEN 4900:ELSE CALL*F6C5:PRINT S$:CC=VAL(S$)
 4910 IF CC=0 THEN RETURN
 4920 IF TB(CC)=0 OR TW(CC)<=0 THEN PRINT " GEHT NICHT !":GOTO 4890
 4930 CALL*F6B8:PRINT "Kauf von wieviel LKW    ";:BEEP:INPUT C:CALL*F6C5
 4940 IF C=0 OR C>TW(CC)THEN RETURN
 4950 GOSUB 2330:IF D=0 THEN RETURN
 4960 KS(S)=KS(S)-(C*LP(CC)):TW(CC)=TW(CC)-C
 4970 FOR K=1 TO SZ:IF TB(CC)=S THEN KS(S)=KS(S)+(.2*(LP(CC))):GOTO 4990
 4980 IF TB(CC)=K THEN KS(K)=KS(K)+(C*LP(CC))
 4990 NEXT K:TA(D)=TA(D)+C:GOSUB 2410
 5000 D=0:DD=0:C=0:CC=0:K=0:RETURN
 5010 CLS
 5020 PRINT "Fuer welches Produkt wollen Sie "
 5030 PRINT "den Preis neu festlegen ?":PRINT
 5040 PRINT " 1 = PUMPENPREIS":PRINT
 5050 PRINT " 2 = TANKWAGENPREIS":PRINT
 5060 PRINT " 3 = BOHRGESTAENGEPREIS":PRINT
 5070 CALL*F6B8:PRINT "Welches Produkt ? ";:BEEP
 5080 S$=INKEY$:IF S$<"1"OR S$>"3"THEN 5080:ELSE PRINT S$:CALL*F6C5:E1=VAL(S$)
 5090 CLS:ON E1 GOTO 5100,5180,5260
 5100 PRINT "NR  FIRMA       PUMPENPREIS":PRINT
 5110 FOR Z=1 TO 2:PRINT Z;PF$(Z);P(Z):PRINT:NEXT Z
 5120 PRINT:CALL*F6B8:PRINT "Welche Firma ? ";:BEEP
 5130 S$=INKEY$:IF S$=""THEN 5130:ELSE PRINT S$:CALL*F6C5:E1=VAL(S$)
 5140 IF E1<=0 OR E1>2 THEN PRINT:PRINT " KEINE FESTLEGUNG":GOSUB 6370:RETURN
 5150 IF PP(E1)<>S THEN PRINT "GEHOERT IHNEN NICHT!":GOTO 5120
 5160 PRINT:GOSUB 5970
 5170 P(E1)=E2:GOSUB 2410:E1=0:E2=0:RETURN
 5180 PRINT "NR  FIRMA       TANKWAGENPREIS  "
 5190 FOR Z=1 TO 4:PRINT Z;TG$(Z);LP(Z):NEXT Z
 5200 PRINT:CALL*F6B8:PRINT "Welche Firma ? ";:BEEP
 5210 S$=INKEY$:IF S$=""THEN 5210:ELSE PRINT S$:CALL*F6C5:E1=VAL(S$)
 5220 IF E1<=0 OR E1>4 THEN PRINT:PRINT " KEINE FESTLEGUNG":GOSUB 6370:RETURN
 5230 IF TB(E1)<>S THEN PRINT "GEHOERT IHNEN NICHT!":GOTO 5200
 5240 PRINT:GOSUB 5970
 5250 LP(E1)=E2:GOSUB 2410:E1=0:E2=0:RETURN
 5260 PRINT "NR  FIRMA       GESTAENGEPREIS  "
 5270 FOR Z=1 TO 3:PRINT Z;B$(Z);BD(Z):NEXT Z
 5280 PRINT:CALL*F6B8:PRINT "Welche Firma ? ";:BEEP
 5290 S$=INKEY$:IF S$=""THEN 5290:ELSE PRINT S$:CALL*F6C5:E1=VAL(S$)
 5300 IF E1<=0 OR E1>3 THEN PRINT:PRINT " KEINE FESTLEGUNG":GOSUB 6370:RETURN
 5310 IF BB(E1)<>S THEN PRINT "GEHOERT IHNEN NICHT!":GOTO 5280
 5320 PRINT:GOSUB 5970
 5330 BD(E1)=E2:GOSUB 2410:E1=0:E2=0:RETURN
 5340 CLS:PRINT "WELCHE DER FOLGENDEN PUMPEN-"
 5350 PRINT "FIRMEN SOLL SABOTIERT UND"
 5360 PRINT "UEBERNOMMEN WERDEN ?":PRINT
 5370 PRINT "GESELLSCHAFT   PREIS  BESITZER"
 5380 PRINT STRING$(32,"-"):PRINT
 5390 FOR Z=1 TO 3
 5400 PRINT Z;PF$(Z);PF(Z)
 5410 FOR ZZ=1 TO SZ:IF PP(Z)=ZZ THEN PRINT TAB(20)SN$(ZZ)
 5420 NEXT ZZ:PRINT:PRINT:NEXT Z:PRINT
 5430 GOSUB 6180
 5440 IF N<=0 OR N>3 OR PP(N)=0 THEN RETURN
 5450 GOSUB 3390
 5460 E=E+100:IF E<100 THEN 5490
 5470 KS(S)=KS(S)-((PF(N)*E)/100)
 5480 GOTO 5510
 5490 KS(S)=KS(S)-((PF(N)*E)/100)
 5500 PP(N)=0:W3=FN Z(100000):PF(N)=W3:P(N)=0
 5510 E=0:Z=0:ZZ=0:RETURN
 5520 CLS:PRINT "WELCHE DER FOLGENDEN TANKWAGEN-"
 5530 PRINT "FIRMEN SOLL SABOTIERT WERDEN ?  "
 5540 PRINT "GESELLSCHAFT   PREIS  BESITZER"
 5550 PRINT STRING$(32,"-"):PRINT
 5560 FOR Z=1 TO 4:PRINT Z;TG$(Z);TP(Z)
 5570 FOR ZZ=1 TO SZ:IF TB(Z)=ZZ THEN PRINT TAB(20)SN$(ZZ)
 5580 NEXT ZZ:PRINT:NEXT Z:PRINT
 5590 GOSUB 6180
 5600 IF N<=0 OR N>4 OR TB(N)=0 THEN RETURN
 5610 GOSUB 3390
 5620 E=E+100:KS(S)=KS(S)-((TP(N)*E)/100)
 5630 IF E<100 THEN 5650
 5640 ZZ=0:Z=0:E=0:RETURN
 5650 TB(N)=0
 5660 W3=FN Z(200000):TP(N)=W3:LP(N)=0:TW(N)=INT(W3/10000)
 5670 ZZ=0:Z=0
 5680 CLS:PRINT "SIE BESITZEN EIN VORKAUFSRECHT"
 5690 PRINT "AUF DIE VON IHNEN SABOTIERTE"
 5700 PRINT "TANKWAGENFIRMA :":PRINT
 5710 PRINT "FIRMA     :"TG$(N)
 5720 PRINT "PREIS     :"TP(N)"$"
 5730 PRINT "LKW-ZAHL  :"TW(N)
 5740 PRINT:PRINT "Wollen Sie kaufen (J/N)? ";:BEEP
 5750 S$=INKEY$:IF S$<>"J"AND S$<>"N"THEN 5750:ELSE PRINT S$
 5760 IF S$="N"THEN RETURN
 5770 TB(N)=S:KS(S)=KS(S)-TP(N)
 5780 PRINT:CALL*F6B8:PRINT "Neuer LKW-Preis  ";:BEEP:INPUT LP(N):CALL*F6C5
 5790 RETURN
 5800 CLS:PRINT "WELCHE DER FOLGENDEN BOHRGESELL-SCHAFTEN SOLL SABOTIERT"
 5810 PRINT "WERDEN ?"
 5820 PRINT:PRINT "GESELLSCHAFT    PREIS  BESITZER"
 5830 PRINT STRING$(32,"-"):PRINT
 5840 FOR Z=1 TO 3
 5850 PRINT Z;B$(Z);BG(Z)
 5860 FOR ZZ=1 TO SZ:IF BB(Z)=ZZ THEN PRINT TAB(20)SN$(ZZ)
 5870 NEXT ZZ:PRINT:PRINT:NEXT Z:PRINT
 5880 GOSUB 6180
 5890 IF N<=0 OR N>3 OR BB(N)=0 THEN RETURN
 5900 GOSUB 3390
 5910 E=E+100:KS(S)=KS(S)-((BG(N)*E)/100)
 5920 IF E<100 THEN 5940
 5930 GOTO 5950
 5940 BB(N)=0:BG(N)=FN Z(100000):BD(N)=0
 5950 E=0:Z=0:ZZ=0:RETURN
 5960 RETURN
 5970 CALL*F6B8:PRINT "Neuer Preis ";:BEEP:INPUT E2:CALL*F6C5
 5980 RETURN
 5990 FOR X=1 TO SZ
 6000 IF BK(X)>0 THEN KS(X)=KS(X)-5000:BK(X)=BK(X)-3000:GOSUB 6120
 6010 IF KS(X)<0 THEN GOSUB 6030
 6020 NEXT X:RETURN
 6030 CLS:PRINT:PRINT:PRINT:PRINT "DIE LUFT IST RAUS "SN$(X)
 6040 BEEP:BEEP:BEEP
 6050 PRINT:PRINT:PRINT "DU BEKOMMST VON DER BANKGESELL- "
 6060 PRINT "SCHAFT  RAFFKE UND SOHN EINEN   "
 6070 PRINT "BANKKREDIT VON 20000$. DU MUSST "
 6080 PRINT "JETZT JEDES JAHR 5000$ ABBE-"
 6090 PRINT:PRINT "ZAHLEN."
 6100 PRINT:GOSUB 6220:CLS
 6110 KS(X)=KS(X)+20000:BK(X)=BK(X)+20000:RETURN
 6120 CLS:GOSUB 6200:PRINT "RAFFKE UND SOHN BANKKREDITAB-"
 6130 PRINT "TEILUNG TEILT "SN$(X)" MIT:":PRINT
 6140 PRINT "NOCH ZAHLBAR   :"BK(X)"$":PRINT
 6150 PRINT "JETZT ZAHLBAR  : 5 0 0 0.--$":PRINT
 6160 PRINT "IHR KAPITAL    :"KS(X)"$":PRINT
 6170 GOSUB 6220:RETURN
 6180 PRINT:CALL*F6B8:PRINT " Welche Firma (Nr) ? ";:BEEP
 6190 S$=INKEY$:IF S$=""THEN 6190:ELSE PRINT S$:N=VAL(S$):CALL*F6C5:RETURN
 6200 FOR B=1 TO 8:PRINT:NEXT B
 6210 RETURN
 6220 PRINT " BITTE DRUECKEN SIE EINE TASTE":POKE DEEK(43),32:HLOAD 36,32
 6230 S$=INKEY$:IF S$=""THEN 6230:ELSE RETURN
 6240!---SOUND 1---
 6250 RESTORE 6270:FOR B=1 TO 31:READ E1,E2,E3:HLOAD E1,E2
 6260 FOR T=0 TO E3:NEXT T:NEXT B:RETURN
 6270 DATA 233,32,0,183,32,0,174,32,0,154,64,64,233,32,0,183,32,0,174,32,0
 6280 DATA 154,64,64,233,32,0,183,32,0,174,32,0,154,64,0,183,64,0,233,64,0
 6290 DATA 183,64,0,207,64,128,183,32,0,207,32,0,233,96,16,233,32,16,183,64,16
 6300 DATA 154,64,16,154,64,16,174,64,32,183,32,0,174,32,0,154,64,0,183,64,0
 6310 DATA 233,64,0,207,64,0,233,64,0
 6320!---SOUND 2---
 6330 RESTORE 6340:FOR B=1 TO 27:READ E1,E2:HLOAD E1,E2:NEXT B:RETURN
 6340 DATA 136,32,128,32,114,64,136,64,136,64,136,64,154,64,154,32,136,32
 6350 DATA 128,64,154,64,154,32,101,32,114,128,136,32,128,32,114,64,136,64
 6360 DATA 136,64,136,64,154,64,154,32,136,32,128,64,183,64,183,64,174,128
 6370!---SOUND 3---
 6380 FOR B=1 TO 3:HLOAD 145,65:HLOAD 65,128:PAUSE 1:NEXT B:RETURN
 6390!---SOUND 4---
 6400 FOR B=20 TO 240 STEP 20:HLOAD B,5:NEXT B:FOR B=240 TO 20 STEP-20:HLOAD B,5:NEXT B
 6410 FOR B=20 TO 240 STEP 20:HLOAD B,5:NEXT B:RETURN
 6420!---SOUND 5---
 6430 FOR T=1 TO 3:FOR B=200 TO 255:HLOAD B,3:NEXT B:NEXT T:RETURN
 6440!---JAHR VORUEBER---
 6450 LOCATE 13,0:PRINT STRING$(33,"");STRING$(30," ");"";STRING$(30," ");
 6460 PRINT "";STRING$(30," ");STRING$(33,""):POKE DEEK(43),32
 6470 PRINT AT(15,12);INT(RZ/1000)
 6480 PRINT AT(15,14);INT((RZ-INT(RZ/1000)*1000)/100)
 6490 PRINT AT(15,16);INT((RZ-INT(RZ/100)*100)/10)
 6500 PRINT AT(15,18);INT(RZ-INT(RZ/10)*10):RETURN
 6510 ON B+1 GOTO 6520,6540,6560,6580,6600,6620,6640,6660,6680,6700
 6520 PRINT AT(16,T);"":PRINT AT(17,T);" ":PRINT AT(18,T);" "
 6530 PRINT AT(19,T);" ":PRINT AT(20,T);"":RETURN
 6540 PRINT AT(16,T);" ":PRINT AT(17,T);" ":PRINT AT(18,T);" "
 6550 PRINT AT(19,T);" ":PRINT AT(20,T);" ":RETURN
 6560 PRINT AT(16,T);"":PRINT AT(17,T);"  ":PRINT AT(18,T);""
 6570 PRINT AT(19,T);"":PRINT AT(20,T);"":RETURN
 6580 PRINT AT(16,T);"":PRINT AT(17,T);"  ":PRINT AT(18,T);""
 6590 PRINT AT(19,T);"  ":PRINT AT(20,T);"":RETURN
 6600 PRINT AT(16,T);"":PRINT AT(17,T);" ":PRINT AT(18,T);""
 6610 PRINT AT(19,T);"  ":PRINT AT(20,T);"  ":RETURN
 6620 PRINT AT(16,T);"":PRINT AT(17,T);"":PRINT AT(18,T);""
 6630 PRINT AT(19,T);"  ":PRINT AT(20,T);"":RETURN
 6640 PRINT AT(16,T);"":PRINT AT(17,T);"":PRINT AT(18,T);""
 6650 PRINT AT(19,T);" ":PRINT AT(20,T);"":RETURN
 6660 PRINT AT(16,T);"":PRINT AT(17,T);"  ":PRINT AT(18,T);" "
 6670 PRINT AT(19,T);"":PRINT AT(20,T);"":RETURN
 6680 PRINT AT(16,T);"":PRINT AT(17,T);" ":PRINT AT(18,T);""
 6690 PRINT AT(19,T);" ":PRINT AT(20,T);"":RETURN
 6700 PRINT AT(16,T);"":PRINT AT(17,T);" ":PRINT AT(18,T);""
 6710 PRINT AT(19,T);"  ":PRINT AT(20,T);"":RETURN
 6720 CLS:GOSUB 6200:PRINT STRING$(32,"-"):PRINT
 6730 PRINT "Sie haben folgende Verkaufsmoeg-lichkeiten:":PRINT
 6740 PRINT STRING$(32,"-"):PRINT:PRINT
 6750 PRINT "1.  VERKAUF EINER FIRMA":PRINT
 6760 PRINT "2.  VERKAUF VON PRODUKTEN":PRINT
 6770 PRINT "3.  VERKAUF EINES OELFELDES":PRINT:PRINT
 6780 PRINT "Was wollen Sie verkaufen ? ";:BEEP
 6790 CALL*F6B8:...Binärcode

