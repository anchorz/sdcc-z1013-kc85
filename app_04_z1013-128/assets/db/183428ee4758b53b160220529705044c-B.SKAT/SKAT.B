Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

    1! SKAT
    2 CLS:PRINT AT(0,0);" ":CLEAR 1000     
    3 GOSUB 386
    4 A1$=CHR$(8):A2$=CHR$(9):A3$=CHR$(10):A4$=CHR$(11)
    5 A5$=STRING$(5,A1$)+A3$:A6$=A5$+"   ":A7$=STRING$(5,A6$)
    6 A8$=STRING$(5,A4$):A9$=STRING$(4,A1$):AA$=STRING$(27,A2$)
    7 AB$=STRING$(22,A2$)
    8 PRINT AT(1,14);"SKAT"
    9 PRINT AT(4,0);"Ich mische die Karten, verteile"      
   10 PRINT AT(6,0);"und sortiere sie. Ich versichere"                         
   11 PRINT AT(8,0);"Ihnen,dass ich Ihnen nicht in"        
   12 PRINT AT(10,0);"die Karten schaue oder deren"       
   13 PRINT AT(12,0);"Verteilung manipuliere."
   14 PRINT AT(15,0);"Wenn Sie >ENTER< in der rechten"  
   15 PRINT AT(17,0);"unteren Ecke sehen  druecken"
   16 PRINT AT(19,0);"Sie bitte die Taste >ENTER<."
   17 PRINT AT(22,0);"Viel Spass  beim Skat"
   18 PRINT AT(23,0);"wuenscht Ihnen"
   19 PRINT AT(25,5);"Wilfried Koster"
   20 PRINT AT(26,5);"Artur-Becker-Ring 52"
   21 PRINT AT(27,5);"Spremberg"
   22 PRINT AT(28,5);"7 5 9 0     Telefon 533560"   
   23 BEEP:PAUSE 4:BEEP:PAUSE 4:BEEP:PAUSE 4:BEEP:PAUSE 4:BEEP
   24 Y7=RND(1):Y7=0
   25 DIM B(3,12),PT(3),K(32),R(2),HR(2),SK(3),R5(3),R2(3),S1(3),S2(3),S3(3)
   26 DIM GP(3):ZV=INT(RND(1)*3)+1:Q=1:GOSUB 80
   27 CLS:PRINT AT(0,0);" "
   28 PRINT AT(15,2);"Einen Moment,"                       
   29 PRINT AT(17,2);"Ich mische die Karten."
   30 PRINT AT(30,10);"* Z 1013 COMPUTER *"
   31 FOR I=1 TO 32:K(I)=1:NEXT I
   32 FOR Z=1 TO 3:FOR T=1 TO 10
   33 FOR I=INT(RND(1)*31)+1 TO 32:IF K(I)=0 THEN NEXT I:GOTO 33
   34 GOSUB 77
   35 K(I)=0:B(Z,T)=W:NEXT T,Z
   36 Z1=1:FOR I=1 TO 32:IF K(I)=0 THEN NEXT I:GOTO 38
   37 GOSUB 77:R(Z1)=W:Z1=2:NEXT I:Z1=0
   38 Z=1:GOSUB 259
   39 GOSUB 264:GOSUB 185:GOSUB 201
   40 GOSUB 241:Z=ZV:FOR RD=1 TO 10:GOSUB 84
   41 PRINT:PRINT STRING$(6,CHR$(10))
   42 PRINT "Dieser Stich geht an Spieler"Z;:PRINT CHR$(8);
   43 IF Z=SP THEN PRINT ", dem     Einzelspieler.":ELSE PRINT
   44 GOSUB 80
   45 Z2=Z:IF RD<10 THEN GOSUB 264
   46 Z=Z2:FB=0:NEXT RD
   47 CLS:FOR I=1 TO 5:BEEP:PAUSE 2:NEXT I     
   48 PRINT "Wertung:"Q;A1$;". Spiel ("INT((Q-1)/3+1);A1$;". Runde )"      
   49 FOR I=1 TO 2:Z1=VAL(RIGHT$(STR$(HR(I)),1)):GOSUB 321:PT(SP)=PT(SP)+W:NEXT I
   50 PRINT:PRINT "Einzelspieler war Spieler Nr"SP;A1$;"."
   51 PRINT:IF SP=1 THEN PRINT "Sie erreichten"PT(SP)"Punkte!" 
   52 IF SP<>1 THEN PRINT "Er erreichte"PT(SP)"Punkte!"
   53 PRINT:IF SF=1 AND PT(SP)>90 THEN FG=1:GOTO 56
   54 IF SF=0 AND PT(SP)>60 THEN FG=1:GOTO 56
   55 FG=0
   56 IF(FG=1 AND SP=1)OR(FG=0 AND SP<>1)THEN PRINT "Sie haben gewonnen":GOTO 58
   57 PRINT "Sie haben verloren Herr Kahl !"
   58 ZV=ZV+1:IF ZV=4 THEN ZV=1
   59 IF PT(SP)>90 OR PT(SP)<30 THEN R5(SP)=R5(SP)+1:PRINT "Schneider";:TT=1
   60 IF SF=1 AND TT=1 THEN PRINT " & angesagt" 
   61 TT=0:PRINT:IF SF=1 THEN R5(SP)=R5(SP)+1
   62 IF HF=1 THEN R5(SP)=R5(SP)+1:HF=0
   63 IF GR=1 THEN GR=0:S2(SP)=24*(R5(SP)+1):GOTO 65
   64 S2(SP)=(R5(SP)+1)*R2(SP)
   65 PRINT AT(16,0);"Spielstand" 
   66 IF(PT(SP)<61)OR(PT(SP)<91 AND SF=1)THEN S2(SP)=-2*S2(SP)
   67 SF=0:IF S2(SP)>=GW OR FG=0 THEN 71
   68 PRINT "Moment...";:IF SP=1 THEN PRINT "Sie haben sich ";:GOTO 70
   69 PRINT "SPIELER"SP "hat sich ";
   70 PRINT "ueberreizt.":PRINT "---verloren!":S2(SP)=-2*S2(SP) 
   71 IF KF=1 THEN S2(SP)=2*S2(SP):KF=0
   72 IF RF=1 THEN S2(SP)=2*S2(SP):RF=0
   73 PRINT AT(18,0);"Spieler1 Spieler2 Spieler3 Spiel"      
   74 GP(SP)=GP(SP)+S2(SP):FOR I=0 TO 2:PRINT AT(20,I*09);GP(I+1):NEXT I        
   75 PRINT AT(20,28);S2(SP)
   76 FOR I=1 TO 3:PT(I)=0:NEXT I:Q=Q+1:GOSUB 80:GOTO 27
   77 W=(INT((I-1)/4)+1)+10*(((I-1)/4-INT((I-1)/4))*4+1)
   78 IF INT((I-1)/4)+1=8 THEN W=W+200
   79 RETURN
   80     PRINT AT(31,25);">ENTER<"
   81 P$=INKEY$:IF P$<>CHR$(13)THEN 81
   82 PRINT AT(31,25);"       "   
   83 RETURN
   84 FB=0:ON Z GOTO 85,86,86
   85 GOSUB 328:GOSUB 348:GOTO 96
   86 IF SP=Z THEN 91
   87 GOSUB 177
   88 IF H=0 THEN GOSUB 169
   89 IF H-10*INT(H/10)=6 THEN GOSUB 180
   90 SK(Z)=H:GOSUB 348:GOTO 96
   91 GOSUB 169:IF VAL(RIGHT$(STR$(H),1))>5 THEN GOSUB 167:GOTO 93
   92 GOTO 94
   93 IF VAL(RIGHT$(STR$(H),1))>5 AND H<200 THEN GOSUB 177:IF H=0 THEN GOSUB 167
   94 IF H=0 THEN GOSUB 177
   95 GOTO 90
   96 FB=INT(SK(Z)/10):Z=Z+1:IF Z=4 THEN Z=1
   97 IF Z=1 THEN GOSUB 328:GOSUB 348:GOTO 120
   98 IF Z=SP THEN 112
   99 IF SK(SP)>0 THEN 104
  100 GOSUB 172:IF H=0 THEN GOSUB 169
  101 IF H=0 THEN ON 1+INT(RND(1)*2)GOSUB 177,180
  102 SK(Z)=H:IF INT(H/10)<>FB AND H<100 THEN SK(Z)=-H
  103 GOSUB 348:GOTO 120
  104 IF SK(SP)>100 THEN 110
  105 GOSUB 172:IF H>SK(1)THEN 102
  106 IF H>0 THEN GOSUB 174:GOTO 102
  107 GOSUB 169
  108 IF H=0 THEN GOSUB 180
  109 GOTO 102
  110 GOSUB 169:IF H=0 THEN GOSUB 180
  111 GOTO 102
  112 ZW=Z-1:IF ZW=0 THEN ZW=3
  113 IF FB>9 THEN 118
  114 GOSUB 172:IF H>SK(ZW)THEN 102
  115 IF H>0 THEN GOSUB 174:GOTO 102
  116 GOSUB 169:IF H>0 THEN 102
  117 GOSUB 180:GOTO 102
  118 GOSUB 167:IF H=0 THEN GOSUB 180
  119 GOTO 102
  120 Z=Z+1:IF Z=4 THEN Z=1
  121 IF Z=1 THEN GOSUB 328:GOSUB 348:GOTO 158
  122 IF Z=SP THEN 140
  123 GOSUB 154:IF H1=SK(SP)THEN 130
  124 IF FB<10 THEN 127
  125 GOSUB 169:IF H=0 THEN GOSUB 177
  126 GOTO 138
  127 GOSUB 172:IF H=0 THEN GOSUB 177
  128 IF H=0 THEN GOSUB 169
  129 GOTO 138
  130 IF FB<9 THEN 133
  131 ON 1+INT(RND(1)*2)GOSUB 167,169:IF H>0 THEN 138
  132 GOSUB 180:GOTO 138
  133 GOSUB 172:IF H>SK(SP)THEN 138
  134 IF H>0 THEN GOSUB 174
  135 IF H=0 THEN GOSUB 169:IF H<SK(PS)THEN GOSUB 167
  136 IF H=0 THEN GOSUB 174
  137 IF H=0 THEN GOSUB 180
  138 SK(Z)=H:IF INT(H/10)<>FB AND H<100 THEN SK(Z)=-H
  139 GOSUB 348:GOTO 158
  140 IF FB>9 THEN 148
  141 GOSUB 172:GOSUB 154:IF H>H1 THEN 138
  142 IF H>0 THEN GOSUB 174:GOTO 138
  143 GOSUB 156:IF SS>8 THEN 147
  144 GOSUB 180:IF H=0 THEN GOSUB 169:GOTO 138
  145 IF H-10*INT(H/10)>4 THEN GOSUB 169
  146 GOTO 138
  147 GOSUB 169:IF H=0 THEN GOSUB 180
  148 GOSUB 156:IF SS>10 THEN 152
  149 GOSUB 169:IF H=0 THEN GOSUB 180:GOTO 138
  150 IF VAL(RIGHT$(STR$(H),2))<6 THEN 138
  151 GOSUB 167:GOTO 138
  152 GOSUB 169:IF H=0 THEN GOSUB 180
  153 GOTO 138
  154 H1=0:FOR I=1 TO 3:IF H1<SK(I)THEN H1=SK(I)
  155 NEXT I:RETURN
  156 SS=0:FOR I=1 TO 3:Z1=VAL(RIGHT$(STR$(SK(I)),1)):GOSUB 321:SS=SS+W
  157 NEXT I:RETURN
  158 FOR Z1=1 TO 3:FOR I=1 TO 10:IF ABS(SK(Z1))=B(Z1,I)THEN B(Z1,I)=0
  159 NEXT I,Z1:IF SK(1)>SK(2)AND SK(1)>SK(3)THEN Z=1:GOTO 161
  160 GOTO 163
  161 IF INT(SK(1)/10)<>FB AND SK(1)<100 THEN 163
  162 GOTO 165
  163 IF SK(2)>SK(3)THEN Z=2:GOTO 165
  164 Z=3
  165 SS=0:FOR I=1 TO 3:Z1=VAL(RIGHT$(STR$(SK(I)),1)):GOSUB 321
  166 SS=SS+W:SK(I)=0:NEXT I:PT(Z)=PT(Z)+SS:RETURN
  167 H=0:FOR I=1 TO 10:IF H<B(Z,I)AND B(Z,I)>100 THEN H=B(Z,I)
  168 NEXT I:RETURN
  169 H=1000:FOR I=1 TO 10:IF H>B(Z,I)AND B(Z,I)>100 THEN H=B(Z,I)
  170 NEXT I:IF H=1000 THEN H=0
  171 RETURN
  172 H=0:FOR I=1 TO 10:IF H<B(Z,I)AND INT(B(Z,I)/10)=FB THEN H=B(Z,I)
  173 NEXT I:RETURN
  174 H=1000:FOR I=1 TO 10:IF H>B(Z,I)AND INT(B(Z,I)/10)=FB THEN H=B(Z,I)
  175 NEXT I:IF H=1000 THEN H=0
  176 RETURN
  177 H1=0:H=0:FOR I=1 TO 10:IF H1<B(Z,I)-10*INT(B(Z,I)/10)AND B(Z,I)<100 THEN 179
  178 NEXT I:H1=0:RETURN
  179 H=B(Z,I):H1=B(Z,I)-10*INT(B(Z,I)/10):GOTO 178
  180 H1=10:H=1000:FOR I=1 TO 10
  181 IF H1>VAL(RIGHT$(STR$(B(Z,I)),1))AND B(Z,I)<100 AND B(Z,I)>0 THEN 184
  182 NEXT I:IF H=1000 THEN H=0
  183 RETURN
  184 H=B(Z,I):H1=VAL(RIGHT$(STR$(B(Z,I)),1)):GOTO 182
  185 FOR Z=1 TO 3:R1=0:R2(Z)=0:R3=0:R5(Z)=0
  186 FOR I=1 TO 4:K(I)=0:NEXT I:Z1=0
  187 FOR I=1 TO 10:IF B(Z,I)>200 THEN Z1=Z1+1:K(Z1)=B(Z,I)
  188 NEXT I:R1=Z1:Z1=0:FOR H=248 TO 218 STEP-10:GOSUB 189:NEXT H:GOTO 191
  189 Z1=Z1+1:FOR I=1 TO 4:IF K(I)=H THEN K(Z1+4)=1:RETURN
  190 NEXT I:K(Z1+4)=0:RETURN
  191 FOR I=5 TO 7:IF K(I)=K(I+1)THEN NEXT I:I=8
  192 R5(Z)=I-4:S3(Z)=R5(Z):FOR I=1 TO 4:K(I)=0:NEXT I
  193 FOR I=1 TO 10:IF B(Z,I)<100 THEN FB=INT(B(Z,I)/10):K(FB)=K(FB)+1
  194 NEXT I:FOR I=1 TO 4:IF R3<K(I)THEN R3=K(I):R2(Z)=I
  195 NEXT I:R2(Z)=8+R2(Z):R4=R2(Z)*(R5(Z)+1):R8=0:FOR I=1 TO 10
  196 IF B(Z,I)-10*INT(B(Z,I)/10)=7 OR B(Z,I)-10*INT(B(Z,I)/10)=6 THEN R8=R8+1
  197 NEXT I:R8=R8/2:R6=(R3-1+R1)*(5+R1)*R8:IF R6>R4 THEN R6=R4:S2(Z)=R4
  198 IF R6<18 THEN R7=18:GOTO 200
  199 READ R4:IF R4<=R6 THEN R7=R4:GOTO 199
  200 R6=R7:RESTORE:S1(Z)=R6:NEXT Z:RETURN
  201 GOSUB 80:PG=ZV:Z1=ZV:GOSUB 202:PH=Z1:GOSUB 202:SP=Z1:GOTO 204
  202 Z1=Z1+1:IF Z1<4 THEN RETURN
  203 Z1=1:RETURN
  204 GOSUB 210:IF H=0 THEN SP=PG:GOTO 208:! 1860
  205 GOSUB 216:IF H=1 THEN 204
  206 PH=PG:GOTO 208
  207 GOSUB 210:IF H=0 THEN SP=PH:GOTO 222
  208 GOSUB 216:IF H=1 THEN 207
  209 GOTO 222
  210 IF SP=1 THEN 213
  211 READ R4:PRINT:PRINT SP ":"R4;:IF R4<S1(SP)+.5 THEN H=1:RETURN
  212 PRINT "Weg";:H=0:RETURN 
  213 READ R4:PRINT:PRINT;R4;
  214 GOSUB 382:IF P$="J"THEN H=1:RETURN
  215 H=0:ZW=R4:RETURN
  216 PRINT PH ":";:IF PH=1 THEN 219
  217 IF R4<=S1(PH)THEN PRINT "Ja";:H=1:ZW=R4:RETURN
  218 PRINT "Weg";:H=0:ZW=R4:RETURN
  219 PRINT;    
  220 GOSUB 382:IF P$="J"THEN H=1:RETURN
  221 H=0:ZW=R4:RETURN
  222 PRINT:PRINT "Spiel geht an Spieler";SP
  223 IF SP=1 THEN GOSUB 80:GOSUB 271:RETURN
  224 FOR I=1 TO 2:B(SP,I+10)=R(I):NEXT I
  225 FOR I=1 TO 4:K(I)=0:NEXT I:FOR I=1 TO 12:FB=INT(B(SP,I)/10)
  226 IF FB<10 THEN K(FB)=K(FB)+1
  227 NEXT I:F1=0:FB=0:FOR I=1 TO 4:IF F1<K(I)THEN FB=I:F1=K(I)
  228 NEXT I:P=FB:GOSUB 301:GOSUB 235:PRINT " IST TRUMPF":S1(SP)=8+P
  229 S2(SP)=(1+S3(SP))*S1(SP):FOR I=1 TO 2
  230 A=INT(RND(1)*12)+1:IF B(SP,A)>100 OR B(SP,A)-10*INT(B(SP,A)/10)=7 THEN 230
  231 HR(I)=B(SP,A):B(SP,A)=0
  232 NEXT I
  233 Z1=0:FOR I=1 TO 12:IF B(SP,I)>0 THEN Z1=Z1+1:K(Z1)=B(SP,I)
  234 NEXT I:FOR I=1 TO 10:B(SP,I)=K(I):NEXT I:GOSUB 80:GOSUB 264:RETURN
  235 PRINT "__";
  236 IF P=1 THEN PRINT "Karo";:RETURN   
  237 IF P=2 THEN PRINT "Herz";:RETURN
  238 IF P=3 THEN PRINT "Pik";:RETURN
  239 IF P=4 THEN PRINT "Kreuz";:RETURN
  240 PRINT "Grand";:RETURN
  241 IF SP<>1 THEN 251
  242 PRINT:FOR T=2 TO 3
  243 H1=0:FOR J=1 TO 10:IF B(T,J)>100 THEN H1=H1+1
  244 NEXT J:IF H1>4-SF AND T<>SP THEN PRINT "_Spieler"T ": Kontra !":KF=1
  245 FOR J=1 TO 700:NEXT J
  246 NEXT T:IF KF=0 THEN 257
  247 IF SP=1 THEN 254
  248 FOR J=1 TO 10:IF VAL(RIGHT$(STR$(B(SP,J)),1))>6 OR B(SP,J)>200 THEN H1=H1+1
  249 NEXT J:IF H1>5 THEN PRINT "_Spieler"SP ": Re !!":RF=1:FOR J=1 TO 1000:NEXT J
  250 GOTO 257
  251 PRINT:PRINT "Kontra ";
  252 GOSUB 382:IF P$="J"THEN KF=1
  253 GOTO 242
  254 PRINT:PRINT "Re ";
  255 GOSUB 382:IF P$="J"THEN RF=1
  256 GOTO 257
  257 GOSUB 264
  258 RETURN
  259 FOR T=1 TO 10
  260 FOR I=1 TO 10:IF Z1<B(Z,I)THEN Z1=B(Z,I)
  261 NEXT I:K(T)=Z1:FOR I=1 TO 10:IF B(Z,I)=Z1 THEN B(Z,I)=0
  262 NEXT I:Z1=0:NEXT T
  263 FOR J=1 TO 10:B(Z,J)=K(J):NEXT J:RETURN
  264 CLS:PRINT "Ihre Karten:":PRINT
  265 Z=1:FOR T=1 TO 9
  266 IF T=5 THEN PRINT:PRINT STRING$(4,CHR$(10))                                 
  267 IF T=9 THEN PRINT:PRINT STRING$(4,CHR$(10))
  268 PRINT T;:GOSUB 379:NEXT T
  269 T=10:PRINT 0;:GOSUB 379
  270 PRINT:PRINT STRING$(4,CHR$(10)):PRINT:RETURN
  271 PRINT "Hand ";
  272 GOSUB 382
  273 IF P$="J"THEN HF=1:FOR I=1 TO 2:HR(I)=R(I):NEXT I:GOTO 292
  274 GOTO 275
  275 F1=1:GOSUB 264:PRINT:PRINT "Skat";:PRINT                            
  276 FOR I=1 TO 2:B(1,10+I)=R(I):NEXT I
  277 FOR T=11 TO 12:PRINT T;:GOSUB 379:NEXT T
  278 K(1)=13:K(2)=13                                                        
  279 IF T9=1 THEN CALL*F6B8
  280 INPUT "Druecken Nr";K(1):PRINT AA$;:INPUT "Nr";K(2)
  281 CALL*F6C5
  282 IF K(1)=0 THEN K(1)=10
  283 IF K(2)=0 THEN K(2)=10
  284 IF K(1)=K(2)THEN PRINT AB$;A4$;A4$;:GOTO 278
  285 FOR I=1 TO 2
  286 IF K(I)<1 OR K(I)>12 THEN PRINT AB$;A4$;A4$;:GOTO 278
  287 HR(I)=B(1,K(I)):NEXT I
  288 Z1=0:FOR I=1 TO 2:B(1,K(I))=0:NEXT I:FOR I=1 TO 12
  289 IF B(1,I)<>0 THEN Z1=Z1+1:K(Z1)=B(1,I)
  290 NEXT I:FOR I=1 TO 10:B(1,I)=K(I):NEXT I
  291 F4=1:Z=1:GOSUB 259
  292 GOSUB 264:PRINT "Was ist Trumpf?":PRINT "1. Karo     2. Herz" 
  293 PRINT "3. Pik      4. Kreuz    5. Grand" 
  294 IF T9=1 THEN CALL*F6B8
  295 P$=INKEY$:P=VAL(P$)
  296 GR=0
  297 IF P>5 OR P<1 THEN 295
  298 PRINT "Nr."P
  299 CALL*F6C5
  300 IF P=5 THEN 311
  301 S2(1)=(P+8)*(S3(1)+1):FOR Z=1 TO 3:FOR T=1 TO 10
  302 IF B(Z,T)<100 AND INT(B(Z,T)/10)=P THEN B(Z,T)=100+B(Z,T)
  303 NEXT T,Z
  304 FOR T=11 TO 12:IF B(SP,T)<100 AND INT(B(SP,T)/10)=P THEN B(SP,T)=100+B(SP,T)
  305 NEXT T:Z=1:GOSUB 259:IF F1=0 THEN GOSUB 264
  306 IF F4=0 THEN 310
  307 F4=0:SF=0:PRINT "Schneider ansagen ";
  308 GOSUB 382:IF P$="J"THEN SF=1
  309 GOTO 310
  310 GOSUB 313:RETURN
  311 GR=1:S2(1)=24*(S3(1)+1):Z=1:GOSUB 259:IF F1=0 THEN GOSUB 264
  312 GOSUB 306:RETURN
  313 Z1=1:R2(SP)=8+P:IF P=5 THEN R2(SP)=24
  314 FOR T=1 TO 4:K(T)=0:NEXT T
  315 FOR T=1 TO 4:FOR I=1 TO 10:IF B(SP,I)=258-10*T THEN K(T)=1
  316 NEXT I,T:FOR T=1 TO 3:IF K(T)=K(T+1)THEN Z1=Z1+1:NEXT T
  317 IF HF=1 THEN Z1=Z1+1
  318 R5(SP)=Z1:S2(SP)=(R5(SP)+1)*R2(SP)
  319!W.Koster 7590 Spremberg Tel.533560 Prv.
  320 GW=ZW:RETURN
  321 ON Z1 GOTO 322,322,322,323,324,325,326,327
  322 W=0:RETURN
  323 W=3:RETURN
  324 W=4:RETURN
  325 W=10:RETURN
  326 W=11:RETURN
  327 W=2:RETURN
  328 PRINT AT(18,19);"Karte Nr.?"  
  329 IF T9=1 THEN CALL*F6B8
  330 P$=INKEY$:IF P$=""THEN 330  
  331 IF P$<"0"OR P$>"9"THEN 330
  332 P=VAL(P$):IF P=0 THEN P=10
  333 IF B(1,P)=0 THEN 330
  334 CALL*F6C5
  335 IF SK(2)=0 AND SK(3)=0 THEN 345
  336 GE=2:IF SK(2)=0 THEN GE=3
  337 F=0:FOR G=1 TO 10
  338 IF B(1,G)>100 THEN F=F+1
  339 NEXT G
  340 IF F>0 AND B(1,P)<100 AND SK(GE)>100 THEN 344
  341 F=0:FOR G=1 TO 10:IF B(1,G)>100 THEN NEXT G
  342 IF INT(B(1,G)/10)=INT(SK(GE)/10)AND INT(B(1,P)/10)<>INT(SK(GE)/10)THEN 344
  343 NEXT G:GOTO 345
  344 PRINT AT(22,21);"Bedienen! ":GOSUB 80:GOTO 328
  345 SK(1)=B(1,P)
  346 IF FB>0 AND INT(SK(1)/10)<>FB AND SK(1)<100 THEN SK(1)=-SK(1)
  347 RETURN
  348!
  349 IF Z=SP THEN PRINT " * ";:GOTO 351
  350 PRINT Z;
  351 Q$=RIGHT$(STR$(SK(Z)),2)
  352 GOSUB 353:RETURN
  353 Z1=VAL(LEFT$(Q$,1)):IF Z1=0 THEN PRINT "     ";:RETURN
  354 ON 5-Z1 GOTO 355,356,357,358
  355 B$="":GOTO 359
  356 B$="":GOTO 359
  357 B$="":GOTO 359
  358 B$=""     
  359 IF F2=1 THEN F2=0:PRINT "     ";:RETURN
  360 Z1=VAL(RIGHT$(Q$,1))
  361 ON 9-Z1 GOTO 362,363,364,365,366,367,368,369
  362 A$="B":GOTO 370
  363 A$="A":GOTO 370
  364 A$="10":GOTO 370
  365 A$="K":GOTO 370
  366 A$="D":GOTO 370
  367 A$="9":GOTO 370
  368 A$="8":GOTO 370
  369 A$="7"
  370 PRINT "";A7$;A5$;
  371 PRINT "";A8$;A9$;
  372 PRINT A$;A3$;A1$;
  373 IF A$="10"THEN PRINT A1$;
  374 PRINT B$;A3$;A3$;A2$;
  375 PRINT B$;A3$;A1$;
  376 IF A$="10"THEN PRINT A1$;
  377 PRINT A$;A2$;A8$;
  378 RETURN
  379 Q$=RIGHT$(STR$(B(Z,T)),2):GOSUB 353:RETURN
  380 DATA 18,20,22,23,24,27,30,33,35,36,44,45,46,48,50,54,55,60,63,66,70
  381 DATA 72,77,80,81,84,88,90,96,99,100,108,110,120,144,168
  382 PRINT "(J/N)? ";:BEEP:PAUSE 4:BEEP:PAUSE 4:BEEP
  383 P$=INKEY$:IF P$="J"THEN PRINT "Ja";:RETURN
  384 IF P$<>"N"THEN 383
  385 PRINT "Nein";:RETURN
  386 CLS
  387 PRINT
  388 PRINT
  389 PRINT
  390 PRINT "Das Programm kann die Tastatur"
  391 PRINT "bei Zahleneingaben in die Shift"
  392 PRINT "Ebene Null umschalten."
  393 PRINT
  394 PRINT "Es ist bei der Arbeit mit der"
  395 PRINT "Folientastatur von Riesa eine"
  396 PRINT "Erleichterung bei der Eingabe."
  397 PRINT
  398 PRINT "Das gleichzeitige druecken der"
  399 PRINT "S1-Taste entfaellt."
  400 PRINT
  401 PRINT 
  402 PRINT
  403 PRINT "Soll umgeschaltet werden ?"
  404 PRINT:A22=RND(1):A22=0 
  405 PRINT "              > J/N < "
  406 L$=INKEY$
  407 IF L$="" THEN 406
  408 IF L$="J" THEN T9=1
  409 IF L$<>"J" THEN T9=2
  410 CLS
  411 RETURN
