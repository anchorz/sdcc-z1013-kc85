Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

   10!**************************
   12!*
   14!*
   16!* RUMPELSTILZCHEN
   18!* _______________
   20!*
   22!* C.: STEFFEN POLSTER
   24!*     LUTHERSTRASSE 56
   26!*     KARL-MARX-STADT
   28!*     9022
   30!*
   32!* D.: 25./28. 9.1986
   34!*     VERSION 1
   36!*
   38!* AUS DEM PROGRAMM "BURG"  
   40!* VON A.SCHROEDER WURDEN
   42!* GRAFIK-ELEMENTE VERWENDET
   44!*
   46!**************************
   48 WINDOW:CLS:CLEAR          
   50 DATA 229,197,33,0,236,6,240,54,32,35,54,32,35,54,32,35,54,32,35,16
   52 DIM BF(20),Z$(42),KR(42)
   54 DATA 242,193,225,201
   58 PRINT "              "     
   60 PRINT "                  "   
   62 PRINT "                  "   
   64 PRINT "                     "   
   66 PRINT "                     "   
   68 PRINT "                  "   
   70 PRINT:PRINT
   72 PRINT "                "
   74 PRINT "                       "
   76 PRINT "                      "
   78 PRINT "                       "
   80 PRINT "                       "
   82 PRINT "                 "
   84 PRINT:PRINT:PRINT
   86 PRINT "ABENTEUER IM MAERCHENLAND"      
   88 PRINT:PRINT "SPANNEND UND EINMALIG":PRINT
   90 PRINT:PRINT             
   92 GOSUB 1454
   94 DATA ""         
   96 DATA ES WAR EINMAL ...
   98 DATA "DASS EIN MUELLER UM SICH VORM "        
   99 DATA "KOENIG GROSS ZU TUN,ERZAEHLTE,"
  100 DATA "SEINE TOCHTER KOENNE STROH ZU"           
  102 DATA "GOLD SPINNEN.    DER KOENIG,"
  104 DATA "DAVON ANGETAN, LIESS DIE"           
  106 DATA "ARME JUNGFER IN EIN VERLIES"
  108 DATA "FUEHREN, UND TRUG IHR AUF, "          
  109 DATA "EINEN BERG STROH ZU GOLD "
  110 DATA "ZU SPINNEN ."
  112 DATA ""         
  114 RESTORE 94
  116 FOR I=1 TO 12:READ B$:PRINT B$:PRINT:NEXT I
  118 GOSUB 986
  120 DATA "ALS GEBILDETER, MODERNER MENSCH"
  122 DATA KENNST DU DAS MAERCHEN VOM
  124 DATA "      "
  126 DATA "        RUMPELSTILZCHEN  "
  128 DATA "      "
  130 DATA "UND VERTRITTST NATUERLICH GERN"
  131 DATA "DAS HUEBSCHE MAEDCHEN."
  132 DATA "WIE IST DEIN DENKWUERDIGER NAME"
  134 RESTORE 120:FOR I=1 TO 8 :READ B$:PRINT B$:PRINT:NEXT I
  136 INPUT N$:IF LEN(N$)=0 THEN N$="UNBEKANNTER"
  138 PRINT AT(24,0);"VIEL ERFOLG FUER DICH "          
  140 PRINT AT(26,0);N$," "," DU HILFSBEREITER HELD"
  142 GOSUB 986
  143 GOSUB 990
  144 PRINT AT(26,0);"BEVOR DU IN DAS VERLIES MUSST,"
  146 PRINT AT(28,0);"DARFST DU DIR 3 DINGE WAEHLEN"       
  148 GOSUB 1036
  149! TUER
  150 FOR I=7 TO 15:PRINT AT(I,17);"":NEXT I:PAUSE 45
  152 GOSUB 922
  154 FOR I=7 TO 9:G(I)=-1:NEXT I:FOR I=1 TO 6:G(I)=0:NEXT I:G(10)=2
  156 XX$="NICHT MOEGLICH !! "
  158 PRINT AT(28,0);"SPIEGEL(1), GOLDENER RING(2)  "         
  159 PRINT AT(29,0);"KERZE(3), TARNKAPPE(4)"
  160 PRINT AT(30,0);"SCHLUESSEL(5), KAEFIG(6)"
  162 PRINT AT(26,0);"WAS MOECHTEST DU ?           "   
  164 Q$=INKEY$:IF LEN(Q$)>0 THEN X=VAL(Q$):GOTO 176:ELSE 164    
  166 PRINT AT(26,0);"UND ALS ZWEITES ?        "
  168 Q$=INKEY$:IF LEN(Q$)>0 THEN Y=VAL(Q$):GOTO 178:ELSE 168
  170 PRINT AT(26,0);"UND ALS DRITTES ?     "
  172 Q$=INKEY$:IF LEN(Q$)>0 THEN Z=VAL(Q$):GOTO 182:ELSE 172
  174 GOTO 184
  176 IF(X<1 OR X>6)THEN PRINT AT(26,0);XX$:PAUSE 25:GOTO 162:ELSE 166
  178 IF(Y<1 OR Y>6 OR Y=X)THEN PRINT AT(26,0);XX$:PAUSE 20:GOTO 166
  180 GOTO 170
  182 IF(Z<1 OR Z>6 OR Z=X OR Z=Y)THEN PRINT AT(26,0);XX$:PAUSE 20:GOTO 170
  184 G(X)=1:G(Y)=1:G(Z)=1
  186 DATA SPIEGEL,GOLDENER RING,KERZE,TARNKAPPE,SCHLUESSEL,KAEFIG,VOGEL
  188 DATA FEUERZEUG,APFEL
  190 RESTORE 186:FOR I=1 TO 9:READ G$(I):NEXT I
  192 WINDOW 25,31,0,31:CLS:PRINT "DU ERWAEHLTEST"
  194 PRINT G$(X)+" , "+G$(Y)+" , "+G$(Z)
  196 PRINT AT(27,0);" ":PAUSE 45:CLS:PRINT "ABER NUN MUSST DU IN DAS VERLIES "
  198 PRINT AT(26,0);" "
  200 GOSUB 1036
  202 EW=3
  204 PAUSE 35
  206!
  208 GOSUB 990:GOSUB 1004
  210 GOSUB 1068
  212 FOR I=1 TO 9:IF G(I)<>1 THEN 222
  214 ON I GOSUB 932,938,944,950,956,962,968,974,980
  216 FOR J=1 TO 20:READ BF(J):NEXT J
  218 K=K+1:FOR J=0 TO 4:FOR JJ=0 TO 3:POKE-4586+K*5+J*32+JJ,BF(J*4+JJ+1):NEXT JJ    
  220 NEXT J
  222 NEXT I 
  224 IF EV=4 THEN RETURN
  226 FOR I=0 TO 5+1*(3-EW):FOR J=0 TO 2*I
  228 POKE-4592+J+I*32-I-(3-EW)*1 ,204:NEXT J
  230 NEXT I:POKE-4319,32
  232 ON EV GOTO 252,372
  234 PRINT AT(27,0);"DA IST DER HAUFEN STROH !!"
  236 PRINT AT(28,0);"KANNST DU IHN ZU GOLD SPINNEN ?"
  238 IF PEEK(4)=74 THEN WINDOW 25,31,0,31:GOTO 244
  242 IF PEEK(4)=78 THEN 270:ELSE 238
  244 CLS:PRINT "DANN BITTE, ZEIGE ES !":PRINT "DU GROSSMAUL !!!!!"
  246 PRINT AT(23,0);" "
  248 PAUSE 55:WINDOW:CLS   : PRINT AT(10,10);"8 STUNDEN SPAETER":POKE-5120,32
  250 PAUSE 45: CLS   :EV=1:K=0:GOTO 208
  252 WINDOW 25,31,0,31
  254 PRINT "DER HAUFEN STROH IST NOCH NICHT"         
  256 PRINT "KLEINER UND DER MORGEN NAHT."
  258 PRINT AT(29,0);"BRAUCHST DU NUN HILFE?"
  260 IF INKEY$<>""THEN 262:ELSE 260
  262 CLS:PRINT AT(29,0);"ES IST ABER ZU SPAET !":PAUSE 45:GOSUB 1012
  264 PRINT AT(27,0);"STEIGE DIE TREPPE HINAUF !!"            
  266 PRINT AT(29,0);"DER GALGEN WARTET !!":PAUSE 80
  268 GOTO 896
  270 WINDOW 25,31,0,31:CLS
  272 PRINT "WAS GIBST DU RUMPELSTILZCHEN,":PRINT "WENN ER DIR HILFT ? "        
  273 INPUT "";GE$
  274 FOR I=1 TO 6
  276 IF G$(I)=GE$ THEN 282
  278 NEXT I
  280 CLS:PRINT "DAS HAST DU JA GAR NICHT !!":PAUSE 25:GOTO 270
  282 IF G(I)=1 THEN 286
  284 GOTO 280
  286 IF EW=1 THEN 312
  288 CLS:PRINT:PRINT "  OKAY":PRINT AT(28,0);" ":G(I)=-1:PAUSE 30
  290 WINDOW:CLS
  292 DATA "DER KOENIG IST WIE JEDER KOENIG"
  294 DATA "SEHR HABGIERIG UND VERLANGT,"
  296 DATA "GEGEN ALLE ABMACHUNGEN, EINEN"
  297 DATA "NEUERLICHEN BEWEIS DEINER KUNST."
  298 DATA "DU KOMMST ERNEUT IN DAS VERLIES"
  300 DATA "UND DER HAUFEN STROH IST "
  302 DATA "JETZT GROESSER!, "," DEIN BESITZ: "
  304 RESTORE 292:FOR I=1 TO 8:READ B$:PRINT B$:PRINT:NEXT I:EW=0
  306 FOR I=1 TO 6:IF G(I)=1 THEN PRINT TAB(17);G$(I):PRINT:EW=EW+1
  308 NEXT I:PRINT AT(18,0);" ":PRINT AT(20,0);" "
  310 GOSUB 986:K=0:WINDOW:CLS    :GOTO 208
  312 DATA "RUMPELSTILZCHEN IST HAESSLICH       UND NIMMT KEINEN SPIEGEL."
  314 DATA "GOLDENE RINGE HAT RUMPI GENUG            "
  316 DATA "WAS SOLL RUMPI MIT EINER KERZE ?         "
  318 DATA "EINE TARNKAPPE HAT RUMPI SELBST "         
  320 DATA "SCHLUESSEL BRAUCHT RUMPI NICHT !         "
  322 DATA "KAEFIGE SIND FUER VOEGEL WIE      DICH DA"
  324 CLS:RESTORE 312:FOR I=1 TO 6:READ B$
  326 IF G(I)=1 THEN PRINT B$:I=6
  328 NEXT I
  330 PRINT AT(27,0);"DIESES MAL WILL RUMPI DEIN LEBEN   " :PAUSE 60
  332 CLS:PRINT "DU KANNST DICH ABER FREI KAUFEN,"      
  334 PRINT "INDEM DU RUMPI DEN STEIN DER  "          
  336 PRINT "WEISEN AUS DEM SCHLOSS DES "                   
  337 PRINT "KOENIGS HOLST.":PAUSE 65
  338 CLS:PRINT AT(29,0);"WILLST DU ?"             
  340 IF PEEK(4)=74 THEN 350    
  342 IF PEEK(4)=78 THEN 346:ELSE 340
  346 CLS:PRINT "SCHADE UM DICH UND DEIN JUNGES   LEBEN !!!!!!":PAUSE 35
  348 GOSUB 1012:GOTO 264
  350 WINDOW:CLS
  352 DATA "DU WIRST DICH JETZT DURCH DIE "      
  354 DATA "RAUME DES SCHLOSSES BEWEGEN  "
  356 DATA "UND DEN STEIN DER WEISEN SUCHEN "                       
  357 DATA "AUF DIE FRAGE WOHIN ?,GIB "
  358 DATA "JEWEILS N,O,S,W FUER DIE HIMMELS"     
  360 DATA "RICHTUNGEN ODER H,R FUER HOCH "                  
  361 DATA "UND RUNTER EIN. WUENSCHT"
  362 DATA "DU EINE INVENTUR, DRUECKE I."           
  364 RESTORE 352:FOR I=1 TO 8 :READ B$:PRINT B$:PRINT:NEXT I
  366 PRINT AT(18,0);" "
  368 GOSUB 986
  370 EV=2:GOTO 208
  372 GOSUB 1026
  374 RESTORE 812:FOR I=0 TO 40:READ Z$(I):NEXT
  376 ZN=9
  378 WINDOW 25,31,0,31:CLS
  380 DATA S,W,N,O,H,R
  382 RESTORE 380:FOR I=1 TO 6:READ RI$(I):NEXT
  384 PRINT AT(25,0);"WOHIN ?         "
  386 R$=INKEY$:IF LEN(R$)=0 THEN 386:ELSE 388            
  388 IF R$="I"THEN GOTO 654
  390 GOSUB 798
  392 IF RR=0 THEN PRINT AT(25,0);"NICHT MOEGLICH !":PAUSE 30:GOTO 384
  394 GOSUB 438:IF KK=1 THEN RR=0:GOTO 392
  396 GOSUB 802:IF RR=0 THEN PRINT AT(25,0);"NICHT MOEGLICH !":PAUSE 20:GOTO 384
  398 SC=SC+1
  400 IF ZN=34 THEN 424
  402 IF ZN=37 THEN 428
  404 GOTO 420
  406 GOSUB 990:IF MID$(Z$(ZN),3,2)<>"00"THEN GOSUB 1020
  408 IF MID$(Z$(ZN),5,2)<>"00"THEN GOSUB 1036
  410 IF MID$(Z$(ZN),7,2)<>"00"THEN GOSUB 1026
  412 IF MID$(Z$(ZN),9,2)<>"00"THEN GOSUB 1004
  414 IF MID$(Z$(ZN),11,2)<>"00"THEN GOSUB 1008
  416 IF MID$(Z$(ZN),13,1)="A" THEN GOSUB 1012
  418 RETURN
  420 GOSUB 690
  422 GOTO 384
  424 GOSUB 1292:PRINT AT(28,0);"DU BIST IN DEN SCHLOSSGRABEN    GEFALLEN"
  426 PRINT AT(30,0);"UND JAEMMERLICH ERSOFFEN !!!":PAUSE 90:GOTO 896
  428 PRINT AT(28,0);"IN DIESEM DUNKLEN KELLERLOCH "          
  429 PRINT AT(29,0);"WIMMELT ES VON RATTEN. DENEN"
  430 PRINT AT(30,0);"BIST DU LEIDER NICHT GEWACHSEN!" 
  432 POKE-5120,32
  434 FOR I=1 TO 75:X=-4600+200*RND(1):POKE X,201:PAUSE 1:POKE X,32:NEXT
  436 PAUSE 70:GOTO 896
  438 IF RIGHT$(Z$(ZN),1)=R$ THEN KK=1:GOTO 444
  440 KK=0:RETURN
  444 IF R$="R"THEN 450
  446 PRINT AT(28,0);"DIE TUER IST NOCH GESPERRT !":PAUSE 20
  448 PRINT AT(28,0);STRING$(30,CHR$(32)):RETURN
  450 PRINT AT(28,0);"OHNE LICHT KANNST DU NICHT IN":PRINT AT(29,0);"DEN KELLER"
  452 PAUSE 25:RETURN
  454 PRINT AT(27,0);"DAS IST RUMPELSTILZCHENS LAGER":K=0:WINDOW 25,31,0,31
  456 GOTO 1500
  458 IF KR(19)=1 THEN PRINT AT(28,0);"DU HAST SCHON EINMAL GEWAEHLT":GOTO 492
  460 PRINT AT(28,0);"DU DARFST DIR EINEN GEGENSTAND  WAELEN!"
  462 IF G(5)<>1 THEN G(5)=-1
  464 FOR I=1 TO 9:IF G(I)<>-1 THEN 476
  466 ON I GOSUB 932,938,944,950,956,962,968,974,980
  468 FOR J=1 TO 20:READ BF(J):NEXT J
  470 K=K+1:FOR J=0 TO 4:FOR JJ=0 TO 3
  472 POKE-4599+K*5+J*32+JJ,BF(J*4+JJ+1):NEXT JJ
  474 NEXT J
  476 NEXT I
  478 INPUT "DU WUENSCHST ?";W$
  480 PRINT AT(28,0);"D"
  482 FOR I=1 TO 9:IF G(I)<>-1 THEN 490
  484 IF(I=7 AND G(6)<>1)THEN 490
  486 OK$="OKAY                                  "
  488 IF W$=G$(I)THEN G(I)=1:I=9:CLS:PRINT AT(27,9);OK$:GOTO 492
  490 NEXT I:CLS:PRINT AT(27,0);"NICHT MOEGLICH !!!":PAUSE 30:CLS:GOTO 478            
  492 PAUSE 30:KR(19)=1
  494 CLS:RETURN
  496 IF KR(17)=1 THEN GOSUB 1338:GOSUB 1350:RETURN
  498 PRINT AT(27,0);"DAS UNGEHEUER HAT SCHON LANGE":PRINT AT(28,0);"GEWARTET !"
  500 WINDOW 25,31,0,31
  502 GOSUB 1280:!?AT(28,0);"D"
  504 IF G(2)=1 THEN 532
  506 IF G(4)=1 THEN 508:ELSE 510
  508 PRINT AT(29,0);"MIT DER TARNKAPPE BIST DU ABER":PRINT AT(30,0);"SICHER"   
  509 PAUSE 40:CLS:RETURN
  510 IF G(7)=1 THEN 512:ELSE 514
  512 PRINT AT(29,0);"GIB IHM DEN VOGEL ZUM FRESSEN!":G(7)=0:PAUSE 40:CLS:RETURN
  514 PAUSE 15:PRINT AT(29,0);"UND VERSPEISST DICH MIT VIEL     VERGNUEGEN"
  516 PAUSE 75:GOTO 896
  518 GOSUB 968:FOR I=1 TO 20:READ BF(I):NEXT I:IF G(7)=1 THEN 530
  520 FOR J=0 TO 4:FOR JJ=0 TO 3
  522 POKE-4753+J*32+JJ,BF(J*4+JJ+1):NEXT JJ:NEXT J
  524 IF G(6)=1 THEN 526:ELSE 528
  526 PRINT AT(29,0);"DER VOGEL GEHOERT DIR !!":PAUSE 35:G(7)=1:RETURN
  528 PRINT AT(30,0);"OHNE KAEFIG BEKOMMST DU IHN      NICHT !!"
  530 PAUSE 35:RETURN
  532 PAUSE 40:PRINT AT(25,0);"MIT DEM RING VERWANDELST DU ES"       
  533 PRINT AT(26,0);"IN EINE PRINZESSIN. ZUM DANK"
  534 PRINT AT(27,0);"ERHAELST DU DEN GOLDENEN "
  536 PRINT AT(28,0);"SCHLUESSEL UND EINEN KUSS !!":G(10)=1
  538 GOSUB 1338
  540 PAUSE 75:WINDOW 25,31,0,31:CLS
  542 KR(17)=1:GOTO 586
  544 FOR I=7 TO 10:PRINT AT(I,12);"":NEXT:RETURN
  546 DATA 4,8,3,2,6,1,5,9
  548 IF KR(22)=1 THEN RETURN
  550 RESTORE 546
  552 FOR I=1 TO 8:READ DD:IF G(DD)<>1 THEN I=8
  554 NEXT I
  556 ON DD GOSUB 932,938,944,950,956,962,968,974,980
  558 FOR J=1 TO 20:READ BF(J):NEXT
  560 FOR J=0 TO 4:FOR JJ=0 TO 3
  562 POKE-4690+J*32+JJ,BF(J*4+JJ+1):NEXT JJ:NEXT J
  564 PRINT AT(27,0);"IN DER ECKE LIEGT DIE           "+G$(DD)
  566 PRINT AT(29,0);"DU DARFST ZUGREIFEN !"
  568 G(DD)=1:KR(22)=1
  570 PAUSE 35:RETURN
  572 IF G(1)<>1 THEN PRINT AT(30,0);"DU ERHAELST DEN SPIEGEL !":G(1)=1:RETURN
  574 RETURN
  576 IF KR(31)=1 THEN RETURN
  578 IF G(9)=1 THEN RETURN
  580 PRINT AT(27,0);"UNTER DEM BETT LIEGT DER APFEL" 
  582 PRINT AT(28,0);"WELCHER JETZT DEIN IST !!"
  584 G(9)=1:RETURN
  586 KR(8)=1:KR(20)=1
  588 Z$(20)=LEFT$(Z$(20),12):Z$(8)=LEFT$(Z$(8),12)
  590 RETURN
  592 FOR I=1 TO 150:PRINT AT(13,20);"":PRINT AT(14,19);"   "
  594 PRINT AT(15,18);"  ":PRINT AT(16,18);"  "
  596 PRINT AT(17,18);" ",CHR$(191),"",CHR$(142)," "
  598 PRINT AT(18,19);"   ":PRINT AT(19,20);""
  600 PRINT AT(15,20);"":PRINT AT(16,20);" "
  602 PRINT AT(17,20);""
  604 PRINT AT(25,0);"GRATULATION !"
  606 PRINT AT(26,0);"DU HAST DEN 'STEIN DER WEISEN'   GEFUNDEN":NEXT I
  608 PAUSE 40:GOTO 1318
  610 IF G(1)=1 THEN PRINT AT(27,0);"DEN SPIEGEL GIBST DU FUERS LEBEN":ELSE 614    
  612 G(1)=0:RETURN
  614 PAUSE 40:PRINT AT(27,0);"OHNE SPIEGEL WIRST DU VON IHM IN"     
  616 PRINT AT(28,0);"EINE HAESSLICHE KROETE":PRINT AT(29,20);"VERWANDELT"
  618 FR=10:GOSUB 1364:PAUSE 95:GOTO 896
  620 IF G(9)<>1 THEN 630
  622 IF G(2)=1 THEN 1506
  624 PRINT AT(27,0);"VON DIESER RIESENSPINNE ERHAELST"      
  626 PRINT AT(28,0);"DU FUER DEINEN APFEL DEN"
  627 PRINT AT(29,0);"GOLDENEN RING !!!"
  628 G(2)=1:KR(2)=1:G(9)=-1:PAUSE 40:RETURN
  630 PRINT AT(27,0);"DU HATTEST KEINE CHANCE !              "
  632 PRINT AT(28,0);"DIESE RIESENSPINNE HAT DICH     VERNASCHT "
  634 PAUSE 85:GOTO 896
  636 IF G(10)=1 THEN 646
  638 PRINT AT(27,0);"DU HAST ZWAR EINEN,ABER NICHT "        
  640 PRINT AT(28,0);"DEN GOLDENEN SCHLUESSEL !!!"
  642 PRINT AT(29,0);"IN DEM NETZ VERFAENGST DU DICH"         
  644 PAUSE 85:GOTO 896
  646 IF KR(2)=0 THEN 662
  648 PRINT AT(26,0);"MIT DEM GOLDENEN SCHLUESSEL"
  650 PRINT AT(27,0);"WEICHT DAS MAGISCHE NETZ VON"
  651 PRINT AT(28,0);"GANZ ALLEINE !!!"
  652 PAUSE 40:RETURN
  654 YN=ZN:K=-3:EV=4:GOSUB 206
  656 PRINT AT(27,0);"DEIN BESITZ  !!"
  658 GOSUB 986:ZN=YN:GOTO 398
  660 PRINT AT(27,0);"DAS LOCH FUEHRT IN DEN ERSTEN   STOCK ":RETURN
  662 GOSUB 1196:PAUSE 20:GOTO 630
  664 PRINT AT(26,0);"IHRE NEUGIER TRIEB SIE IN DAS "        
  665 PRINT AT(27,0);"FUERCHTERLICHSTE KELLERVERLIES."
  666 GOSUB 1068
  668 PRINT AT(28,0);"HIER GIBT ES KEIN ENTKOMMEN !!"         
  670 PRINT AT(29,0);"SIE MUESSEN !!  ERSAUFEN !!"            
  672 PAUSE 50
  674 FOR I=9 TO 19:PRINT AT(I,18);"":PAUSE 1:NEXT:PAUSE 1
  676 FOR I=19 TO 9 STEP-1:PRINT AT(I,0);""        
  678 PAUSE 3:NEXT
  680 PAUSE 70:GOTO 896
  682 PRINT AT(7,7);"",CHR$(191),"",CHR$(142),"":FOR I=8 TO 11
  684 PRINT AT(I,7);"   ":NEXT:PRINT AT(12,7);"":RETURN
  686 FOR A=9 TO 19 STEP 2:PRINT AT(A,18);"":NEXT:FOR A=10 TO 18 STEP 2
  688 PRINT AT(A,18);" ":NEXT:RETURN
  690 IF ZN=19 THEN GOSUB 406:GOSUB 454:GOTO 794
  692 IF ZN=36 THEN GOSUB 406:GOSUB 1394:GOTO 664
  694 IF ZN=5 THEN GOSUB 406:GOSUB 1146:GOSUB 1406:GOTO 794
  696 IF ZN=33 THEN GOSUB 406:GOSUB 1074:GOTO 794
  698 IF ZN=35 THEN GOSUB 406:GOSUB 1068:GOSUB 1060:GOSUB 660:GOTO 794
  700 IF ZN=3 THEN GOSUB 406:GOSUB 1238:GOSUB 1356:GOSUB 1364:GOSUB 610:GOTO 794
  702 IF ZN=11 THEN GOSUB 406:GOSUB 1254:GOSUB 1370:GOTO 794
  704 IF ZN=31 THEN GOSUB 406:GOSUB 1220:GOSUB 576:GOTO 794
  706 IF ZN=28 THEN GOSUB 406:GOSUB 1272:GOSUB 544:GOTO 794
  708 IF ZN=2 THEN GOSUB 406:GOSUB 1196:GOSUB 620:GOTO 794
  710 IF(ZN=8 AND KR(8)=0)THEN GOSUB 406:GOSUB 1050:GOSUB 1432:GOTO 758
  712 IF(ZN=8 AND KR(8)<>0)THEN GOSUB 406:GOSUB 1432:GOTO 758
  714 IF ZN=22 THEN GOSUB 406:GOSUB 1060:GOSUB 682:GOSUB 546:GOTO 794
  716 IF ZN=32 THEN GOSUB 1086:GOTO 794
  718 IF ZN=27 THEN GOSUB 406:GOSUB 1128:GOSUB 572:GOTO 794
  720 IF ZN=10 THEN GOSUB 406:GOSUB 1118:GOTO 794
  722 IF ZN=29 THEN GOSUB 406:GOSUB 1184:GOSUB 636:GOTO 744
  724 IF ZN=38 THEN GOSUB 1086:GOTO 794
  726 IF ZN=9 THEN K=-3:EV=4:GOSUB 206:GOSUB 1026:GOTO 794
  728 IF ZN=17 THEN GOSUB 406:GOSUB 1068:GOSUB 496:GOTO 794
  730 IF G(3)=1 OR G(8)=1 THEN KR(13)=1:Z$(15)=LEFT$(Z$(15),13) 
  732 IF ZN=15 THEN GOSUB 1316
  734 IF ZN=15 AND KR(13)=0 THEN Z$(15)=Z$(15)+"R":GOSUB 406:GOTO 784
  736 IF ZN=12 THEN GOSUB 406:GOSUB 1158:GOSUB 518:GOTO 794
  738 IF ZN=13 THEN GOSUB 1154
  740 IF KR(13)=1 THEN Z$(13)=LEFT$(Z$(13),12)
  742 IF(ZN=13 AND KR(13)=0)THEN Z$(13)=Z$(13)+"R":GOSUB 406:GOTO 794
  744 GOSUB 406
  746 IF(ZN=22 OR ZN=25)THEN GOSUB 682
  748 IF(ZN=20 AND KR(20)=0)THEN Z$(20)=Z$(20)+"O"
  750 IF ZN=24 THEN GOSUB 686:GOSUB 1380:GOSUB 1420
  752 IF ZN=40 THEN GOSUB 1428
  754 IF G(5)=1 THEN KR(8)=1
  756 IF ZN=7 THEN PRINT AT(27,0);"DIES IST DER HINTERE KELLERGANG  "
  758 IF(ZN=8 AND KR(8)=0)THEN Z$(8)=Z$(8)+"N"
  760 IF ZN=4 THEN GOSUB 1046:GOSUB 1394
  762 IF ZN=39 THEN GOTO 592
  764 IF(ZN=0 OR ZN=1)THEN GOSUB 1048
  766 IF ZN=14 THEN GOSUB 1370:GOSUB 1380:GOSUB 1386
  768 IF ZN=24 THEN GOSUB 1180
  770 IF ZN=25 THEN GOSUB 1356:GOSUB 1388
  772 IF ZN=30 THEN GOSUB 1406
  774 IF ZN=23 THEN GOSUB 1216:GOSUB 1394
  776 IF ZN=7 THEN GOSUB 1380:GOSUB 1420
  778 IF ZN=21 THEN GOSUB 1082
  780 IF ZN=0 THEN 1356
  782 IF ZN=6 THEN GOSUB 1180
  784 IF ZN=15 THEN GOSUB 1406
  786 IF ZN=1 THEN GOSUB 1370
  788 IF(ZN=20 OR ZN=16)THEN GOSUB 1412
  790 IF ZN=13 THEN GOSUB 1154
  792 IF ZN=26 THEN GOSUB 1060:GOSUB 1390:POKE-4577,136:POKE-4458,145:POKE-4457,159
  794 RETURN
  796 GOTO 796
  798 FOR I=1 TO 6:IF R$=RI$(I)THEN RR=I:I=6
  800 NEXT I:RETURN
  802 RJ$=MID$(Z$(ZN),2*RR-1,2)
  804 IF VAL(RJ$)=0 THEN RR=0:RETURN
  806 ZN=ZN+VAL(RJ$)
  808 PRINT AT(25,9);"OKAY            ":PAUSE 5:RETURN
  810 GOTO 810
  812 DATA 000003010000
  814 DATA 00-103010000
  816 DATA 00-100000000
  818 DATA-30000010000
  820 DATA-3-100010900A
  822 DATA 000803000000
  824 DATA 000000010900A
  826 DATA 00-100010030
  828 DATA-3-128000000
  830 DATA 000000010000
  832 DATA 00-103010000
  834 DATA 00-100000000
  836 DATA 000000010000 
  838 DATA-3-1030100-9
  840 DATA 00-103200000
  842 DATA 0000000109-9A
  844 DATA 00-100010900A
  846 DATA-3-100000000
  848 DATA 0000030009-9A
  850 DATA 0000000000-9
  852 DATA 00-103090000
  854 DATA-31303010000
  856 DATA 00-100010000
  858 DATA-3-103000900A
  860 DATA-300000000-9
  862 DATA 0000000100-9
  864 DATA-3-102000000
  866 DATA 0000000600-9
  868 DATA-20300000000
  870 DATA 00-900010000
  872 DATA 00-109040800A
  874 DATA 000000-30000
  876 DATA 0000000000-9
  878 DATA 00-607020000
  880 DATA 000000000000
  882 DATA 00-2000000-9
  884 DATA 000000000000
  886 DATA 000000000000
  888 DATA 0000000000-8
  890 DATA-90000000000
  892 DATA-70000000000
  894 GOTO 894
  896 WINDOW:CLS
  898 AU$="":FOR I=2 TO 18:PRINT AT(I,15);AU$:NEXT:AU$=""
  900 FOR I=6 TO 8:PRINT AT(I,11);AU$:NEXT
  902 AU$=""          
  904 PRINT AT(18,0);AU$:PRINT AT(23,0);AU$
  906 FOR I=19 TO 22:PRINT AT(I,0);"":PRINT AT(I,30);"":NEXT
  908! PRINT(0,0);"SPIELZEIT":PRINTAT(2,3);PEEK(30),"MIN"
  910 PRINT AT(20,2);"HIER RUHT IN FRIEDEN " 
  912! PRINT(4,0);"SCHRITTE":PRINTAT(6,3);SC
  914 IF N$="UNBEKANNTER"THEN N$="EIN "+N$
  916 PRINT AT(21, 3);N$
  918 PAUSE 125:GOTO 10
  920 GOTO 920
  922 PRINT AT(14,6);" ":PRINT AT(14,24);"      "
  924 PRINT AT(15,6);"     ":PRINT AT(15,24);"    " 
  926 PRINT AT(16,6);"  ":PRINT AT(16,24);"     "
  928 PRINT AT(17,6);"":PRINT AT(17,24);"    "
  930 PRINT AT(18,6);"  ":PRINT AT(18,24);"    ":RETURN
  932 DATA 32,174,173,32,32,159,192,32,32,171,172,32,32,192,159,32
  934 DATA 32,192,159
  936 RESTORE 932:RETURN
  938 DATA 32,32,32,32,32,32,32,32,32,146,149,32,192,150,147,159,32
  940 DATA 150,147,32
  942 RESTORE 938:RETURN
  944 DATA 32,246,32,32,32,199,199,32,32,199,199,32,32,199,199,32
  946 DATA 32,199,199,32
  948 RESTORE 944:RETURN
  950 DATA 32,32,32,32,32,221,32,32,174,199,173,32,197,197,197,32
  952 DATA 197,197,197,32
  954 RESTORE 950:RETURN
  956 DATA 32,174,173,32,32,171,172,32,32,32,159,32,32,224,159,32
  958 DATA 32,248,159,32
  960 RESTORE 956:RETURN
  962 DATA 32,248,248,248,192,200,200,200,192,200,200,200,192,200,200,200
  964 DATA 192,200,200,200
  966 RESTORE 962:RETURN
  968 DATA 32,32,131,32,32,129,214,151,149,200,130,32,129,248,128,32
  970 DATA 32,136,32,32
  972 RESTORE 968:RETURN
  974 DATA 32,32,193,159,129,128,136,159,198,198,32,32,198,198,128,32
  976 DATA 198,198,32,32
  978 RESTORE 974:RETURN
  980 DATA 32,200,131,32,32,130,193,32,174,158,173,32,159,32,192,32
  982 DATA 171,248,172,32
  984 RESTORE 980:RETURN
  986 PRINT AT(28,06);"WEITER MIT ... ENTER"
  988 IF PEEK(0004)=13   THEN WINDOW : CLS  :RETURN:ELSE 986
  990 REM GRUNDRAUM:WINDOW:CLS
  991 WINDOW:CLS
  992 FOR A=0 TO 3:PRINT AT(A,A);"":NEXT:C=20:FOR A=0 TO 3:PRINT AT(C,A);"":C=C-1   
  993 NEXT
  994 C=30:FOR A=0 TO 3:PRINT AT(A,C);"":C=C-1:NEXT:C=17:FOR A=27 TO 30
  996 PRINT AT(C,A);""
  998 C=C+1:NEXT:FOR A=4 TO 26:PRINT AT(3,A);"":NEXT:FOR A=4 TO 16:PRINT AT(A,4);""
 1001 NEXT
 1002 FOR A=4 TO 16:PRINT AT(A,26);"":NEXT:FOR A=4 TO 26:PRINT AT(17,A);"":NEXT     
 1003 RETURN
 1004 REM LOCH IN DER DECKE
 1006 FOR A=15 TO 23:PRINT AT(0,A);"":NEXT:PRINT AT(1,15);"":RETURN
 1008 REM LOCH IM BODEN
 1010 FOR A=15 TO 23:PRINT AT(20,A);"":NEXT:PRINT AT(19,15);"":RETURN
 1012 REM LEITER
 1014 FOR A=1 TO 19 STEP 2:PRINT AT(A,18);"":NEXT:FOR A=2 TO 18 STEP 2
 1016 PRINT AT(A,18);" "
 1018 NEXT:RETURN
 1020 REM TUER LINKS
 1022 PRINT AT(5,0);"":PRINT AT(6,0);" ":PRINT AT(7,0);"":FOR A=8 TO 16:PRINT AT(A,0);" "
 1024 NEXT:PRINT AT(17,0);"":PRINT AT(18,0);"  ":PRINT AT(19,0);" ":RETURN
 1026 REM TUER RECHTS
 1028 PRINT AT(5,29);"":PRINT AT(6,28);" ":PRINT AT(7,28);"":FOR A=8 TO 16
 1030 PRINT AT(A,28);" "
 1032 NEXT:PRINT AT(17,28);"":PRINT AT(18,28);"  ":PRINT AT(19,29);" "
 1034 RETURN
 1036 REM TUER IM HINTERGRUND
 1038 FOR A=16 TO 22:PRINT AT(6,A);"":NEXT:PRINT AT(7,15);"     "
 1040 PRINT AT(8,15);"  "
 1042 FOR A=9 TO 15:PRINT AT(A,15);"     ":NEXT:PRINT AT(14,17);""
 1044 PRINT AT(16,15);"     ":FOR A=16 TO 22:PRINT AT(17,A);" ":NEXT:RETURN
 1046 PRINT AT(28,0);"DIES IST DER KELLERRAUM IM TURM " :RETURN
 1048 PRINT AT(28,0);"DU BIST IM VORDEREN KELLERGANG !":RETURN
 1050 IF KR(8)=1 THEN 1056:ELSE PRINT AT(5,1);"":PRINT AT(6,1);""
 1052 FOR A=7 TO 17:PRINT AT(A,1);"":NEXT:PRINT AT(18,1);"",CHR$(142)
 1054 PRINT AT(19,1);CHR$(142)
 1056 IF KR(8)=0 THEN FOR A=7 TO 16:PRINT AT(A,16);"":NEXT
 1058 PRINT AT(28,0);"DER HINTERE KELLERGANG !":RETURN
 1060 PRINT AT(13,15);"":PRINT AT(14,14);"           "
 1062 PRINT AT(15,14);"            ":PRINT AT(16,14);"  "
 1064 PRINT AT(17,15);"           ":PRINT AT(18,16);""
 1066 RETURN
 1068 PRINT AT(6,17);"":PRINT AT(7,17);"":PRINT AT(8,17);""
 1070 PRINT AT(9,17);CHR$(142),"",CHR$(191):PRINT AT(10,17);""
 1072 RETURN
 1074 PRINT AT(15,22);"":PRINT AT(16,12);"    "
 1076 PRINT AT(17,11);"          ":PRINT AT(18,11);""
 1078 PRINT AT(19,11);""
 1080 PRINT AT(28,0);"DAS TURMVERLIES !":RETURN
 1082 PRINT AT(28,0);"DIESER RAUM BEFINDET SICH IM "      
 1084 PRINT AT(29,0);"ERSTEN STOCKWERK DES TURMES !":RETURN
 1086 CLS
 1088 PRINT AT(5,6);"",CHR$(142),"",CHR$(191),"",CHR$(142),""
 1090 PRINT AT(5,20);CHR$(191)
 1092 PRINT AT(5,21);"",CHR$(142),"",CHR$(191),""
 1094 PRINT AT(6,5);"",CHR$(142),"      ",CHR$(191),""
 1096 PRINT AT(7,4);"",CHR$(142),"  ",CHR$(191),""
 1098 PRINT AT(8,4);"    ":FOR A=9 TO 13:FOR C=7 TO 31
 1100 PRINT AT(A,C);"":NEXT:NEXT:PRINT AT(9,4);"":PRINT AT(9,30);""
 1102 PRINT AT(10,1);"",CHR$(142),"":PRINT AT(10,27);"",CHR$(191),""
 1104 PRINT AT(11,0);"",CHR$(142),"":PRINT AT(11,27);"",CHR$(191),""
 1106 PRINT AT(12,0);"":PRINT AT(12,30);"":PRINT AT(13,0);"":PRINT AT(13,30);"
 1108 PRINT AT(14,0);"":PRINT AT(14,26);"":B=6:C=20:FOR A=14 TO 20:PRINT AT(A,B);"
 1110 PRINT AT(A,C);"":B=B-1:C=C+1:NEXT
 1112 GOSUB 1008:IF G(5)=1 THEN 1116:ELSE G(5)=1
 1114 PRINT AT(28,0);"HIER LIEGT EIN SCHLUESSEL FUER   DICH !"
 1116 PRINT AT(27,0);"DU BIST GANZ OBEN AUF DEM TURM !":GOSUB 586:RETURN
 1118 PRINT AT(16,13);"":PRINT AT(17,12);""
 1120 FOR A=12 TO 26:PRINT AT(18,A);"":NEXT:PRINT AT(19,13);""
 1122 PRINT AT(19,24);""
 1124 PRINT AT(20,13);"":PRINT AT(20,25);""
 1126 PRINT AT(28,0);"DER WARTESAAL !":RETURN
 1128 PRINT AT(7,15);" ":PRINT AT(8,15);""
 1130 PRINT AT(9,15);CHR$(191),"",CHR$(142):PRINT AT(10,16);""
 1132 PRINT AT(11,16);"":PRINT AT(12,14);""
 1134 PRINT AT(13,14);"":PRINT AT(13,15);"":PRINT AT(13,24);""
 1136 PRINT AT(14,15);"":PRINT AT(15,15);""
 1138 PRINT AT(16,14);"":PRINT AT(17,13);""
 1140 PRINT AT(18,12);""
 1142 PRINT AT(19,12);""
 1144 PRINT AT(28,0);"DIES IST DER TRONSAAL !":RETURN
 1146 PRINT AT(14,1);"":PRINT AT(15,1);CHR$(142),""
 1148 PRINT AT(16,1);"",CHR$(142)
 1150 PRINT AT(17,1);CHR$(142),"",CHR$(142),"":PRINT AT(18,1);"",CHR$(142),"
 1152 PRINT AT(19,1);"":PRINT AT(28,0);"MITTLERER FLUR IM KELLER !":RETURN
 1154 PRINT AT(28,0);"DIES IST EIN DURCHGANGSZIMMER !":RETURN
 1156 PRINT AT(28,0);"HINTERER FLUR IM ERDGESCHOSS !":RETURN
 1158 PRINT AT(10,1);"",CHR$(142),"":PRINT AT(11,0);"",CHR$(142),""
 1160 PRINT AT(12,0);"":PRINT AT(13,0);"":PRINT AT(14,0);""
 1162 PRINT AT(15,0);""
 1164 PRINT AT(16,0);"":PRINT AT(17,0);" ":PRINT AT(18,0);""
 1166 PRINT AT(19,0);" "
 1168 PRINT AT(20,0);"":FOR A=4 TO 29:PRINT AT(14,A);"":NEXT:FOR A=4 TO 30
 1170 PRINT AT(15,A);"":NEXT:PRINT AT(14,30);"":PRINT AT(15,31);"":FOR A=4 TO 31
 1172 PRINT AT(16,A);"":NEXT:PRINT AT(17,7);"":PRINT AT(17,27);""
 1174 PRINT AT(18,7);""
 1176 PRINT AT(18,27);"":PRINT AT(17,8);"":PRINT AT(17,26);""
 1178 PRINT AT(27,0);"IN DER KUECHE IST DER VOGEL !":RETURN
 1180 PRINT AT(29,0);"DIE LEITER FUEHRT IN DAS ERD-   GESCHOSS  !":RETURN
 1182 PRINT AT(29,0);"DAS LOCH FUEHRT IN DEN KELLER !":RETURN
 1184 C=30:FOR A=0 TO 20:PRINT AT(A,C);"":C=C-1:NEXT:C=30:FOR A=4 TO 20
 1186 PRINT AT(A,C);""
 1188 C=C-1:NEXT:C=30:FOR A=8 TO 20:PRINT AT(A,C);"":C=C-1:NEXT:C=30:FOR A=12 TO 20
 1190 PRINT AT(A,C);"":C=C-1:NEXT:C=30:FOR A=16 TO 20:PRINT AT(A,C);"":C=C-1:NEXT
 1192 PRINT AT(20,30);"":C=20:FOR A=10 TO 19:PRINT AT(A,C);"":C=C+1:NEXT
 1194 RETURN
 1196 FOR A=0 TO 5:PRINT AT(A,14);"":NEXT:PRINT AT(5,5);"":PRINT AT(5,21);""
 1198 PRINT AT(6,4);"        ":PRINT AT(7,3);"    
 1200 PRINT AT(8,5);"    ":PRINT AT(9,4);"    "
 1202 PRINT AT(10,6);"  ",CHR$(191),"",CHR$(142),"  "
 1204 PRINT AT(11,5);"    ",CHR$(191),"",CHR$(142),"    "
 1206 PRINT AT(12,11);"":PRINT AT(13,11);CHR$(191),"",CHR$(142)
 1208 PRINT AT(14,12);"",CHR$(142),CHR$(191),"":PRINT AT(15,12);"  "
 1210 PRINT AT(27,0);"DU BIST INS NETZ EINER RIESIGEN "      
 1212 PRINT AT(28,0);"SPINNE GERATEN !":GOSUB 1184:PAUSE 20:RETURN
 1214 PRINT AT(28,0);"DER VORDERE FLUR IM ERSTEN STOCK  ":RETURN
 1216 PRINT AT(28,0);"DIES IST DER MITTLERE FLUR !":RETURN
 1218 PRINT AT(28,0);"DU BIST IM HINTEREN FLUR !":RETURN
 1220 PRINT AT(9,15);"":PRINT AT(10,14);""
 1222 PRINT AT(11,13);"":PRINT AT(12,13);"
 1224 PRINT AT(13,13);"          ":PRINT AT(14,13);"          
 1226 PRINT AT(15,13);"            ":PRINT AT(15,17);""
 1228 PRINT AT(16,13);"            ":PRINT AT(16,17);""
 1230 PRINT AT(17,13);"            ":PRINT AT(17,16);""
 1232 PRINT AT(18,13);"            ":PRINT AT(18,15);""
 1234 PRINT AT(19,13);"            ":PRINT AT(19,14);""
 1236 PRINT AT(20,13);"            ":RETURN
 1238 PRINT AT(7,23);"":PRINT AT(8,26);"":PRINT AT(9,22);""
 1240 PRINT AT(9,27);""
 1242 PRINT AT(10,22);" ":PRINT AT(11,23);" ":PRINT AT(12,21);""
 1244 PRINT AT(13,20);" ":PRINT AT(14,20);" ":PRINT AT(15,21);""
 1246 PRINT AT(16,21);"  ":PRINT AT(17,21);"  ":PRINT AT(18,21);"  "
 1248 PRINT AT(19,20);"    "
 1250 PRINT AT(27,0);"DU STEHST VOR EINEM ZAUBERER !"
 1252 RETURN
 1254 PRINT AT(6,19);"":PRINT AT(7,19);" ":PRINT AT(8,19);" "
 1256 PRINT AT(9,19);""
 1258 PRINT AT(10,19);"":PRINT AT(11,20);"":PRINT AT(12,6);""
 1260 PRINT AT(12,19);""
 1262 PRINT AT(13,6);"":PRINT AT(13,18);""
 1264 PRINT AT(14,6);CHR$(191),"",CHR$(142)
 1266 PRINT AT(14,18);"":PRINT AT(15,6);"":PRINT AT(15,18);""
 1268 PRINT AT(16,6);"":PRINT AT(17,6);""
 1270 PRINT AT(28,0);"DAS WC !":RETURN
 1272 PRINT AT(16,14);"":PRINT AT(17,13);""
 1274 PRINT AT(18,12);"  ":PRINT AT(19,12);""
 1276 PRINT AT(20,12);""
 1278 PRINT AT(28,0);"DU BIST IM BADERAUM !":RETURN
 1280 PRINT AT(9,22);"    ":PRINT AT(10,23);"":PRINT AT(11,22);""
 1282 PRINT AT(12,23);"":PRINT AT(13,22);""
 1284 PRINT AT(14,21);"",CHR$(142),"",CHR$(191),""
 1286 PRINT AT(15,22);"":PRINT AT(16,22);"",CHR$(191),"",CHR$(142),""
 1288 PRINT AT(17,22);"    ":PRINT AT(18,21);"",CHR$(142)
 1290 PRINT AT(18,27);CHR$(191),"":RETURN
 1292 CLS
 1294 PRINT "":PRINT "    ":PRINT "    ":PRINT ""
 1296 PRINT "    ":PRINT "    ":PRINT "":PRINT ""
 1298 PRINT "":PRINT "":PRINT ""
 1300 PRINT "":PRINT " '       "
 1302 PRINT "      ''   ":PRINT "  '         "
 1304 PRINT "      '    "
 1306 PRINT "      '            " 
 1308 PRINT "'                  '" 
 1310 PRINT "                       "
 1312 PRINT AT(19,1);" "
 1314 RETURN
 1316 PRINT AT(28,0);"DU BEFINDEST DICH IM TURM!            ":RETURN
 1318 WINDOW:CLS
 1320 DATA "MIT VIEL MUEHE HAST DU DEIN ZIEL"
 1322 DATA "ERREICHT UND UEBERGIBST DEN "                 
 1323 DATA "'STEIN DER WEISEN' AN RUMPEL-"
 1324 DATA "STIELZCHEN UND ERHAELTST ALS "      
 1326 DATA "DANK DAS VERWANDELTE UNGEHEUER,","D.H. ALSO DIE PRINZESSIN,"
 1328 DATA ZUR FRAU.," ",WERDE MIT IHR GLUECKLICH
 1330 DATA DU HILFSBEREITER HELD.
 1332 RESTORE 1320:FOR I=1 TO 10:READ B$:PRINT B$:PRINT:NEXT I
 1334 PRINT AT(20,0);" ":PRINT AT(23,0);"SCHRITTE",SC
 1336!RINTAT(21,0);"ZEIT    ",PEEK(30),"MIN":PAUSE160:GOTO10
 1338   PRINT AT(17,25);"  ":PRINT AT(18,25);"":PRINT AT(19,25);"  
 1340 PRINT AT(20,25);"    ":PRINT AT(21,25);"  "
 1342 PRINT AT(22,24);"      ":PRINT AT(23,25);"  "      
 1344 PRINT AT(23,25);"    ":PRINT AT(24,25);""
 1346 PRINT AT(25,24);"     ":PRINT AT(26,26);"  "  
 1348 RETURN
 1350 PRINT AT(28,0);"DAS IST DIE VON DIR ERLOESTE "        
 1352 PRINT AT(29,0);"HUEBSCHE PRINZESSIN !!!"
 1354 RETURN
 1356 PRINT AT(13,4);"":PRINT AT(14,3);"          "
 1358 PRINT AT(15,2);"          ":PRINT AT(16,2);""
 1360 PRINT AT(17,2);"":PRINT AT(18,4);"   "
 1362 PRINT AT(19,4);"   ":RETURN
 1364 IF FR=0 THEN FR=5
 1366 PRINT AT(12,FR);"":PRINT AT(13,FR);"  ":PRINT AT(14,FR-1);""
 1368 PRINT AT(15,FR-1);"":RETURN
 1370 PRINT AT(6,27);"":PRINT AT(7,27);"":PRINT AT(8,28);""
 1372 PRINT AT(9,26);" ":PRINT AT(10,26);"":PRINT AT(11,27);""
 1374 PRINT AT(12,26);" ":PRINT AT(13,26);"":PRINT AT(14,27);""
 1376 PRINT AT(15,26);"":PRINT AT(16,27);"":PRINT AT(17,27);""
 1378 RETURN
 1380 PRINT AT(9,7);"":FOR I=10 TO 12:PRINT AT(I,7);"":NEXT
 1382 PRINT AT(13,7);"":PRINT AT(14,6);""
 1384 FOR I=15 TO 17:PRINT AT(I,7);"":PRINT AT(I,10);"":NEXT:RETURN
 1386 PRINT AT(28,0);"DU BIST IM MITTLEREN FLUR.":RETURN
 1388 PRINT AT(28,0);"DAS LOCH FUEHRT IN DAS ERDGE-    SCHOSS !":RETURN
 1390 PRINT AT(10,26);"  ":PRINT AT(11,26);" ":PRINT AT(12,26);" "
 1392 PRINT AT(13,26);" ":PRINT AT(14,27);"":RETURN
 1394 PRINT AT(6,25);"":PRINT AT(7,24);"     ":PRINT AT(8,24);"   "
 1396 PRINT AT(9,24);"    ":PRINT AT(10,24);"   "
 1398 PRINT AT(11,24);"     "
 1400 PRINT AT(12,24);"":PRINT AT(13,25);"":PRINT AT(14,25);"
 1402 PRINT AT(15,25);"":PRINT AT(16,25);"":PRINT AT(17,25);"    "
 1404 RETURN
 1406 PRINT AT(9,8);"":PRINT AT(10,7);" ":PRINT AT(11,5);""
 1408 PRINT AT(12,5);"":PRINT AT(13,6);"":PRINT AT(14,6);""
 1410 FOR I=15 TO 17:PRINT AT(I,5);"":NEXT:RETURN
 1412 PRINT AT(11,4);"":PRINT AT(12,3);"      "
 1414 PRINT AT(13,3);" ":PRINT AT(14,3);" "
 1416 PRINT AT(15,3);" ":PRINT AT(16,3);" "
 1418 PRINT AT(17,3);"":RETURN
 1420 PRINT AT(5,15);"":PRINT AT(6,15);"    "
 1422 PRINT AT(7,15);"    ":PRINT AT(8,15);"    "
 1424 PRINT AT(9,15);CHR$(142),"",CHR$(191):PRINT AT(10,15);""
 1426 RETURN
 1428 GOSUB 1420:GOSUB 1412:GOSUB 1394
 1430 PRINT AT(28,0);"EIN ABSTELLRAUM !":RETURN
 1432 PRINT AT(27,0);"KOBOLDE HABEN DICH UEBERFALLEN UND"
 1434 GOSUB 1446
 1436 DI=INT(10*RND(1)):IF G(DI)<>1 THEN 1436
 1438 PRINT AT(28,2);"HABEN DICH BESTOHLEN. VERLUST:  "+G$(DI)
 1440 G(DI)=-1:KR(19)=0:PAUSE 30
 1442 L$=STRING$(32,CHR$(32))
 1444 PRINT AT(28,0);L$:PRINT AT(29,0);L$:PRINT AT(30,0);L$:RETURN
 1446 ZU=0:FOR I=1 TO 9:IF G(I)=1 THEN ZU=1
 1448 NEXT I:IF ZU=1 THEN RETURN
 1450 PRINT AT(29,0);"DIR DEIN LEBEN GERAUBT !!"
 1452 PAUSE 75:GOTO 896
 1454 PRINT AT(7,26);"":PRINT AT(8,24);" ":PRINT AT(9,23);"  "
 1456 PRINT AT(28,2);"WEITER MIT ... ENTER"
 1458 PRINT AT(10,22);"  ":PRINT AT(11,22);"":PRINT AT(12,23);""
 1460 FOR I=13 TO 16:PRINT AT(I,22);"":NEXT
 1462 PRINT AT(17,22);CHR$(191),"",CHR$(142)
 1464 PRINT AT(11,20);"":PRINT AT(12,20);"":PRINT AT(13,20);CHR$(191),""
 1466 PRINT AT(14,20);" "    
 1468 PRINT AT(14,21);CHR$(191):PRINT AT(15,20);"  ":PRINT AT(16,20);" "
 1470 PRINT AT(17,20);" ":PRINT AT(18,21);"  ":PRINT AT(19,21);"    "
 1472 PRINT AT(20,20);"",CHR$(142),"     ":PRINT AT(21,23);" "
 1474 PRINT AT(22,22);"  ",CHR$(191),"":PRINT AT(14,26);""
 1476 PRINT AT(11,27);" ":PRINT AT(12,27);" ":PRINT AT(13,26);"  "
 1478 PRINT AT(15,27);"":PRINT AT(16,27);"":PRINT AT(17,27);""
 1480 IF PEEK(004)=13 THEN WINDOW:CLS:RETURN
 1482 PRINT AT(11,20);" ":PRINT AT(12,20);" ":PRINT AT(13,20);"  "
 1484 PRINT AT(14,20);"":PRINT AT(15,20);"":PRINT AT(16,20);""
 1486 PRINT AT(17,20);"":PRINT AT(11,27);"":PRINT AT(12,27);""
 1488 PRINT AT(13,26);"",CHR$(142):PRINT AT(14,26);CHR$(142)," "
 1490 FOR I=15 TO 17:PRINT AT(I,27);" ":NEXT
 1492 PRINT AT(18,21);"  ":PRINT AT(19,21);"    "
 1494 PRINT AT(20,20);"     ",CHR$(191),"":PRINT AT(21,23);" " 
 1496 PRINT AT(22,22);"",CHR$(142),"  "        
 1498 IF PEEK(0004)=13  THEN WINDOW: CLS  :RETURN:ELSE 1464
 1500 ZU=0:FOR I=1 TO 9:IF G(I)=-1 THEN ZU=ZU+1
 1502 NEXT:IF ZU>1 THEN 458
 1504 PRINT AT(29,0);"WIE DU SIEHST IST ES LEER !!":RETURN
 1506 PRINT AT(28,0);"DIE SPINNE FRISST DEINEN APFEL !       "
 1508 PRINT AT(29,0);"                                       ":GOTO 628
 1510!************************
 1512!*
 1514!* ENDE
 1516!*
 1518!************************
