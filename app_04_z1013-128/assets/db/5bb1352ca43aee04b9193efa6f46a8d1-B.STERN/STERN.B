Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

    2 REM ERSTELLT VON ANDREAS
    4 REM SCHROEDER/BERTSDORF
    6 REM HAUPTSTR.53
    8 REM **************************
   10 DATA 46,3,22,25,36,1,62,3,211,8,219,2,230,15,254,5,202,56,0,75,238
   12 DATA 128,211,2,66,16,254,238,128,211,2,66,16,254,13,32,239,21,28
   14 DATA 45,32,233,46,48,75,238,128,211,66,16,254,13,201
   16 FOR I=1 TO 53:READ A:POKE 16128+I-1,A:NEXT I
   18 WINDOW:CLS:PRINT:PRINT TAB(3);"* * * > S T E R N < * * *"
   20 PRINT AT(5,4);"ERLAEUTERUNGEN (J/N)?"
   21 POKE(DEEK(43)),32
   22 IF INKEY$<>"J"AND INKEY$<>"N"THEN 22
   24 IF INKEY$="N"THEN GOTO 58
   26 CLS
   28 PRINT:PRINT " * * * > SPIELREGELN < * * *":PRINT:PRINT:PRINT
   30 PRINT "SIE HABEN DIE AUFGABE,IHR RAUM-" :PRINT
   32 PRINT "SCHIFF SICHER DURCH DAS":PRINT
   34 PRINT "SONNENSYSTEM ZU STEUREN UND VOR-"
   36 PRINT "HANDENE METEORITEN ZU ZERSTOEREN"
   38 PRINT STRING$(31,"-"):PRINT
   40 PRINT " METEORITEN  =  .":PRINT
   42 PRINT " STATIONEN   =  ";CHR$(20)"
   44 PRINT:PRINT STRING$(31,"-")
   46 PRINT "BEI BESCHAEDIGUNG VON WELTRAUM-":PRINT
   48 PRINT "STATIONEN ERFOLGT EIN PUNKTAB-"
   50 PRINT "ZUG!"
   52 PRINT:PRINT "Bei Kollision=0 gibt es Zusatz  punkte"
   54 PRINT:PRINT "BEI ZEIT=300 WIRD DAS SPIEL BE- ENDET"
   56 PRINT AT(31,25);">ENTER<":INPUT "";IN$
   58 CLEAR 50:WINDOW:CLS
   60 FOR I=1 TO 10:POKE 16129,13-I:CALL 16128:NEXT I:POKE 16131,25
   62 IF RE>=6060 THEN PRINT "MAX.PUNKTZAHL ERREICHT !!!!":RE=0:PAUSE 50:GOTO 58
   64 WINDOW:CLS:PRINT AT(0,0);" ":ZE=0
   66 Q=0
   68 R=0:FOR I=1 TO 50
   70 X=RND(1)*25+2
   72 Y=RND(1)*28+1
   74 PRINT AT(X,Y);CHR$(20):NEXT I
   76 FOR J=1 TO 77
   78 X=RND(1)*27+2
   80 Y=RND(1)*30+1
   82 PRINT AT(X,Y);CHR$(213):NEXT J
   84 PRINT AT(1,0);STRING$(32,"")
   86 PRINT AT(28,0);STRING$(32,"")
   88 X=-4655:Z=0:PRINT AT(0,1);"ZEIT:"
   90 PRINT AT(30,4);".KOLLISION":PRINT AT(30,22);".METEORIT"
   92 PRINT AT(31,0);"PUNKTE:":PRINT AT(31,13);"(",RE,")"
   94 PRINT AT(31,7);PU
   96 FOR I=1 TO 10:POKE 16131,25+I:CALL 16128:NEXT I
   98 GOTO 104
  100 IF PEEK(Y)=248 THEN Y=X
  102 RETURN
  104 P=21:POKE X,P
  106 ZE=ZE+1:PRINT AT(0,6);ZE
  108 T$=INKEY$
  110 IF T$="U"THEN Z=-32
  112 IF T$=CHR$(32)THEN Z=32
  114 IF T$=CHR$(8)THEN Z=-1
  116 IF T$=CHR$(9)THEN Z=1
  118 Y=X+Z
  120 GOSUB 100
  122 PU=Q*100-(R*100)
  124 PRINT AT(31,7);PU
  126 IF ZE>=300 THEN Z=0:FOR I=1 TO 10:CALL 16128:NEXT I:PAUSE 10:CLS:GOTO 158
  128 IF PEEK(Y)=20 THEN R=R+1
  130 PRINT AT(30,1);R
  132 IF PEEK(Y)=20 THEN POKE Y,P:GOTO 144
  134 IF PEEK(Y)=213 THEN Q=Q+1:BEEP:PRINT AT(30,18);Q
  136 POKE Y,P:POKE X,32
  138 X=Y
  140 GOTO 104
  142 END
  144 S=X:J=32
  146 FOR I=1 TO 3:CALL 16128:NEXT I
  148 L=S+J:IF PEEK(L)=248 THEN L=S:PRINT AT(27,0);STRING$(31," "):GOTO 134
  150 H=32:IF PEEK(L)=213 THEN H=213
  152 IF PEEK(L)=20 THEN R=R+1:FOR I=1 TO 3:CALL 16128:NEXT I
  154 POKE L,20:POKE S,H:S=L
  156 GOTO 148
  158 REM AUSWERTUNG
  160 IF R=0 AND PU>=6000 AND ZE<300 THEN FOR I=1 TO 3:CALL 16128:NEXT I:GOTO 198
  162 PRINT:PRINT
  164 PRINT TAB(5);"A U S W E R T U N G:":PRINT TAB(5);STRING$(20,"-"):PRINT
  166 PRINT:PRINT
  168 IF R=0 THEN FOR I=1 TO 3:POKE 16129,3:CALL 16128:NEXT I
  170 PRINT:POKE 16129,16:FOR I=1 TO 2:CALL 16128:NEXT I
  172 IF R=0 THEN PU=PU+Q
  174 PRINT:PRINT:PRINT "KOLLISION ->:",R:PRINT:PRINT "METEORITEN->:",Q
  176 IF R=0 AND ZE<300 THEN PU=PU+500
  178 PRINT:PRINT "ZEIT      ->:";ZE
  180 PRINT:PRINT "PUNKTE    ->:",PU
  182 WINDOW 27,31,0,31
  184 PRINT TAB(4);"SPIEL BEENDEN?(J/N)?":PRINT
  186 PRINT AT(29,0);" "
  188 IF PU>RE THEN RE=PU
  190 PU=0:Q=0
  192 IF INKEY$<>"J"AND INKEY$<>"N"THEN 192
  194 IF INKEY$="J"THEN CLS:PRINT "AUF WIEDERSEHEN!":PAUSE 50:WINDOW:CLS:END
  196 POKE 16129,3:POKE 16131,25:GOTO 62
  198 WINDOW:CLS:REM WUETEND
  200 WINDOW 5,12,8,17
  202 PRINT TAB(3);CHR$(155);CHR$(161);CHR$(153)
  204 PRINT TAB(2);CHR$(174);CHR$(158);CHR$(158);CHR$(158);CHR$(173)
  206 PRINT TAB(2);CHR$(159);CHR$(140);" ";CHR$(140);CHR$(192)
  208 PRINT TAB(1);CHR$(129);CHR$(159);" ";CHR$(157);" ";CHR$(192);CHR$(128)
  210 PRINT TAB(2);CHR$(159);" ";CHR$(158);" ";CHR$(192)
  212 PRINT TAB(2);CHR$(171);" ";CHR$(160);" ";CHR$(172) 
  214 PRINT TAB(3);CHR$(158);CHR$(158);CHR$(158)
  216 PAUSE 10
  218 WINDOW 15,31,0,31
  220 PRINT
  222 RESTORE 234
  224 FOR I=1 TO 4:READ X$:PRINT X$
  226 FOR J=1 TO 2:BEEP:PRINT AT(10,12);CHR$(140):BEEP:PAUSE 2
  228 BEEP:PRINT AT(10,12);CHR$(160):BEEP:PAUSE 2
  230 NEXT J,I
  232 PAUSE 30:PRINT AT(7,11);CHR$(160):PAUSE 5:PRINT AT(7,11);CHR$(140)
  234 DATA "WENN DU SO WEITER MACHST,":PRINT
  236 DATA "FLIEGEN MIR MEINE":PRINT
  238 DATA "CHIPS UM DIE OHREN !!!":PRINT
  240 DATA "ALSO BREMSE DICH !!"
  242 PAUSE 30:CLS:GOTO 168
