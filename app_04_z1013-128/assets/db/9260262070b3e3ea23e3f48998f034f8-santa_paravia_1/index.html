<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml"  xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_01.png" alt="Screenshot 1" height="256" width="256" />
<img src="screenshot_02.png" alt="Screenshot 2" height="256" width="256" />
<img src="screenshot_03.png" alt="Screenshot 3" height="256" width="256" />
<img src="screenshot_04.png" alt="Screenshot 4" height="256" width="256" />
</div>
<div class="filelist">2bc0 5987 0000 B ... <a href="santa_paravia_1.z80">Santa&nbsp;Paravia*1*</a></div>
<pre><pre>10!*************************
20!
30!UMGESCHRIEBEN FUER 
40!
50!Z 1013 am 30.APRIL 1990
60!
70!VON ANDREAS ZIERMANN
80!
90!4372 AKEN
100!
110!DESSAUER-LANDSTR.25/2
120!
130!************************
140 JA=1400:WINDOW:CLS:A$=STRING$(128,""):FOR I=0 TO 7:PRINT AT(I*4,0);A$:NEXT
150 FOR M=0 TO 7:READ TB$(M):NEXT
160 DATA SIR,BARON,COUNT,MARQUIS,DUKE,GRAND DUKE,PRINCE,KING
170 DATA SANTA PARAVIA,FIBONACCIO,TORRICELLA,MOLINETTO,FONTA,MARINETTO
180 WINDOW 1,5,1,30:CLS:POKE-5120,199:WINDOW 2,5,2,29:PRINT "  WIEVIELE PERSONEN";
190 PRINT " WOLLEN":PRINT:PRINT "     SPIELEN(1-6) ?":WINDOW 4,4,23,26
200 GOSUB 4000:SA=VAL(A$):IF SA&lt;1 OR SA&gt;6 THEN 200
210 WINDOW 7,15,1,30:CLS:WINDOW 8,15,2,29
220 FOR SP=1 TO SA:READ T$:CLS
230 PRINT "WER IST DER BEHERRSCHER VON"
240 PRINT T$:INPUT "&gt;";N$:PRINT:N$=LEFT$(N$,8)
250 PRINT "WIE SOLL ' "T$ " '":PRINT "VON JETZT AN HEISSEN ?":U$="":INPUT "&gt;";U$
260 U$=LEFT$(U$,8)
270 N$(SP)=" "+N$+" OF "+U$:IF U$=""THEN N$(SP)=N$(SP)+T$
280 S1(SP)=25:S2(SP)=10:S3(SP)=5:JU(SP)=2
290 GD(SP)=1000:LA(SP)=10000:KO(SP)=5000
300 LS(SP)=100:VO(SP)=2000
310 AD(SP)=4:SO(SP)=30:GE(SP)=5:KA(SP)=25
320 NEXT
330 WINDOW 17,30,1,30:CLS:WINDOW 18,30,2,29:PRINT "SPIELSTAERKE :":PRINT "============"
340 PRINT:PRINT "1.ANFAENGER     3.MEISTER"
350 PRINT:PRINT "2.MIT ERFAHRUNG 4.KOENNER":PRINT:PRINT "5.-8.WAHNSINN":PRINT:PRINT
360 PRINT "SPIELSTAERKE :";:WINDOW 28,28,18,20
370 GOSUB 4000:SS=VAL(A$):IF SS=0 OR A$=""OR SS&gt;8 THEN 370
380 SS=SS+4:TJ(0)=1:N$(0)=" PEPPONE OF MONTERANE"
390 FOR SP=1 TO SA:SU(SP)=INT(1413+SS*1.5+RND(1)*26+RND(1)*10):NEXT
400 FOR SP=1 TO SA:IF SU(SP)&lt;JA THEN 480
410 IF SU(SP)=JA THEN 490
420 IF VO(SP)&lt;200 THEN SU(SP)=JA:GOTO 490
430 WINDOW 1,30,1,30:CLS:GOSUB 910
440 GOSUB 1310
450 GOSUB 1870
460 GOSUB 2430
470 GOSUB 3030
480 NEXT:JA=JA+1:GOTO 400
490 WINDOW:CLS:PRINT:IF VO(SP)&lt;200 THEN 500
500 PRINT " HURRA!!";TB$(TJ(SP));N$(SP)
510 PRINT
520 PRINT " WURDE ERMORDET":GOTO 550
530 PRINT " WIR TRAUERN UM";TB$(TJ(SP));NA$(SP)
540 PRINT
550 PRINT " GEB.1381,GEST.";JA
560 H$=" SIE":IF TJ(SP)&lt;8 THEN H$=" ER"
570 PRINT
580 PRINT H$;" STARB
590 PRINT
600 IF VO(SP)&gt;200 THEN 640
610 PRINT " NACH LANGJAEHRIGER TYRANNEI"
620 PRINT
630 PRINT " DURCH EIN ATTENTAT!":GOTO 780
640 IF JA&lt;1449 THEN 680
650 PRINT " IN HOHEM ALTER NACH EINER"
660 PRINT
670 PRINT " LANGEN HERRSCHAFT":GOTO 780
680 ON(RND(1)*5)GOTO 720,730,740,770,780
690 PRINT " AN EINER GRIPPE,WAEHREND EINES"
700 PRINT
710 PRINT " EISIGEN WINTERS":GOTO 780
720 PRINT " WAEHREND EINER POCKENEPEDEMIE.":GOTO 780
730 PRINT " DURCH VERSEUCHTES WASSER        AN TYPHUS.":GOTO 780
740 PRINT " NACH EINEM RAUBUEBERFALL        WAEHREND"
750 PRINT
760 PRINT " EINER REISE.":GOTO 780
770 PRINT " DURCH VERGIFTETES FLEISCH."
780 INPUT Q$:IF Q$=""THEN 790:GOTO 780
790 GOSUB 3320
800 VS=VS+1:IF VS&lt;SA THEN 480
810 PRINT "_";" DAS SPIEL IST LEIDER ZU ENDE,   DA ALLE"
820 PRINT " "
830 PRINT " TEILNEHMER DER TOD EREILT HAT."
840 PRINT " "
850 PRINT " "
860 PRINT " ICH HABE EUCH ALLE UEBERLEBT."
870 PRINT " "
880 PRINT " "
890 PRINT TB$(1);N$(0)
900 INPUT Q$:IF Q$&lt;&gt;""THEN GOSUB 3320:END:GOTO 900
910 Z=RND(1)*30:KO(SP)=INT(KO(SP)*(100-Z)/100):RV=INT(Z)
920 WE=(INT(RND(1)*5)+INT(RND(1)*6)+2)/2
930 H=LA(SP):M=(VO(SP)-100*MU(SP))*5:IF M&lt;0 THEN M=0
940 IF M&lt;H THEN H=M
950 M=KO(SP):IF M&lt;H THEN H=M
960 ER=H*(WE-.5):KO(SP)=KO(SP)+ER
970 GOSUB 3200:FL=0
980 IF ER&lt;1 THEN Z=2:GOTO 1010
990 Z=VE*1/ER:IF Z&gt;2 THEN Z=2
1000 IF Z&lt;.8 THEN Z=.8
1010 LP=INT((3*WE+INT(RND(1)*6)+INT(RND(1)*6)+12)*Z)
1020 KP=INT((20-WE*3+INT(RND(1)*5)+INT(RND(1)*5))*4*Z)
1030 WINDOW 2,30,2,29:PRINT "MAN SCHREIBT DAS JAHR";JA;",";
1040 PRINT B$(TJ(SP));N$(SP):PRINT AT(5,0);STRING$(32,""):WINDOW 7,30,2,29
1050 IF FL&gt;0 THEN 1090:FL=1
1060 PRINT "DIE RATTEN FRASSEN";RV;"%.":PRINT
1070 ON WE GOSUB 3210,3220,3230,3240,3250
1080 PRINT "  (";ER;"STERS)":PRINT AT(12,0);STRING$(32,""):WINDOW 14,30,2,30
1090 PRINT "KORNRESERVEN : ";INT(KO(SP));"STER"
1100 PRINT "KORNVERLANGEN: ";INT(VE);"STER"
1110 PRINT "KORNPREIS    : ";KP
1120 PRINT "LANDPREIS    : ";LP*.1;"P.HEKTAR"
1130 PRINT "GOLD         : ";GD(SP);"FLORIN":PRINT AT(20,0);STRING$(32,"")
1140 WINDOW 22,29,2,29:PRINT "1.KORN KAUF 2.KORN VERKAUF"
1150 PRINT "3.LAND KAUF 4.LAND VERKAUF":PRINT AT(25,0);STRING$(32,"")
1160 WINDOW 27,30,2,29:CLS
1170 PRINT "WAS MACHEN SIE?":IN=0:INPUT "&gt;";IN
1180 IF IN&lt;0 OR IN&gt;4 THEN 1170
1190 PRINT
1200 IF IN&gt;0 THEN ON IN GOSUB 3750,3780,3820,3850:GOTO 1030
1210 CLS:PRINT "WIEVIEL KORN VERTEILEN SIE  AN ";
1220 INPUT "IHRE UNTERTANEN ?";GG
1230 FL=FL+1:IF FL=2 AND GG=0 THEN 1030
1240 IF GG&lt;KO(SP)*.2 THEN GOSUB 1270:GOTO 1210
1250 IF GG&gt;KO(SP)*.8 THEN GOSUB 1290:GOTO 1210
1260 KO(SP)=KO(SP)-GG:RETURN
1270 CLS:PRINT "SIE MUESSEN MINDESTENS 20%  DES ";
1280 PRINT "KORNS VERTEILEN!":PAUSE 40:RETURN
1290 CLS:PRINT "SIE MUESSEN MINDESTENS 20%  DES ";
1300 PRINT "KORNS ALS NOTRESERVE BE-HALTEN!":PAUSE 40:RETURN
1310 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
1320 IF VE&gt;GG THEN 1580
1330 GR=6:SR=2:GOSUB 3890
1340 IF S1(SP)+S2(SP)&lt;35 THEN KA(SP)=INT(KA(SP)+1+RND(1)*4)
1350 IF S3(SP)&lt;RND(1)*20+1 THEN GOSUB 1860
1360 IF VE*1.3&gt;GG THEN 1420
1370 H=VO(SP)/1000:Z=10*(GG-VE)/GG
1380 H=Z*H*(1+RND(1)*25)+1+RND(1)*40:H=RND(1)*H+2:VO(SP)=INT(VO(SP)+H)
1390 PRINT INT(H);"MENSCHEN SIND ZUGE-":PRINT "WANDERT."
1400 H=RND(1)*(H/5)+1:IF H&gt;50 THEN H=50
1410 KA(SP)=KA(SP)+H:AD(SP)=AD(SP)+1:GE(SP)=GE(SP)+3
1420 H=MA(SP)*75:IF H&gt;0 THEN GD(SP)=GD(SP)+H
1430 PRINT "IHR MARKT BRACHTE ";H:PRINT "FLORIN ";
1440 PRINT "AN ZOLL EIN.":PRINT
1450 H=MU(SP)*INT(60+RND(1)*460):IF H&gt;0 THEN GD(SP)=GD(SP)+H
1460 PRINT "IHRE MUEHLE MACHTE EINEN    GEWINN VON";
1470 PRINT H;"FLORIN.":PRINT
1480 H=SO(SP)*3:GD(SP)=GD(SP)-H:PRINT "SIE ZAHLTEN IHREN SOLDATEN":PRINT H;"FLORIN";
1490 PRINT " ALS SOLD."
1500 PRINT
1510 IF LA(SP)/320-20&gt;SO(SP)THEN 1610
1520 IF LA(SP)/250-20&gt;SO(SP)THEN 1550
1530 FOR M=1 TO SA:IF M&lt;&gt;SP AND SO(SP)&lt;SO(M)/1.4 THEN 1610
1540 NEXT
1550 PRINT " "
1560 INPUT " ";I$:IF I$=""OR I$&lt;&gt;""THEN RETURN
1570 GOTO 1560
1580 H=(VE-GG)*100/VE-9:IF H&gt;65 THEN H=65
1590 IF H&lt;0 THEN H=0
1600 GR=2:SR=H+7:GOSUB 3890:GOTO 1420
1610 REM
1620 AG=0
1630 FOR M=1 TO SA:IF SP=M THEN 1650
1640 IF SO(M)&gt;SO(SP)AND SO(M)&gt;LA(M)/400 AND SO(M)=&gt;SO(AG)THEN AG=M
1650 NEXT:IF AG&gt;0 THEN LV=(SO(AG)*200+LA(SP)/4)*(0.3+RND(1)*0.4):GOTO 1670
1660 LV=1000*RND(1)*9000:IF LA(SP)&gt;30000 THEN LV=LV+RND(1)*(LA(SP)-300005)
1670 IF LV&gt;LA(SP)-5000 THEN LV=LA(SP)/2-2500
1680 IF LV&lt;2 THEN 1550
1690 LA(AG)=LA(AG)+LV:LA(SP)=LA(SP)-LV
1700 H=RND(1)*40+2:IF H&gt;SO(SP)-15 THEN H=SO(SP)-15
1710 SO(SP)=SO(SP)-H
1720 PRINT
1730 PRINT "ALARM  !!!!!"
1740 PRINT
1750 PRINT TB$(TJ(AG));N$(AG):PRINT "GREIFT AN.DIE SCHLACHT GING"
1760 PAUSE 30
1770 PRINT "LEIDER VERLOREN ..."
1780 PRINT
1790 PRINT "ES GAB ";LV;" HEKTAR LAND UND"
1800 PRINT
1810 PRINT "       ";INT(H);"SOLDATEN VERLUST
1820 PRINT
1830 PRINT "ERHOEHEN SIE IHRE"
1840 PRINT "VERTEIDIGUNGSFAEHIGKEIT !!!"
1850 FOR M=1 TO 2000:NEXT:GOTO 1550
1860 AD(SP)=INT(AD(SP)+RND(1)*2):GE(SP)=INT(GE(SP)+RND(1)*3):RETURN
1870 OD=TJ(SP)/8-INT(TJ(SP)/8):OD=OD*8:WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
1880 SN=(JU(SP)*300-500)*(OD+1)
1890 Z=(150-S1(SP)-S2(SP)-S3(SP))/100:IF Z&lt;1^E-2 THEN Z=1^E-2
1900 A1=INT(((AD(SP)*100+GE(SP)*75+KA(SP)*20)*Z+LS(SP))*S1(SP)/100)
1910 A2=INT(((AD(SP)*50+KA(SP)*25+LS(SP)*0.1)*Z*(5-JU(SP)))*S2(SP)/200)
1920 A3=INT((AD(SP)*250+LS(SP)/5+10*JU(SP)*Z)*S3(SP)/100)
1930 PRINT TB$(TJ(SP));N$(SP)
1940 PRINT
1950 PRINT "ES WURDEN ";A1+A2+A3+SN;"FLORIN AN"
1960 PRINT "STEUERN ERHOBEN.";
1970 PRINT AT(7,0);STRING$(32,""): WINDOW 9,29,2,29
1980 PRINT "KONSUMSTEUER  (";S1(SP);"%):";A1
1990 PRINT
2000 PRINT "HANDELSSTEUER (";S2(SP);"%):";A2
2010 PRINT
2020 PRINT "EINKOMMENSTEUER(";S3(SP);"%):";A3
2030 PRINT
2040 ON JU(SP)GOSUB 3260,3270,3280,3290
2050 PRINT
2060 WINDOW 19,29,2,29
2070 PRINT " NUMMERN ZUM AENDERN (1-4)"
2080 PRINT " SONST 0.":WINDOW 25,25,25,25
2090 GOSUB 4000:I$=A$
2100 IN=VAL(I$):IF IN&gt;4 THEN 2090
2110 WINDOW 25,29,2,29
2120 IF IN&gt;0 THEN ON IN GOTO 2290,2320,2350,2380
2130 GD(SP)=GD(SP)+A1+A2+A3+SN
2140 IF GD(SP)&lt;0 THEN GD(SP)=GD(SP)*1.5
2150 IF GD(SP)+2000+OD*1500=&gt;0 THEN RETURN
2160 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
2170 PRINT TB$(TJ(SP));N$(SP);" !!!"
2180 PRINT
2190 PRINT "DURCH IHRE TOTALE MISSWIRT-"
2200 PRINT "SCHAFT SIND SIE JETZT BANK- ROTT !!!"
2210 PRINT
2220 PRINT "IHR VERMOEGEN WURDE ENT-    EIGNET"
2230 PRINT "UND VERSTEIGERT !!!"
2240 MA(SP)=0:MU(SP)=O:PT(SP)=0:KT(SP)=0
2250 GOSUB 3200 IF KO(SP)&gt;2*VE THEN KO(SP)=2*VE
2260 LA(SP)=6000:GD(SP)=100:LS(SP)=100
2270 GOSUB 4000
2280 RETURN
2290 H=0:CLS:INPUT "NEUE KONSUMSTEUER (0-100)";H
2300 IF H&lt;0 OR H&gt;100 THEN H=25
2310 S1(SP)=H:GOTO 1870
2320 H=0:CLS:INPUT "NEUE HANDELSSTEUER (0-50)";H
2330 IF H&lt;0 OR H&gt;50 THEN H=10
2340 S2(SP)=H:GOTO 1870
2350 H=0:CLS:INPUT "NEUE EINKOMMENSTEUER (0-25)";H
2360 IF H&lt;0 OR H&gt;25 THEN H=5
2370 S3(SP)=H:GOTO 1870
2380 CLS:PRINT "1. SEHR FAIR  2. MILD"
2390 PRINT "3. STRENG     4. ABSCHEULICH";
2400 INPUT "WELCHE JUSTIZ PRAKTIZIEREN  SIE ?";H
2410 IF H&lt;0 OR H&gt;4 THEN 1870
2420 JU(SP)=H:GOTO 1870
2430 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
2440 PRINT "  STAATLICHE INVESTITIONEN"
2450 PRINT
2460 PRINT "  VON ";TB$(TJ(SP));N$(SP);
2470 PRINT AT(6,0);STRING$(32,""):WINDOW 8,29,2,29
2480 H$="00 FL"
2490 PRINT "1.MARKTPLATZ        :10";H$
2500 PRINT "2.KORNMUEHLE        :20"H$
2510 PRINT "3.PALAST(TEILW.)    :30"H$ 
2520 PRINT "4.KATHEDRALE(TEILW.):50"H$ 
2530 PRINT "5.SOLDATEN REKRUTIEREN:500FL"
2540 PRINT
2550 PRINT
2560 IF GD(SP)&lt;0 THEN 2580
2570 GOTO 2600
2580 PRINT " SIE HABEN BEREITS ";-GD(SP);"       "
2590 PRINT " FLORIN SCHULDEN!!!":GOTO 2620
2600 PRINT " IN DER SCHATZKAMMER LIEGEN "
2610 PRINT " ";GD(SP);" FLORIN."
2620 PRINT
2630 PRINT " WEITERE EINGABEN:"
2640 PRINT " 8. JAHRESENDE
2650 PRINT " 6. VERGLEICH DER VOELKER"
2660 PRINT " 7. VERGLEICH BESITZTUEMER"
2670 WINDOW 29,29,2,29
2680 PRINT "IHRE WAHL :";:WINDOW 29,29,14,15
2690 GOSUB 4000:IN=VAL(A$):IF IN&gt;8 THEN 2690
2700 M=GD(SP)+3500+2800*OD:IF IN=8 OR A$="0"THEN RETURN
2710 WINDOW 24,29,2,29:CLS:ON IN GOSUB 2720,2780,2820,2880,2940,3320,3540:GOTO 2430
2720 PRINT " WIEVIELE MARKTPLAETZE       WOLLEN";
2730 INPUT " SIE KAUFEN ?";H
2740 IF H&lt;1 THEN RETURN
2750 IF M&lt;H*1000 THEN 2990
2760 MA(SP)=MA(SP)+H:GD(SP)=GD(SP)-H*1000
2770 LS(SP)=INT(LS(SP)+H*10):KA(SP)=INT(KA(SP)+H*5):RETURN
2780 INPUT " WIEVIELE MUEHLEN WOLLEN SIE KAUFEN ?";H
2790 IF H&lt;1 THEN RETURN
2800 IF M&lt;H*2000 THEN 2990
2810 MU(SP)=MU(SP)+H:GD(SP)=GD(SP)-H*2000:LS(SP)=INT(LS(SP)+H*25):RETURN
2820 PRINT " UM WIEVIELE TEILE WOLLEN    SIE";
2830 PRINT "DEN PALAST WEITER-":INPUT " BAUEN ?";H
2840 IF H&lt;1 THEN RETURN
2850 IF M&lt;H*3000 THEN 2990
2860 PT(SP)=PT(SP)+H:GD(SP)=GD(SP)-H*3000:LS(SP)=INT(LS(SP)+H*50)
2870 FOR M=1 TO H:AD(SP)=AD(SP)+1+(INT(RND(1)*2)):NEXT M:RETURN
2880 PRINT "UM WIEVIEL TEILE WOLLEN SIE DIE";
2890 INPUT " KATHEDRALE WEITERBAUEN ?";H
2900 IF H&lt;1 THEN RETURN
2910 IF M&lt;H*5000 THEN 2990
2920 KT(SP)=KT(SP)+H:GD(SP)=GD(SP)-H*5000:LS(SP)=INT(LS(SP)+H*100)
2930 FOR M=1 TO H:GE(SP)=GE(SP)+1+INT(RND(1)*6):NEXT:RETURN
2940 PRINT " WIEVIELE EINHEITEN WOLLEN   SIE";
2950 INPUT " REKRUTIEREN ?";H
2960 IF H&lt;1 OR H*20&gt;VO(SP) THEN RETURN
2970 IF M&lt;H*500 THEN 2990
2980 SO(SP)=SO(SP)+H*20:VO(SP)=VO(SP)-H*20:GD(SP)=GD(SP)-H*500:RETURN
2990 PRINT
3000 PRINT " FUER SO HOHE AUSGABEN       KRIEGEN";
3010 PRINT " SIE KEINEN KREDIT  MEHR !!!"
3020 INPUT I$:IF I$&lt;&gt;"" OR I$="" THEN RETURN
3030 REM
3040 SU=0:M=MA(SP):GOSUB 3300
3050 M=MU(SP):GOSUB 3300:M=PT(SP):GOSUB 3300:M=KT(SP):GOSUB 3300
3060 M=GD(SP)/1000:GOSUB 3300:M=AD(SP)/5:GOSUB 3300
3070 M=KA(SP)/50:GOSUB 3300:M=AD(SP)/5:GOSUB 3300
3080 M=SO(SP)/50:GOSUB 3300:M=GE(SP)/10:GOSUB 3300
3090 M=VO(SP)/2000:GOSUB 3300:M=LS(SP)/500:GOSUB 3300
3100 TJ=SU/SS-JU(SP)-1:IF TJ&gt;7 THEN TJ=7
3110 IF JA+2=SJ(SP) THEN TJ(SP)=TJ(SP)+1
3120 IF TJ&gt;TJ(SP) AND 7 THEN TJ(SP)=TJ(SP) AND 8 OR TJ
3130 IF(TJ(SP) AND 7)&lt;7 THEN RETURN
3140 WINDOW:CLS:PRINT:PRINT:PRINT:PRINT
3150 PRINT "    DAS SPIEL IST ZU ENDE!!"
3160 PRINT:PRINT
3170 PRINT "  ES LEBE KING ";N$(SP)
3180 FOR Q9=1 TO 4000:NEXT
3190 GOSUB 3320:GOSUB 3540:END
3200 VE=AD(SP)*100+GE(SP)*40+KA(SP)*30+SO(SP)*10+VO(SP)*5:RETURN
3210 PRINT "DUERRE-HUNGERSNOT DROHT":RETURN
3220 PRINT "MIESES WETTER-MIESE ERNTE":RETURN
3230 PRINT "NORMALES WETTER-GUTE ERNTE":RETURN
3240 PRINT "GUTES WETTER-SCHOENE ERNTE":RETURN
3250 PRINT "BOMBENWETTER-GROSSE ERNTE":RETURN
3260 PRINT "JUSTIZ( SEHR FAIR ): ";SN:RETURN
3270 PRINT "JUSTIZ(   MILD    ): ";SN:RETURN
3280 PRINT "JUSTIZ(  STRENG   ): ";SN:RETURN
3290 PRINT "JUSTIZ(ABSCHEULICH): ";SN:RETURN
3300 SU=SU+M:IF M&gt;10 THEN SU=SU-M+10
3310 RETURN
3320 WINDOW 1,30,1,30:CLS:WINDOW 2,29,2,29
3330 A=0
3340 FOR M=1 TO SA
3350 A=A+1
3360 PRINT A;". ";TB$(TJ(M));N$(M)
3370 NEXT
3380 PRINT
3390 PRINT TAB(1)"LAND";TAB(13)"GOLD";TAB(19)"SOLDATEN"
3400 A=0
3410 FOR M=1 TO SA
3420 A=A+1
3430 PRINT A;". ";TAB(1)LA(M);TAB(12)GD(M);TAB(18)INT(SO(M)) 
3440 NEXT
3450 PRINT
3460 PRINT TAB(1)"ADEL";TAB(6)"KLERUS";TAB(13)"HAENDLER";TAB(22)"VOLK"
3470 A=0
3480 FOR M=1 TO SA
3490 A=A+1
3500 PRINT A ".";
3510 PRINT TAB(2)INT(AD(M));TAB(5)GE(M);TAB(12)INT(KA(M));TAB(21)INT(VO(M))
3520 NEXT
3530 GOSUB 4000:CLS:RETURN
3540 WINDOW 8,29,2,29:CLS
3550 PRINT "  EIGENTUM DER HERRSCHAFTS-            HAEUSER" 
3560 PRINT " =========================== ":A=0
3570 FOR M=1 TO SA
3580 A=A+1
3590 PRINT A;". ";TB$(TJ(M));N$(M)
3600 NEXT
3610 PRINT "
3620 PRINT TAB(3)"LAND";TAB(12)"GOLD";TAB(21)"MAERKTE";
3630 A=0
3640 FOR M=1 TO SA
3650 A=A+1
3660 PRINT A;". ";TAB(2)LA(M);TAB(15)GD(M);TAB(27)MA(M)
3670 NEXT
3680 PRINT
3690 PRINT TAB(3)"MUEHLEN";TAB(11)"PALAST";TAB(18)"KATHEDRALE"
3700 A=0
3710 FOR M=1 TO SA
3720 A=A+1
3730 PRINT A;". ";TAB(2)MU(M);TAB(10)PT(M);TAB(17)KT(M)
3740 NEXT:GOSUB 4000:CLS:RETURN
3750 CLS:H=0:INPUT "WIEVIEL KORN WOLLEN SIE     KAUFEN ?";H
3760 IF H&lt;1 THEN RETURN
3770 GD(SP)=GD(SP)-H*KP/1000:KO(SP)=KO(SP)+H:RETURN
3780 CLS:H=0:INPUT "WIEVIEL KORN VERKAUFEN      SIE ?";H
3790 IF H&lt;1 THEN RETURN
3800 IF KO(SP)-H&lt;VE(SP)/4 THEN 3780
3810 GD(SP)=GD(SP)+H*KP/1000:KO(SP)=KO(SP)-H:RETURN
3820 CLS:H=0:INPUT "WIEVIEL LAND WOLLEN SIE     KAUFEN ?";H
3830 IF H&lt;1 THEN RETURN
3840 GD(SP)=GD(SP)-H*LP/10:LA(SP)=LA(SP)+H:RETURN
3850 CLS:H=0:INPUT "WIEVIEL LAND VERKAUFEN      SIE ?";H
3860 IF H&lt;1 THEN RETURN
3870 IF LA(SP)-H&lt;5000 THEN 3850
3880 GD(SP)=GD(SP)+H*LP/10:LA(SP)=LA(SP)-H:RETURN
3890 H=INT((RND(1)*GR+1)*VO(SP)/100+2):VO(SP)=VO(SP)+H
3900 PRINT INT(H);"MENSCHEN WURDEN GEBOREN"
3910 PRINT
3920 H=INT((RND(1)*SR)*VO(SP)/100+2):VO(SP)=VO(SP)-H
3930 PRINT INT(H);"MENSCHEN STARBEN."
3940 IF JU(SP)&lt;3 THEN 3990
3950 H=INT((JU(SP)-2)^2*(2+RND(1))*VO(SP)/180+2):VO(SP)=VO(SP)-H
3960 PRINT
3970 PRINT INT(H);"MENSCHEN FLOHEN VOR DER";
3980 PRINT "   ABSCHEULICHEN JUSTIZ.";
3990 PRINT AT(9,0);STRING$(32,""):WINDOW 11,29,2,29:RETURN
4000 A$=INKEY$:IF A$=""THEN 4000:ELSE PRINT:PRINT A$;:RETURN
</pre>
</pre>
</body>
</html>
