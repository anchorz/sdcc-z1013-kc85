<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml"  xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_01.png" alt="Screenshot 1" height="256" width="256" />
</div>
<div class="filelist">1000 221f 0000 b ... <a href="b.LANDESVERWALTER.z80">LANDESVERWALTER&nbsp;</a></div>
<pre>L A N D E S V E R W A L T E R


DU BIST HERRSCHER DER ROEMISCHEN 
KOLONIE IN AFRIKA, DEIN VERWALTER 
GALLIUS TEILT DIR JEDES JAHR DIE 
DATEN UEBER DEIN LAND MIT.

DU MUSST ENTSCHEIDEN, WAS GETAN WIRD.

DEINE AMTSZEIT BETRAEGT VORERST
ZEHN JAHRE.
                               
<pre>Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

10 OUTCHAR 12
20 P."L A N D E S V E R W A L T E R"
25 P.;P.
30 P."DU BIST HERRSCHER DER ROEMI-";P.
35 P."SCHEN KOLONIE IN AFRIKA,DEIN";P.
40 P."VERWALTER GALLIUS TEILT DIR";P.
45 P."JEDES JAHR DIE DATEN UEBER DEIN";P.
50 P."LAND MIT."
55 P.;P."DU MUSST ENTSCHEIDEN, WAS GE-";P.
60 P."TAN WIRD.";P.
65 P."DEINE AMTSZEIT BETRAEGT VORERST";P.
70 P."ZEHN JAHRE.";P.;P.
72 P."                         Enter"
75 K=INCHAR;O.12
80 D=0;R=0;Z=0;J=0;P=95;S=2800;H=3000
90 E=H-S;Y=3;A=H/Y;I=5;Q=1
100 D=0
110 B=0
111 FOR K=1 TO 1000;NEXT K;P.
130 Z=Z+1
140 P."IM JAHR ",#1,Z," STARBEN ",B," LEUTE."
146 P.
150 P.#1,I," KAMEN INS LAND."
155 P.
160 P=P+I;IF Q&gt;0 GOTO 200
170 P=P/2
180 P."EINE FURCHTBARE PLAGE TRAF"
185 P."DEIN LAND. DIE HAELFTE DER"
190 P."LEUTE STARB."
200 P.;P."HIER IST DIE STATISTIK:"
205 P.;P.P,"  EINWOHNER"
210 P.A,"  MORGEN LAND"
220 P.Y,"  SCHEFFEL/MORGEN ERNTE"
230 P.E,"  SCHEFFEL FRASSEN RATTEN"
240 P.S,"  SCHEFFEL GELAGERT"
250 IF Z=J+10 GOTO 720
260 C=RND(10);Y=C+17
270 P.Y,"  SCH./MORGEN LANDPREIS"
275 P.
280 P."WIEVIELE MORGEN SOLL ICH KAUFEN?"
290 GOSUB 950;IF Y*Q&lt;=S G.310
300 GOSUB 670;GOTO 280
310 IF Q=0 GOTO 325
320 A=A+Q;S=S-Y*Q;C=0;GOTO 370
325 P. 
330 P."WIEVIEL LAND SOLL ICH VERKAUFEN?"
340 GOSUB 950;IF Q&lt;A GOTO 360
350 GOSUB 650;GOTO 330
360 A=A-Q;S=S+Y*Q;C=0
370 P.
380 P."WIEVIEL GETREIDE ALS NAHRUNG ?"
385 P.
390 GOSUB 950
400 IF Q&lt;=S GOTO 420
410 GOSUB 670;GOTO 380
420 S=S-Q;C=1
425 P.
430 P."WIEVIEL MORGEN BEPFLANZEN ?"
435 P.
440 INPUT""B;IF B=0 GOTO 540
450 IF B&lt;0 GOTO 700
460 IF B&lt;=A GOTO 480
470 GOSUB 650;GOTO 430
480 IF B/2&lt;S GOTO 500
490 GOSUB 670;GOTO 430
500 IF B&lt;10*P GOTO 530
510 P."DU HAST NUR ",#1,P," LEUTE."
520 GOTO 430
530 S=S-D/2
540 GOSUB 690;Y=C;H=B*Y;E=0;GOSUB 690
550 X=C/2;IF X*2&lt;C GOTO 570
560 E=S/C
570 S=S-E+H;GOSUB 690;I=(2*A+S/10)/P/10+1
580 C=Q/20;Q=RND(21)-3;IF P&lt;C OUTCHAR 12;GOTO 110
590 B=P-C;IF B&gt;P*3/4 GOTO 610
600 R=((Z-1)*R+B*100/P)/Z;P=C;D=D+B;OUTCHAR 12;GOTO 111
604 OUTCHAR 12;TAB(160)
605 P."WIR HABEN KEIN EINIZIGES KORN"
606 P."GETREIDE MEHR.";P.;P.;GOTO 620
610 OUTCHAR12;TAB(160);P."DIR STARBEN ",#1,B," LEUTE IN"
615 P."EINEM JAHR."
616 PRINT
620 P."FUER DIESE MISSWIRTSCHAFT WIRST"
622 P."DU DEINES AMTES ENTHOBEN UND"
624 P."INS GEFAENGNIS GESPERRT."
635 GOTO 1000
640 P.;P."DAS WAR'S, HAT MICH SEHR GE-    FREUT !";GOTO 1165
650 P."DENKE NACH, DIR GEHOEREN NUR"
655 P.#1,A," MORGEN LAND."
660 RETURN
670 P."DENKE NACH, DIR GEHOEREN NUR"
675 P.#1,S," SCHEFFEL GETREIDE."
680 RETURN
690 C=RND(6);RETURN
700 P."DAS KANN ICH NICHT TUN !"
710 P."HOL DIR EINEN ANDEREN MINISTER.";GOTO 640
720 P.;P."IN DEINER 10JAEHRIGEN AMTSZEIT"
730 P."STARBEN ",#1,D," EINWOHNER DURCH"
732 P."DEINE ENTSCHEIDUNGEN."
740 PRINT
760 L=A/P
770 P."AM ANFANG HATTE JEDER 10 MORGEN"
780 P."UND NUN ",#1,L," MORGEN."
785 P.
790 IF R&gt;25 GOTO 640
795 IF L&lt;7 GOTO 640
800 IF R&gt;10 GOTO 860
805 IF L&lt;9 GOTO 860
810 IF R&gt;3 GOTO 900
815 IF L&lt;10 GOTO 900
820 P.;P."EINE HERVORRAGENDE LANDESFUEH-"
822 P."RUNG WAR IN DEINER ZEIT, UND"
824 P."DIE EINWOHNER WOLLEN DICH NOCH"
826 P."WEITERE 10 JAHRE HABEN."
845 P.;P."                         Enter";K=INCHAR
850 J=J+10;O.12;GOTO 100
860 P."DEINE HARTHERZIGE REGIERUNGS-"
865 P."WEISE ERINNERT AN NERO."
880 P."DIE LEUTE HASSEN DICH."
890 GOTO 640
900 P."DEINE HERRSCHAFT KOENNTE BESSER"
905 P."SEIN, ABER SIE WAR NICHT"
910 P."SCHLECHT."
920 F=(P/3+((RND(100)*P/3)/100))
922 PRINT
925 IF F&lt;P/2 GOTO 640
930 P.#1,F," DEINER LEUTE WOLLEN"
935 P."DICH WIEDER ALS HERRSCHER."
940 P.;P."                        Enter";K=INCHAR
945 GOTO 850
950 INPUT""Q;IF Q&lt;0 GOTO 700
960 RETURN
1000 FOR I=1 TO 4000;NEXT I
1010 OUTCHAR 12
1015 W=HEX(EC00)
1020 FOR I=1 TO 6
1030 FOR J=0 TO 31
1040 POKE(W+J),255
1050 NEXT J
1060 W=W+192
1070 NEXT I
1080 W=HEX(EC04)
1090 FOR I=1 TO 7
1100 FOR J=0 TO 31
1110 POKE(W+J*32),255
1120 NEXT J
1130 W=W+4
1140 NEXT I
1150 FOR I=1 TO 2000;NEXT I
1151 POKE(HEX(ED52)),139
1152 POKE(HEX(ED4E)),139
1153 FOR I=1 TO 500;NEXT I
1154 POKE(HEX(ED52)),248
1155 FOR I=1 TO 200;NEXT I
1156 POKE(HEX(ED52)),139;GOTO 1250
1157 FOR I=1 TO 3000;NEXT I
1158 POKE(HEX(EF9D)),116;POKE(HEX(EF9E)),112
1159 POKE(HEX(EF9F)),101
1160 FOR I=1 TO 1000;NEXT I
1165 P.;P."                         Enter";K=INCHAR;O.12
1170 TAB(320)
1180 P."NOCH EIN VERSUCH ?"
1182 P."                            J/N"
1184 W=INCHAR
1190 IF W='J' O.12;GOTO 80
1195 O.12
1200 STOP
1250 FOR O=1 TO 5
1300 FOR K=HEX(ED72) TO HEX(EFF2) STEP 32
1310 U=PEEK(K)
1320 POKE K,203
1322 FOR V=1 TO 10;NEXT V
1330 POKE K,U
1340 NEXT K
1341 POKE HEX(EFB1),128
1342 POKE HEX(EF92),131
1343 POKE HEX(EFB3),129
1344 FOR V=1 TO 19;NEXT V
1345 POKE HEX(EFB1),32
1346 POKE HEX(EF92),32
1347 POKE HEX(EFB3),32
1350 NEXT O
1360 GOTO 1165
</pre>
</pre>
</body>
</html>
