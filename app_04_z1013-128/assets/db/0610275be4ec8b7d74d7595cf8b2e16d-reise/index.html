<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.1//EN" "http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd">
<html xmlns="http://www.w3.org/1999/xhtml"  xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_01.png"/><img src="screenshot_02.png"/></div><div class="filelist">2bc0 4f9f 0000 B ... <a href="reise.z80">REISE&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;</a></div>
<pre><pre>10!PROGRAMM AUS COMMODORE WELT
20!7/87 S.88 (C)C.-D.HEINECK
30!AENDERUNGEN U.ANPASSUNG
40!DURCH MANFRED LIMBERG
50!OSTROWSKISTR.5B,GREIFSWALD
100 WINDOW:CLS:PRINT "~~~ DIE GROSSE HANDELSFAHRT ~~~":PRINT
110 PRINT:PRINT "DU HAST EIN SCHIFF AUF KREDIT"
120 PRINT:PRINT "GEKAUFT.DIESEN MUSSST DU AN DIE"
130 PRINT:PRINT "BANK IM HEIMATHAFEN ABERDEEN"
140 PRINT:PRINT "ZURUECKZAHLEN.GEWINN BRINGEN"
150 PRINT:PRINT "DIR DIE VERSCHIEDENEN PREISE"
160 PRINT:PRINT "IN DEN 5 VERSCHIEDENEN HAEFEN.
170 PRINT:PRINT "AN LAND UND AUF DEM MEER LAUERN"
180 PRINT:PRINT "GEFAHREN,DIE BANK KASSIERT"
190 PRINT:PRINT "ZINSEN.BEI 25000 PFUND SCHULDEN"
200 PRINT:PRINT "WIRD DEIN SCHIFF GEPFAENDET."
210 PRINT:PRINT "VIEL GLUECK !"
212 PRINT:PRINT "'?'=WEITER MIT 'ENTER'BZW.ZAHL"
213 PRINT:PRINT "EINGEBEN (WENN NOETIG)."
214 PRINT:INPUT A$
220 GOSUB 5540
230 G=10*INT(RND(1)*90):SC=100*INT(RND(1)*100)
240 IF SC&lt;4000 OR SC&gt;9000 THEN 230
250 A=0:W=0:K=0:R=0:E=0:RU=0:LP=50:KE=0:XG=0:SE$="A"
290 GOSUB 5450
300 GOSUB 5340
310 CLS
320 PRINT "WILLST DU"
330 PRINT:PRINT "KAUFEN     K"
340 PRINT:PRINT "VERKAUFEN  V"
350 PRINT:PRINT "SEGELN     S"
355 IF SE$&lt;&gt;"A"THEN 420
360 PRINT:PRINT "ZUR BANK   B"
420 PRINT AT(6,0);S$
430 PRINT AT(8,10);G
440 PRINT AT(10,10);SC
450 PRINT AT(12,10);LP
470 PRINT AT(4,29);D
480 PRINT AT(6,29);W
490 PRINT AT(8,29);K
500 PRINT AT(10,29);R
510 PRINT AT(12,29);J
520 PRINT AT(16,29);P1
530 PRINT AT(18,28);P2
540 PRINT AT(20,28);P3
550 PRINT AT(22,27);P4
560 PRINT AT(24,27);P5
670 D$=INKEY$
680 IF SE$&lt;&gt;"A"AND D$="B"THEN 670
690 IF D$="K"AND LP&gt;0 GOTO 740
700 IF D$="V"AND LP&lt;50 GOTO 1200
710 IF D$="S"GOTO 1660
720 IF D$="B"GOTO 1920
730 GOTO 670
740 CLS:PRINT:PRINT "WAS WILLST DU":PRINT:PRINT "KAUFEN?"
750 PRINT:PRINT "  D/W/K/R/J"
820 K$=INKEY$:IF K$="D"OR K$="W"OR K$="K"OR K$="R"OR K$="J" THEN 840 ELSE 820
840 IF K$="D"THEN P=P1
850 IF K$="W"THEN P=P2
860 IF K$="K"THEN P=P3
870 IF K$="R"THEN P=P4
880 IF K$="J"THEN P=P5         
910 PRINT:PRINT "WIEVIEL WILLST":PRINT:PRINT "DU KAUFEN?"
950 PRINT:INPUT KA
960 IF KA&gt;50 THEN 910
970 IF KA&gt;LP THEN 1130
980 IF(KA*P)&gt;G THEN 1070
990 G=G-(KA*P)
1000 LP=LP-KA
1010 IF K$="D"THEN D=D+KA
1020 IF K$="W"THEN W=W+KA
1030 IF K$="K"THEN K=K+KA
1040 IF K$="R"THEN R=R+KA
1050 IF K$="J"THEN J=J+KA
1060 GOTO 310
1070 CLS
1090 PRINT:PRINT "DU HAST NICHT":PRINT:PRINT "GENUG GELD !"
1120 PAUSE(30):GOTO 910
1130 CLS:PRINT "DU HAST KEINEN":PRINT:PRINT "PLATZ FUER";KA;"!"
1140 PAUSE 30:GOTO 910
1200 CLS
1220 PRINT "WAS WILLST DU":PRINT:PRINT "VERKAUFEN ?"
1250 PRINT:PRINT "D/W/K/R/J"
1280 V$=INKEY$:IF V$="" THEN 1280
1300 IF V$="D"THEN P=P1:GOTO 1350
1310 IF V$="W"THEN P=P2:GOTO 1350
1320 IF V$="K"THEN P=P3:GOTO 1350
1330 IF V$="R"THEN P=P4:GOTO 1350
1340 IF V$="J"THEN P=P5
1350 PRINT
1370 PRINT "WIEVIEL WILLST DU":PRINT:PRINT "VERKAUFEN ?"
1410 INPUT VN
1420 IF VN&gt;50 THEN 1350
1430 IF V$="D"AND VN&gt;D THEN 1570
1440 IF V$="W"AND VN&gt;W THEN 1570
1450 IF V$="K"AND VN&gt;K THEN 1570
1460 IF V$="J"AND VN&gt;J THEN 1570
1470 IF V$="R"AND VN&gt;R THEN 1570
1480 G=G+(P*VN):LP=LP+VN
1500 IF V$="D"THEN D=D-VN
1510 IF V$="W"THEN W=W-VN
1520 IF V$="K"THEN K=K-VN
1530 IF V$="R"THEN R=R-VN
1540 IF V$="J"THEN J=J-VN
1550 IF G&gt;800000 THEN 5130
1560 GOTO 310
1570 CLS:PRINT:PRINT "DU HAST KEINE":PRINT:PRINT VN;"AN BORD !"
1650 PAUSE(40):GOTO 1350
1660 RU=RU+1
1670 XG=0
1680 CLS
1700 PRINT:PRINT "*** STAEDTE ***"
1710 PRINT:PRINT "ABERDEEN     A"
1720 PRINT "PLYMOUTH     P"
1730 PRINT "LONDON       L"
1740 PRINT "NEWCASTLE    N"
1750 PRINT "CARDIFF      C"
1770 SE$=INKEY$
1780 IF SE$&lt;&gt;"A"AND SE$&lt;&gt;"P"AND SE$&lt;&gt;"L"AND SE$&lt;&gt;"N"AND SE$&lt;&gt;"C"THEN 1770
1790 IF S$="ABERDEEN     "AND SE$="A"THEN 1770
1800 GOSUB 5450
1810 IF RU=5 OR RU=10 OR RU=15 THEN 4860
1820 IF RND(1)&gt;.22 AND RND(1)&lt;.3 THEN 3520
1830 IF RND(1)&lt;.15 THEN 4250
1840 IF RND(1)&gt;.55 AND RND(1)&lt;.65 THEN 4480
1850 IF RND(1)&gt;.45 AND RND(1)&lt;.55 THEN 2530
1860 IF RND(1)&gt;.35 AND RND(1)&lt;.45 THEN 3290
1870 IF RND(1)&gt;.80 AND SC&gt;0 THEN 4710:GOTO 1810
1880 GOSUB 5540
1890 SC=INT(1.12*SC)
1900 IF SC&gt;25000 THEN 5650
1910 GOTO 300
1920 CLS:PRINT "WILLST DU DEN":PRINT:PRINT "KREDIT TILGEN   T"
1930 PRINT:PRINT "ODER LEIHEN     L":PRINT
2010 LZ$=INKEY$
2020 IF LZ$&lt;&gt;"L"AND LZ$&lt;&gt;"T"THEN 2010
2030 IF LZ$="L"AND XG&gt;0 THEN 2160
2040 IF LZ$="L"THEN 2260
2050 CLS
2070 PRINT "WIEVIEL WILLST DU":PRINT:PRINT "ZURUECKZAHLEN"
2080 PRINT:INPUT ZU
2120 IF ZU&gt;G OR ZU&gt;SC THEN 2050
2130 SC=SC-ZU
2140 G=G-ZU
2150 GOTO 310
2160 CLS
2170 PRINT "DU KANNST PRO FAHRT":PRINT:PRINT "NUR EINMAL LEIHEN!"
2240 PAUSE(40)
2250 GOTO 310
2260 CLS
2270 PRINT "WIEVIEL WILLST DU":PRINT:PRINT "LEIHEN";:INPUT LE
2320 XG=XG+1
2340 LI=100*INT(RND(1)*50)+1000
2360 IF LE&gt;LI THEN 2400
2370 SC=SC+LE:G=G+LE
2390 GOTO 310
2400 PRINT:PRINT "DAS LIMIT SIND":PRINT:PRINT LI;"PFUND !"
2410 PAUSE(40)
2490 SC=SC+LI:G=G+LI
2520 GOTO 310
2530 WINDOW:CLS:PRINT TAB(224)"    P I R A T E N !   ":PRINT:PRINT
2560 PAUSE(20)
2570 IF KE=1 THEN 2850
2580 IF RND(1)&gt;.5 THEN 3210
2590 IF LP=50 THEN 2730
2600 PRINT:PRINT:PRINT
2620 PRINT "PIRATEN HABEN UNS UEBERFALLEN"
2640 PRINT:PRINT "UND DIE GANZE LADUNG ERBEUTET ."
2660 PAUSE(20)
2670 LP=50:D=0:W=0:R=0:K=0:J=0
2730 X=INT(RND(1)*10)+3
2750 PRINT:PRINT "WAEHREND DES KAMPFES SIND";X
2755 PRINT:PRINT "DEINER MAENNER VERWUNDET WORDEN."
2760 PRINT:PRINT "DIE BEHANDLUNG DURCH EINEN"
2770 PRINT:PRINT "ARZT KOSTET JE 75 PFUND."
2810 PRINT:INPUT A$
2820 G=G-75*X:IF G&lt;0 THEN 3070
2840 GOTO 1880
2850 PRINT "DEINE MANNSCHAFT BENUTZT DIE"
2900 PRINT:PRINT "KANONE VON SCHWARZEN JACK .":PAUSE(20)
2910 IF RND(1)&gt;.2 THEN 3000
2940 PRINT:PRINT "DIE KANONE IST LEIDER KAPUTT !"
2960 PAUSE(20)
2970 KE=0:GOTO 2600
3000 PRINT:PRINT "MIT DER KANONE KONNTE DAS"
3010 PRINT:PRINT "PIRATENSCHIFF VERSENKT WERDEN."
3020 PRINT:PRINT "GUT GEMACHT,JUNGS !"
3050 PAUSE(40)
3060 GOTO 1880
3070 PRINT:PRINT:PRINT "DA DU NICHT GENUG GELD FUER"
3080 PRINT:PRINT "DEN DOKTOR HATTEST,MEUTERTE":PRINT:PRINT "DEINE MANNSCHAFT."
3100 PRINT:PRINT "SIE HAT DAS SCHIFF VERKAUFT,"
3110 PRINT:PRINT "UM DIE RECHNUNG BEZAHLEN ZU"
3120 PRINT:PRINT "KOENNEN."
3160 PRINT:PRINT "NIMM'S LEICHT !"
3170 PRINT:INPUT A$
3180 CLS:PRINT TAB(224)"     E  N  D  E"
3190 PRINT
3200 GOTO 5270
3210 PRINT:PRINT:PRINT
3230 PRINT "DIE PIRATEN HABEN NICHT"
3240 PRINT:PRINT "ANGEGRIFFEN,DU KANNST DEINE"
3250 PRINT:PRINT "REISE FORTSETZEN."
3270 PAUSE(40)
3280 GOTO 1880
3290 WINDOW:CLS:PRINT TAB(224)"    S  T  U  R  M    ":PRINT:PRINT
3320 PAUSE(20)
3330 IF RND(1)&gt;.5 OR LP=50 THEN 3450
3340 PRINT
3360 PRINT "EIN STURM IST AUFGEKOMMEN UND"
3370 PRINT:PRINT "WIR MUSSTEN DIE HALBE LADUNG"
3380 PRINT:PRINT "UEBER BORD WERFEN ! SCHADE !"
3410 D=D-INT(D/2):W=W-INT(W/2):K=K-INT(K/2):R=R-INT(R/2):J=J-INT(J/2)
3420 LP=50-D-W-K-R-J
3430 PRINT:INPUT A$
3440 GOTO 1880
3450 PRINT:PRINT "DER STURM HAT SOFORT AUFGEHOERT"
3460 PRINT:PRINT "UND DU KANNST WEITERFAHREN."
3500 PAUSE(40)
3510 GOTO 1880
3520 WINDOW:CLS:PRINT TAB(224)"*****   L A N D G A N G   *****"
3550 PAUSE(20)
3560 IF RND(1)&gt;.2 THEN 3840
3570 IF R&gt;0 AND RND(1)&gt;.3 THEN 3750
3580 TR=INT(RND(1)*3100):IF TR&lt;500 THEN 3580
3600 PRINT:PRINT "WAEHREND DES LANDGANGES HABEN"
3610 PRINT:PRINT "DIR TRICKBETRUEGER";TR;"PFUND":PRINT:PRINT "ABGENOMMEN!"
3620 PRINT:INPUT A$
3660 G=G-TR:IF G&lt;0 THEN 3690
3680 GOTO 1880
3690 PRINT:PRINT:PRINT "DU BIST JETZT PLEITE":PRINT:PRINT "UND MUSST DIE REISE"
3700 PRINT:PRINT "BEENDEN !"
3730 PAUSE(20)
3740 GOTO 3160
3750 PRINT:PRINT:PRINT "WAEHREND DEINES LANDAUSFLUGES"
3760 PRINT:PRINT "HABEN DEINE LEUTE DEN GANZEN RUM"
3770 PRINT:PRINT "AUSGETRUNKEN!"
3800 PAUSE(40)
3810 LP=LP+R:R=0:GOTO 1880
3840 PRINT:PRINT:PRINT "WAEHREND EINES LANDAUSFLUGES"
3850 PRINT:PRINT "KOMMST DU AN EINER KNEIPE"
3860 PRINT:PRINT "VORBEI UND GEHST HINEIN."
3890 PAUSE(20)
3900 PRINT:PRINT "DU WIRST ZU EINEM GLUECKSSPIEL"
3910 PRINT:PRINT "AUFGEFORDERT.MACHST DU MIT ?":PRINT:PRINT "  (J/N)"
3950 MM$=INKEY$:IF MM$&lt;&gt;"J"AND MM$&lt;&gt;"N"THEN 3950
3970 IF MM$="N"THEN 1880
3980 GV=INT(RND(1)*10100):IF GV&lt;1000 THEN 3980
4000 GZ=INT(RND(1)*6)-1:IF GZ&lt;=0 THEN 4000
4010 PRINT:PRINT "WAEHLE ZWISCHEN DEN ZAHLEN":PRINT:PRINT "   1 UND 5 !"
4040 WN$=INKEY$:WN=VAL(WN$)
4090 IF WN&lt; 1 OR WN &gt; 5 THEN 4040
4100 IF WN&lt;&gt;GZ THEN 4170
4110 PRINT:PRINT "DU HAST RICHTIG GERATEN UND"
4120 PRINT:PRINT "SOMIT";GV;"PFUND GEWONNEN !"
4150 PAUSE(40)
4160 G=G+GV:GOTO 1880
4170 PRINT:PRINT "FALSCH !! DU HAST";INT(GV/5);"PFUND"
4180 PRINT:PRINT "VERLOREN."
4210 PRINT:INPUT A$
4220 G=G-INT(GV/5)
4230 IF G&lt;0 GOTO 3690
4240 GOTO 1880
4250 PK=INT(RND(1)*10100)
4260 IF PK&lt;1000 THEN 4250
4270 IF KE=1 THEN 1880
4280 WINDOW:CLS:PRINT TAB(224)
4290 PRINT "&lt;&lt;&lt;   DER SCHWARZE JACK   &gt;&gt;&gt;"
4300 PAUSE(20)
4310 PRINT:PRINT:PRINT:PRINT "DER SCHWARZE JACK BIETET DIR"
4320 PRINT:PRINT "EINE KANONE ZUM SCHUTZ VOR":PRINT:PRINT "PIRATEN AN."
4330 PRINT:PRINT "SIE KOSTET";PK;"PFUND."
4340 PRINT:PRINT "MOECHTEST DU ?(J/N)"
4380 KK$=INKEY$
4400 IF KK$="J"AND G&lt;PK THEN 4450
4410 IF KK$="N"GOTO 1880
4415 IF KK$="J"GOTO 4420
4417 GOTO 4380
4420 KE=1:G=G-PK
4440 GOTO 1880
4450 PRINT:PRINT:PRINT:PRINT "DU HAST ZUWENIG GELD !"
4460 PAUSE(40)
4470 GOTO 1880
4480 WINDOW:CLS:PRINT TAB(224)"+++    SCHIFF IN SICHT    +++"
4510 PRINT:PRINT "WILLST DU NAEHER HERANFAHREN":PRINT:PRINT "(J/N)"
4550 HF$=INKEY$
4560 IF HF$&lt;&gt;"J"AND HF$&lt;&gt;"N"THEN 4550
4570 IF HF$="N"THEN 1880
4580 IF RND(1)&gt;.5 THEN 2530
4590 VD=INT(RND(1)*10100)
4600 IF VD&lt;1000 THEN 4590
4610 PRINT
4620 PRINT:PRINT "ES IST EIN ENGLISCHES HANDELS-"
4630 PRINT:PRINT "SCHIFF.DU MACHST MIT DEM"
4640 PRINT:PRINT "KAPITAEN EIN GESCHAEFT UND"
4650 PRINT:PRINT "VERDIENST DABEI";VD;"PFUND"
4680 PRINT:INPUT A$
4690 G=G+VD
4700 GOTO 1880
4710 WINDOW:CLS:PRINT:PRINT "=====    EIN GOENNER    ====="
4730 PAUSE(20)
4740 GR=100*INT(RND(1)*101)
4750 IF GR&lt;1000 THEN 4740
4760 IF GR&gt;SC THEN GR=SC
4770 PRINT:PRINT "EIN UNBEKANNTER HAT";GR;"PFUND"
4780 PRINT:PRINT "VON DEINEN SCHULDEN ABBEZAHLT !"
4820 PAUSE(40)
4830 IF SC&lt;GR THEN 4850
4840 SC=SC-GR:GOTO 1880
4850 SC=0:GOTO 1880
4860 WINDOW:CLS:PRINT:PRINT:PRINT "-----  WERFTAUFENTHALT  -----"
4870 MR=INT(RND(1)*5100)
4880 IF MR&lt;500 OR MR&gt;2000 THEN 4870
4890 SR=INT(RND(1)*5100)
4900 IF SR&lt;500 OR SR&gt;2000 THEN 4890
4910 RR=INT(RND(1)*5100)
4920 IF RR&lt;200 OR RR&gt;2000 THEN 4910
4930 GR=MR+SR+RR
4940 PRINT:PRINT:PRINT "DEIN SCHIFF MUSS REPARIERT"
4945 PRINT:PRINT "WERDEN."
4950 PRINT:PRINT "MAST   ";MR
4960 PRINT:PRINT "SEGEL  ";SR
4970 PRINT:PRINT "RUMPF  ";RR:PRINT "------------"
4980 PRINT:PRINT "GESAMT ";GR
4990 PRINT:PRINT "============"
5000 PAUSE(40)
5030 G=G-GR
5040 IF G&lt;0 THEN 5060
5050 GOTO 1880
5060 PRINT:PRINT "DA DU ZUWENIG GELD HAST,DIE"
5070 PRINT:PRINT "RECHNUNG ZU BEZAHLEN,MUSST DU"
5080 PRINT:PRINT "DIE REISE BEENDEN !!!"
5120 GOTO 3160
5130 WINDOW:CLS:PRINT STRING$(32,"")
5140 PRINT:PRINT:PRINT "    HERZLICHEN GLUECKWUNSCH !"
5150 PRINT:PRINT:PRINT STRING$(32,"")
5220 PRINT:PRINT "DA DU UEBER 800000 PFUND"
5230 PRINT:PRINT "VERDIENT HAST,KANNST DU DICH"
5240 PRINT:PRINT "ZUR RUHE SETZEN !"
5250 PRINT:PRINT "GENAUER SPIELSTAND:";G;"PFUND"
5270 PRINT:PRINT:PRINT "NOCH EIN SPIEL ? (J/N)":PRINT
5280 NS$=INKEY$
5300 IF NS$&lt;&gt;"J"AND NS$&lt;&gt;"N"THEN 5280
5310 IF NS$="J"THEN RUN
5320 PRINT:PRINT "AUF WIEDERSEHEN !"
5330 END
5340 P1=5*INT(RND(1)*16):IF P1&lt;5 THEN 5340
5360 P2=10*INT(RND(1)*21):IF P2&lt;50 THEN 5360
5380 P3=10*INT(RND(1)*100):IF P3&lt;300 THEN 5380
5400 P4=100*INT(RND(1)*31):IF P4&lt;500 THEN 5400
5420 P5=100*INT(RND(1)*100):IF P5&lt;1000 THEN 5420
5440 RETURN
5450 IF SE$="A"THEN S$="ABERDEEN     "
5460 IF SE$="P"THEN S$="PLYMOUTH     "
5470 IF SE$="L"THEN S$="LONDON       "
5480 IF SE$="N"THEN S$="NEWCASTLE    "
5490 IF SE$="C"THEN S$="CARDIFF      "
5500 RETURN
5540 WINDOW:CLS:WINDOW 14,24,0,18
5550 PRINT AT(0,1);"Z-1013-SCHIFFAHRTSGESELLSCHAFT"
5552 PRINT AT(1,0);"--------------------------------"
5555 PRINT AT(4,0);"HAFEN:              DIVERSES  "
5560 PRINT AT(6,20);"WAFFEN"
5565 PRINT AT(8,0);"GUTHABEN:           KOHLE"
5570 PRINT AT(10,0);"SCHULDEN:           RUM"
5575 PRINT AT(12,0);"LADEPLATZ:          JUWELEN"
5580 PRINT AT(14,20);"***PREISE***"
5585 PRINT AT(16,20);"DIVERSES":PRINT AT(18,20);"WAFFEN"
5590 PRINT AT(20,20);"KOHLE":PRINT AT(22,20);"RUM" 
5595 PRINT AT(24,20);"JUWELEN"
5600 RETURN
5650 WINDOW:CLS:PRINT TAB(255)
5660 PRINT:PRINT "WEIL DU UEBER 25000 PFUND"
5670 PRINT:PRINT "SCHULDEN HAST, WIRD DEIN SCHIFF"
5680 PRINT:PRINT "GEPFAENDET.ENDE DER REISE"
5690 GOTO 5270
</pre>
</pre>
</body>
</html>
