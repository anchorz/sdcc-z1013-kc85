<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="de" xml:lang="de">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1" />
  <link rel="shortcut icon" href="./favicon.ico" type="image/x-icon" />
  <link rel="icon" href="./favicon.ico" type="image/x-icon" />
  <meta name="description" content="Z1013 Software-Datenbank: Liste aller bekannten Dateien" />
  <meta name="keywords" lang="de" content="U880, Z80, Z1013, software, download, datenbank" />
  <meta name="keywords" lang="en" content="U880, Z80, Z1013, software, download, database" />
  <link rel="stylesheet" type="text/css" href="../formate.css" />

<title>Z1013 Software-Datenbank Eintrag</title>
<meta http-equiv="content-type" content="text/html;charset=UTF-8" />
</head>
<body>
<div><img src="screenshot_01.png" alt="Screenshot 1" height="384"  />
<img src="screenshot_02.png" alt="Screenshot 2" height="384"  >
</div>
<div class="filelist">2bc0 5d05 0000 B ... <a href="monopoly.z80">MONOPOLY        </a></div>
<div class="text"><pre>1 CLEAR 230:WINDOW:CLS:PRINT AT(31,17);"Druecke Taste":GOSUB 241
3 FOR I=1 TO 40:FE(I,1)=0:FE(I,2)=0:FE(I,3)=0:NEXT I
5 DIM SP$(7,2):SP$(1,2)="É":SP$(2,2)="":SP$(3,2)="Ë":SP$(4,2)="Ê"    
7 SP$(5,2)="Ï":SP$(6,2)="":SP$(0,2)=" " 
9 PRINT AT(23,25);STRING$(13," "):GOSUB 511
11 CLS:PRINT " Wieviele Spieler ?":PRINT:PRINT " (2-6)":PRINT 
13 SW=1:GOSUB 601:SX=VAL(ZX$):IF SX&lt;2 OR SX&gt;6 THEN 11
15 DIM SP(SX,5):FOR I=1 TO SX
17 SP(I,1)=30000:SP(I,2)=0:SP(I,3)=0:SP(I,4)=0:SP(I,5)=1
19 CLS:PRINT STR$(I);". Spieler mit ";SP$(I,2):PRINT
21 PRINT " Wie heissen Sie ?":PRINT
23 INPUT SP$(I,1):PRINT:IF LEN(SP$(I,1))&gt;8 THEN 21:ELSE NEXT I
25 PAUSE 15:CLS:PRINT "Ich zeige mal 'nen":PRINT:PRINT "Besitzueberblick."
27 PRINT:GOSUB 543:SW=1:GOSUB 601:GOSUB 513:PRINT:PRINT
29 PRINT "  Im Moment":PRINT "  besitzt":PRINT "noch niemand";:PRINT "   was." 
31 SW=1:GOSUB 601:CLS
33 SS=INT(RND(1)*SX)+1
35 PRINT:PRINT " ";SP$(SS,1):PRINT:PRINT " faengt an.":PRINT:GOSUB 601
37 WINDOW 0,30,0,31:CLS:ZS$=SP$(SS,2)
39 GOSUB 567:GOSUB 409
41 W1=INT(RND(3)*6)+1:W2=INT(RND(4)*6)+1:W3=W1+W2
43 EK=SP(SS,5)+W3:IF EK&gt;40 THEN EK=EK-40
45 IF SP(SS,5)&lt;=0 THEN EK=11+W3
47 IF RU/SX&gt;5 THEN 53
49 IF RND(3)&lt;.3 THEN 63
51 IF FE(EK,2)&lt;&gt;0 OR FE(EK,6)&lt;6 THEN 41:ELSE GOTO 63
53 IF RU/SX&lt;RND(4)*20 OR RND(3)&lt;.3 THEN 61
55 IF FE(EK,2)=0 OR FE(EK,2)=SS THEN 41
57 XX=SP(SS,1)+SP(SS,4):XY=FE(AK,2):XZ=SP(XY,1)+SP(XY,4)
59 IF XX&lt;1.5*XZ THEN 41
61 IF FE(EK,6)=4 OR FE(EK,6)=0 AND RND(5)&gt;.1 THEN 41
63 IF SP(SS,5)&gt;0 THEN 105:ELSE GOSUB 511:PRINT:PRINT " Sie sind noch im"
65 PRINT:PRINT " Gefaengnis !":PAUSE 10:PRINT:PRINT " Zahlen Sie ?":PRINT
67 GOSUB 601:IF ZX$&lt;&gt;"J"THEN 79
69 IF SP(SS,1)&gt;=1000 THEN 75
71 PAUSE 10:PRINT:PRINT " So viel haben Sie":PRINT:PRINT " nicht !"
73 GOSUB 601:GOTO 79
75 SP(SS,1)=SP(SS,1)-1000
77 SP(SS,3)=SP(SS,3)+1000:SP(SS,5)=11:GOTO 105
79 IF W1&lt;&gt;W2 THEN 83:ELSE PAUSE 10:PRINT:PRINT " Sie haben 'nen Pasch" 
81 PRINT:PRINT " und kommen raus!":PAUSE 10:SP(SS,5)=11:GOTO 105
83 IF W3&gt;4 THEN 89:ELSE PAUSE 10:PRINT:PRINT " Sie haben 2,3 oder 4"
85 PRINT:PRINT " und kommen in einem":PRINT:PRINT " Jahr raus.":SP(SS,5)=11
87 GOSUB 601:AK=11:GOTO 147
89 PAUSE 10:PRINT:PRINT " Sie bleiben drin!"
91 SP(SS,5)=SP(SS,5)+1
93 IF SP(SS,5)=-1 THEN 97:ELSE PRINT:PRINT " Noch 2 Jahre."
95 GOSUB 601:GOTO 147
97 PRINT:PRINT " Im naechsten Jahr":PRINT " kommen Sie raus !"
99 PAUSE 5:PRINT:PRINT " Macht 1000 Mark.":BE=1000:GOSUB 629
101 IF BE=0 THEN 235
103 GOSUB 601:SP(SS,3)=SP(SS,3)+1000:SP(SS,5)=11:AK=11:GOTO 147
105 EL=FN PO(EK):AK=FN PO(SP(SS,5))
107 GOSUB 343:AK=FN PO(AK):IF AK&lt;&gt;EL THEN 107
109 AK=EK:PAUSE 5:GOSUB 511:PRINT:PRINT " Nun sind Sie da."
111 PRINT:PRINT " Die Wuerfel zeigten":PRINT
113 PRINT STR$(W1);" und";STR$(W2);"."
115 PRINT:IF W1&lt;&gt;W2 THEN 123
117 PAUSE 15:PRINT " Das ist ein Pasch!"
119 PA=PA+1:IF PA&lt;3 THEN 123
121 PAUSE 10:GOSUB 759:GOSUB 543:PAUSE 10:GOSUB 569:PAUSE 5:GOTO 147
123 PAUSE 8:GOSUB 543:PAUSE 8:GOSUB 569:PAUSE 8:GOSUB 511
125 PRINT:PRINT " Ihr Standort ist:"
127 PRINT:PRINT " ";FE$(AK):PRINT:PAUSE 15
129 IF FE(AK,6)&lt;6 THEN 217
131 IF FE(AK,2)&lt;&gt;0 THEN 151
133 GOSUB 513:PRINT:PRINT "Wollen Sie " :PRINT "   kaufen ?":PAUSE 10
135 PRINT:PRINT:PRINT "Preis:";STR$(FE(AK,5)):PRINT "îîîîî"        
137 GOSUB 601:IF ZX$&lt;&gt;"J"THEN 147
139 IF SP(SS,1)&gt;=FE(AK,5)THEN 145:ELSE PRINT:PRINT " Sie sind "      
141 PRINT:PRINT "nicht liquid":PRINT "und muessen":PRINT "  HANDELN"
143 PAUSE 15:FO=1:GOSUB 647:FO=0:GOTO 133
145 GOSUB 579:IF SP(SS,4)=0 THEN 229
147 GOSUB 511:PRINT " Wollen Sie handeln ?":GOSUB 601:IF ZX$&lt;&gt;"J" THEN 229
149 GOSUB 647:GOTO 147
151 IF FE(AK,2)&lt;&gt;SS THEN 157
153 GOSUB 513:PRINT " Hier sind":PRINT " SIE selbst":PRINT:PRINT " Besitzer"
155 GOSUB 601:GOTO 147
157 GOSUB 513:PRINT:PRINT "Sie muessen   Miete":PRINT " zahlen an:"  
159 PRINT:PRINT SP$(FE(AK,2),1):MZ=FN MI(200*RND(5)):GOSUB 589:GOSUB 601
161 GOSUB 511:PRINT " Mietrechnung:":PRINT
163 IF FE(AK,6)&gt;6 THEN 177
165 XY=0:XX=250:FOR XI=6 TO 36 STEP 10
167 IF FE(XI,2)=FE(AK,2)THEN XX=XX*2:XY=XY+1
169 NEXT XI:IF XY=1 THEN PRINT " Besitzer hat einen":PRINT " Bahnhof.":GOTO 173
171 PRINT " Besitzer hat";STR$(XY):PRINT " Bahnhoefe."
173 PRINT:PRINT " Grundwert:";STR$(XX):MI=FN MI(XX*MK)+MZ
175 PRINT " Miete    :";STR$(MI):GOTO 207
177 IF FE(AK,6)&gt;7 THEN 187
179 IF FE(13,2)=FE(29,2)THEN GOTO 183:ELSE XX=80*W3:MI=FN MI(XX*MK)+MZ
181 PRINT " Besitzer hat ein":PRINT " Werk.":GOTO 185
183 PRINT " Besitzer hat beide":PRINT " Werke.":XX=200*W3:MI=FN MI(XX*MK)+MZ
185 PRINT:PRINT " Grundwert:";STR$(XX):PRINT " Miete    :";STR$(MI):GOTO 207
187 XX=1.5:FOR XI=2 TO 40 
189 IF FE(XI,6)&lt;&gt;FE(AK,6)THEN 193
191 IF FE(XI,2)&lt;&gt;FE(AK,2)THEN XX=1:GOTO 195
193 NEXT XI
195 XY=FE(AK,5)/8*XX:MI=FN MI(XY*MK)+MZ:IF XX=1 THEN ZN=5:GOTO 199
197 PRINT " Besitzer hat die":PRINT " ganze Gruppe.":PRINT:ZN=8
199 PRINT " Grundwert:";STR$(XY):PRINT " Miete    :";STR$(MI) 
201 IF FE(AK,1)=0 THEN 207
203 FOR XI=1 TO FE(AK,1)
205 PAUSE 10:BEEP:MI=MI*2:PRINT AT(ZN,12);STR$(MI):NEXT XI
207 GOSUB 601:BE=MI:GOSUB 629:IF BE=0 THEN MI=RT:GOTO 211
209 SP(SS,2)=SP(SS,2)-MI
211 XX=FE(AK,2):SP(XX,1)=SP(XX,1)+MI:SP(XX,2)=SP(XX,2)+MI
213 IF BE=0 THEN 235:ELSE PAUSE 10:GOTO 147
215 IF RU/SX-INT(RU/SX)=0 THEN GOSUB 469SW=1:GOSUB 601
217 IF FE(AK,6)=0 THEN 147
219 IF FE(AK,6)=1 THEN GOSUB 763:GOTO 147
221 IF FE(AK,6)=2 THEN GOSUB 745:GOTO 147
223 IF FE(AK,6)=3 THEN GOSUB 753:GOTO 147
225 IF FE(AK,6)=4 THEN 147
227 IF FE(AK,6)=5 THEN GOSUB 759:GOTO 147:ELSE STOP
229 IF SP(SS,5)=-3 THEN 235
231 IF SP(SS,5)&gt;0 THEN SP(SS,5)=AK
233 IF W1=W2 THEN 37
235 RU=RU+1:PA=0:SS=SS+1:IF SS&gt;SX THEN SS=1
237 IF SP(SS,5)=0 THEN 235
239 GOTO 37
241 DEF FN PO(PO)=PO+1-(INT(PO/40)*40)
243 DEF FN MI(M)=INT(M/20)*20 
245 MK=1:GK=0:RU=1:SX=1
247 DATA 22,15,19,10,13,7,9,5,6,19,22,13,15,9,10,6,7,5
249 DIM ST$(18):DIM ST(18,2)
251 FOR I=1 TO 18 STEP 2
253 READ ST(I,1),ST(I+1,1) 
255 ST(I,2)=46-(2*ST(I,1))
257 ST$(I)=STRING$(ST(I,1)-1," ")
259 ST(I+1,2)=46-(2*ST(I+1,1))
261 ST$(I+1)=STRING$(ST(I+1,1)-1," ")
263 NEXT I
265 ST$(1)=STRING$(ST(1,1)-1," ")
267 ST$(18)=STRING$(ST(18,1)-1," ")
269 ZS$="Ë":GOSUB 409
271 DIM FE$(40):DIM FE(40,6)
273 DATA "* LOS *",0,0,0,"Badstr.",5,1200,8,"* Gemeinsch. *",0,0,1
275 DATA "Turmstr.",1,1200,8,"* Steuer *",0,4000,3,"Suedbahnhof",0,4000,6
277 DATA "Chausseestr.",2,2000,9
279 DATA "Elisenstr.",2,2000,9,"* Ereignis *",0,0,2,"Poststr.",2,2400,9  
281 DATA "Am Gefaengnis",0,0,4,"Seestr.",0,2800,10,"E.-Werke",0,3000,7
283 DATA "Hafenstr.",0,2800,10,"Neue Strasse",0,3200,10
285 DATA "Westbahnhof",0,4000,6,"Muenchner Str.",5,3600,11
287 DATA "Wiener Str.",5,3600,11,"* Gemeinsch. *",0,0,1
289 DATA "Berliner Str.",5,4000,11,"* Parken *",0,0,4
291 DATA "Theaterstr.",1,4400,12,"* Ereignis *",0,0,2
293 DATA "Museumsstr.",2,4400,12,"Opernpl.",2,4800,12,"Nordbahnhof",0,4000,6
295 DATA "Lessingstr.",0,5200,13,"Schillerstr.",0,5200,13
297 DATA "Wasserwerke",0,3000,7,"Goethestr.",1,5600,13
299 DATA "* Ins Gefaengnis *",0,0,5,"Bahnhofstr.",5,6000,14
301 DATA "Rathauspl.",5,6000,14,"* Gemeinsch. *",0,0,1,"Gruene Str.",5,6400 
303 DATA 14,"Hauptbahnhof",0,4000,6,"* Ereignis *",0,0,2,"Parkstr.",1,7000
305 DATA 15,"* Verm.-St. *",0,2000,3,"Schlossallee",1,8000,15
307 FOR I=1 TO 40
309 READ FE$(I),FE(I,1),FE(I,5),FE(I,6):NEXT I
311 DIM SC$(11)
313 DATA "Ç         P"
315 DATA " ¨       © "
317 DATA " ¡       ¡ "
319 DATA "         "
321 DATA "     ·³    "
323 DATA "    ·³·³   "
325 DATA "   ·³·³·³  "
327 DATA "           "
329 DATA " ¡       ¡ "
331 DATA " §       ª "
333 DATA "L         G"
335 FOR I=1 TO 11:READ SC$(I):NEXT I
337 AD=0:AK=1:GOSUB 519:PRINT " MONOPOLY":WINDOW
339 AK=FN PO(AK)
341 GOSUB 343:IF INKEY$=""AND AK&lt;40 THEN 339:ELSE RETURN
343 BEEP:ZN=9:GOSUB 437
345 PRINT AT(16, 9);"":PRINT AT(15,11);"":PRINT AT(13,29);CHR$(144) 
347 ZN=7:GOSUB 437
349 ZN=6:GOSUB 437
351 GOSUB 451
353 FOR J=1 TO 4 
355  PRINT AT(ST(J,1),ST(J,2));ST$(J)
357 NEXT J
359 FOR J=10 TO 15
361 PRINT AT(ST(J,1),ST(J,2)-2);ST$(J)
363 NEXT J
365 AX=AK
367 IF FE(AX,1)=0 THEN GOTO 377
369 PRINT AT(21,19);" ÅÅÅ ":PRINT AT(20,19);" ÅÅÅ " 
371 PRINT AT(19,19);"ÅÅÅ"
373 PRINT AT(18,20);" ":PRINT AT(17,21);"":IF FE(AK,1)=5 THEN GOTO 377
375 PRINT AT(20,21);RIGHT$(STR$(FE(AK,1)),1)
377 AX=FN PO(AX):IF FE(AX,1)=0 THEN GOTO 383
379 PRINT AT(14,25);"ÅÅ":PRINT AT(13,24);"ÅÅ"
381 PRINT AT(12,25);""
383 AX=FN PO(AX):IF FE(AX,1)=0 THEN GOTO 387
385 PRINT AT(9,28);"Å":PRINT AT(8,28);"" 
387 AX=FN PO(AX):IF FE(AX,1)&lt;&gt;0 THEN PRINT AT(6,31);"²" 
389 PRINT AT(21,5);FE$(AK)
391 ZN=7:GOSUB 437
393 ZN=9:GOSUB 437
395 ZN=14:GOSUB 437
397 PRINT AT(16,9 );"":PRINT AT(15,12);""
399 IF AK&lt;&gt;1 THEN 405
401 GOSUB 511:PRINT "Sie bekommen Gehalt.":PRINT:FOR I=1 TO 4:BEEP:NEXT
403 PRINT "   &gt;&gt; 4000 Mark &lt;&lt; ":SP(SS,1)=SP(SS,1)+4000 
405 RETURN
407!
409! UP ANFANGSBILD STRICHE
411 FOR SI=1 TO 13
413 PRINT AT(24-SI,18+SI);"":NEXT SI
415 FOR SI=1 TO 15
417 PRINT AT(5+SI,32-(2*SI));""
419 PRINT AT(5+SI,31-(2*SI));"":NEXT SI
421 FOR SI=12 TO 13:PRINT AT(7,31);" "
423 PRINT AT(ST(SI,1) ,ST(SI,2)-2)  ;ST$(SI)     
425  NEXT SI: PRINT AT(7,30);"  "
427 GOSUB 451
429 SA=12:SN=12:ZA=14:ZN=14
431 GOSUB 437:PRINT AT(16,9 );"":PRINT AT(15,12);""
433 RETURN
435!
437! UP SPIELFIGUR
439 FOR ZI=ZA TO ZA+4     
441  PRINT AT(ZI,SA);"    ":NEXT ZI
443 PRINT AT(ZN,SN);" ®­ "
445 PRINT AT(ZN+1,SN);" «¬ ":PRINT AT(ZN+2,SN);"  "
447 PRINT AT(ZN+3,SN);"¬øø«":PRINT AT(ZN+4,SN);"ø",ZS$,"È"
449 ZA=ZN:SA=SN:PRINT AT(13,16);CHR$(147):RETURN
451!
453! UP HAEUSER LOESCHEN
455 PRINT AT(6,31);" ":PRINT AT(9,28);" ":PRINT AT(8,28);" "
457 PRINT AT(12,25);"  ":PRINT AT(21,19);"     ":PRINT AT(20,19);"     "
459 PRINT AT(19,19);"     ":PRINT AT(18,20);"   ":PRINT AT(13,24);"    "
461 PRINT AT(14,25);"  ":PRINT AT(17,21);" "
463 PRINT AT(21,3);"                   "
465 RETURN
467!
469 WINDOW:CLS:PRINT AT(0,5);"BESITZERUEBERBLICK  :"
471 PRINT AT(2,0);"ÇVermoegen/ ÅImmobilien/ ÆMieten"     
473 GU=0:IM=0:MT=0:FOR GX=1 TO SX
475  PRINT AT(23,GX*6-3);LEFT$(SP$(GX,1),5)
477 PRINT AT(22,GX*6-3);"     ":IF GU&lt;SP(GX,1)THEN GU=SP(GX,1)
479 IF IM&lt;SP(GX,4)THEN IM=SP(GX,4)
481 IF MT&lt;ABS(SP(GX,2))THEN MT=ABS(SP(GX,2))
483 NEXT GX:PRINT AT(27,0);"MaxÇ",STR$(GU)
485 PRINT AT(27,11);"MaxÅ",STR$(IM):PRINT AT(27,22);"MaxÆ",STR$(MT)
487 GU=GU/16:IM=IM/16:MT=MT/16
489 FOR GX=1 TO SX:GY=6*GX-3
491 IF SP(GX,1)=0 THEN 495
493 FOR GZ=1 TO SP(GX,1)/GU:PRINT AT(22-GZ,GY);"Ç":NEXT GZ
495 IF SP(GX,4)=0 THEN 499
497 FOR GZ=1 TO SP(GX,4)/IM:PRINT AT(22-GZ,GY+2);"Å":NEXT GZ
499 IF SP(GX,2)=0 THEN 505
501 FOR GZ=1 TO ABS(SP(GX,2)/MT):PRINT AT(22-GZ,GY+4);"Æ":NEXT GZ
503 IF SP(GX,2)&lt;0 THEN PRINT AT(21,GY+4);"M" 
505 NEXT GX:SW=1:GOSUB 601:CLS
507 RETURN
509 WO=0:WU=23:WL=0:WR=31:GOSUB 523:GOTO 521
511 WO=0:WU=10:WL=0:WR=23:GOSUB 523:GOTO 521
513 WO=3:WU=16:WL=18:WR=31:GOSUB 523:GOTO 521
515 WO=18:WU=30:WL=19:WR=31:GOSUB 523:GOTO 521
517 WO=19:WU=30:WL=0:WR=7:GOSUB 523:GOTO 521
519 WO=0:WU=4:WL=2:WR=19:GOSUB 523:GOTO 521
521 RETURN
523 WINDOW WO,WU,WL,WR:CLS      
525 WINDOW WO+2,WU-1,WL+1,WR-1
527 PRINT AT(WO,WR);"©":PRINT AT(WU,WR);"ª":PRINT AT(WU,WL);"§"
529 PRINT AT(WO,WL);"¨"
531 FOR WI=WL+1 TO WR-1:PRINT AT(WO,WI);" ":NEXT WI       
533 FOR WI=WO+1 TO WU-1:PRINT AT(WI,WR);"¡":NEXT WI
535 FOR WI=WR-1 TO WL+1 STEP -1:PRINT AT(WU,WI);" ":NEXT WI
537 FOR WI=WU-1 TO WO+1 STEP -1:PRINT AT(WI,WL);"¡":NEXT WI
539  RETURN
541!
543 GOTO 545
545 GOSUB 515:WINDOW     
547 FOR WI=1 TO 11
549 PRINT AT(18+WI,20);SC$(WI):NEXT WI
551 FOR WI=2 TO 10:PRINT AT(30-WI,20);SP$(FE(WI,2),2):NEXT WI
553 FOR WI=12 TO 20:PRINT AT(19, 9+WI);SP$(FE(WI,2),2):NEXT WI
555 FOR WI=22 TO 30:PRINT AT(WI-2,30);SP$(FE(WI,2),2):NEXT WI
557 FOR WI=32 TO 40:PRINT AT(29,61-WI);SP$(FE(WI,2),2):NEXT WI 
559 IF AK&lt;11 THEN PRINT AT(30-AK,20);"":PAUSE 5:RETURN
561 IF AK&lt;21 THEN PRINT AT(19,9+AK);"":PAUSE 5:RETURN
563 IF AK&lt;31 THEN PRINT AT(AK-2,30);"":PAUSE 5:RETURN
565  PRINT AT(29,61-AK);"":PAUSE 5:RETURN
567 GOSUB 519:PRINT " ";SP$(SS,1):WINDOW:RETURN
569 GOSUB 517:WINDOW
571 PRINT AT(22,1);"Ver-":PRINT AT(23,1);"moegen"
573 PRINT AT(25,1);RIGHT$(STR$(SP(SS,1)),6)
575 PRINT AT(27,1);"Immob.":PRINT AT(29,1);RIGHT$(STR$(SP(SS,4)),6)
577 RETURN
579 GOTO 581
581 SP(SS,4)=SP(SS,4)+FE(AK,5)
583 SP(SS,1)=SP(SS,1)-FE(AK,5)
585 FE(AK,2)=SS:PAUSE 5:GOSUB 543:PAUSE 10:GOSUB 569
587 RETURN
589 GOTO 591
591 GOSUB 517:WINDOW
593 PRINT AT(22,1);"Miet-":PRINT AT(23,1);"koeff."
595 PRINT AT(25,1);LEFT$(STR$(MK),6)
597 PRINT AT(27,1);"Zuschl":PRINT AT(29,1);STR$(MZ)
599 RETURN
601! UP EINGABE
603 IF SW=1 THEN 607:ELSE RX=INT(RU/SX)+1
605 PRINT AT(31,8 );STR$(RX),".",SP$(SS,1):PRINT AT(31,19);"(@/$/#/ENT)"
607 XX$=INKEY$:IF XX$="#"THEN DE=0
609 ZX$=INKEY$:IF ZX$&lt;&gt;"" THEN 613
611 PRINT AT(31,31);"É":PRINT AT(31,31);"":IF DE=0 THEN 609:ELSE CLS:GOTO 619
613 BEEP:PAUSE 1:XX$=INKEY$
615 IF ZX$="@"THEN GOSUB 469:GOTO 609
617 IF ZX$="$"THEN DE=1:ELSE GOTO 627
619 IF(RND(5)&gt;.6 OR RU/SX&lt;RND(2)*10)AND ZF$&lt;&gt;"J"THEN ZX$="J":GOTO 625
621 ZF=INT(RND(4)*5)+1:IF ZF*RND(6)&gt;3 THEN 621
623 ZX$=RIGHT$(STR$(ZF),1)
625 PRINT AT(31,10);ZX$:PAUSE 15:ZF$=ZX$
627 PRINT AT(31,9);"                      ":SW=0:RETURN 
629 IF BE&gt;SP(SS,1)THEN PAUSE 10:PRINT:ELSE GOTO 641
631 PRINT:PRINT " Sie sind":PRINT:PRINT "  NICHT":PRINT:PRINT "liquid !"
633 GOSUB 569:PAUSE 20
635 IF SP(SS,4)=0 THEN GOSUB 739:BE=0:GOTO 645
637 FO=1:GOSUB 647:FO=0:IF SP(SS,1)&gt;=BE THEN 641
639 GOSUB 511:PRINT:PRINT " Forderung:";STR$(BE):GOSUB 601:GOTO 635
641 SP(SS,1)=SP(SS,1)-BE:GOSUB 511:PRINT:PRINT "   Es genuegt !"
643 PAUSE 10:GOSUB 569
645 RETURN
647 GOSUB 511:PRINT "Handeln von Immobilien":PAUSE 3:PRINT
649 XY=0:XX=0:FOR XI=1 TO 40
651 IF FE(XI,2)=SS THEN 655
653 IF XI=40 THEN 659:ELSE 667
655 PRINT STR$(XX+1);".";FE$(XI);" (";RIGHT$(STR$(FE(XI,1)),1);")"
657 XX=XX+1:XZ(XX)=XI:IF XX&lt;5 AND XI&lt;40 THEN 667
659 IF XX=0 THEN 669
661 PRINT:PRINT " Was?":GOSUB 601:XZ=VAL(ZX$):IF XZ&gt;XX OR XZ&lt;1 THEN 665
663 XY=XZ(XZ):GOTO 669
665 XX=0:PRINT
667 NEXT XI
669 IF XY=0 THEN 737
671 CLS:PRINT " ";FE$(XY);" (";RIGHT$(STR$(FE(XY,1)),1);")"
673 PRINT:PRINT " Wert:";STR$(FE(XY,5)):PRINT
675 IF FE(XY,6)&lt;8 OR FE(XY,1)&gt;4 OR FO=1 THEN 679
677 PRINT " Hauskauf ?":GOSUB 601:IF ZX$="J"THEN 705
679 IF FE(XY,1)=0 THEN 687
681 PRINT " Hausverkauf ?":GOSUB 601:IF ZX$&lt;&gt;"J"THEN 687
683 IF FO=0 THEN PRINT:PRINT " Wirklich?":ELSE:GOTO 723
685 GOSUB 601:IF ZX$="J"THEN 723
687 PRINT " Verkauf ?":GOSUB 601:IF ZX$&lt;&gt;"J"THEN 737
689 IF FO=0 THEN PRINT:PRINT " Wirklich?":ELSE:GOTO 693
691 GOSUB 601:IF ZX$="J"THEN 693:ELSE:GOTO 737
693 GOSUB 513:XX=0:IF FE(XY,1)=0 THEN 699
695 PRINT "  Verkauf":PRINT "  Haeuser:":XX=FE(XY,1)*INT((XY-1)/10+1)*500 
697 PRINT STR$(XX):PRINT
699 PRINT " VERKAUF":PRINT " gesamt:":PRINT:XZ=XX+FE(XY,5):PRINT STR$(XZ)
701 SP(SS,1)=SP(SS,1)+XZ:SP(SS,4)=SP(SS,4)-XX*2-FE(XY,5):FE(XY,1)=0
703 FE(XY,2)=0:GOSUB 601:GOTO 735
705 GOSUB 513:PRINT:PRINT " Preis pro":PRINT " Haus:"
707 PRINT:XX=INT((XY-1)/10+1)*1000:PRINT STR$(XX):PRINT:PRINT " Wieviele ?"
709 PRINT:GOSUB 601:XZ=VAL(ZX$):IF XZ&gt;=0 AND XZ&lt;=5-FE(XY,1)THEN 713
711 PRINT "Geht nicht!":GOTO 709
713 PRINT STR$(XZ):IF SP(SS,1)&gt;=XZ*XX THEN 719
715 PRINT:PRINT "So viel Geld":PRINT " haben Sie":PRINT " NICHT !":PAUSE 15    
717 GOTO 705
719 FE(XY,1)=FE(XY,1)+XZ 
721 SP(SS,1)=SP(SS,1)-XZ*XX:SP(SS,4)=SP(SS,4)+XZ*XX:GOTO 735
723 GOSUB 513:PRINT:PRINT " Verkauf":PRINT "  von":PRINT " Haeuser:"
725 XX=INT((XY-1)/10+1)*500:PRINT:PRINT " Wieviele ?"
727 PRINT:GOSUB 601:XZ=VAL(ZX$)
729 IF XZ&lt;0 OR XZ&gt;FE(XY,1)THEN PRINT " Geht nicht !":GOTO 727
731 PRINT STR$(XZ):FE(XY,1)=FE(XY,1)-XZ
733 SP(SS,1)=SP(SS,1)+XZ*XX:SP(SS,4)=SP(SS,4)-2*XZ*XX:GOTO 735
735 PAUSE 10:GOSUB 543:PAUSE 5:GOSUB 569
737 RETURN
739 GOSUB 511:PRINT:PRINT " Spieler:":PRINT:PRINT " ";SP$(SS,1):PRINT
741 PRINT " muss ausscheiden.":SP(SS,5)=0:PRINT:GOSUB 601
743 RT=SP(SS,1):SP(SS,1)=0:SP(SS,2)=0:SP(SS,3)=0:SP(SS,4)=0:RETURN
745 GOSUB 513:PRINT:PRINT "Neuer Miet-":PRINT
747 MK=2*RND(9):IF MK&lt;0.05 THEN 747
749 PRINT "koeffizient:":PRINT:PRINT " ";LEFT$(STR$(MK),6)
751 GOSUB 601:RETURN
753 GOSUB 513:PRINT:PRINT " Steuer-":PRINT "zahlung:":PRINT
755 BE=FE(AK,5):PRINT STR$(BE):PRINT:GOSUB 601
757 GOSUB 629:SP(SS,3)=SP(SS,3)+BE:RETURN
759 GOSUB 513:PRINT:PRINT " Sie gehen":PRINT "  in das":PRINT " Gefaengnis" 
761 SP(SS,5)=-3:GOSUB 601:RETURN
763 IF SP(SS,4)&lt;30000 AND RND(2)*SP(SS,4)&lt;5000 AND GK&gt;0 THEN 771
765 GOSUB 513:PRINT:PRINT " Zahlung an     die"
767 PRINT:PRINT "Gemeinschaft":PRINT:BE=FN MI(SP(SS,4)*(RND(4)*.1+.05)+20)
769 PRINT " Summe:";STR$(BE):GOSUB 601:GOSUB 629:GK=GK+BE:GOTO 779
771 GOSUB 513:PAUSE 10:BEEP:BEEP:BEEP:PAUSE 3:BEEP:BEEP:BEEP
773 PAUSE 3:BEEP:BEEP:BEEP:PAUSE 10:PRINT:PRINT "Ausschuettung":PRINT
775 PRINT "  Sie":PRINT " erhalten:":PRINT:PRINT STR$(GK):PRINT
777 PAUSE 10:SP(SS,1)=SP(SS,1)+GK:GK=0:GOSUB 569:GOSUB 601
779 RETURN
</pre>
</div>
</body>
</html>
