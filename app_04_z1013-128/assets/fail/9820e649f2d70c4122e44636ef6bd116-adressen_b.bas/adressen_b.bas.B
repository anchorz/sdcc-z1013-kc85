Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

 1000 A=1000:GOTO 20:REM ### MINI-ADRESSENVERWALTUNG ###
 1010 DIM A$(5),S$(5)
 1020 A$(1)="Name Vorame:"
 1030 A$(2)="Strasse Nr.:"
 1040 A$(3)="PLZ Ort    :"
 1050 A$(4)="Telefon    :"
 1060 A$(5)="Bemerkung  :"
 1070 DIM P$(5)
 1080 P$(1)=" EINGABE "
 1090 P$(2)=" AUSGABE "
 1110 P$(3)="  LADEN  "
 1120 P$(4)="SPEICHERN"
 1130 P$(5)=" AENDERN "
 1140 P$(0)="M E N U E"
 1150 DIM D$(150,5)
 1160 GOTO 7030
 1200 REM --------------
 1210 REM  UEBERSCHRIFT
 1220 REM --------------
 1230 GOSUB 100
 1240 SR$=" ADRESSENVERWALTUNG "
 1250 PRINT "    ";:GOSUB 150:PRINT
 1260 PRINT
 1270 RETURN
 1300 REM ----------------
 1310 REM  PROGRAMMKOEPFE
 1320 REM ----------------
 1330 PRINT "          *************"
 1340 PRINT "          * ";P$(X);" *"
 1350 PRINT "          *************"
 1360 PRINT
 1370 RETURN
 1400 REM -------
 1410 REM  MENUE
 1420 REM -------
 1430 PRINT:PRINT "    ";:SR$="1":GOSUB 150:PRINT;
 1440 PRINT "      ";P$(1):PRINT
 1450 PRINT "    ";:SR$="2":GOSUB 150:PRINT;
 1460 PRINT "      ";P$(2):PRINT
 1470 PRINT "    ";:SR$="3":GOSUB 150:PRINT;
 1480 PRINT "     ";P$(3):PRINT
 1490 PRINT "    ";:SR$="4":GOSUB 150:PRINT;
 1500 PRINT "       ";P$(4):PRINT
 1510 PRINT "    ";:SR$="5":GOSUB 150:PRINT;
 1520 PRINT "      ";P$(5):PRINT:PRINT:PRINT
 1530 PRINT:PRINT "         Bitte auswaehlen !"
 1550 X=0:GOSUB 210
 1560 IF IN$="1" THEN X=1
 1570 IF IN$="2" THEN X=2
 1580 IF IN$="3" THEN X=3
 1590 IF IN$="4" THEN X=4
 1600 IF IN$="5" THEN X=5
 1610 IF(X>0)AND(X<6) THEN 1630
 1620 GOTO 1550
 1630 RETURN
 1700 REM --------------------
 1710 REM  DATEN IM RECHNER ?
 1720 REM --------------------
 1730 IF Z>0 THEN 1770
 1740 F=1:GOSUB 250:GOSUB 250:GOSUB 250:PRINT:PRINT
 1750 PRINT " ----- Keine Daten im Rechner ! -----"
 1760 SD=20:GOSUB 450
 1770 RETURN
 1800 REM -------------------
 1810 REM  KASSETTE EINLEGEN
 1820 REM -------------------
 1830 PRINT:PRINT " --- Kassette einlegen und <TASTE> ---"
 1840 GOSUB 210
 1850 RETURN
 2000 REM ---------
 2010 REM  EINGABE
 2020 REM ---------
 2030 GOSUB 1230:GOSUB 1330
 2040 Z=Z+1
 2050 FOR I=1 TO 5
 2060 PRINT A$(I);:INPUT D$(Z,I)
 2070 PRINT
 2080 NEXT I:PRINT:PRINT:PRINT:PRINT
 2090 PRINT "     Weitere Eingaben ?   (J/N)"
 2100 GOSUB 210
 2110 IF IN$="N" THEN 2990
 2120 IF IN$="J" THEN 2140
 2130 GOTO 2100
 2140 GOTO 2030
 2990 RETURN
 3000 REM ---------
 3010 REM  AUSGABE
 3020 REM ---------
 3030 GOSUB 1230:GOSUB 1330
 3040 PRINT:PRINT:PRINT:PRINT
 3050 PRINT "    Beliebigen Suchbegriff eingeben !"
 3060 PRINT:PRINT:PRINT:PRINT
 3080 PRINT:PRINT:PRINT
 3090 SR$="<TASTE>":PRINT "          ";:GOSUB 150:PRINT
 3100 GOSUB 210
 3110 GOSUB 1230:GOSUB 1330
 3120 FOR I=1 TO 5:S$(I)=""
 3130 PRINT A$(I);:INPUT S$(I)
 3140 PRINT
 3150 NEXT I:K=0
 3160 FOR Y=1 TO Z
 3170 S=0
 3180 FOR I=1 TO 5
 3190 IF S$(I)="" THEN S=S+1:GOTO 3210
 3200 IF LEFT$(D$(Y,I),LEN(S$(I)))=S$(I) THEN S=S+1
 3210 NEXT I
 3220 IF S<>5 THEN 3310
 3230 GOSUB 1230:GOSUB 1330
 3240 FOR I=1 TO 5
 3250 PRINT A$(I);D$(Y,I)
 3260 PRINT:K=1
 3270 NEXT I
 3280 PRINT:PRINT:PRINT:PRINT "      ";
 3290 SR$="<TASTE>":GOSUB 150:PRINT
 3300 GOSUB 210
 3310 NEXT Y
 3320 IF K=1 THEN RETURN
 3330 GOSUB 1230:GOSUB 1330
 3340 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
 3350 GOSUB 250:GOSUB 250:GOSUB 250
 3360 PRINT " --- Diese Datei existiert nicht ! ---"
 3370 SD=20:GOSUB 450
 3990 RETURN
 4000 REM -------
 4010 REM  LADEN
 4020 REM -------
 4030 GOSUB 1830:NF$="A-DATEI":NF=0:GOSUB 500
 4040 GOSUB 540:Z=VAL(IN$)
 4050 FOR Y=1 TO Z:FOR I=1 TO 5
 4060 GOSUB 540
 4070 D$(Y,I)=IN$
 4080 NEXT I
 4090 NEXT Y
 4100 GOSUB 580
 4990 RETURN
 5000 REM -----------
 5010 REM  SPEICHERN
 5020 REM -----------
 5030 GOSUB 1830:NF$="A-DATEI":NF=1:GOSUB 500
 5040 SR=Z:GOSUB 300:GOSUB 560
 5050 FOR Y=1 TO Z:FOR I=1 TO 5
 5060 SR$=D$(Y,I)
 5070 GOSUB 560
 5080 NEXT I
 5090 NEXT Y
 5100 GOSUB 580
 5990 RETURN
 6000 REM ---------
 6010 REM  AENDERN
 6020 REM ---------
 6030 GOSUB 1230:GOSUB 1330
 6040 PRINT:PRINT
 6050 SR$="V":PRINT "     ";:GOSUB 150:PRINT;
 6060 PRINT "    Vorwaerts blaettern":PRINT
 6070 SR$="R":PRINT "     ";:GOSUB 150:PRINT;
 6080 PRINT "    Rueckwaerts blaettern":PRINT
 6090 SR$="A":PRINT "     ";:GOSUB 150:PRINT;
 6100 PRINT "    Aendern":PRINT
 6110 SR$="L":PRINT "     ";:GOSUB 150:PRINT;
 6120 PRINT "    Loeschen":PRINT
 6130 SR$="M":PRINT "     ";:GOSUB 150:PRINT;
 6140 PRINT "    Menue"
 6150 PRINT:PRINT:PRINT:PRINT "          ";:SR$="<TASTE>"
 6160 GOSUB 150:PRINT:ZZ=0
 6170 GOSUB 210:IF(IN$="L")OR(IN$="A") THEN 6170
 6180 GOSUB 210:IF(IN$="V")AND(ZZ<Z) THEN ZZ=ZZ+1:GOTO 6240
 6190 IF(IN$="R")AND(ZZ>1) THEN ZZ=ZZ-1:GOTO 6240
 6200 IF IN$="M" THEN 7030
 6210 IF IN$="A" THEN 6450
 6220 IF IN$="L" THEN 6300
 6230 GOTO 6180
 6240 GOSUB 1230:GOSUB 1330
 6250 FOR I=1 TO 5
 6260 PRINT I;") ";A$(I);D$(ZZ,I)
 6270 PRINT
 6280 NEXT I
 6290 GOTO 6180
 6300 PRINT:PRINT:PRINT:PRINT
 6310 PRINT "   Diese Adresse wirklich loeschen ?"
 6320 PRINT
 6330 PRINT "           (J/N)"
 6340 GOSUB 210
 6350 IF IN$="N" THEN 6240
 6360 IF IN$="J" THEN 6380
 6370 GOTO 6340
 6380 IF ZZ=Z THEN ZZ=ZZ-1:GOTO 6430
 6390 FOR Y=ZZ TO Z-1:FOR I=1 TO 5
 6400 D$(Y,I)=D$(Y+1,I)
 6410 NEXT I
 6420 NEXT Y
 6430 Z=Z-1
 6440 GOTO 6240
 6450 PRINT:PRINT:PRINT
 6460 INPUT "     Welche Nr.(1-5)";NR
 6470 IF(NR<1)OR(NR>5) THEN 6240
 6480 PRINT
 6490 INPUT "Neuer Inhalt ";D$(ZZ,NR)
 6500 GOTO 6240
 6990 RETURN
 7000 REM --------
 7010 REM  ANFANG
 7020 REM --------
 7030 GOSUB 1230:REM UEBERSCHRIFT
 7040 F=0:X=0:GOSUB 1330:REM PROGRAMMKOEPFE
 7050 GOSUB 1430:REM MENUE
 7060 IF(X=2)OR(X=4)OR(X=5) THEN GOSUB 1730
 7070 IF F=1 THEN 7030
 7090 ON X GOSUB 2030,3030,4030,5030,6030
 7100 GOTO 7030
30000 REM  -----------------------------
30010 REM  ----     BEMERKUNG       ----
30020 REM  -   UM DIE SEQ.DATEI ZU     -
30030 REM  - ERWEITERN,MUSS SIE VORHER -
30040 REM  - GELADEN WERDEN,DANN NEUE  -
30050 REM  -   DATEI SPEICHERN !       -
30060 REM  -----------------------------
32000 REM -----------------------------
32010 REM - BUCH '64 FUER EINSTEIGER' -
32020 REM -        ALS VORLAGE        -
32030 REM -UMGESCHRIEBEN FUER BASICODE-
32040 REM -      VON BEN ABRAHAM      -
32050 REM -    2000 NEUBRANDENBURG    -
32060 REM -     JAHNSTR.37            -
32070 REM -   AUF COMMODORE 64        -
32080 REM -   '''VIEL SPASS !'''      -
32090 REM -----------------------------
32100 REM
32110 REM ERSTAUSSTRAHLUNG: RADIO DDR
32120 REM-SPEZIAL 900119
