Zeichensatz UTF-8+Z1013()+CTRL()-ohne Umlaute(äöüß)

    1 WINDOW:CLS:WINDOW 3,31,0,31
    2 PRINT "  "
    3 PRINT "  "TAB(28)""
    4 PRINT "   *** COMPUTER - SKAT *** "
    5 PRINT "  "TAB(28)""
    6 PRINT "  "
    7 PRINT:PRINT:PRINT TAB(6)"Z 1013 - COMPUTER"
    8 PRINT:PRINT:PRINT TAB(4)"COMPUTER-CLUB robotron"
    9 PRINT:PRINT:PRINT "Implementiert von H.-J.BACHMANN"
   10 PRINT:PRINT:PRINT "   Verbessert von D. Ambras"
   11 WINDOW:GOSUB 77:CLS:PRINT AT(0,0);" ":CLEAR 1000
   12 A1$=CHR$(8):A2$=CHR$(9):A3$=CHR$(10):A4$=CHR$(11)
   13 A5$=STRING$(5,A1$)+A3$:A6$=""+A5$+"":C0$=A1$+" "+STRING$(5,A2$)
   14 A7$=A2$+" "+A5$+"":A8$="  "+A5$+"   "+A5$+"  "
   15 A9$=""+A5$+"  ":A0$=""+A5$+""+STRING$(6,A4$)
   16 AA$=STRING$(18,A2$):GOSUB 343
   17 PRINT AT(1,9);"*** SKAT ***"
   18 PRINT:PRINT:PRINT:PRINT:PRINT "Ich mische die Karten, verteile"
   19 PRINT:PRINT "und sortiere sie. Ich versichere"
   20 PRINT "Ihnen, dass ich Ihnen nicht in"
   21 PRINT:PRINT "die Karten schaue oder deren"
   22 PRINT:PRINT "Verteilung manipuliere."
   23 DIM B(3,12),K(32)
   24 ZV=INT(RND(1)*3)+1:Q=1
   25 PRINT:PRINT:PRINT "Einen Moment,"
   26 PRINT:PRINT "Ich mische die Karten."
   27 FOR I=1 TO 32:K(I)=1:NEXT
   28 FOR Z=1 TO 3:FOR T=1 TO 10
   29 FOR I=INT(RND(1)*31)+1 TO 32:IF K(I)=0 THEN NEXT:GOTO 29
   30 GOSUB 74
   31 K(I)=0:B(Z,T)=W:NEXT T:NEXT
   32 Z1=1:FOR I=1 TO 32:IF K(I)=0 THEN NEXT:GOTO 34
   33 GOSUB 74:R(Z1)=W:Z1=2:NEXT:Z1=0
   34 Z=1:GOSUB 250
   35 GOSUB 255:GOSUB 184:GOSUB 200
   36 GOSUB 235:Z=ZV:FOR RD=1 TO 10:GOSUB 83
   37 PRINT:PRINT STRING$(6,CHR$(10))
   38 PRINT "Dieser Stich geht an Spieler"Z;CHR$(8)",";
   39 IF Z=SP THEN PRINT:PRINT "dem Einzelspieler.";:ELSE PRINT CHR$(8);"."
   40 GOSUB 77
   41 ZF=Z:IF RD<10 THEN GOSUB 255
   42 Z=ZF:FB=0:NEXT RD
   43 CLS:PRINT "Wertung:"Q;A1$;". Spiel ("INT((Q-1)/3+1);A1$;". Runde )"
   44 FOR I=1 TO 2:Z1=VAL(RIGHT$(STR$(HR(I)),1)):W=WU(Z1):PT(SP)=PT(SP)+W:NEXT
   45 PRINT:IF SP<>1 THEN 48
   46 PRINT "Sie waren Einzelspieler"
   47 PRINT:PRINT "und erreichten"PT(SP)"Punkte.":GOTO 50
   48 PRINT "Einzelspieler war Spieler Nr."SP;A1$ "."
   49 PRINT "Er erreichte"PT(SP)"Punkte."
   50 PRINT:IF SF=1 AND PT(SP)>90 THEN FG=1:GOTO 53
   51 IF SF=0 AND PT(SP)>60 THEN FG=1:GOTO 53
   52 FG=0
   53 IF(FG=1 AND SP=1)OR(FG=0 AND SP<>1)THEN PRINT "Sie haben gewonnen.":GOTO 55
   54 PRINT "Sie haben verloren."
   55 ZV=ZV+1:IF ZV=4 THEN ZV=1
   56 IF PT(SP)>90 OR PT(SP)<30 THEN R5(SP)=R5(SP)+1:PRINT:PRINT "Schneider.";:TT=1
   57 IF SF=1 AND TT=1 THEN PRINT A1$ " & angesagt." 
   58 TT=0:PRINT:IF SF=1 THEN R5(SP)=R5(SP)+1
   59 IF HF=1 THEN R5(SP)=R5(SP)+1:HF=0
   60 IF GR=1 THEN GR=0:S2(SP)=24*(R5(SP)+1):GOTO 62
   61 S2(SP)=(R5(SP)+1)*R2(SP)
   62 PRINT AT(16,0);"Spielstand :"
   63 IF(PT(SP)<61)OR(PT(SP)<91 AND SF=1)THEN S2(SP)=-2*S2(SP)
   64 SF=0:IF S2(SP)>=GW OR FG=0 THEN 68
   65 PRINT "Moment...":IF SP=1 THEN PRINT "Sie haben sich ";:GOTO 67
   66 PRINT "SPIELER"SP "hat sich ";
   67 PRINT "ueberreizt.":PRINT "---verloren!":S2(SP)=-2*S2(SP) 
   68 IF KF=1 THEN S2(SP)=2*S2(SP):KF=0
   69 IF RF=1 THEN S2(SP)=2*S2(SP):RF=0
   70 PRINT AT(18,0);"Spieler1 Spieler2 Spieler3 Spiel"
   71 GP(SP)=GP(SP)+S2(SP):FOR I=0 TO 2:PRINT AT(20,I*09);GP(I+1):NEXT I
   72 PRINT AT(20,28);S2(SP)
   73 FOR I=1 TO 3:PT(I)=0:NEXT:Q=Q+1:GOSUB 77:GOTO 25
   74 W=(INT((I-1)/4)+1)+10*(((I-1)/4-INT((I-1)/4))*4+1)
   75 IF INT((I-1)/4)+1=8 THEN W=W+200
   76 RETURN
   77 PRINT AT(22,23);">ENTER<"
   78 XL=RND(1):P$=INKEY$:IF P$=CHR$(13)THEN 81
   79 QZ=QZ+1:IF QZ=15 THEN GOSUB 81
   80 IF QZ=30 THEN QZ=0:GOTO 77:ELSE 78
   81 PRINT AT(22,23);"       "
   82 RETURN
   83 FB=0:ON Z GOTO 84,85,85
   84 GOSUB 305:GOSUB 323:GOTO 95
   85 IF SP=Z THEN 90
   86 GOSUB 176
   87 IF H=0 THEN GOSUB 168
   88 IF H-10*INT(H/10)=6 THEN GOSUB 179
   89 SK(Z)=H:GOSUB 323:GOTO 95
   90 GOSUB 168:IF VAL(RIGHT$(STR$(H),1))>5 THEN GOSUB 166:GOTO 92
   91 GOTO 93
   92 IF VAL(RIGHT$(STR$(H),1))>5 AND H<200 THEN GOSUB 176:IF H=0 THEN GOSUB 166
   93 IF H=0 THEN GOSUB 176
   94 GOTO 89
   95 FB=INT(SK(Z)/10):Z=Z+1:IF Z>3 THEN Z=1
   96 IF Z=1 THEN GOSUB 305:GOSUB 323:GOTO 119
   97 IF Z=SP THEN 111
   98 IF SK(SP)>0 THEN 103
   99 GOSUB 171:IF H=0 THEN GOSUB 168
  100 IF H=0 THEN ON 1+INT(RND(1)*2)GOSUB 176,179
  101 SK(Z)=H:IF INT(H/10)<>FB AND H<100 THEN SK(Z)=-H
  102 GOSUB 323:GOTO 119
  103 IF SK(SP)>100 THEN 109
  104 GOSUB 171:IF H>SK(1)THEN 101
  105 IF H>0 THEN GOSUB 173:GOTO 101
  106 GOSUB 168
  107 IF H=0 THEN GOSUB 179
  108 GOTO 101
  109 GOSUB 168:IF H=0 THEN GOSUB 179
  110 GOTO 101
  111 ZW=Z-1:IF ZW=0 THEN ZW=3
  112 IF FB>9 THEN 117
  113 GOSUB 171:IF H>SK(ZW)THEN 101
  114 IF H>0 THEN GOSUB 173:GOTO 101
  115 GOSUB 168:IF H>0 THEN 101
  116 GOSUB 179:GOTO 101
  117 GOSUB 166:IF H=0 THEN GOSUB 179
  118 GOTO 101
  119 Z=Z+1:IF Z>3 THEN Z=1
  120 IF Z=1 THEN GOSUB 305:GOSUB 323:GOTO 157
  121 IF Z=SP THEN 139
  122 GOSUB 153:IF H1=SK(SP)THEN 129
  123 IF FB<10 THEN 126
  124 GOSUB 168:IF H=0 THEN GOSUB 176
  125 GOTO 137
  126 GOSUB 171:IF H=0 THEN GOSUB 176
  127 IF H=0 THEN GOSUB 168
  128 GOTO 137
  129 IF FB<9 THEN 132
  130 ON 1+INT(RND(1)*2)GOSUB 166,168:IF H>0 THEN 137
  131 GOSUB 179:GOTO 137
  132 GOSUB 171:IF H>SK(SP)THEN 137
  133 IF H>0 THEN GOSUB 173
  134 IF H=0 THEN GOSUB 168:IF H<SK(PS)THEN GOSUB 166
  135 IF H=0 THEN GOSUB 173
  136 IF H=0 THEN GOSUB 179
  137 SK(Z)=H:IF INT(H/10)<>FB AND H<100 THEN SK(Z)=-H
  138 GOSUB 323:GOTO 157
  139 IF FB>9 THEN 147
  140 GOSUB 171:GOSUB 153:IF H>H1 THEN 137
  141 IF H>0 THEN GOSUB 173:GOTO 137
  142 GOSUB 155:IF SS>8 THEN 146
  143 GOSUB 179:IF H=0 THEN GOSUB 168:GOTO 137
  144 IF H-10*INT(H/10)>4 THEN GOSUB 168
  145 GOTO 137
  146 GOSUB 168:IF H=0 THEN GOSUB 179
  147 GOSUB 155:IF SS>10 THEN 151
  148 GOSUB 168:IF H=0 THEN GOSUB 179:GOTO 137
  149 IF VAL(RIGHT$(STR$(H),2))<6 THEN 137
  150 GOSUB 166:GOTO 137
  151 GOSUB 168:IF H=0 THEN GOSUB 179
  152 GOTO 137
  153 H1=0:FOR I=1 TO 3:IF H1<SK(I)THEN H1=SK(I)
  154 NEXT:RETURN
  155 SS=0:FOR I=1 TO 3:Z1=VAL(RIGHT$(STR$(SK(I)),1)):W=WU(Z1):SS=SS+W
  156 NEXT:RETURN
  157 FOR Z1=1 TO 3:FOR I=1 TO 10:IF ABS(SK(Z1))=B(Z1,I)THEN B(Z1,I)=0
  158 NEXT:NEXT:IF SK(1)>SK(2)AND SK(1)>SK(3)THEN Z=1:GOTO 160
  159 GOTO 162
  160 IF INT(SK(1)/10)<>FB AND SK(1)<100 THEN 162
  161 GOTO 164
  162 IF SK(2)>SK(3)THEN Z=2:GOTO 164
  163 Z=3
  164 SS=0:FOR I=1 TO 3:Z1=VAL(RIGHT$(STR$(SK(I)),1)):W=WU(Z1)
  165 SS=SS+W:SK(I)=0:NEXT:PT(Z)=PT(Z)+SS:RETURN
  166 H=0:FOR I=1 TO 10:IF H<B(Z,I)AND B(Z,I)>100 THEN H=B(Z,I)
  167 NEXT:RETURN
  168 H=1000:FOR I=1 TO 10:IF H>B(Z,I)AND B(Z,I)>100 THEN H=B(Z,I)
  169 NEXT:IF H=1000 THEN H=0
  170 RETURN
  171 H=0:FOR I=1 TO 10:IF H<B(Z,I)AND INT(B(Z,I)/10)=FB THEN H=B(Z,I)
  172 NEXT:RETURN
  173 H=1000:FOR I=1 TO 10:IF H>B(Z,I)AND INT(B(Z,I)/10)=FB THEN H=B(Z,I)
  174 NEXT:IF H=1000 THEN H=0
  175 RETURN
  176 H1=0:H=0:FOR I=1 TO 10:IF H1<B(Z,I)-10*INT(B(Z,I)/10)AND B(Z,I)<100 THEN 178
  177 NEXT:H1=0:RETURN
  178 H=B(Z,I):H1=B(Z,I)-10*INT(B(Z,I)/10):GOTO 177
  179 H1=10:H=1000:FOR I=1 TO 10
  180 IF H1>VAL(RIGHT$(STR$(B(Z,I)),1))AND B(Z,I)<100 AND B(Z,I)>0 THEN 183
  181 NEXT:IF H=1000 THEN H=0
  182 RETURN
  183 H=B(Z,I):H1=VAL(RIGHT$(STR$(B(Z,I)),1)):GOTO 181
  184 FOR Z=1 TO 3:R1=0:R2(Z)=0:R3=0:R5(Z)=0
  185 FOR I=1 TO 4:K(I)=0:NEXT:Z1=0
  186 FOR I=1 TO 10:IF B(Z,I)>200 THEN Z1=Z1+1:K(Z1)=B(Z,I)
  187 NEXT:R1=Z1:Z1=0:FOR H=248 TO 218 STEP-10:GOSUB 188:NEXT H:GOTO 190
  188 Z1=Z1+1:FOR I=1 TO 4:IF K(I)=H THEN K(Z1+4)=1:RETURN
  189 NEXT:K(Z1+4)=0:RETURN
  190 FOR I=5 TO 7:IF K(I)=K(I+1)THEN NEXT:I=8
  191 R5(Z)=I-4:S3(Z)=R5(Z):FOR I=1 TO 4:K(I)=0:NEXT
  192 FOR I=1 TO 10:IF B(Z,I)<100 THEN FB=INT(B(Z,I)/10):K(FB)=K(FB)+1
  193 NEXT:FOR I=1 TO 4:IF R3<K(I)THEN R3=K(I):R2(Z)=I
  194 NEXT:R2(Z)=8+R2(Z):R4=R2(Z)*(R5(Z)+1):R8=0:FOR I=1 TO 10
  195 IF B(Z,I)-10*INT(B(Z,I)/10)=7 OR B(Z,I)-10*INT(B(Z,I)/10)=6 THEN R8=R8+1
  196 NEXT:R8=R8/2:R6=(R3-1+R1)*(5+R1)*R8:IF R6>R4 THEN R6=R4:S2(Z)=R4
  197 IF R6<18 THEN R7=18:GOTO 199
  198 READ R4:IF R4<=R6 THEN R7=R4:GOTO 198
  199 R6=R7:RESTORE:S1(Z)=R6:NEXT:RETURN
  200 GOSUB 77:PG=ZV:Z1=ZV:GOSUB 201:PH=Z1:GOSUB 201:SP=Z1:GOTO 203
  201 Z1=Z1+1:IF Z1<4 THEN RETURN
  202 Z1=1:RETURN
  203 GOSUB 209:IF H=0 THEN SP=PG:GOTO 207
  204 GOSUB 215:IF H=1 THEN 203
  205 PH=PG:GOTO 207
  206 GOSUB 209:IF H=0 THEN SP=PH:GOTO 220
  207 GOSUB 215:IF H=1 THEN 206
  208 GOTO 220
  209 IF SP=1 THEN 212
  210 READ R4:PRINT:PRINT SP;A1$;"> ";:IF R4<S1(SP)+.5 THEN PRINT R4;:H=1:RETURN
  211 PRINT "Weg ";:H=0:RETURN
  212 READ R4:PRINT:PRINT R4;
  213 GOSUB 340:IF P$="J"THEN H=1:RETURN
  214 H=0:ZW=R4:RETURN
  215 PRINT PH;A1$;"> ";:IF PH=1 THEN 218
  216 IF R4<=S1(PH)THEN PRINT "Ja  ";:H=1:ZW=R4:RETURN
  217 PRINT "Weg ";:H=0:ZW=R4:RETURN
  218 GOSUB 340:IF P$="J"THEN H=1:RETURN
  219 H=0:ZW=R4:RETURN
  220 PRINT:PRINT "Spiel geht an Spieler"SP "."
  221 IF SP=1 THEN GOSUB 77:GOSUB 261:RETURN
  222 FOR I=1 TO 2:B(SP,I+10)=R(I):NEXT
  223 FOR I=1 TO 4:K(I)=0:NEXT:FOR I=1 TO 12:FB=INT(B(SP,I)/10)
  224 IF FB<10 THEN K(FB)=K(FB)+1
  225 NEXT:F1=0:FB=0:FOR I=1 TO 4:IF F1<K(I)THEN FB=I:F1=K(I)
  226 NEXT:P=FB:GOSUB 286:GOSUB 233:S1(SP)=8+P
  227 S2(SP)=(1+S3(SP))*S1(SP):FOR I=1 TO 2
  228 A=INT(RND(1)*12)+1:IF B(SP,A)>100 OR B(SP,A)-10*INT(B(SP,A)/10)=7 THEN 228
  229 HR(I)=B(SP,A):B(SP,A)=0
  230 NEXT
  231 Z1=0:FOR I=1 TO 12:IF B(SP,I)>0 THEN Z1=Z1+1:K(Z1)=B(SP,I)
  232 NEXT:FOR I=1 TO 10:B(SP,I)=K(I):NEXT:GOSUB 77:GOSUB 255:RETURN
  233 PRINT:PRINT GF$(P);:IF P=5 THEN PRINT ".":RETURN
  234 PRINT "  ist Trumpf.":RETURN
  235 IF SP<>1 THEN 244
  236 PRINT:FOR T=2 TO 3
  237 H1=0:FOR J=1 TO 10:IF B(T,J)>100 THEN H1=H1+1
  238 NEXT:IF H1>4-SF AND T<>SP THEN PRINT "Spieler"T ": Kontra !":KF=1
  239 NEXT:PAUSE 30:IF KF=0 THEN 255
  240 IF SP=1 THEN 247
  241 FOR J=1 TO 10:IF VAL(RIGHT$(STR$(B(SP,J)),1))>6 OR B(SP,J)>200 THEN H1=H1+1
  242 NEXT:IF H1>5 THEN PRINT "Spieler"SP ": Re !!":RF=1
  243 PAUSE 30:GOTO 255
  244 PRINT:PRINT "Kontra ";
  245 GOSUB 336:IF P$="J"THEN KF=1
  246 GOTO 236
  247 PRINT:PRINT "Re ";
  248 GOSUB 336:IF P$="J"THEN RF=1
  249 GOTO 255
  250 FOR T=1 TO 10
  251 FOR I=1 TO 10:IF Z1<B(Z,I)THEN Z1=B(Z,I)
  252 NEXT:K(T)=Z1:FOR I=1 TO 10:IF B(Z,I)=Z1 THEN B(Z,I)=0
  253 NEXT:Z1=0:NEXT
  254 FOR J=1 TO 10:B(Z,J)=K(J):NEXT:RETURN
  255 CLS:PRINT "         Ihre Karten :":PRINT "         "
  256 Z=1:FOR T=1 TO 9
  257 IF T=5 OR T=9 THEN PRINT:PRINT STRING$(4,CHR$(10))
  258 PRINT STR$(T);:GOSUB 333:NEXT
  259 PRINT " 0";:GOSUB 333
  260 PRINT:PRINT STRING$(6,CHR$(10)):RETURN
  261 PRINT "Hand ";
  262 GOSUB 336:PAUSE 30
  263 IF P$="J"THEN HF=1:FOR I=1 TO 2:HR(I)=R(I):NEXT:GOTO 280
  264 F1=1:GOSUB 255:PRINT:PRINT INK 6;"Skat :"
  265 FOR I=1 TO 2:B(1,10+I)=R(I):NEXT
  266 FOR T=11 TO 12:PRINT MID$(STR$(T),2);:GOSUB 333:NEXT
  267 K(1)=13:K(2)=13:PRINT "    Druecken :":PRINT
  268 PRINT AA$;"Nr.:";:INPUT "";K(1)
  269 PRINT AA$;"Nr.:";:INPUT "";K(2)
  270 IF K(1)=0 THEN K(1)=10
  271 IF K(2)=0 THEN K(2)=10
  272 IF K(1)=K(2)THEN PRINT:GOTO 268
  273 FOR I=1 TO 2
  274 IF K(I)<1 OR K(I)>12 THEN PRINT AB$;A4$;A4$;:GOTO 267
  275 HR(I)=B(1,K(I)):NEXT
  276 Z1=0:FOR I=1 TO 2:B(1,K(I))=0:NEXT:FOR I=1 TO 12
  277 IF B(1,I)<>0 THEN Z1=Z1+1:K(Z1)=B(1,I)
  278 NEXT:FOR I=1 TO 10:B(1,I)=K(I):NEXT
  279 F4=1:Z=1:GOSUB 250
  280 GOSUB 255:PRINT:PRINT "Was ist Trumpf ?":PRINT "1."GF$(1)"   2."GF$(2)
  281 PRINT "3."GF$(3)"   4."GF$(4)"   5."GF$(5) 
  282 P$=INKEY$:P=VAL(P$)
  283 GR=0
  284 IF P>5 OR P<1 THEN 282
  285 GOSUB 233:IF P=5 THEN 296
  286 S2(1)=(P+8)*(S3(1)+1):FOR Z=1 TO 3:FOR T=1 TO 10
  287 IF B(Z,T)<100 AND INT(B(Z,T)/10)=P THEN B(Z,T)=100+B(Z,T)
  288 NEXT:NEXT
  289 FOR T=11 TO 12:IF B(SP,T)<100 AND INT(B(SP,T)/10)=P THEN B(SP,T)=100+B(SP,T)
  290 NEXT:Z=1:GOSUB 250:IF F1=0 THEN GOSUB 255
  291 IF F4=0 THEN 295
  292 F4=0:SF=0:PRINT "Schneider ansagen ";
  293 GOSUB 336:IF P$="J"THEN SF=1
  294 GOTO 295
  295 GOSUB 298:RETURN
  296 GR=1:S2(1)=24*(S3(1)+1):Z=1:GOSUB 250:IF F1=0 THEN GOSUB 255
  297 GOSUB 291:RETURN
  298 Z1=1:R2(SP)=8+P:IF P=5 THEN R2(SP)=24
  299 FOR T=1 TO 4:K(T)=0:NEXT
  300 FOR T=1 TO 4:FOR I=1 TO 10:IF B(SP,I)=258-10*T THEN K(T)=1
  301 NEXT:NEXT:FOR T=1 TO 3:IF K(T)=K(T+1)THEN Z1=Z1+1:NEXT
  302 IF HF=1 THEN Z1=Z1+1
  303 R5(SP)=Z1:S2(SP)=(R5(SP)+1)*R2(SP)
  304 GW=ZW:RETURN
  305 PRINT AT(17,16);"Karte Nr. ?"
  306 P$=INKEY$
  307 IF P$<"0"OR P$>"9"THEN 306
  308 P=VAL(P$):IF P=0 THEN P=10
  309 IF B(1,P)=0 THEN 306
  310 IF SK(2)=0 AND SK(3)=0 THEN 320
  311 GE=2:IF SK(2)=0 THEN GE=3
  312 F=0:FOR G=1 TO 10
  313 IF B(1,G)>100 THEN F=F+1
  314 NEXT
  315 IF F>0 AND B(1,P)<100 AND SK(GE)>100 THEN 319
  316 F=0:FOR G=1 TO 10:IF B(1,G)>100 THEN NEXT
  317 IF INT(B(1,G)/10)=INT(SK(GE)/10)AND INT(B(1,P)/10)<>INT(SK(GE)/10)THEN 319
  318 NEXT:GOTO 320
  319 PRINT AT(19,16);"Bedienen !":GOSUB 77:GOTO 305
  320 SK(1)=B(1,P)
  321 IF FB>0 AND INT(SK(1)/10)<>FB AND SK(1)<100 THEN SK(1)=-SK(1)
  322 RETURN
  323 IF Z=SP THEN PRINT " *";:GOTO 325
  324 PRINT STR$(Z);
  325 Q$=RIGHT$(STR$(SK(Z)),2)
  326 Z1=VAL(LEFT$(Q$,1)):IF Z1=0 THEN PRINT C0$;:RETURN
  327 IF F2=1 THEN F2=0:PRINT C0$;:RETURN
  328 Z2=VAL(RIGHT$(Q$,1)):B$=RIGHT$(GF$(Z1),1)
  329 PRINT A6$;KP$(Z2);:IF Z2=6 THEN PRINT A1$;
  330 PRINT A7$;B$;A8$;B$;A9$;
  331 IF Z2=6 THEN PRINT A1$;
  332 PRINT KP$(Z2);A0$;:RETURN
  333 Q$=RIGHT$(STR$(B(Z,T)),2):GOSUB 326:RETURN
  334 DATA 18,20,22,23,24,27,30,33,35,36,44,45,46,48,50,54,55,60,63,66,70
  335 DATA 72,77,80,81,84,88,90,96,99,100,108,110,120,144,168
  336 GOSUB 347:IF P$="J"THEN PRINT "? Ja.";:RETURN
  337 IF P$<>"N"THEN 336
  338 PRINT "? Nein.";:RETURN
  339 XY=DEEK(43):POKE XY,32:RETURN
  340 GOSUB 347:IF P$="J"THEN PRINT "Ja ";:RETURN
  341 IF P$<>"N"THEN 340
  342 PRINT "Weg";:RETURN
  343 RESTORE 343:FOR A=1 TO 5:READ GF$(A):NEXT:FOR A=4 TO 8:READ WU(A):NEXT
  344 FOR A=1 TO 8:READ KP$(A):NEXT:RESTORE:RETURN
  345 DATA "Karo  ","Herz  ","Pik   ","Kreuz ","Grand
  346 DATA 3,4,10,11,2, 7,8,9,O,K,10,A,U
  347 QZ$=STRING$(8,CHR$(8)):PRINT "(J/N) ? ";QZ$;
  348 P$=INKEY$:IF P$=""THEN 348
  349 PRINT STRING$(8," ");QZ$;
  350 PQ=ASC(P$)AND 95:P$=CHR$(PQ):RETURN
